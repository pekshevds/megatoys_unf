#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СразуНаПринтер        = Параметры.СразуНаПринтер;
	КоличествоЭкземпляров = 1;
	ШаблонЭтикетки        = Параметры.ШаблонЭтикетки;
	
	Если ЗначениеЗаполнено(Параметры.АдресВХранилище) Тогда
		АдресВХранилище = ПоместитьВоВременноеХранилище(
			ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище),
			УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка"));
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("КоличествоЭкземпеляров", 1);
	ПараметрыПечати.Вставить("СтруктураДанных", ДанныеПечатиСЗаполненнымШаблоном());
	ПараметрыПечати.Вставить("АдресВХранилище", "");
	ПараметрыПечати.Вставить("КаждаяЭтикеткаНаНовомЛисте", Истина);
	ПараметрыПечати.Вставить("СразуНаПринтер", СразуНаПринтер);
	ПараметрыПечати.Вставить("РежимПечати", "ЭтикеткаКодМаркировкиИСМП");
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьЭтикетокИЦенников",
		"ЭтикеткаКодМаркировкиИСМП",
		ПараметрКоманды,
		ВладелецФормы,
		ПараметрыПечати);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеПечатиСЗаполненнымШаблоном()
	
	ДанныеПечати = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Для Каждого СтрокаПечати Из ДанныеПечати.ОбъектыПечати Цикл
		СтрокаПечати.ШаблонЭтикетки = ШаблонЭтикетки;
	КонецЦикла;
	
	Возврат ДанныеПечати;
	
КонецФункции

#КонецОбласти