
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Цвет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначениеКопирования, "Цвет").Получить();
		Иначе
			Цвет = Метаданные.ЭлементыСтиля.БазовыйЦветСостояний.Значение;
		КонецЕсли;
		
		ОтобразитьНовыеДанные(Цвет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОтобразитьНовыеДанные(ТекущийОбъект.Цвет.Получить());
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗначениеЦвета = Цвет;
	
	Если ЗначениеЦвета = Новый Цвет(0, 0, 0) Тогда
		ЗначениеЦвета = Неопределено;
	КонецЕсли;
	
	ТекущийОбъект.Цвет = Новый ХранилищеЗначения(ЗначениеЦвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_СостоянияЛидов", Объект.Ссылка, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УстановитьЦвет(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбработчикОповещенияЦветВыборНажатие", ЭтотОбъект);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("МассивЦветов", МассивЦветовСостоянийЛидов());
	ПараметрыОткрытияФормы.Вставить("Цвет", Цвет);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦветаУНФ", ПараметрыОткрытияФормы, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработчикОповещенияЦветВыборНажатие(НовыйЦвет, ДополнительныеПараметры) Экспорт
	
	Если НовыйЦвет <> Неопределено И НовыйЦвет <> КодВозвратаДиалога.Отмена Тогда
		ОтобразитьНовыеДанные(НовыйЦвет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция МассивЦветовСостоянийЛидов()
	
	МассивЦветов = Новый Массив;
	
	МассивЦветов.Добавить(Новый Цвет(189, 105, 57));
	МассивЦветов.Добавить(Новый Цвет(153, 135, 35));
	МассивЦветов.Добавить(Новый Цвет(88, 163, 88));
	МассивЦветов.Добавить(Новый Цвет(82, 163, 163));
	МассивЦветов.Добавить(Новый Цвет(132, 61, 184));
	МассивЦветов.Добавить(Новый Цвет(189, 32, 82));
	МассивЦветов.Добавить(Новый Цвет(107, 93, 1));
	МассивЦветов.Добавить(Новый Цвет(132, 156, 54));
	МассивЦветов.Добавить(Новый Цвет(66, 113, 179));
	МассивЦветов.Добавить(Новый Цвет(122, 122, 122));
	
	Возврат МассивЦветов;
	
КонецФункции

&НаСервере
Процедура ОтобразитьНовыеДанные(НовыйЦвет)
	Цвет = НовыйЦвет;
	
	Элементы.Цвет.ЦветТекста = Цвет;
	Элементы.Цвет.ЦветФона = Цвет;
КонецПроцедуры

#КонецОбласти

