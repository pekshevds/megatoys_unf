#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ЗагрузкаЭлементаСправочника

Функция ДанныеИзФайлаОбмена(
		ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка = Ложь,
		Подпись = Неопределено,
		ЗаявлениеНаОтзыв = Неопределено,
		ПодписьЗаявленияНаОтзыв = Неопределено,
		ДанныеАрхива = Неопределено) Экспорт
	
	ДанныеДоверенности = Новый Структура;
	
	ЭтоФайл = Ложь;
	ТекущиеДанные = ВходящиеДанные;
	Если ТипЗнч(ВходящиеДанные) = Тип("ДвоичныеДанные") Тогда
		ТекущиеДанные = ВходящиеДанные;
	ИначеЕсли ЭтоАдресВременногоХранилища(ВходящиеДанные) Тогда
		ТекущиеДанные = ПолучитьИзВременногоХранилища(ВходящиеДанные);
	ИначеЕсли ТипЗнч(ВходящиеДанные) = Тип("Строка") И НайтиФайлы(ВходящиеДанные).Количество() > 0 Тогда
		ЭтоФайл = Истина;
	КонецЕсли;
	ДокументОбработки = ПодготовитьДокументDOM(ТекущиеДанные);
	
	Если ЭтоФайл Тогда
		ТекущиеДанные = Новый ДвоичныеДанные(ВходящиеДанные);
	КонецЕсли;
	ДанныеДоверенности.Вставить("ДанныеВыгрузки", ТекущиеДанные);
	
	ТекущиеДанные = Подпись;
	Если Подпись <> Неопределено Тогда
		Если ТипЗнч(Подпись) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = Подпись;
		ИначеЕсли ЭтоАдресВременногоХранилища(Подпись) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(Подпись);
		ИначеЕсли ТипЗнч(Подпись) = Тип("Строка") И НайтиФайлы(Подпись).Количество() > 0 Тогда
			ТекущиеДанные = Новый ДвоичныеДанные(Подпись);
		КонецЕсли;
	КонецЕсли;
	ДанныеДоверенности.Вставить("ДанныеПодписи", ТекущиеДанные);
	
	ТекущиеДанные = ЗаявлениеНаОтзыв;
	Если ЗаявлениеНаОтзыв <> Неопределено Тогда
		Если ТипЗнч(ЗаявлениеНаОтзыв) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = ЗаявлениеНаОтзыв;
		ИначеЕсли ЭтоАдресВременногоХранилища(ЗаявлениеНаОтзыв) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(ЗаявлениеНаОтзыв);
		ИначеЕсли ТипЗнч(ЗаявлениеНаОтзыв) = Тип("Строка") И НайтиФайлы(ЗаявлениеНаОтзыв).Количество() > 0 Тогда
			ТекущиеДанные = Новый ДвоичныеДанные(ЗаявлениеНаОтзыв);
		КонецЕсли;
	КонецЕсли;
	ДанныеДоверенности.Вставить("ДанныеЗаявленияНаОтзыв", ТекущиеДанные);
	Если ТекущиеДанные <> Неопределено Тогда
		ДокументОтзыва = ПодготовитьДокументDOM(ТекущиеДанные);
		СведенияФайла = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл");
		Если СведенияФайла.Количество() = 1 Тогда
			ЭлементВыборки = СведенияФайла[0];
			ИмяФайлаЗаявленияНаОтзыв = ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИдФайл");
			Если ЗначениеЗаполнено(ИмяФайлаЗаявленияНаОтзыв) Тогда
				ДанныеДоверенности.Вставить("ИмяФайлаЗаявленияНаОтзыв", ИмяФайлаЗаявленияНаОтзыв + ".xml");
			КонецЕсли;
		КонецЕсли;
		
		СведенияДоверенности = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл/Документ/СвЗаяв");
		Если СведенияДоверенности.Количество() = 1 Тогда
			ЭлементВыборки = СведенияДоверенности[0];
			ДанныеДоверенности.Вставить("УникальныйИдентификаторДоверенностиОтзыва",
				ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИдДовер"));
			ДанныеДоверенности.Вставить("ДатаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаОтз", '00010101'));
			ДанныеДоверенности.Вставить("ПричинаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ПричОтз", ""));
		КонецЕсли;
		
		СведенияДоверителяЮЛ = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл/Документ/СвДоверит/НПЮЛ");
		СведенияДоверителяФЛ = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл/Документ/СвДоверит/НПФЛ");
		СведенияДоверителяИО = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл/Документ/СвДоверит/ИО");
		Если СведенияДоверителяЮЛ.Количество() = 1 Тогда
			ТипОрганизацииОтзыва = "ЮЛ";
			СведенияДоверителя = СведенияДоверителяЮЛ;
		ИначеЕсли СведенияДоверителяФЛ.Количество() = 1 Тогда
			ТипОрганизацииОтзыва = "ФЛ";
			СведенияДоверителя = СведенияДоверителяФЛ;
		Иначе
			ТипОрганизацииОтзыва = "ИО";
			СведенияДоверителя = СведенияДоверителяИО;
		КонецЕсли;
		Если СведенияДоверителя.Количество() = 1 Тогда
			ЭлементВыборки = СведенияДоверителя[0];
			Если ТипОрганизацииОтзыва = "ЮЛ" Тогда
				ДанныеДоверенности.Вставить("ДолжностьРуководителяДоверителяОтзыва",
					ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@Должность"));
			ИначеЕсли ТипОрганизацииОтзыва = "ФЛ" Тогда
				ДанныеДоверенности.Вставить("ДатаРожденияДоверителяОтзыва",
					ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаРожд", '00010101'));
			КонецЕсли;
		КонецЕсли;
		
		СведенияПодписанта = ПолучитьВыборкуЭлементовDOM(ДокументОтзыва, "/Файл/Документ/Подписант");
		Если СведенияПодписанта.Количество() = 1 Тогда
			ЭлементВыборки = СведенияПодписанта[0];
			ДанныеДоверенности.Вставить("ТелефонПодписантаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Тлф"));
		КонецЕсли;
	КонецЕсли;
	
	ТекущиеДанные = ПодписьЗаявленияНаОтзыв;
	Если ПодписьЗаявленияНаОтзыв <> Неопределено Тогда
		Если ТипЗнч(ПодписьЗаявленияНаОтзыв) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = ПодписьЗаявленияНаОтзыв;
		ИначеЕсли ЭтоАдресВременногоХранилища(ПодписьЗаявленияНаОтзыв) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(ПодписьЗаявленияНаОтзыв);
		ИначеЕсли ТипЗнч(ПодписьЗаявленияНаОтзыв) = Тип("Строка")
			И НайтиФайлы(ПодписьЗаявленияНаОтзыв).Количество() > 0 Тогда
			ТекущиеДанные = Новый ДвоичныеДанные(ПодписьЗаявленияНаОтзыв);
		КонецЕсли;
	КонецЕсли;
	ДанныеДоверенности.Вставить("ДанныеПодписиЗаявленияНаОтзыв", ТекущиеДанные);
	
	ТекущиеДанные = ДанныеАрхива;
	Если ДанныеАрхива <> Неопределено Тогда
		Если ТипЗнч(ДанныеАрхива) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = ДанныеАрхива;
		ИначеЕсли ЭтоАдресВременногоХранилища(ДанныеАрхива) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(ДанныеАрхива);
		ИначеЕсли ТипЗнч(ДанныеАрхива) = Тип("Строка")
			И НайтиФайлы(ДанныеАрхива).Количество() > 0 Тогда
			ТекущиеДанные = Новый ДвоичныеДанные(ДанныеАрхива);
		КонецЕсли;
	КонецЕсли;
	ДанныеДоверенности.Вставить("ДанныеАрхива", ТекущиеДанные);
	
	СведенияФайла = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл");
	Если СведенияФайла.Количество() = 1 Тогда
		ЭлементВыборки = СведенияФайла[0];
		ИмяФайлаВыгрузки = ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИдФайл");
		Если ЗначениеЗаполнено(ИмяФайлаВыгрузки) Тогда
			Если ВРег(Лев(ИмяФайлаВыгрузки, 12)) = "ON_OFFDOVER_" Тогда
				ДанныеДоверенности = ДанныеИзФайлаОбменаДляОтмены(ДокументОбработки, ОбновлятьСуществующийИлиСсылка, Подпись);
				Возврат ДанныеДоверенности;
			ИначеЕсли ВРег(Лев(ИмяФайлаВыгрузки, 9)) <> "ON_DOVEL_" Тогда
				Возврат Новый Структура("ТекстОшибки", НСтр("ru = 'Неверный тип доверенности'"));
			КонецЕсли;
			
			ДанныеДоверенности.Вставить("ИмяФайлаВыгрузки", ИмяФайлаВыгрузки + ".xml");
		КонецЕсли;
	Иначе
		Возврат Новый Структура("ТекстОшибки", НСтр("ru = 'Неверный тип доверенности'"));
	КонецЕсли;
	
	СведенияДоверенности = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДов");
	Если СведенияДоверенности.Количество() = 1 Тогда
		ЭлементВыборки = СведенияДоверенности[0];
		ДанныеДоверенности.Вставить("НомерДоверенности", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НомДовер"));
		ДанныеДоверенности.Вставить("ДатаВыдачи", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаНач", '00010101'));
		ДанныеДоверенности.Вставить("ДатаОкончания", ПолучитьЗначениеДокумента(ЭлементВыборки, "ДатаОкон", '00010101'));
		ДанныеДоверенности.Вставить("СрокДействия", ПолучитьЗначениеДокумента(ЭлементВыборки, "СрокДейст", ""));
		ДанныеДоверенности.Вставить("СведенияОбИнформационнойСистеме", ПолучитьЗначениеДокумента(ЭлементВыборки, "СведСистОтм", ""));
		ДанныеДоверенности.Вставить("НомерРодительскойДоверенности", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НомПредДовер", ""));
		ДанныеДоверенности.Вставить("КодНалоговогоОрганаПредставления", ПолучитьЗначениеДокумента(ДокументОбработки, "//Файл/Документ/@КодНО", ""));
	КонецЕсли;
	
	СписокНО = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДов/КодНОДейст");
	ДанныеДоверенности.Вставить("НалоговыеОрганыДействия", Новый Массив);
	ДанныеДоверенности.Вставить("ФИО", Новый Массив);
	ДанныеДоверенности.Вставить("ПолномочияПредставителя", Новый Массив);
	ДанныеДоверенности.Вставить("УдостоверенияЛичности", Новый Массив);
	ДанныеДоверенности.Вставить("ТипОрганизации", "");
	
	Для Каждого СтрокаМассива Из СписокНО Цикл
		НоваяСтрока = Новый Структура("КодНалоговогоОргана", СтрокаМассива.ТекстовоеСодержимое);
		ДанныеДоверенности.НалоговыеОрганыДействия.Добавить(НоваяСтрока);
	КонецЦикла;
	
	СведенияДоверителяЮЛ = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/НПЮЛ");
	СведенияДоверителяФЛ = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/НПФЛ");
	СведенияДоверителяИО = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/ИО");
	
	Если СведенияДоверителяЮЛ.Количество() = 1 Тогда
		ДанныеДоверенности.ТипОрганизации = "ЮЛ";
		СведенияДоверителя = СведенияДоверителяЮЛ;
	ИначеЕсли СведенияДоверителяФЛ.Количество() = 1 Тогда
		ДанныеДоверенности.ТипОрганизации = "ФЛ";
		СведенияДоверителя = СведенияДоверителяФЛ;
	Иначе
		ДанныеДоверенности.ТипОрганизации = "ИО";
		СведенияДоверителя = СведенияДоверителяИО;
	КонецЕсли;
	
	РеквизитыПоискаОрганизации = Новый Соответствие;
	
	Если СведенияДоверителя.Количество() = 1 Тогда
		ЭлементВыборки = СведенияДоверителя[0];
		Если ДанныеДоверенности.ТипОрганизации = "ЮЛ" Тогда
			ДанныеДоверенности.Вставить("ДоверительЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НаимОрг"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННЮЛ"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "@КПП"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ОГРН"));
			
			ДанныеДоверенности.Вставить("ДоверительЮЛ_АдрРус", ПолучитьЗначениеДокумента(ЭлементВыборки, "АдрРФ/АдрРус"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_АдрЛат", ПолучитьЗначениеДокумента(ЭлементВыборки, "АдрРФ/АдрЛат"));
			
			ДанныеДоверенности.Вставить("ЛицоБезДовФЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвФЛ/@ИННФЛ"));
			ДанныеДоверенности.Вставить("ЛицоБезДовФЛ_СНИЛС", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвФЛ/@СНИЛС"));
			ДанныеДоверенности.Вставить("ЛицоБезДовФЛ_Гражданство", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвФЛ/@Гражданство", Справочники.СтраныМира.ПустаяСсылка()));
			ДанныеДоверенности.Вставить("ЛицоБезДовФЛ_ДатаРождения", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвФЛ/@ДатаРожд", '00010101'));
			ДанныеДоверенности.Вставить("ЛицоБезДовФЛ_Должность", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвФЛ/@Должность"));
			
			ДанныеДоверенности.Вставить("ЛицоБезДовЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвОрг/@НаимОрг"));
			ДанныеДоверенности.Вставить("ЛицоБезДовЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвОрг/@ИННЮЛ"));
			ДанныеДоверенности.Вставить("ЛицоБезДовЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвОрг/@КПП"));
			ДанныеДоверенности.Вставить("ЛицоБезДовЮЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "ЛицоБезДов/СвОрг/@ОГРН"));
			
			Если ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовФЛ_ИНН) Тогда
				ДанныеДоверенности.Вставить("ЛицоБезДовФЛ",
									ПодыскатьИнформациюВладельца("Контрагенты,ФизическиеЛица", 
									ДанныеДоверенности.ЛицоБезДовФЛ_ИНН, , ДанныеДоверенности.ЛицоБезДовФЛ_СНИЛС));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовЮЛ_ИНН) Тогда
				ДанныеДоверенности.Вставить("ЛицоБезДовЮЛ",
									ПодыскатьИнформациюВладельца("Контрагенты,Организации", 
									ДанныеДоверенности.ЛицоБезДовЮЛ_ИНН, ДанныеДоверенности.ЛицоБезДовЮЛ_КПП));
				Если НЕ ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовЮЛ)
					И ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовЮЛ_НаимОрг) Тогда
					ДанныеДоверенности.ЛицоБезДовЮЛ = ДанныеДоверенности.ЛицоБезДовЮЛ_НаимОрг;
				КонецЕсли;
			КонецЕсли;
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеДоверенности.ДоверительЮЛ_ИНН);
			СправочникОрганизации = Метаданные.Справочники.Найти("Организации");
			РеквизитКПП = ?(СправочникОрганизации = Неопределено, Неопределено, СправочникОрганизации.Реквизиты.Найти("КПП"));
			Если РеквизитКПП <> Неопределено Тогда
				РеквизитыПоискаОрганизации.Вставить("КПП", ДанныеДоверенности.ДоверительЮЛ_КПП);
			КонецЕсли;
			
		ИначеЕсли ДанныеДоверенности.ТипОрганизации = "ИО" Тогда
			ДанныеДоверенности.Вставить("ДоверительЮЛ_ИностраннаяОрганизация", Истина);
			ДанныеДоверенности.Вставить("ДоверительЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НаимИО"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННЮЛ"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "@КПП"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_СтрРег", ПолучитьЗначениеДокумента(ЭлементВыборки, "@СтрРег", Справочники.СтраныМира.ПустаяСсылка()));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_НаимРегОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НаимРегОрг"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_РегНомер", ПолучитьЗначениеДокумента(ЭлементВыборки, "@РегНомер"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_КодНПРег", ПолучитьЗначениеДокумента(ЭлементВыборки, "@КодНПРег"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_АдрЛат", ПолучитьЗначениеДокумента(ЭлементВыборки, "АдрСтрРег/АдрЛат"));
			ДанныеДоверенности.Вставить("ДоверительЮЛ_АдрРус", ПолучитьЗначениеДокумента(ЭлементВыборки, "АдрСтрРег/АдрРус"));
			
			ДанныеДоверенности.Вставить("ДоверительФЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@ИННФЛ"));
			ДанныеДоверенности.Вставить("ДоверительФЛ_ДатаРождения", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@ДатаРожд", '00010101'));
			ДанныеДоверенности.Вставить("ДоверительФЛ_МестоРожд", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@МестоРожд"));
			ДанныеДоверенности.Вставить("ДоверительФЛ_Пол", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@Пол", Перечисления.ПолФизическогоЛица.Мужской));
			ПризнакГражданства = ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@ПрГражд");
			Если ПризнакГражданства = 1 Тогда
				ДанныеДоверенности.Вставить("ДоверительФЛ_Гражданство", Справочники.СтраныМира.Россия);
			Иначе
				ДанныеДоверенности.Вставить("ДоверительФЛ_Гражданство", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@Гражданство", Справочники.СтраныМира.ПустаяСсылка()));
			КонецЕсли;
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеДоверенности.ДоверительЮЛ_ИНН);
			СправочникОрганизации = Метаданные.Справочники.Найти("Организации");
			РеквизитКПП = ?(СправочникОрганизации = Неопределено, Неопределено, СправочникОрганизации.Реквизиты.Найти("КПП"));
			Если РеквизитКПП <> Неопределено Тогда
				РеквизитыПоискаОрганизации.Вставить("КПП", ДанныеДоверенности.ДоверительЮЛ_КПП);
			КонецЕсли;
			
		ИначеЕсли ДанныеДоверенности.ТипОрганизации = "ФЛ" Тогда
			ДанныеДоверенности.Вставить("ДоверительФЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННФЛ"));
			ДанныеДоверенности.Вставить("ДоверительФЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ОГРНИП"));
			ДанныеДоверенности.Вставить("ДоверительФЛ_СНИЛС", ПолучитьЗначениеДокумента(ЭлементВыборки, "@СНИЛС"));
			ДанныеДоверенности.Вставить("ДоверительФЛ_Гражданство", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Гражданство", Справочники.СтраныМира.ПустаяСсылка()));
			ДанныеДоверенности.Вставить("ДоверительФЛ_ДатаРождения", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаРожд", '00010101'));
			
			ДокументИП = Новый Структура;
			СерияНомер = ПодготовитьПредставлениеСерияНомер(ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@СерНомДок"));
			ДокументИП.Вставить("СерДок", СерияНомер.Серия);
			ДокументИП.Вставить("НомДок", СерияНомер.Номер);
			ДокументИП.Вставить("ДатаДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@ДатаДок", '00010101'));
			ДокументИП.Вставить("ВыдДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@ВыдДок"));
			ДокументИП.Вставить("КодВыдДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@КодВыдДок"));
			ДокументИП.Вставить("ВидДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@КодВидДок", Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка()));
			ДокументИП.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ);
			ДанныеДоверенности.УдостоверенияЛичности.Добавить(ДокументИП);
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеДоверенности.ДоверительФЛ_ИНН);
			
		КонецЕсли;
		
	КонецЕсли;
	
	СведенияУполномоченных = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвУпПред");
	Для Каждого СтрокаМассива Из СведенияУполномоченных Цикл
		НоваяСтрока = Новый Структура;
		ПолномочияПредставителя = ПолучитьВыборкуЭлементовDOM(СтрокаМассива, "ПрОблПолн");
		Для каждого ПолномочиеПредставителя Из ПолномочияПредставителя Цикл
			ПредставлениеВида = СокрЛП(ПолномочиеПредставителя.ТекстовоеСодержимое);
			Если ПредставлениеВида = "99" Тогда
				НоваяСтрока.Вставить("ПризнакПолныеПолномочия", Истина);
			ИначеЕсли СтрДлина(ПредставлениеВида) = 2 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПредставлениеВида) Тогда
				ВидПолномочия = Число(ПредставлениеВида);
				Если ВидПолномочия >= 1 И ВидПолномочия <= КоличествоВидовПолномочийПредставителя() Тогда
					НоваяСтрока.Вставить("Признак" + ПредставлениеВида, Истина);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ДанныеДоверенности.ПолномочияПредставителя.Добавить(НоваяСтрока);
		
		ТипВладельца = Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ;
		ИнформацияОрганизации = ПолучитьВыборкуЭлементовDOM(СтрокаМассива, "СвПред/СвОрг");
		Если ИнформацияОрганизации.Количество() > 0 Тогда
			ДанныеДоверенности.Вставить("ПредставительЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ИнформацияОрганизации[0], "@НаимОрг"));
			ДанныеДоверенности.Вставить("ПредставительЮЛ_ИНН", ПолучитьЗначениеДокумента(ИнформацияОрганизации[0], "@ИННЮЛ"));
			ДанныеДоверенности.Вставить("ПредставительЮЛ_КПП", ПолучитьЗначениеДокумента(ИнформацияОрганизации[0], "@КПП"));
			ДанныеДоверенности.Вставить("ПредставительЮЛ_ОГРН", ПолучитьЗначениеДокумента(ИнформацияОрганизации[0], "@ОГРН"));
		КонецЕсли;
		
		СведенияФЛ = ПолучитьВыборкуЭлементовDOM(СтрокаМассива, "СвПред/СведФизЛ");
		Если СведенияФЛ.Количество() > 0 Тогда
			СведенияФЛ = СведенияФЛ[0];
			ДанныеДоверенности.Вставить("ПредставительФЛ_ИНН", ПолучитьЗначениеДокумента(СведенияФЛ, "@ИННФЛ"));
			ДанныеДоверенности.Вставить("ПредставительФЛ_ОГРН", ПолучитьЗначениеДокумента(СведенияФЛ, "@ОГРНИП"));
			ДанныеДоверенности.Вставить("ПредставительФЛ_СНИЛС", ПолучитьЗначениеДокумента(СведенияФЛ, "@СНИЛС"));
			ДанныеДоверенности.Вставить("ПредставительФЛ_Гражданство", ПолучитьЗначениеДокумента(СведенияФЛ, "@Гражданство", Справочники.СтраныМира.ПустаяСсылка()));
			ДанныеДоверенности.Вставить("ПредставительФЛ_ДатаРождения", ПолучитьЗначениеДокумента(СведенияФЛ, "@ДатаРожд", '00010101'));
			
			НоваяСтрока = Новый Структура;
			НоваяСтрока.Вставить("Фамилия", ПолучитьЗначениеДокумента(СведенияФЛ, "ФИО/@Фамилия"));
			НоваяСтрока.Вставить("Имя", ПолучитьЗначениеДокумента(СведенияФЛ, "ФИО/@Имя"));
			НоваяСтрока.Вставить("Отчество", ПолучитьЗначениеДокумента(СведенияФЛ, "ФИО/@Отчество"));
			НоваяСтрока.Вставить("Владелец", ТипВладельца);
			ДанныеДоверенности.ФИО.Добавить(НоваяСтрока);
			
			ДокументИП = Новый Структура;
			СерияНомер = ПодготовитьПредставлениеСерияНомер(ПолучитьЗначениеДокумента(СведенияФЛ, "УдЛичн/@СерНомДок"));
			ДокументИП.Вставить("СерДок", СерияНомер.Серия);
			ДокументИП.Вставить("НомДок", СерияНомер.Номер);
			ДокументИП.Вставить("ДатаДок", ПолучитьЗначениеДокумента(СведенияФЛ, "УдЛичн/@ДатаДок", '00010101'));
			ДокументИП.Вставить("ВыдДок", ПолучитьЗначениеДокумента(СведенияФЛ, "УдЛичн/@ВыдДок"));
			ДокументИП.Вставить("ВидДок", ПолучитьЗначениеДокумента(СведенияФЛ, "УдЛичн/@КодВидДок", Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка()));
			ДокументИП.Вставить("КодВыдДок", ПолучитьЗначениеДокумента(СведенияФЛ, "УдЛичн/@КодВыдДок"));
			ДокументИП.Вставить("Владелец", ТипВладельца);
			ДанныеДоверенности.УдостоверенияЛичности.Добавить(ДокументИП);
		КонецЕсли;
		
		Представитель = Неопределено;
		Если ДанныеДоверенности.Свойство("ПредставительЮЛ_ИНН")
			И ЗначениеЗаполнено(ДанныеДоверенности.ПредставительЮЛ_ИНН) Тогда
			Представитель = ПодыскатьИнформациюВладельца("Контрагенты,Организации", 
								ДанныеДоверенности.ПредставительЮЛ_ИНН, ДанныеДоверенности.ПредставительЮЛ_КПП);
		КонецЕсли;
		
		Если НЕ ДанныеДоверенности.Свойство("ПредставительЮЛ_ИНН")
			ИЛИ НЕ ЗначениеЗаполнено(ДанныеДоверенности.ПредставительЮЛ_ИНН)
			И НЕ ЗначениеЗаполнено(Представитель) И ЗначениеЗаполнено(ДанныеДоверенности.ПредставительФЛ_ОГРН) Тогда
			Представитель = ПодыскатьИнформациюВладельца("Контрагенты", ДанныеДоверенности.ПредставительФЛ_ИНН);
		КонецЕсли;
		
		Если НЕ ДанныеДоверенности.Свойство("ПредставительЮЛ_ИНН")
			ИЛИ НЕ ЗначениеЗаполнено(ДанныеДоверенности.ПредставительЮЛ_ИНН) И НЕ ЗначениеЗаполнено(Представитель) Тогда
			Представитель = ПодыскатьИнформациюВладельца("ФизическиеЛица", ДанныеДоверенности.ПредставительФЛ_ИНН);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Представитель) И ДанныеДоверенности.Свойство("ПредставительЮЛ_НаимОрг")
			И ЗначениеЗаполнено(ДанныеДоверенности.ПредставительЮЛ_НаимОрг) Тогда
			Представитель = ДанныеДоверенности.ПредставительЮЛ_НаимОрг;
		КонецЕсли;
		
		ДанныеДоверенности.Вставить("Представитель", Представитель);
		
	КонецЦикла;
	
	СведенияПодписанта = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/Подписант");
	Если СведенияПодписанта.Количество() = 1 Тогда
		ЭлементВыборки = СведенияПодписанта[0];
		
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("Фамилия", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Фамилия"));
		НоваяСтрока.Вставить("Имя", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Имя"));
		НоваяСтрока.Вставить("Отчество", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Отчество"));
		Если ДанныеДоверенности.ТипОрганизации = "ФЛ" Тогда
			НоваяСтрока.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ);
		Иначе
			НоваяСтрока.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительРук);
			Если ДанныеДоверенности.ТипОрганизации = "ЮЛ" И ДанныеДоверенности.Свойство("ЛицоБезДовФЛ")
				И НЕ ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовФЛ) И (ЗначениеЗаполнено(НоваяСтрока.Фамилия)
				ИЛИ ЗначениеЗаполнено(НоваяСтрока.Имя) ИЛИ ЗначениеЗаполнено(НоваяСтрока.Отчество)) Тогда
				ДанныеДоверенности.ЛицоБезДовФЛ = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(НоваяСтрока);
			КонецЕсли;
		КонецЕсли;
		ДанныеДоверенности.ФИО.Добавить(НоваяСтрока);
	КонецЕсли;
	
	ОрганизацияДоверенности = НайтиЭлементИБ(Справочники.Организации.ПустаяСсылка(), РеквизитыПоискаОрганизации);
	ДанныеДоверенности.Вставить("Доверитель", ОрганизацияДоверенности);
	Если НЕ ЗначениеЗаполнено(ДанныеДоверенности.Доверитель) И ДанныеДоверенности.Свойство("ДоверительЮЛ_НаимОрг")
		И ЗначениеЗаполнено(ДанныеДоверенности.ДоверительЮЛ_НаимОрг) Тогда
		ДанныеДоверенности.Доверитель = ДанныеДоверенности.ДоверительЮЛ_НаимОрг;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОрганизацияДоверенности) И ЗначениеЗаполнено(РеквизитыПоискаОрганизации["КПП"]) Тогда
		РеквизитыПоискаОрганизации.Удалить("КПП");
		ОрганизацияДоверенности = НайтиЭлементИБ(Справочники.Организации.ПустаяСсылка(), РеквизитыПоискаОрганизации);
		Если НЕ ЗначениеЗаполнено(ОрганизацияДоверенности) И ЗначениеЗаполнено(РеквизитыПоискаОрганизации["ИНН"]) Тогда
			РеквизитыПоискаОрганизации.Удалить("ИНН");
			ОрганизацияДоверенности = НайтиЭлементИБ(Справочники.Организации.ПустаяСсылка(), РеквизитыПоискаОрганизации);
		КонецЕсли;
	КонецЕсли;
	ДанныеДоверенности.Вставить("Организация", ОрганизацияДоверенности);
	ДанныеДоверенности.Вставить("Владелец", ОрганизацияДоверенности);
	
	Если ЗначениеЗаполнено(ДанныеДоверенности.Организация)
		И ЗначениеЗаполнено(ДанныеДоверенности.НомерРодительскойДоверенности) Тогда
		
		СтрокаПоиска = Новый Структура;
		СтрокаПоиска.Вставить("НомерДоверенности", ДанныеДоверенности.НомерРодительскойДоверенности);
		СтрокаПоиска.Вставить("Организация", ДанныеДоверенности.Организация);
		НашлиДоверенность = НайтиЭлементИБ(Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ПустаяСсылка(), СтрокаПоиска);
		ДанныеДоверенности.Вставить("РодительскаяДоверенность", НашлиДоверенность);
	КонецЕсли;
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Функция ДанныеИзФайлаОбменаДляОтмены(ВходящиеДанные, ОбновлятьСуществующийИлиСсылка = Ложь, Подпись = Неопределено) Экспорт
	
	ДанныеЗаявления = Новый Структура;
	ДанныеЗаявления.Вставить("ЭтоОтзыв", Истина);
	
	ЭтоФайл = Ложь;
	Если ТипЗнч(ВходящиеДанные) = Тип("ДокументDOM") Тогда
		ДокументОбработки = ВходящиеДанные;
	Иначе
		ТекущиеДанные = ВходящиеДанные;
		Если ТипЗнч(ВходящиеДанные) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = ВходящиеДанные;
		ИначеЕсли ЭтоАдресВременногоХранилища(ВходящиеДанные) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(ВходящиеДанные);
		ИначеЕсли ТипЗнч(ВходящиеДанные) = Тип("Строка") И НайтиФайлы(ВходящиеДанные).Количество() > 0 Тогда
			ЭтоФайл = Истина;
		КонецЕсли;
		ДокументОбработки = ПодготовитьДокументDOM(ТекущиеДанные);
	КонецЕсли;
	
	Если ЭтоФайл Тогда
		ТекущиеДанные = Новый ДвоичныеДанные(ВходящиеДанные);
	КонецЕсли;
	ДанныеЗаявления.Вставить("ДанныеВыгрузки", ТекущиеДанные);
	
	ТекущиеДанные = Подпись;
	Если Подпись <> Неопределено Тогда
		Если ТипЗнч(Подпись) = Тип("ДвоичныеДанные") Тогда
			ТекущиеДанные = Подпись;
		ИначеЕсли ЭтоАдресВременногоХранилища(Подпись) Тогда
			ТекущиеДанные = ПолучитьИзВременногоХранилища(Подпись);
		ИначеЕсли ТипЗнч(Подпись) = Тип("Строка") И НайтиФайлы(Подпись).Количество() > 0 Тогда
			ТекущиеДанные = Новый ДвоичныеДанные(Подпись);
		КонецЕсли;
	КонецЕсли;
	ДанныеЗаявления.Вставить("ДанныеПодписи", ТекущиеДанные);
	
	СведенияФайла = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл");
	Если СведенияФайла.Количество() = 1 Тогда
		ЭлементВыборки = СведенияФайла[0];
		ИмяФайлаВыгрузки = ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИдФайл");
		Если ЗначениеЗаполнено(ИмяФайлаВыгрузки) Тогда
			ДанныеЗаявления.Вставить("ИмяФайлаВыгрузки", ИмяФайлаВыгрузки + ".xml");
		КонецЕсли;
	КонецЕсли;
	
	СведенияДоверенности = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвЗаяв");
	Если СведенияДоверенности.Количество() = 1 Тогда
		ЭлементВыборки = СведенияДоверенности[0];
		ДанныеЗаявления.Вставить("НомерДоверенности", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НомДовер"));
		ДанныеЗаявления.Вставить("УникальныйИдентификаторДоверенностиОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИдДовер"));
		ДанныеЗаявления.Вставить("ДатаВыдачи", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаНач", '00010101'));
		ДанныеЗаявления.Вставить("ДатаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаОтз", '00010101'));
		ДанныеЗаявления.Вставить("СведенияОбИнформационнойСистеме", ПолучитьЗначениеДокумента(ЭлементВыборки, "СведСистОтм", ""));
		ДанныеЗаявления.Вставить("ПричинаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ПричОтз", ""));
		ДанныеЗаявления.Вставить("КодНалоговогоОрганаПредставления", ПолучитьЗначениеДокумента(ДокументОбработки, "//Файл/Документ/@КодНО", ""));
	КонецЕсли;
	
	ДанныеЗаявления.Вставить("ФИО", Новый Массив);
	ДанныеЗаявления.Вставить("УдостоверенияЛичности", Новый Массив);
	ДанныеЗаявления.Вставить("ТипОрганизации", "");
	СведенияДоверителяЮЛ = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/НПЮЛ");
	СведенияДоверителяФЛ = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/НПФЛ");
	СведенияДоверителяИО = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/СвДоверит/ИО");
	
	Если СведенияДоверителяЮЛ.Количество() = 1 Тогда
		ДанныеЗаявления.ТипОрганизации = "ЮЛ";
		СведенияДоверителя = СведенияДоверителяЮЛ;
	ИначеЕсли СведенияДоверителяФЛ.Количество() = 1 Тогда
		ДанныеЗаявления.ТипОрганизации = "ФЛ";
		СведенияДоверителя = СведенияДоверителяФЛ;
	Иначе
		ДанныеЗаявления.ТипОрганизации = "ИО";
		СведенияДоверителя = СведенияДоверителяИО;
	КонецЕсли;
	
	РеквизитыПоискаОрганизации = Новый Соответствие;
	
	Если СведенияДоверителя.Количество() = 1 Тогда
		ЭлементВыборки = СведенияДоверителя[0];
		Если ДанныеЗаявления.ТипОрганизации = "ЮЛ" Тогда
			ДанныеЗаявления.Вставить("ДоверительЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НаимОрг"));
			ДанныеЗаявления.Вставить("ДоверительЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННЮЛ"));
			ДанныеЗаявления.Вставить("ДоверительЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "@КПП"));
			ДанныеЗаявления.Вставить("ДоверительЮЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ОГРН"));
			
			ДанныеЗаявления.Вставить("ЛицоБезДовФЛ_Должность", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@Должность"));
			ДанныеЗаявления.Вставить("ДолжностьРуководителяДоверителяОтзыва",
				ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@Должность"));
			
			ДанныеЗаявления.Вставить("ЛицоБезДовЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@НаимОрг"));
			ДанныеЗаявления.Вставить("ЛицоБезДовЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@ИННЮЛ"));
			ДанныеЗаявления.Вставить("ЛицоБезДовЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@КПП"));
			ДанныеЗаявления.Вставить("ЛицоБезДовЮЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОрг/@ОГРН")); 
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеЗаявления.ДоверительЮЛ_ИНН);
			РеквизитыПоискаОрганизации.Вставить("КПП", ДанныеЗаявления.ДоверительЮЛ_КПП);
			
		ИначеЕсли ДанныеЗаявления.ТипОрганизации = "ИО" Тогда
			ДанныеЗаявления.Вставить("ДоверительЮЛ_ИностраннаяОрганизация", Истина);
			ДанныеЗаявления.Вставить("ДоверительЮЛ_НаимОрг", ПолучитьЗначениеДокумента(ЭлементВыборки, "@НаимИО"));
			ДанныеЗаявления.Вставить("ДоверительЮЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННЮЛ"));
			ДанныеЗаявления.Вставить("ДоверительЮЛ_КПП", ПолучитьЗначениеДокумента(ЭлементВыборки, "@КПП"));
			
			ПризнакГражданства = ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@ПрГражд");
			Если ПризнакГражданства = 1 Тогда
				ДанныеЗаявления.Вставить("ДоверительФЛ_Гражданство", Справочники.СтраныМира.Россия);
			Иначе
				ДанныеЗаявления.Вставить("ДоверительФЛ_Гражданство", ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@Гражданство", Справочники.СтраныМира.ПустаяСсылка()));
			КонецЕсли;
			ИННФЛ = ПолучитьЗначениеДокумента(ЭлементВыборки, "СвРукОП/@ИННФЛ");
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеЗаявления.ДоверительЮЛ_ИНН);
			РеквизитыПоискаОрганизации.Вставить("КПП", ДанныеЗаявления.ДоверительЮЛ_КПП);
			
		ИначеЕсли ДанныеЗаявления.ТипОрганизации = "ФЛ" Тогда
			ДанныеЗаявления.Вставить("ДоверительФЛ_ИНН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ИННФЛ"));
			ДанныеЗаявления.Вставить("ДоверительФЛ_ОГРН", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ОГРНИП"));
			ДанныеЗаявления.Вставить("ДоверительФЛ_ДатаРождения", ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаРожд", '00010101'));
			ДанныеЗаявления.Вставить("ДатаРожденияДоверителяОтзыва",
				ПолучитьЗначениеДокумента(ЭлементВыборки, "@ДатаРожд", '00010101'));
			
			ДокументИП = Новый Структура;
			СерияНомер = ПодготовитьПредставлениеСерияНомер(ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@СерНомДок"));
			ДокументИП.Вставить("СерДок", СерияНомер.Серия);
			ДокументИП.Вставить("НомДок", СерияНомер.Номер);
			ДокументИП.Вставить("ДатаДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@ДатаДок", '00010101'));
			ДокументИП.Вставить("ВыдДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@ВыдДок"));
			ДокументИП.Вставить("КодВыдДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@КодВыдДок"));
			ДокументИП.Вставить("ВидДок", ПолучитьЗначениеДокумента(ЭлементВыборки, "УдЛичн/@КодВидДок", Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка()));
			ДокументИП.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ);
			ДанныеЗаявления.УдостоверенияЛичности.Добавить(ДокументИП);
			
			РеквизитыПоискаОрганизации.Вставить("ИНН", ДанныеЗаявления.ДоверительФЛ_ИНН);
		КонецЕсли;
		
	КонецЕсли;
	
	СведенияПодписанта = ПолучитьВыборкуЭлементовDOM(ДокументОбработки, "/Файл/Документ/Подписант");
	Если СведенияПодписанта.Количество() = 1 Тогда
		ЭлементВыборки = СведенияПодписанта[0];
		
		НоваяСтрока = Новый Структура;
		
		ДанныеЗаявления.Вставить("ЛицоБезДовФЛ", "");
		ДанныеЗаявления.Вставить("ТелефонПодписантаОтзыва", ПолучитьЗначениеДокумента(ЭлементВыборки, "@Тлф"));
		НоваяСтрока.Вставить("Фамилия", ПолучитьЗначениеДокумента(ЭлементВыборки, "ФИО/@Фамилия"));
		НоваяСтрока.Вставить("Имя", ПолучитьЗначениеДокумента(ЭлементВыборки, "ФИО/@Имя"));
		НоваяСтрока.Вставить("Отчество", ПолучитьЗначениеДокумента(ЭлементВыборки, "ФИО/@Отчество"));
		Если ДанныеЗаявления.ТипОрганизации = "ФЛ" Тогда
			НоваяСтрока.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ);
		Иначе
			НоваяСтрока.Вставить("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительРук);
			Если ДанныеЗаявления.ТипОрганизации = "ЮЛ" И ДанныеЗаявления.Свойство("ЛицоБезДовФЛ")
				И НЕ ЗначениеЗаполнено(ДанныеЗаявления.ЛицоБезДовФЛ) И (ЗначениеЗаполнено(НоваяСтрока.Фамилия)
				ИЛИ ЗначениеЗаполнено(НоваяСтрока.Имя) ИЛИ ЗначениеЗаполнено(НоваяСтрока.Отчество)) Тогда
				ДанныеЗаявления.ЛицоБезДовФЛ = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(НоваяСтрока);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеЗаявления.ФИО.Добавить(НоваяСтрока);
	КонецЕсли;
	
	ДанныеЗаявления.Вставить("Доверитель",
		НайтиЭлементИБ(Справочники.Организации.ПустаяСсылка(), РеквизитыПоискаОрганизации));
	Если НЕ ЗначениеЗаполнено(ДанныеЗаявления.Доверитель) И ДанныеЗаявления.Свойство("ДоверительЮЛ_НаимОрг")
		И ЗначениеЗаполнено(ДанныеЗаявления.ДоверительЮЛ_НаимОрг) Тогда
		ДанныеЗаявления.Доверитель = ДанныеЗаявления.ДоверительЮЛ_НаимОрг;
	КонецЕсли;
	
	ДанныеЗаявления.Вставить("Организация", ?(Подпись = Неопределено
		ИЛИ НЕ ДанныеЗаявления.Свойство("Представитель")
		ИЛИ ТипЗнч(ДанныеЗаявления.Представитель) <> Тип("СправочникСсылка.Организации"),
		ДанныеЗаявления.Доверитель, ДанныеЗаявления.Представитель));
	ДанныеЗаявления.Вставить("Владелец", ?(Подпись = Неопределено
		ИЛИ НЕ ДанныеЗаявления.Свойство("Представитель")
		ИЛИ ТипЗнч(ДанныеЗаявления.Представитель) <> Тип("СправочникСсылка.Организации"),
		ДанныеЗаявления.Доверитель, ДанныеЗаявления.Представитель));
	
	Возврат ДанныеЗаявления;
	
КонецФункции

Функция ЗагрузитьЭлементИзФайлаОбмена(
		ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка = Ложь,
		Подпись = Неопределено,
		ЗаявлениеНаОтзыв = Неопределено,
		ПодписьЗаявленияНаОтзыв = Неопределено,
		ДанныеАрхива = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("Ссылка", Неопределено);
	Результат.Вставить("Ошибка", "");
	Результат.Вставить("ЭтоОтзыв", Ложь);
	
	ДанныеПодготовленны = 0;
	КлючевыеРеквизиты = "";
	ТекстОшибки = "";
	Успешно = Истина;
	
	Попытка
		ДанныеДоверенности = ДанныеИзФайлаОбмена(
			ВходящиеДанные,
			ОбновлятьСуществующийИлиСсылка,
			Подпись,
			ЗаявлениеНаОтзыв,
			ПодписьЗаявленияНаОтзыв,
			ДанныеАрхива);
		Если ДанныеДоверенности.Свойство("ТекстОшибки") И ЗначениеЗаполнено(ДанныеДоверенности.ТекстОшибки) Тогда
			Результат.Ошибка = ДанныеДоверенности.ТекстОшибки;
			Успешно = Ложь;
		КонецЕсли;
	Исключение
		Результат.Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Успешно = Ложь;
	КонецПопытки;
	
	Если Успешно Тогда
		Если ДанныеДоверенности.ТипОрганизации = "ЮЛ" Тогда
			КлючевыеРеквизиты = "ДоверительЮЛ_ИНН, ДоверительЮЛ_КПП";
		ИначеЕсли ДанныеДоверенности.ТипОрганизации = "ФЛ" Тогда
			КлючевыеРеквизиты = "ДоверительФЛ_ИНН, ДоверительФЛ_СНИЛС";
		Иначе
			КлючевыеРеквизиты = "ДоверительЮЛ_ИНН, ДоверительЮЛ_КПП";
		КонецЕсли;
		
		КлючевыеРеквизиты = КлючевыеРеквизиты + ", НомерДоверенности, ДатаВыдачи, КодНалоговогоОрганаПредставления";
		Если НЕ ДанныеДоверенности.Свойство("ЭтоОтзыв") ИЛИ НЕ ДанныеДоверенности.ЭтоОтзыв Тогда
			КлючевыеРеквизиты = КлючевыеРеквизиты + ", ПредставительФЛ_ИНН";
		КонецЕсли;
		
		КлючевыеРеквизиты = Новый Структура(КлючевыеРеквизиты);
		
		Для Каждого СтрокаКлюча Из КлючевыеРеквизиты Цикл
			Если ДанныеДоверенности.Свойство(СтрокаКлюча.Ключ) 
				И ЗначениеЗаполнено(ДанныеДоверенности[СтрокаКлюча.Ключ]) Тогда
				ДанныеПодготовленны = ДанныеПодготовленны + 1;
			ИначеЕсли ПустаяСтрока(ТекстОшибки) Тогда
				ТекстОшибки = НСтр("ru = 'Не заполнены реквизиты справочника'") + ":  " + СтрокаКлюча.Ключ;
			Иначе
				ТекстОшибки = ТекстОшибки + ", " + СтрокаКлюча.Ключ;
			КонецЕсли;
		КонецЦикла;
		
		Если КлючевыеРеквизиты.Количество() = ДанныеПодготовленны Тогда
			ТекущийЭлемент = ЗаписатьЭлементСправочника(ДанныеДоверенности, ОбновлятьСуществующийИлиСсылка);
			Результат.Ссылка = ТекущийЭлемент;
			Если ЗначениеЗаполнено(ТекущийЭлемент) Тогда
				Результат.Выполнено = Истина;
				Если ДанныеДоверенности.Свойство("ЭтоОтзыв") И ДанныеДоверенности.ЭтоОтзыв Тогда
					Результат.ЭтоОтзыв = Истина;
				КонецЕсли;
			Иначе
				Результат.Ошибка = НСтр("ru = 'Не удалось запись элемент справочника. Подробности в журнале регистрации'");
			КонецЕсли;
		Иначе
			Результат.Ошибка = ТекстОшибки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаЭлементаСправочника

Функция ВыгрузитьЭлементВФайлОбмена(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Истина,
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	ПараметрыВозвратаВызова = Новый Структура;
	ПараметрыВозвратаВызова.Вставить("ВозвращатьАдресВоВременномХранилище", Ложь);
	ПараметрыВозвратаВызова.Вставить("ВозвращатьИмяФайлаСРасширением", 		Истина);
	ПараметрыВозвратаВызова.Вставить("ВозвращатьПриложеннуюПодпись", 		Ложь);
	Если ПараметрыВозврата <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыВозвратаВызова, ПараметрыВозврата);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Содержимое", 		"");
	Результат.Вставить("ИмяФайла", 			"");
	Результат.Вставить("НомерДоверенности", "");
	
	Если ПараметрыВозвратаВызова.ВозвращатьПриложеннуюПодпись Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникСсылка,
			"ИмяФайлаВыгрузки, ФайлВырузки, ЭлектроннаяПодпись");
		ДанныеВыгрузки = РеквизитыОбъекта.ФайлВырузки.Получить();
		ДанныеПодписи = РеквизитыОбъекта.ЭлектроннаяПодпись.Получить();
		Если ЗначениеЗаполнено(РеквизитыОбъекта.ИмяФайлаВыгрузки) И ДанныеВыгрузки <> Неопределено
			И ДанныеПодписи <> Неопределено Тогда
			
			Результат = Новый Массив;
			
			АдресВыгрузки = ПоместитьВоВременноеХранилище(ДанныеВыгрузки, Новый УникальныйИдентификатор);
			Результат.Добавить(Новый Структура("Имя, Адрес", РеквизитыОбъекта.ИмяФайлаВыгрузки, АдресВыгрузки));
			
			ИмяФайлаПодписи = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(
				РеквизитыОбъекта.ИмяФайлаВыгрузки).ИмяБезРасширения + "_SGN_"
				+ нрег(Строка(Новый УникальныйИдентификатор())) + ".sgn";
			АдресПодписи = ПоместитьВоВременноеХранилище(ДанныеПодписи, Новый УникальныйИдентификатор);
			Результат.Добавить(Новый Структура("Имя, Адрес", ИмяФайлаПодписи, АдресПодписи));
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверитьВыгрузку Тогда
		РезультатыПроверки = ПроверитьВозможностьВыгрузки(СправочникСсылка);
		
		Если РезультатыПроверки.Количество() <> 0 Тогда
			Для каждого РезультатПроверки Из РезультатыПроверки Цикл
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(РезультатПроверки.ТекстОшибки);
			КонецЦикла;
			
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
				НСтр("ru = 'Не удалось сформировать машиночитаемую доверенность (ФНС, распределенный реестр)'"));
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСправочника = ДанныеИзИнформационнойБазы(СправочникСсылка);
	
	ИдентификаторФайла = ?(ЗначениеЗаполнено(ИдентификаторФайла), ИдентификаторФайла, ДанныеСправочника.Номер);
	Если НЕ ЗначениеЗаполнено(КодНалоговогоОрганаПолучателя) Тогда
		Если ОпределятьКодНалоговогоОрганаПолучателя И ЗначениеЗаполнено(ДанныеСправочника.Организация) Тогда
			УстановитьПривилегированныйРежим(Истина);
			ЭтоКрупнейший    = ДокументооборотСКО.ЭтоКрупнейший(ДанныеСправочника.Организация);
			КодНОКрупнейшего = ДокументооборотСКО.КодНОКрупнейшего(ДанныеСправочника.Организация);
			УстановитьПривилегированныйРежим(Ложь);
			КодНалоговогоОрганаПолучателя = ?(ЭтоКрупнейший,
				СокрЛП(КодНОКрупнейшего), ДанныеСправочника.КодНО);
		Иначе
			КодНалоговогоОрганаПолучателя = ДанныеСправочника.КодНО;
		КонецЕсли;
	КонецЕсли;
	ИННКПП = ?(ЗначениеЗаполнено(ДанныеСправочника.ДоверительЮЛ_ИНН),
		СокрЛП(ДанныеСправочника.ДоверительЮЛ_ИНН) + СокрЛП(ДанныеСправочника.ДоверительЮЛ_КПП),
		СокрЛП(ДанныеСправочника.ДоверительФЛ_ИНН));
	ДатаСеанса = ТекущаяДатаСеанса();
	
	Результат.ИмяФайла = "ON_DOVEL_"
		+ КодНалоговогоОрганаПолучателя
		+ "_" + СокрЛП(ДанныеСправочника.КодНО)
		+ "_" + ИННКПП
		+ "_" + Формат(ДатаСеанса, "ДФ=yyyyMMdd")
		+ "_" + СокрЛП(ИдентификаторФайла);
	
	УниверсальныйФорматФНС = Истина;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИдФайл", Результат.ИмяФайла);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ВерсФорм", "5.01");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "xsi:noNamespaceSchemaLocation", "ON_DOVEL_1_885_00_05_01_01.xsd", Ложь);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance", Ложь);

	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КодНО", ДанныеСправочника.КодНО);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КНД", ДанныеСправочника.КНД);
	
	// фомирование раздела Сведедения о доверенности
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвДов");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НомДовер", ДанныеСправочника.Номер);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаНач", ДанныеСправочника.ДатаВыдачи);
	Если НЕ УниверсальныйФорматФНС И НЕ ФорматДоверенностиПоПриказуФНС Тогда
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НомПредДовер", ДанныеСправочника.ПредыдующийНомер, Ложь);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеСправочника.ДатаОкончания) Тогда
		ЗаписатьУзелXML(ЗаписьXML, "ДатаОкон", ДанныеСправочника.ДатаОкончания);
	Иначе
		ЗаписатьУзелXML(ЗаписьXML, "СрокДейст", ДанныеСправочника.СрокДействия);
	КонецЕсли;
	Если НЕ УниверсальныйФорматФНС И НЕ ФорматДоверенностиПоПриказуФНС Тогда
		ЗаписатьУзелXML(ЗаписьXML, "СведСистОтм", ДанныеСправочника.СведенияОбИнформационнойСистеме);
	КонецЕсли;
	
	Для Каждого СтрокаМассива Из ДанныеСправочника.КодыНО Цикл
		ЗаписатьУзелXML(ЗаписьXML, "КодНОДейст", СтрокаМассива.КодНО, Ложь);
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// фомирование раздела Сведения о доверителе
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвДоверит");
	
	Если ДанныеСправочника.ДоверительЮЛ_ТипОрганизации = "ИО" Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("ИО");
		
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимИО", ДанныеСправочника.ДоверительЮЛ_НаимОрг);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ДоверительЮЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ДоверительЮЛ_КПП);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "СтрРег", ДанныеСправочника.ДоверительЮЛ_СтрРег);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимРегОрг", ДанныеСправочника.ДоверительЮЛ_НаимРегОрг, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "РегНомер", ДанныеСправочника.ДоверительЮЛ_РегНомер);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КодНПРег", ДанныеСправочника.ДоверительЮЛ_КодНПРег, Ложь);
		
		ВыгрузитьАдресУзла(ЗаписьXML, ДанныеСправочника.ДоверительЮЛ_АдрРус, ДанныеСправочника.ДоверительЮЛ_АдрЛат, "АдрСтрРег");
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвРукОП");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ДоверительФЛ_ИНН, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Пол", ДанныеСправочника.ДоверительФЛ_Пол);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаРожд", ДанныеСправочника.ДоверительФЛ_ДатаРождения);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "МестоРожд", ДанныеСправочника.ДоверительФЛ_МестоРожд, Ложь);
		Если ДанныеСправочника.ДоверительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "1");
		ИначеЕсли ЗначениеЗаполнено(ДанныеСправочника.ДоверительФЛ_Гражданство) Тогда
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "2");
		Иначе
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "3");
		КонецЕсли;
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Гражданство", ДанныеСправочника.ДоверительФЛ_Гражданство, Ложь);
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	ИначеЕсли ДанныеСправочника.ДоверительЮЛ_ТипОрганизации = "ЮЛ" Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("НПЮЛ");
		
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимОрг", ДанныеСправочника.ДоверительЮЛ_НаимОрг);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ДоверительЮЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ДоверительЮЛ_КПП);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРН", ДанныеСправочника.ДоверительЮЛ_ОГРН);
		
		ВыгрузитьАдресУзла(ЗаписьXML, ДанныеСправочника.ДоверительЮЛ_АдрРус, "", "АдрРФ");
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ЛицоБезДов");
		
		Если ЗначениеЗаполнено(ДанныеСправочника.ЛицоБезДовЮЛ_ИНН) Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента("СвОрг");
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимОрг", ДанныеСправочника.ЛицоБезДовЮЛ_НаимОрг);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ЛицоБезДовЮЛ_ИНН);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ЛицоБезДовЮЛ_КПП);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРН", ДанныеСправочника.ЛицоБезДовЮЛ_ОГРН, Ложь);
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвФЛ");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ЛицоБезДовФЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "СНИЛС", ДанныеСправочника.ЛицоБезДовФЛ_СНИЛС);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Гражданство", ДанныеСправочника.ЛицоБезДовФЛ_Гражданство, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаРожд", ДанныеСправочника.ЛицоБезДовФЛ_ДатаРождения, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Должность", ДанныеСправочника.ЛицоБезДовФЛ_Должность, Ложь);
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("НПФЛ");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ДоверительФЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРНИП", ДанныеСправочника.ДоверительФЛ_ОГРН, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "СНИЛС", ДанныеСправочника.ДоверительФЛ_СНИЛС);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Гражданство", ДанныеСправочника.ДоверительФЛ_Гражданство, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаРожд", ДанныеСправочника.ДоверительФЛ_ДатаРождения, Ложь);
		
		ВыгрузитьУдостоверениеУзла(ЗаписьXML, 
				ДанныеСправочника.ЛицоБезДовФЛ_ВидДок,
				ДанныеСправочника.ЛицоБезДовФЛ_СерДок,
				ДанныеСправочника.ЛицоБезДовФЛ_НомДок,
				ДанныеСправочника.ЛицоБезДовФЛ_ДатаДок,
				ДанныеСправочника.ЛицоБезДовФЛ_ВыдДок,
				ДанныеСправочника.ЛицоБезДовФЛ_КодВыдДок);
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// фомирование раздела Сведения об уполномоченном представителе
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвУпПред");
	
	Для каждого ПолномочиеПредставителя Из ДанныеСправочника.ПолномочияПредставителя Цикл
		ЗаписатьУзелXML(ЗаписьXML, "ПрОблПолн", ПолномочиеПредставителя);
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвПред");
	
	Если ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН) Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвОрг");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимОрг", ДанныеСправочника.ПредставительЮЛ_НаимОрг);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ПредставительЮЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ПредставительЮЛ_КПП);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРН", ДанныеСправочника.ПредставительЮЛ_ОГРН, Ложь);
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СведФизЛ");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ПредставительФЛ_ИНН);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРНИП", ДанныеСправочника.ПредставительФЛ_ОГРН, Ложь);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "СНИЛС", ДанныеСправочника.ПредставительФЛ_СНИЛС);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Гражданство", ДанныеСправочника.ПредставительФЛ_Гражданство, Ложь);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаРожд", ДанныеСправочника.ПредставительФЛ_ДатаРождения, Ложь);
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	ВыгрузитьФИОУзла(
		ЗаписьXML,
		ДанныеСправочника.ПредставительФЛ_Фамилия,
		ДанныеСправочника.ПредставительФЛ_Имя,
		ДанныеСправочника.ПредставительФЛ_Отчество);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ВыгрузитьУдостоверениеУзла(ЗаписьXML, 
			ДанныеСправочника.ПредставительФЛ_ВидДок,
			ДанныеСправочника.ПредставительФЛ_СерДок,
			ДанныеСправочника.ПредставительФЛ_НомДок,
			ДанныеСправочника.ПредставительФЛ_ДатаДок,
			ДанныеСправочника.ПредставительФЛ_ВыдДок,
			ДанныеСправочника.ПредставительФЛ_КодВыдДок);
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// фомирование раздела Сведения об подписанте доверителя
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ВыгрузитьФИОУзла(
		ЗаписьXML,
		ДанныеСправочника.ЛицоБезДовФЛ_Фамилия,
		ДанныеСправочника.ЛицоБезДовФЛ_Имя,
		ДанныеСправочника.ЛицоБезДовФЛ_Отчество);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Результат.Содержимое = ЗаписьXML.Закрыть();
	
	Если ПараметрыВозвратаВызова.ВозвращатьИмяФайлаСРасширением Тогда
		Результат.ИмяФайла = Результат.ИмяФайла + ".xml";
	КонецЕсли;
	
	Результат.НомерДоверенности = ДанныеСправочника.Номер;
	
	Если ПараметрыВозвратаВызова.ВозвращатьАдресВоВременномХранилище Тогда
		ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(Результат.Содержимое, "windows-1251");
		АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
		Результат = Новый Структура("Имя, Адрес", Результат.ИмяФайла, АдресФайла);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьЭлементВФайлОбменаДляОтмены(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Истина,
		ПроверитьВыгрузку = Ложь,
		ПараметрыОперации = Неопределено) Экспорт
	
	ПараметрыОперацииВызова = Новый Структура;
	ПараметрыОперацииВызова.Вставить("ВозвращатьАдресВоВременномХранилище", Ложь);
	ПараметрыОперацииВызова.Вставить("ВозвращатьИмяФайлаСРасширением", 		Истина);
	ПараметрыОперацииВызова.Вставить("ПричинаОтмены", 						"");
	ПараметрыОперацииВызова.Вставить("УникальныйИдентификаторДоверенности", "");
	ПараметрыОперацииВызова.Вставить("ДатаОтмены", 							Неопределено);
	ПараметрыОперацииВызова.Вставить("ДолжностьРуководителяДоверителя", 	"");
	ПараметрыОперацииВызова.Вставить("ДатаРожденияДоверителя", 				Неопределено);
	ПараметрыОперацииВызова.Вставить("ТелефонПодписанта", 					"");
	Если ПараметрыОперации <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОперацииВызова, ПараметрыОперации);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Содержимое", 	"");
	Результат.Вставить("ИмяФайла", 		"");
	
	Если ПроверитьВыгрузку Тогда
		РезультатыПроверки = ПроверитьВозможностьВыгрузкиДляОтмены(СправочникСсылка, ПараметрыОперацииВызова);
		
		Если РезультатыПроверки.Количество() <> 0 Тогда
			Для каждого РезультатПроверки Из РезультатыПроверки Цикл
				ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(РезультатПроверки.ТекстОшибки);
			КонецЦикла;
			
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
				НСтр("ru = 'Не удалось сформировать заявление на отзыв машиночитаемой доверенности ФНС из распределенного реестра'"));
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСправочника = ДанныеИзИнформационнойБазы(СправочникСсылка);
	
	Если НЕ ФорматДоверенностиПоПриказуФНС Тогда
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку("windows-1251");
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
		ЗаписьXML.ЗаписатьАтрибут("ТД", XMLСтрока("Заявление на отзыв МЧД"));
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвДов");
		ЗаписьXML.ЗаписатьАтрибут("НомДовер", XMLСтрока(ДанныеСправочника.Номер));
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвПрич");
		ЗаписьXML.ЗаписатьАтрибут("Отз", XMLСтрока(ПараметрыОперацииВызова.ПричинаОтмены));
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		Результат.ИмяФайла = "revoke";
		Результат.Содержимое = ЗаписьXML.Закрыть();
		
		Если ПараметрыОперацииВызова.ВозвращатьИмяФайлаСРасширением Тогда
			Результат.ИмяФайла = Результат.ИмяФайла + ".xml";
		КонецЕсли;
		
		Если ПараметрыОперацииВызова.ВозвращатьАдресВоВременномХранилище Тогда
			ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(Результат.Содержимое, "windows-1251");
			АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
			Результат = Новый Структура("Имя, Адрес", Результат.ИмяФайла, АдресФайла);
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	ИдентификаторФайла = ?(ЗначениеЗаполнено(ИдентификаторФайла),
		ИдентификаторФайла, ДанныеСправочника.Номер);
	Если НЕ ЗначениеЗаполнено(КодНалоговогоОрганаПолучателя) Тогда
		Если ОпределятьКодНалоговогоОрганаПолучателя И ЗначениеЗаполнено(ДанныеСправочника.Организация) Тогда
			УстановитьПривилегированныйРежим(Истина);
			ЭтоКрупнейший    = ДокументооборотСКО.ЭтоКрупнейший(ДанныеСправочника.Организация);
			КодНОКрупнейшего = ДокументооборотСКО.КодНОКрупнейшего(ДанныеСправочника.Организация);
			УстановитьПривилегированныйРежим(Ложь);
			КодНалоговогоОрганаПолучателя = ?(ЭтоКрупнейший,
				СокрЛП(КодНОКрупнейшего), ДанныеСправочника.КодНО);
		Иначе
			КодНалоговогоОрганаПолучателя = ДанныеСправочника.КодНО;
		КонецЕсли;
	КонецЕсли;
	ИННКПП = ?(ЗначениеЗаполнено(ДанныеСправочника.ДоверительЮЛ_ИНН),
		СокрЛП(ДанныеСправочника.ДоверительЮЛ_ИНН) + СокрЛП(ДанныеСправочника.ДоверительЮЛ_КПП),
		СокрЛП(ДанныеСправочника.ДоверительФЛ_ИНН));
	ДатаСеанса = ТекущаяДатаСеанса();
	
	Результат.ИмяФайла = "ON_OFFDOVER_"
		+ КодНалоговогоОрганаПолучателя
		+ "_" + СокрЛП(ДанныеСправочника.КодНО)
		+ "_" + ИННКПП
		+ "_" + Формат(ДатаСеанса, "ДФ=yyyyMMdd")
		+ "_" + СокрЛП(ИдентификаторФайла);
	
	УниверсальныйФорматФНС = Истина;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Файл");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИдФайл", Результат.ИмяФайла);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ВерсФорм", "5.01");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "xsi:noNamespaceSchemaLocation", "ON_OFFDOVER_1_884_00_05_01_01.xsd", Ложь);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance", Ложь);

	ЗаписьXML.ЗаписатьНачалоЭлемента("Документ");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КодНО", ДанныеСправочника.КодНО);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КНД", "1110311");
	
	// фомирование раздела Сведедения о доверенности
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвЗаяв");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НомДовер", ДанныеСправочника.Номер);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИдДовер", ПараметрыОперацииВызова.УникальныйИдентификаторДоверенности);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаНач", ДанныеСправочника.ДатаВыдачи);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаОтз", ПараметрыОперацииВызова.ДатаОтмены);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПричОтз", ПараметрыОперацииВызова.ПричинаОтмены, Ложь);
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// фомирование раздела Сведения о доверителе
	ЗаписьXML.ЗаписатьНачалоЭлемента("СвДоверит");
	
	Если ДанныеСправочника.ДоверительЮЛ_ТипОрганизации = "ИО" Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("ИО");
		
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимИО", ДанныеСправочника.ДоверительЮЛ_НаимОрг);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ДоверительЮЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ДоверительЮЛ_КПП);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвРукОП");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ДоверительФЛ_ИНН, Ложь);
		Если ДанныеСправочника.ДоверительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "1");
		ИначеЕсли ЗначениеЗаполнено(ДанныеСправочника.ДоверительФЛ_Гражданство) Тогда
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "2");
		Иначе
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ПрГражд", "3");
		КонецЕсли;
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	ИначеЕсли ДанныеСправочника.ДоверительЮЛ_ТипОрганизации = "ЮЛ" Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("НПЮЛ");
		
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимОрг", ДанныеСправочника.ДоверительЮЛ_НаимОрг);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ДоверительЮЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ДоверительЮЛ_КПП);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРН", ДанныеСправочника.ДоверительЮЛ_ОГРН);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("СвРукОрг");
		
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Должность", ПараметрыОперацииВызова.ДолжностьРуководителяДоверителя);
		
		Если ЗначениеЗаполнено(ДанныеСправочника.ЛицоБезДовЮЛ_ИНН) Тогда
			ЗаписьXML.ЗаписатьНачалоЭлемента("СвОрг");
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "НаимОрг", ДанныеСправочника.ЛицоБезДовЮЛ_НаимОрг);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННЮЛ", ДанныеСправочника.ЛицоБезДовЮЛ_ИНН);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КПП", ДанныеСправочника.ЛицоБезДовЮЛ_КПП);
			ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРН", ДанныеСправочника.ЛицоБезДовЮЛ_ОГРН, Ложь);
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	Иначе
		ЗаписьXML.ЗаписатьНачалоЭлемента("НПФЛ");
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ИННФЛ", ДанныеСправочника.ДоверительФЛ_ИНН);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ОГРНИП", ДанныеСправочника.ДоверительФЛ_ОГРН, Ложь);
		ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаРожд", ПараметрыОперацииВызова.ДатаРожденияДоверителя);
		
		ВыгрузитьУдостоверениеУзла(ЗаписьXML,
				ДанныеСправочника.ЛицоБезДовФЛ_ВидДок,
				ДанныеСправочника.ЛицоБезДовФЛ_СерДок,
				ДанныеСправочника.ЛицоБезДовФЛ_НомДок,
				ДанныеСправочника.ЛицоБезДовФЛ_ДатаДок,
				ДанныеСправочника.ЛицоБезДовФЛ_ВыдДок,
				ДанныеСправочника.ЛицоБезДовФЛ_КодВыдДок);
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	// фомирование раздела Сведения об подписанте доверителя
	ЗаписьXML.ЗаписатьНачалоЭлемента("Подписант");
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Тлф", ПараметрыОперацииВызова.ТелефонПодписанта);
	ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
	ВыгрузитьФИОУзла(ЗаписьXML,
		ДанныеСправочника.ЛицоБезДовФЛ_Фамилия,
		ДанныеСправочника.ЛицоБезДовФЛ_Имя,
		ДанныеСправочника.ЛицоБезДовФЛ_Отчество);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Результат.Содержимое = ЗаписьXML.Закрыть();
	
	Если ПараметрыОперацииВызова.ВозвращатьИмяФайлаСРасширением Тогда
		Результат.ИмяФайла = Результат.ИмяФайла + ".xml";
	КонецЕсли;
	
	Если ПараметрыОперацииВызова.ВозвращатьАдресВоВременномХранилище Тогда
		ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(Результат.Содержимое, "windows-1251");
		АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
		Результат = Новый Структура("Имя, Адрес", Результат.ИмяФайла, АдресФайла);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка) Экспорт
	
	Результат = Новый Массив;
	
	Если ТипЗнч(ОбъектИлиСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		Доверенность = ОбъектИлиСсылка.ПолучитьОбъект();
	Иначе
		Доверенность = ОбъектИлиСсылка;
	КонецЕсли;
	
	// основные
	
	Если НЕ ЗначениеЗаполнено(Доверенность.НомерДоверенности) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан номер доверенности'"), "НомерДоверенности"));
	КонецЕсли;
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ГодДатыСеанса = Год(ДатаСеанса);
	МинимальнаяДата = Дата(1900, 1, 1);
	ГодМаксимальнойДаты = Цел((ГодДатыСеанса + 50) / 100) * 100 + 99;
	МаксимальнаяДата = Дата(ГодМаксимальнойДаты, 1, 1);
	МаксимальнаяДата = КонецГода(МаксимальнаяДата);
	
	Если НЕ ЗначениеЗаполнено(Доверенность.ДатаВыдачи) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задана дата выдачи'"), "ДатаВыдачи"));
	ИначеЕсли Доверенность.ДатаВыдачи < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ДатаВыдачи"));
	ИначеЕсли Доверенность.ДатаВыдачи > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ДатаВыдачи"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Доверенность.ДатаОкончания) И НЕ ЗначениеЗаполнено(Доверенность.СрокДействия) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не заданы дата окончания или срок действия'"), "ДатаОкончания"));
	ИначеЕсли ЗначениеЗаполнено(Доверенность.ДатаОкончания) И Доверенность.ДатаОкончания < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата окончания действия не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ДатаОкончания"));
	ИначеЕсли ЗначениеЗаполнено(Доверенность.ДатаОкончания) И Доверенность.ДатаОкончания > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата окончания действия не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ДатаОкончания"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Доверенность.ДатаВыдачи) И ЗначениеЗаполнено(Доверенность.ДатаОкончания)
		И Доверенность.ДатаОкончания < Доверенность.ДатаВыдачи Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Дата выдачи доверенности не может быть больше даты окончания действия'"), "ДатаОкончания"));
	КонецЕсли;
	
	// доверитель
	
	Доверитель_ЮридическоеЛицо = ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ИНН);
	
	Если Доверитель_ЮридическоеЛицо Тогда
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_НаимОрг) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано наименование организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ИНН) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан ИНН организации доверителя'"), "Доверитель"));
		ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
			Доверенность.ДоверительЮЛ_ИНН, Ложь) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ИНН организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_КПП) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан КПП организации доверителя'"), "Доверитель"));
		ИначеЕсли СтрДлина(СокрЛП(Доверенность.ДоверительЮЛ_КПП)) <> 9 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный КПП организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если Доверенность.ДоверительЮЛ_ИностраннаяОрганизация Тогда
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_АдрРус)
				И НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_АдрЛат) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан адрес организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_СтрРег) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана страна регистрации организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_РегНомер) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан регистрационный номер организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана фамилия руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задано имя руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ИНН)
				И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
				Доверенность.ДоверительФЛ_ИНН, Истина) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный ИНН руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительФЛ_Пол) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан пол руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ДатаРождения) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана дата рождения руководителя обособленного подразделения доверителя'"), "Доверитель"));
			ИначеЕсли Доверенность.ДоверительФЛ_ДатаРождения < МинимальнаяДата Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					СтрШаблон(
						НСтр("ru = 'Дата рождения руководителя обособленного подразделения доверителя не может быль раньше %1'"),
						Формат(МинимальнаяДата, "ДЛФ=Д")),
					"Доверитель"));
			ИначеЕсли Доверенность.ДоверительФЛ_ДатаРождения > МаксимальнаяДата Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					СтрШаблон(
						НСтр("ru = 'Дата рождения руководителя обособленного подразделения доверителя не может быль позднее %1'"),
						Формат(МаксимальнаяДата, "ДЛФ=Д")),
					"Доверитель"));
			КонецЕсли;
			
		Иначе
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_АдрРус) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан адрес организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ОГРН) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан ОГРН организации доверителя'"), "Доверитель"));
			ИначеЕсли СтрДлина(СокрЛП(Доверенность.ДоверительЮЛ_ОГРН)) <> 13 Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный ОГРН организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			ЛицоБезДов_ЮридическоеЛицо = ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ИНН);
			
			Если ЛицоБезДов_ЮридическоеЛицо Тогда
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_НаимОрг) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задано наименование управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ИНН) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задан ИНН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
					Доверенность.ЛицоБезДовЮЛ_ИНН, Ложь) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный ИНН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_КПП) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задан КПП управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				ИначеЕсли СтрДлина(СокрЛП(Доверенность.ЛицоБезДовЮЛ_КПП)) <> 9 Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный КПП управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ОГРН)
					И СтрДлина(СокрЛП(Доверенность.ЛицоБезДовЮЛ_ОГРН)) <> 13 Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный ОГРН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
			КонецЕсли;
			
			СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана фамилия физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задано имя физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовФЛ_ИНН) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан ИНН физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
				Доверенность.ЛицоБезДовФЛ_ИНН, Истина) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный ИНН физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовФЛ_СНИЛС) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан СНИЛС физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			ИначеЕсли СтрДлина(СокрЛП(Доверенность.ЛицоБезДовФЛ_СНИЛС)) <> 14 Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный СНИЛС физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
		
		Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задана фамилия доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано имя доверителя'"), "Доверитель"));
		КонецЕсли;
		
		СтрокиУдостоверений = Доверенность.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВидДок) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан вид документа доверителя'"), "Доверитель"));
		КонецЕсли;
		
		КодВидаДокумента = ДокументооборотСКОВызовСервера.ПолучитьКодВидаДокументаФизическогоЛица(
			?(СтрокиУдостоверений.Количество() = 0, "", СтрокиУдостоверений[0].ВидДок));
		Если ЗначениеЗаполнено(КодВидаДокумента) И КодВидаДокумента <> "07" И КодВидаДокумента <> "10"
			И КодВидаДокумента <> "11" И КодВидаДокумента <> "12" И КодВидаДокумента <> "13" И КодВидаДокумента <> "15"
			И КодВидаДокумента <> "19" И КодВидаДокумента <> "21" И КодВидаДокумента <> "24" Тогда
			
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Вид документа доверителя не поддерживается'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтрокиУдостоверений[0].СерДок)
			+ СокрЛП(СтрокиУдостоверений[0].НомДок)) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не заданы серия и номер документа доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() <> 0 И СтрДлина(СокрЛП(СтрокиУдостоверений[0].СерДок)
			+ СокрЛП(СтрокиУдостоверений[0].НомДок)) > 25 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Длина серии и номера документа доверителя больше 25 символов'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ДатаДок) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задана дата выдачи документа доверителя'"), "Доверитель"));
		ИначеЕсли СтрокиУдостоверений[0].ДатаДок < МинимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата выдачи документа доверителя не может быль раньше %1'"),
					Формат(МинимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		ИначеЕсли СтрокиУдостоверений[0].ДатаДок > МаксимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата выдачи документа доверителя не может быль позднее %1'"),
					Формат(МаксимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		КонецЕсли;
		
		Если (СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВыдДок))
			И КодВидаДокумента = "21" Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано наименование органа, выдавшего документ доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если (СтрокиУдостоверений.Количество() <> 0 И ЗначениеЗаполнено(СтрокиУдостоверений[0].КодВыдДок))
			И СтрДлина(СокрЛП(СтрокиУдостоверений[0].КодВыдДок)) <> 7 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Длина кода подразделения документа доверителя не 7 символов'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ИНН) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан ИНН доверителя'"), "Доверитель"));
		ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
			Доверенность.ДоверительФЛ_ИНН, Истина) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ИНН доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ОГРН) И СтрДлина(СокрЛП(Доверенность.ДоверительФЛ_ОГРН)) <> 15 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ОГРНИП доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительФЛ_СНИЛС) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан СНИЛС доверителя'"), "Доверитель"));
		ИначеЕсли СтрДлина(СокрЛП(Доверенность.ДоверительФЛ_СНИЛС)) <> 14 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный СНИЛС доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ДатаРождения)
			И Доверенность.ДоверительФЛ_ДатаРождения < МинимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата рождения доверителя не может быль раньше %1'"),
					Формат(МинимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		ИначеЕсли ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ДатаРождения)
			И Доверенность.ДоверительФЛ_ДатаРождения > МаксимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата рождения доверителя не может быль позднее %1'"),
					Формат(МаксимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		КонецЕсли;
	КонецЕсли;
	
	// представитель
	
	ТребоватьОГРНДляПредставителяФЛ = Ложь;
	Представитель_ЮридическоеЛицо = ЗначениеЗаполнено(Доверенность.ПредставительЮЛ_ИНН);
	
	Если Представитель_ЮридическоеЛицо Тогда
		Если НЕ ЗначениеЗаполнено(Доверенность.ПредставительЮЛ_НаимОрг) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано наименование организации представителя'"), "Представитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ПредставительЮЛ_ИНН) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан ИНН организации представителя'"), "Представитель"));
		ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
			Доверенность.ПредставительЮЛ_ИНН, Ложь) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ИНН организации представителя'"), "Представитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ПредставительЮЛ_КПП) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан КПП организации представителя'"), "Представитель"));
		ИначеЕсли СтрДлина(СокрЛП(Доверенность.ПредставительЮЛ_КПП)) <> 9 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный КПП организации представителя'"), "Представитель"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Доверенность.ПредставительЮЛ_ОГРН) И СтрДлина(СокрЛП(Доверенность.ПредставительЮЛ_ОГРН)) <> 13 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ОГРН организации представителя'"), "Представитель"));
		КонецЕсли;
	КонецЕсли;
	
	СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задана фамилия представителя'"), "ПредставительФЛ_ФИО"));
	КонецЕсли;
	
	Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задано имя физлица-представителя'"), "ПредставительФЛ_ФИО"));
	КонецЕсли;
	
	СтрокиУдостоверений = Доверенность.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
		ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ")));
	
	Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВидДок) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан вид документа физлица-представителя'"), "Доверитель"));
	КонецЕсли;
	
	КодВидаДокумента = ДокументооборотСКОВызовСервера.ПолучитьКодВидаДокументаФизическогоЛица(
		?(СтрокиУдостоверений.Количество() = 0, "", СтрокиУдостоверений[0].ВидДок));
	Если ЗначениеЗаполнено(КодВидаДокумента) И КодВидаДокумента <> "07" И КодВидаДокумента <> "10"
		И КодВидаДокумента <> "11" И КодВидаДокумента <> "12" И КодВидаДокумента <> "13" И КодВидаДокумента <> "15"
		И КодВидаДокумента <> "19" И КодВидаДокумента <> "21" И КодВидаДокумента <> "24" Тогда
		
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Вид документа физлица-представителя не поддерживается'"), "ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтрокиУдостоверений[0].СерДок)
		+ СокрЛП(СтрокиУдостоверений[0].НомДок)) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не заданы серия и номер документа физлица-представителя'"), "ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если СтрокиУдостоверений.Количество() <> 0 И СтрДлина(СокрЛП(СтрокиУдостоверений[0].СерДок)
		+ СокрЛП(СтрокиУдостоверений[0].НомДок)) > 25 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Длина серии и номера документа физлица-представителя больше 25 символов'"), "ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ДатаДок) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задана дата выдачи документа физлица-представителя'"), "ПредставительФЛ_Удостоверение"));
	ИначеЕсли СтрокиУдостоверений[0].ДатаДок < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи документа физлица-представителя не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ПредставительФЛ_Удостоверение"));
	ИначеЕсли СтрокиУдостоверений[0].ДатаДок > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи документа физлица-представителя не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если (СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВыдДок))
		И КодВидаДокумента = "21" Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задано наименование органа, выдавшего документ физлица-представителя'"),
			"ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если (СтрокиУдостоверений.Количество() <> 0 И ЗначениеЗаполнено(СтрокиУдостоверений[0].КодВыдДок))
		И СтрДлина(СокрЛП(СтрокиУдостоверений[0].КодВыдДок)) <> 7 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Длина кода подразделения документа физлица-представителя не 7 символов'"),
			"ПредставительФЛ_Удостоверение"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Доверенность.ПредставительФЛ_ИНН) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан ИНН физлица-представителя'"), "ПредставительФЛ_ИНН"));
	ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
		Доверенность.ПредставительФЛ_ИНН, Истина) Тогда
		
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный ИНН физлица-представителя'"), "ПредставительФЛ_ИНН"));
	КонецЕсли;
	
	Если ТребоватьОГРНДляПредставителяФЛ И НЕ Представитель_ЮридическоеЛицо
		И НЕ ЗначениеЗаполнено(Доверенность.ПредставительФЛ_ОГРН) Тогда
		
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан ОГРНИП физлица-представителя'"), "ПредставительФЛ_ОГРН"));
		
	ИначеЕсли ЗначениеЗаполнено(Доверенность.ПредставительФЛ_ОГРН)
		И СтрДлина(СокрЛП(Доверенность.ПредставительФЛ_ОГРН)) <> 15 Тогда
		
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный ОГРНИП физлица-представителя'"), "ПредставительФЛ_ОГРН"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Доверенность.ПредставительФЛ_СНИЛС) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан СНИЛС физлица-представителя'"), "ПредставительФЛ_СНИЛС"));
	ИначеЕсли СтрДлина(СокрЛП(Доверенность.ПредставительФЛ_СНИЛС)) <> 14 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный СНИЛС физлица-представителя'"), "ПредставительФЛ_СНИЛС"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Доверенность.ПредставительФЛ_ДатаРождения)
		И Доверенность.ПредставительФЛ_ДатаРождения < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата рождения физлица-представителя не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ПредставительФЛ_ДатаРождения"));
	ИначеЕсли ЗначениеЗаполнено(Доверенность.ПредставительФЛ_ДатаРождения)
		И Доверенность.ПредставительФЛ_ДатаРождения > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата рождения физлица-представителя не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ПредставительФЛ_ДатаРождения"));
	КонецЕсли;
	
	// налоговые органы
	
	Если НЕ ЗначениеЗаполнено(Доверенность.КодНалоговогоОрганаПредставления) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан код налогового органа - получателя'"), "КодНалоговогоОрганаПредставления"));
	ИначеЕсли СтрДлина(СокрЛП(Доверенность.КодНалоговогоОрганаПредставления)) <> 4 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный код налогового органа - получателя'"), "КодНалоговогоОрганаПредставления"));
	КонецЕсли;
	
	Для каждого НалоговыйОрганДействия Из Доверенность.НалоговыеОрганыДействия Цикл
		Если СтрДлина(СокрЛП(НалоговыйОрганДействия.КодНалоговогоОргана)) <> 4 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Указан некорректный код налогового органа, для которого действует доверенность, ""%1""'"),
					НалоговыйОрганДействия.КодНалоговогоОргана),
				"КодНалоговогоОрганаПредставления"));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьВозможностьВыгрузкиДляОтмены(ОбъектИлиСсылка, ПараметрыОперации = Неопределено) Экспорт
	
	ПараметрыОперацииВызова = Новый Структура;
	ПараметрыОперацииВызова.Вставить("ПричинаОтмены", 							"");
	ПараметрыОперацииВызова.Вставить("ДатаОтмены", 								Неопределено);
	ПараметрыОперацииВызова.Вставить("УникальныйИдентификаторДоверенности", 	"");
	ПараметрыОперацииВызова.Вставить("ДолжностьРуководителяДоверителя", 		"");
	ПараметрыОперацииВызова.Вставить("ДатаРожденияДоверителя", 					Неопределено);
	ПараметрыОперацииВызова.Вставить("ТелефонПодписанта", 						"");
	Если ПараметрыОперации <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОперацииВызова, ПараметрыОперации);
	КонецЕсли;
	
	Результат = Новый Массив;
	
	Если ТипЗнч(ОбъектИлиСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		Доверенность = ОбъектИлиСсылка.ПолучитьОбъект();
	Иначе
		Доверенность = ОбъектИлиСсылка;
	КонецЕсли;
	
	// основные
	
	Если НЕ ЗначениеЗаполнено(Доверенность.НомерДоверенности) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан номер доверенности'"), "НомерДоверенности"));
	КонецЕсли;
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ГодДатыСеанса = Год(ДатаСеанса);
	МинимальнаяДата = Дата(1900, 1, 1);
	ГодМаксимальнойДаты = Цел((ГодДатыСеанса + 50) / 100) * 100 + 99;
	МаксимальнаяДата = Дата(ГодМаксимальнойДаты, 1, 1);
	МаксимальнаяДата = КонецГода(МаксимальнаяДата);
	
	Если НЕ ЗначениеЗаполнено(Доверенность.ДатаВыдачи) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задана дата выдачи'"), "ДатаВыдачи"));
	ИначеЕсли Доверенность.ДатаВыдачи < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ДатаВыдачи"));
	ИначеЕсли Доверенность.ДатаВыдачи > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата выдачи не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ДатаВыдачи"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОперацииВызова.ДатаОтмены) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задана дата отзыва'"), "ДатаОтмены"));
	ИначеЕсли ПараметрыОперацииВызова.ДатаОтмены < МинимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата отзыва не может быль раньше %1'"),
				Формат(МинимальнаяДата, "ДЛФ=Д")),
			"ДатаОтмены"));
	ИначеЕсли ПараметрыОперацииВызова.ДатаОтмены > МаксимальнаяДата Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			СтрШаблон(
				НСтр("ru = 'Дата отзыва не может быль позднее %1'"),
				Формат(МаксимальнаяДата, "ДЛФ=Д")),
			"ДатаОтмены"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Доверенность.ДатаВыдачи) И ЗначениеЗаполнено(ПараметрыОперацииВызова.ДатаОтмены)
		И ПараметрыОперацииВызова.ДатаОтмены < Доверенность.ДатаВыдачи Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Дата выдачи доверенности не может быть больше даты отзыва'"), "ДатаОтмены"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОперацииВызова.УникальныйИдентификаторДоверенности) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан уникальный идентификатор доверенности'"), "УникальныйИдентификаторДоверенности"));
	ИначеЕсли СтрДлина(СокрЛП(ПараметрыОперацииВызова.УникальныйИдентификаторДоверенности)) <> 36 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный уникальный идентификатор доверенности'"),
			"УникальныйИдентификаторДоверенности"));
	КонецЕсли;
	
	// доверитель
	
	Доверитель_ЮридическоеЛицо = ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ИНН);
	
	Если Доверитель_ЮридическоеЛицо Тогда
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_НаимОрг) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано наименование организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ИНН) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан ИНН организации доверителя'"), "Доверитель"));
		ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
			Доверенность.ДоверительЮЛ_ИНН, Ложь) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ИНН организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_КПП) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан КПП организации доверителя'"), "Доверитель"));
		ИначеЕсли СтрДлина(СокрЛП(Доверенность.ДоверительЮЛ_КПП)) <> 9 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный КПП организации доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если Доверенность.ДоверительЮЛ_ИностраннаяОрганизация Тогда
			СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана фамилия руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задано имя руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ИНН)
				И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
				Доверенность.ДоверительФЛ_ИНН, Истина) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный ИНН руководителя обособленного подразделения доверителя'"), "Доверитель"));
			КонецЕсли;
			
		Иначе
			Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительЮЛ_ОГРН) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задан ОГРН организации доверителя'"), "Доверитель"));
			ИначеЕсли СтрДлина(СокрЛП(Доверенность.ДоверительЮЛ_ОГРН)) <> 13 Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Указан некорректный ОГРН организации доверителя'"), "Доверитель"));
			КонецЕсли;
			
			ЛицоБезДов_ЮридическоеЛицо = ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ИНН);
			
			Если ЛицоБезДов_ЮридическоеЛицо Тогда
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_НаимОрг) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задано наименование управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ИНН) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задан ИНН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
					Доверенность.ЛицоБезДовЮЛ_ИНН, Ложь) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный ИНН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_КПП) Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Не задан КПП управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				ИначеЕсли СтрДлина(СокрЛП(Доверенность.ЛицоБезДовЮЛ_КПП)) <> 9 Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный КПП управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Доверенность.ЛицоБезДовЮЛ_ОГРН)
					И СтрДлина(СокрЛП(Доверенность.ЛицоБезДовЮЛ_ОГРН)) <> 13 Тогда
					Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
						НСтр("ru = 'Указан некорректный ОГРН управляющей организации, действующей без доверенности'"), "ЛицоБезДовЮЛ"));
				КонецЕсли;
			КонецЕсли;
			
			СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
				ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительРук")));
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана фамилия физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
			
			Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задано имя физического лица со стороны доверителя'"), "ЛицоБезДовФЛ"));
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ПараметрыОперацииВызова.ДолжностьРуководителяДоверителя) Тогда
				Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
					НСтр("ru = 'Не задана должность руководителя организации - доверителя'"), "ДолжностьРуководителяДоверителя"));
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		СтрокиФИО = Доверенность.ФИО.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
		
		Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Фамилия) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задана фамилия доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиФИО.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиФИО[0].Имя) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано имя доверителя'"), "Доверитель"));
		КонецЕсли;
		
		СтрокиУдостоверений = Доверенность.УдостоверенияЛичности.НайтиСтроки(Новый Структура("Владелец",
			ПредопределенноеЗначение("Перечисление.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ")));
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВидДок) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан вид документа доверителя'"), "Доверитель"));
		КонецЕсли;
		
		КодВидаДокумента = ДокументооборотСКОВызовСервера.ПолучитьКодВидаДокументаФизическогоЛица(
			?(СтрокиУдостоверений.Количество() = 0, "", СтрокиУдостоверений[0].ВидДок));
		Если ЗначениеЗаполнено(КодВидаДокумента) И КодВидаДокумента <> "07" И КодВидаДокумента <> "10"
			И КодВидаДокумента <> "11" И КодВидаДокумента <> "12" И КодВидаДокумента <> "13" И КодВидаДокумента <> "15"
			И КодВидаДокумента <> "19" И КодВидаДокумента <> "21" И КодВидаДокумента <> "24" Тогда
			
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Вид документа доверителя не поддерживается'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СокрЛП(СтрокиУдостоверений[0].СерДок)
			+ СокрЛП(СтрокиУдостоверений[0].НомДок)) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не заданы серия и номер документа доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() <> 0 И СтрДлина(СокрЛП(СтрокиУдостоверений[0].СерДок)
			+ СокрЛП(СтрокиУдостоверений[0].НомДок)) > 25 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Длина серии и номера документа доверителя больше 25 символов'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ДатаДок) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задана дата выдачи документа доверителя'"), "Доверитель"));
		ИначеЕсли СтрокиУдостоверений[0].ДатаДок < МинимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата выдачи документа доверителя не может быль раньше %1'"),
					Формат(МинимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		ИначеЕсли СтрокиУдостоверений[0].ДатаДок > МаксимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата выдачи документа доверителя не может быль позднее %1'"),
					Формат(МаксимальнаяДата, "ДЛФ=Д")),
				"Доверитель"));
		КонецЕсли;
		
		Если (СтрокиУдостоверений.Количество() = 0 ИЛИ НЕ ЗначениеЗаполнено(СтрокиУдостоверений[0].ВыдДок))
			И КодВидаДокумента = "21" Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задано наименование органа, выдавшего документ доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если СтрокиУдостоверений.Количество() <> 0 И ЗначениеЗаполнено(СтрокиУдостоверений[0].КодВыдДок)
			И СтрДлина(СокрЛП(СтрокиУдостоверений[0].КодВыдДок)) <> 7 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Длина кода подразделения документа доверителя не 7 символов'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ИНН) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задан ИНН доверителя'"), "Доверитель"));
		ИначеЕсли НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(
			Доверенность.ДоверительФЛ_ИНН, Истина) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ИНН доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Доверенность.ДоверительФЛ_ОГРН) И СтрДлина(СокрЛП(Доверенность.ДоверительФЛ_ОГРН)) <> 15 Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Указан некорректный ОГРНИП доверителя'"), "Доверитель"));
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыОперацииВызова.ДатаРожденияДоверителя) Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				НСтр("ru = 'Не задана дата рождения доверителя'"), "ДатаРожденияДоверителя"));
		ИначеЕсли ПараметрыОперацииВызова.ДатаРожденияДоверителя < МинимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата рождения доверителя не может быль раньше %1'"),
					Формат(МинимальнаяДата, "ДЛФ=Д")),
				"ДатаРожденияДоверителя"));
		ИначеЕсли ПараметрыОперацииВызова.ДатаРожденияДоверителя > МаксимальнаяДата Тогда
			Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
				СтрШаблон(
					НСтр("ru = 'Дата рождения доверителя не может быль позднее %1'"),
					Формат(МаксимальнаяДата, "ДЛФ=Д")),
				"ДатаРожденияДоверителя"));
		КонецЕсли;
	КонецЕсли;
	
	// налоговые органы
	
	Если НЕ ЗначениеЗаполнено(Доверенность.КодНалоговогоОрганаПредставления) Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Не задан код налогового органа - получателя'"), "КодНалоговогоОрганаПредставления"));
	ИначеЕсли СтрДлина(СокрЛП(Доверенность.КодНалоговогоОрганаПредставления)) <> 4 Тогда
		Результат.Добавить(Новый Структура("ТекстОшибки, Поле",
			НСтр("ru = 'Указан некорректный код налогового органа - получателя'"), "КодНалоговогоОрганаПредставления"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КоличествоВидовПолномочийПредставителя() Экспорт
	
	Возврат 39;
	
КонецФункции

Функция ПолучитьСведенияДоверенности(СправочникСсылка, ОпределитьСтатусДоверенности = Ложь) Экспорт
	
	ДанныеСправочника = ДанныеИзИнформационнойБазы(СправочникСсылка);
	
	// структура с результатом
	Сведения = Новый Структура;
	
	// добавляем код НО
	Сведения.Вставить("КодНО", СокрЛП(ДанныеСправочника.КодНО));
	Сведения.Вставить("КодыНО", ДанныеСправочника.КодыНО);
	
	// формируем структуру с учетными сведениями о самой доверенности
	УчетныеСведенияОДоверенности = Новый Структура;
	УчетныеСведенияОДоверенности.Вставить("Номер",
		?(ЗначениеЗаполнено(ДанныеСправочника.Номер), СокрЛП(ДанныеСправочника.Номер), "БН"));
	УчетныеСведенияОДоверенности.Вставить("Дата", ДанныеСправочника.ДатаВыдачи);
	УчетныеСведенияОДоверенности.Вставить("ДатаОкончания", ДанныеСправочника.ДатаОкончания);
	Сведения.Вставить("УчСвДов", УчетныеСведенияОДоверенности);
	
	// формируем сведения о представляемом лице
	СведенияОПредставляемомЛице = Новый Структура;
	СведенияОПредставляемомЛице.Вставить("ЯвляетсяФЛ", НЕ ЗначениеЗаполнено(ДанныеСправочника.ДоверительЮЛ_ИНН));
	Если СведенияОПредставляемомЛице.ЯвляетсяФЛ Тогда
		// общие сведения
		КодСтраныГражданства = ?(ЗначениеЗаполнено(ДанныеСправочника.ДоверительФЛ_Гражданство),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСправочника.ДоверительФЛ_Гражданство, "Код"), "");
		
		СведенияОПредставляемомЛице.Вставить("ИННФЛ", 		СокрЛП(ДанныеСправочника.ДоверительФЛ_ИНН));
		СведенияОПредставляемомЛице.Вставить("ОГРНИП", 		СокрЛП(ДанныеСправочника.ДоверительФЛ_ОГРН));
		СведенияОПредставляемомЛице.Вставить("Гражданство", СокрЛП(КодСтраныГражданства));
		СведенияОПредставляемомЛице.Вставить("ДатаРожд", 	ДанныеСправочника.ДоверительФЛ_ДатаРождения);
		
		// формируем структуру с Ф.И.О.
		ФИОПредпринимателя = Новый Структура;
		ФИОПредпринимателя.Вставить("Фамилия", 	ДанныеСправочника.ЛицоБезДовФЛ_Фамилия);
		ФИОПредпринимателя.Вставить("Имя", 		ДанныеСправочника.ЛицоБезДовФЛ_Имя);
		ФИОПредпринимателя.Вставить("Отчество", ДанныеСправочника.ЛицоБезДовФЛ_Отчество);
		СведенияОПредставляемомЛице.Вставить("ФИО", ФИОПредпринимателя);
		
		// формируем структуру со сведениями об удостоверении личности
		КодВидаДокумента = ДокументооборотСКОВызовСервера.ПолучитьКодВидаДокументаФизическогоЛица(
			ДанныеСправочника.ЛицоБезДовФЛ_ВидДок);
		
		УдЛичнПредпринимателя = Новый Структура;
		УдЛичнПредпринимателя.Вставить("ВидДок", СокрЛП(КодВидаДокумента));
		УдЛичнПредпринимателя.Вставить("СерНомДок", СокрЛП(СокрЛП(ДанныеСправочника.ЛицоБезДовФЛ_СерДок) + " "
			+ СокрЛП(ДанныеСправочника.ЛицоБезДовФЛ_НомДок)));
		УдЛичнПредпринимателя.Вставить("ДатаДок", ДанныеСправочника.ЛицоБезДовФЛ_ДатаДок);
		УдЛичнПредпринимателя.Вставить("ВыдДок", СокрЛП(ДанныеСправочника.ЛицоБезДовФЛ_ВыдДок));
		УдЛичнПредпринимателя.Вставить("КодВыдДок", СокрЛП(ДанныеСправочника.ЛицоБезДовФЛ_КодВыдДок));
		СведенияОПредставляемомЛице.Вставить("УдЛичн", УдЛичнПредпринимателя);
		
	Иначе
		// общие сведения
		СведенияОПредставляемомЛице.Вставить("НаимОрг", СокрЛП(ДанныеСправочника.ДоверительЮЛ_НаимОрг));
		СведенияОПредставляемомЛице.Вставить("ИННЮЛ", СокрЛП(ДанныеСправочника.ДоверительЮЛ_ИНН));
		СведенияОПредставляемомЛице.Вставить("КПП", СокрЛП(ДанныеСправочника.ДоверительЮЛ_КПП));
		СведенияОПредставляемомЛице.Вставить("ОГРН", СокрЛП(ДанныеСправочника.ДоверительЮЛ_ОГРН));
		
		// формируем структуру со сведениями о руководителе
		ФИОРуководителяОрганизации = Новый Структура;
		ФИОРуководителяОрганизации.Вставить("Фамилия", 	ДанныеСправочника.ЛицоБезДовФЛ_Фамилия);
		ФИОРуководителяОрганизации.Вставить("Имя", 		ДанныеСправочника.ЛицоБезДовФЛ_Имя);
		ФИОРуководителяОрганизации.Вставить("Отчество", ДанныеСправочника.ЛицоБезДовФЛ_Отчество);
		СвРукОргЮЛ = Новый Структура;
		СвРукОргЮЛ.Вставить("ИННФЛ", СокрЛП(ДанныеСправочника.ДоверительФЛ_ИНН));
		СвРукОргЮЛ.Вставить("ФИО", ФИОРуководителяОрганизации);
		СведенияОПредставляемомЛице.Вставить("СвРукОрг", СвРукОргЮЛ);
	КонецЕсли;
	Сведения.Вставить("СвПредст", СведенияОПредставляемомЛице);
	
	// формируем сведения о доверителе
	СвДовер = Новый Структура;
	СвДовер.Вставить("ПрДов", ?(ЗначениеЗаполнено(ДанныеСправочника.ЛицоБезДовЮЛ_ИНН), 2, 1));
	
	Если ЗначениеЗаполнено(ДанныеСправочника.ЛицоБезДовЮЛ_ИНН) Тогда
		// общие сведения
		ДоверительЮЛ = Новый Структура;
		ДоверительЮЛ.Вставить("НаимОрг", 	СокрЛП(ДанныеСправочника.ЛицоБезДовЮЛ_НаимОрг));
		ДоверительЮЛ.Вставить("ИННЮЛ", 		СокрЛП(ДанныеСправочника.ЛицоБезДовЮЛ_ИНН));
		ДоверительЮЛ.Вставить("КПП", 		СокрЛП(ДанныеСправочника.ЛицоБезДовЮЛ_КПП));
		ДоверительЮЛ.Вставить("ОГРН", 		СокрЛП(ДанныеСправочника.ЛицоБезДовЮЛ_ОГРН));
		
		// сведения о руководителе
		СвРукОрг = Новый Структура;
		СвРукОрг.Вставить("ИННФЛ", "");
		ФИОРуководителяОрганизации = Новый Структура;
		ФИОРуководителяОрганизации.Вставить("Фамилия", 	"");
		ФИОРуководителяОрганизации.Вставить("Имя", 		"");
		ФИОРуководителяОрганизации.Вставить("Отчество", "");
		СвРукОрг.Вставить("ФИО", ФИОРуководителяОрганизации);
		ДоверительЮЛ.Вставить("СвРукОрг", СвРукОрг);
		
		СвДовер.Вставить("СвЮЛ", ДоверительЮЛ);
	КонецЕсли;
	Сведения.Вставить("СвДовер", СвДовер);
	
	// выгружаем сведения о представителе и его полномочиях
	СвУпПред = Новый Структура;
	
	// выгружаем сведения о представителе
	СвПред = Новый Структура;
	
	Если ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН) Тогда
		СвПредставительЮЛ = Новый Структура;
		СвПредставительЮЛ.Вставить("НаимОрг", 	СокрЛП(ДанныеСправочника.ПредставительЮЛ_НаимОрг));
		СвПредставительЮЛ.Вставить("ИННЮЛ", 	СокрЛП(ДанныеСправочника.ПредставительЮЛ_ИНН));
		СвПредставительЮЛ.Вставить("КПП", 		СокрЛП(ДанныеСправочника.ПредставительЮЛ_КПП));
		СвПредставительЮЛ.Вставить("ОГРН", 		СокрЛП(ДанныеСправочника.ПредставительЮЛ_ОГРН));
		СвПред.Вставить("СвОрг", СвПредставительЮЛ);
	КонецЕсли;
	
	КодСтраныГражданства = ?(ЗначениеЗаполнено(ДанныеСправочника.ПредставительФЛ_Гражданство),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСправочника.ПредставительФЛ_Гражданство, "Код"), "");
	
	СвПредставительФЛ = Новый Структура;
	СвПредставительФЛ.Вставить("ИННФЛ", 		СокрЛП(ДанныеСправочника.ПредставительФЛ_ИНН));
	СвПредставительФЛ.Вставить("ОГРНИП", 		СокрЛП(ДанныеСправочника.ПредставительФЛ_ОГРН));
	СвПредставительФЛ.Вставить("Гражданство", 	СокрЛП(КодСтраныГражданства));
	СвПредставительФЛ.Вставить("ДатаРожд", 		ДанныеСправочника.ПредставительФЛ_ДатаРождения);
	
	ФИОПредставителя = Новый Структура;
	ФИОПредставителя.Вставить("Фамилия", 	ДанныеСправочника.ПредставительФЛ_Фамилия);
	ФИОПредставителя.Вставить("Имя", 		ДанныеСправочника.ПредставительФЛ_Имя);
	ФИОПредставителя.Вставить("Отчество", 	ДанныеСправочника.ПредставительФЛ_Отчество);
	СвПредставительФЛ.Вставить("ФИО", ФИОПредставителя);
	
	КодВидаДокумента = ДокументооборотСКОВызовСервера.ПолучитьКодВидаДокументаФизическогоЛица(
		ДанныеСправочника.ПредставительФЛ_ВидДок);
	
	УдЛичнПредпринимателя = Новый Структура;
	УдЛичнПредпринимателя.Вставить("ВидДок", СокрЛП(КодВидаДокумента));
	УдЛичнПредпринимателя.Вставить("СерНомДок", СокрЛП(СокрЛП(ДанныеСправочника.ПредставительФЛ_СерДок) + " "
		+ СокрЛП(ДанныеСправочника.ПредставительФЛ_НомДок)));
	УдЛичнПредпринимателя.Вставить("ДатаДок", ДанныеСправочника.ПредставительФЛ_ДатаДок);
	УдЛичнПредпринимателя.Вставить("ВыдДок", СокрЛП(ДанныеСправочника.ПредставительФЛ_ВыдДок));
	УдЛичнПредпринимателя.Вставить("КодВыдДок", СокрЛП(ДанныеСправочника.ПредставительФЛ_КодВыдДок));
	СвПредставительФЛ.Вставить("УдЛичн", УдЛичнПредпринимателя);
	ПредставительЗаданСтрокойФИО = (ЗначениеЗаполнено(ДанныеСправочника.Представитель)
		И НЕ ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН)
		И ТипЗнч(ДанныеСправочника.Представитель) = Тип("Строка")
		И ДанныеСправочника.Представитель = ДокументооборотСКОКлиентСервер.ПолучитьПредставлениеФИО(ФИОПредставителя));
	СвПредставительФЛ.Вставить("ЯвляетсяСотрудникомОрганизации",
		ДанныеСправочника.ПредставительЯвляетсяСотрудником
			И НЕ ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН)
		ИЛИ ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН)
			И ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_КПП)
			И ДанныеСправочника.ДоверительЮЛ_ИНН = ДанныеСправочника.ПредставительЮЛ_ИНН
			И ДанныеСправочника.ДоверительЮЛ_КПП = ДанныеСправочника.ПредставительЮЛ_КПП
		ИЛИ НЕ ЗначениеЗаполнено(ДанныеСправочника.ПредставительЮЛ_ИНН)
			И НЕ ЗначениеЗаполнено(ДанныеСправочника.ПредставительФЛ_ОГРН)
		ИЛИ ПредставительЗаданСтрокойФИО);
	СвПред.Вставить("СведФизЛ", СвПредставительФЛ);
	
	СвУпПред.Вставить("СвПред", СвПред);
	ОргФЛ = Новый Структура("ФЛ", СокрЛП(ДанныеСправочника.ДоверительЮЛ_ИНН));
	ПолнДов = Новый Массив;
	ПолномочияПредставителя = Новый Структура;
	ПолномочияПредставителя.Вставить("ПрОблПолн", 	ДанныеСправочника.ПолномочияПредставителя);
	ПолномочияПредставителя.Вставить("ОргФЛ", 		ОргФЛ);
	ПолномочияПредставителя.Вставить("ОКАТО", 		"");
	ПолнДов.Добавить(ПолномочияПредставителя);
	СвУпПред.Вставить("ПолнДов", ПолнДов);
	
	Сведения.Вставить("СвУпПред", СвУпПред);
	
	Если ОпределитьСтатусДоверенности Тогда
		Сведения.Вставить("Статус", ДанныеСправочника.Статус);
	КонецЕсли;
	
	// возвращаем полученную структуру
	Возврат Сведения;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
		"ПрисоединитьДополнительныеТаблицы
		|ЭтотСписок КАК ЭтотСписок
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Владельцы
		|	ПО Владельцы.Ссылка = ЭтотСписок.Организация
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения
		|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = Владельцы.Ссылка
		|;
		|РазрешитьЧтение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|ИЛИ ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
		|;
		|РазрешитьИзменениеЕслиРазрешеноЧтение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область БазовыйФункционалDOM

// https://soltau.ru/index.php/themes/dev/item/413

Функция ПодготовитьДокументDOM(ВходящиеДанные)
	
	ЭтоФайл = Ложь;
	ТекущиеДанные = ВходящиеДанные;
	Если ТипЗнч(ВходящиеДанные) = Тип("ДвоичныеДанные") Тогда
		ТекущиеДанные = ВходящиеДанные;
	ИначеЕсли ЭтоАдресВременногоХранилища(ВходящиеДанные) Тогда
		ТекущиеДанные = ПолучитьИзВременногоХранилища(ВходящиеДанные);
	ИначеЕсли ТипЗнч(ВходящиеДанные) = Тип("Строка") И НайтиФайлы(ВходящиеДанные).Количество() > 0 Тогда
		ЭтоФайл = Истина;
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	Если ТипЗнч(ТекущиеДанные) = Тип("ДвоичныеДанные") Тогда
		БуферДанных = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ТекущиеДанные);
		ЧтениеXML.ОткрытьПоток(Новый ПотокВПамяти(БуферДанных));
	ИначеЕсли ЭтоФайл Тогда
		ЧтениеXML.ОткрытьФайл(ТекущиеДанные);
	Иначе	
		ЧтениеXML.УстановитьСтроку(ТекущиеДанные);
	КонецЕсли;
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM  = ПостроительDOM.Прочитать(ЧтениеXML);
	
	Возврат ДокументDOM;
	
КонецФункции

Функция ПолучитьВыборкуЭлементовDOM(УзелВладелец, ТекстПоиска)
	
	Результат = Новый Массив;

	ДокументВладелец = УзелВладелец.ДокументВладелец;
	РазыменовательИмен = Новый РазыменовательПространствИменDOM(ДокументВладелец);
	НашлиЭлементы = ДокументВладелец.ВычислитьВыражениеXPath(ТекстПоиска, УзелВладелец, РазыменовательИмен);
	
	ОчереднойЭлемент = НашлиЭлементы.ПолучитьСледующий();
	Пока ОчереднойЭлемент <> Неопределено Цикл
		Результат.Добавить(ОчереднойЭлемент);
		ОчереднойЭлемент = НашлиЭлементы.ПолучитьСледующий();
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПредставлениеДаты(ПредставлениеДаты)
	
	Результат = ПредставлениеДаты;
	
	МассивСтроки = СтрРазделить(ПредставлениеДаты, ".", Ложь);
	
	Если МассивСтроки.Количество() = 3 И СтрДлина(МассивСтроки[2]) = 4 Тогда
		Результат = МассивСтроки[2] + МассивСтроки[1] + МассивСтроки[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПредставлениеПола(ПредставлениеПола)
	
	Если ПредставлениеПола = "2" Тогда
		Результат = Перечисления.ПолФизическогоЛица.Женский;
	Иначе
		Результат = Перечисления.ПолФизическогоЛица.Мужской;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПредставлениеСтраны(ПредставлениеСтраны)
	
	Результат = Справочники.СтраныМира.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ПредставлениеСтраны) Тогда
		Результат = Справочники.СтраныМира.НайтиПоКоду(ПредставлениеСтраны);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПредставлениеВидаДокумента(ПредставлениеДокумента)
	
	ВсеВарианты = Новый Соответствие;
	ВсеВарианты.Вставить("07", "Военный билет");
	ВсеВарианты.Вставить("10", "Паспорт иностранного гражданина");
	ВсеВарианты.Вставить("11", "Свидетельство о рассмотрении ходатайства о признании лица беженцем на территории Российской Федерации по существу");
	ВсеВарианты.Вставить("12", "Вид на жительство в Российской Федерации");
	ВсеВарианты.Вставить("13", "Удостоверение беженца");
	ВсеВарианты.Вставить("15", "Разрешение на временное проживание в Российской Федерации");
	ВсеВарианты.Вставить("19", "Свидетельство о предоставлении временного убежища на территории Российской Федерации");
	ВсеВарианты.Вставить("21", "Паспорт гражданина Российской Федерации");
	ВсеВарианты.Вставить("24", "Удостоверение личности военнослужащего Российской Федерации");
	
	Результат = Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ПредставлениеДокумента) Тогда
		Если Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодМВД") <> Неопределено Тогда
			Результат = Справочники.ВидыДокументовФизическихЛиц.НайтиПоРеквизиту("КодМВД", ПредставлениеДокумента);
		ИначеЕсли Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодФНС") <> Неопределено Тогда
			Результат = Справочники.ВидыДокументовФизическихЛиц.НайтиПоРеквизиту("КодФНС", ПредставлениеДокумента);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПредставлениеСерияНомер(ПредставлениеСерияНомер)
	
	Результат = Новый Структура;
	Результат.Вставить("Серия", ПредставлениеСерияНомер);
	Результат.Вставить("Номер", "");
	
	МассивСтроки = СтрРазделить(ПредставлениеСерияНомер, " ", Ложь);
	
	Если МассивСтроки.Количество() > 2 Тогда
		Результат.Вставить("Серия", МассивСтроки[0] + " " + МассивСтроки[1]);
		МассивСтроки.Удалить(0);
		МассивСтроки.Удалить(0);
		Результат.Вставить("Номер", СтрСоединить(МассивСтроки, " "));
	ИначеЕсли МассивСтроки.Количество() = 2 Тогда
		Результат.Вставить("Серия", МассивСтроки[0]);
		Результат.Вставить("Номер", МассивСтроки[1]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиЭлементИБ(ЗначениеПоУмолчанию, ФильтрыПоиска)
	
	Результат = ЗначениеПоУмолчанию;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектПоиска.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр КАК ОбъектПоиска
	|ГДЕ
	|	НЕ ОбъектПоиска.ПометкаУдаления
	|{ГДЕ
	|	ОбъектПоиска.Ссылка.*}";
	
	МетаданныеОбъекта = ЗначениеПоУмолчанию.Метаданные();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр", МетаданныеОбъекта.ПолноеИмя());
	Построитель = Новый ПостроительОтчета;
	Построитель.Текст = ТекстЗапроса;
	Построитель.ЗаполнитьНастройки();
	
	Для Каждого СтрокаКлюча Из ФильтрыПоиска Цикл
		НовыйОтбор = Построитель.Отбор.Добавить("Ссылка." + СтрокаКлюча.Ключ);
		НовыйОтбор.Использование = Истина;
		НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
		НовыйОтбор.Значение = СтрокаКлюча.Значение;
	КонецЦикла;
	
	Запрос = Построитель.ПолучитьЗапрос();
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Количество() И Выборка.Следующий() Тогда
			Результат = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодыскатьИнформациюВладельца(СписокСправочников, ИНН, КПП = "", СНИЛС = "")
	
	Результат = Неопределено;
	
	МассивСправочников = СтрРазделить(СписокСправочников, ",", Ложь);
	
	Если МассивСправочников.Найти("Организации") <> Неопределено Тогда
		СтрокаПоиска = Новый Структура;
		СтрокаПоиска.Вставить("ИНН", ИНН);
		СправочникОрганизации = Метаданные.Справочники.Найти("Организации");
		РеквизитКПП = ?(СправочникОрганизации = Неопределено, Неопределено, СправочникОрганизации.Реквизиты.Найти("КПП"));
		Если РеквизитКПП <> Неопределено Тогда
			СтрокаПоиска.Вставить("КПП", КПП);
		КонецЕсли;
		Результат = НайтиЭлементИБ(Справочники.Организации.ПустаяСсылка(), СтрокаПоиска);
	КонецЕсли;
	
	ИмяСправочникаКонтрагенты = "Контрагенты";
	Если НЕ ЗначениеЗаполнено(Результат) И МассивСправочников.Найти(ИмяСправочникаКонтрагенты) <> Неопределено
		И ЭлектронныйДокументооборотСКонтролирующимиОрганами.РеквизитыСправочникаКонтрагентовДоступны(Истина, Истина) Тогда
		СтрокаПоиска = Новый Структура;
		СтрокаПоиска.Вставить("ИНН", ИНН);
		Если ЗначениеЗаполнено(КПП) Тогда
			СтрокаПоиска.Вставить("КПП", КПП);
		КонецЕсли;
		Результат = НайтиЭлементИБ(Справочники[ИмяСправочникаКонтрагенты].ПустаяСсылка(), СтрокаПоиска);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат) И МассивСправочников.Найти("ФизическиеЛица") <> Неопределено Тогда
		СправочникФизическиеЛица = Метаданные.Справочники.Найти("ФизическиеЛица");
		Если СправочникФизическиеЛица <> Неопределено И СправочникФизическиеЛица.Реквизиты.Найти("ИНН") <> Неопределено Тогда
			СтрокаПоиска = Новый Структура;
			СтрокаПоиска.Вставить("ИНН", ИНН);
			Если ЗначениеЗаполнено(СНИЛС) Тогда
				Если СправочникФизическиеЛица.Реквизиты.Найти("СтраховойНомерПФР") <> Неопределено Тогда
					СтрокаПоиска.Вставить("СтраховойНомерПФР", СНИЛС);
				ИначеЕсли СправочникФизическиеЛица.Реквизиты.Найти("СНИЛС") <> Неопределено Тогда
					СтрокаПоиска.Вставить("СНИЛС", СНИЛС);
				КонецЕсли;
			КонецЕсли;
			Результат = НайтиЭлементИБ(Справочники.ФизическиеЛица.ПустаяСсылка(), СтрокаПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Находит элемент документа DOM, проверяет что оно одно и пытается получить значение и привести тип.
//
Функция ПолучитьЗначениеДокумента(УзелВладелец, ТекстПоиска, ЗначениеПоУмолчанию = Неопределено)
	
	Результат = ЗначениеПоУмолчанию;
	
	ВыборкаЭлементов = ПолучитьВыборкуЭлементовDOM(УзелВладелец, ТекстПоиска);
	
	Если ВыборкаЭлементов.Количество() = 1 Тогда
		
		ЭлементВыборки = ВыборкаЭлементов[0];
		Если ТипЗнч(ЭлементВыборки) = Тип("АтрибутDOM") Тогда
			ПредставлениеЗначения = ЭлементВыборки.Значение;
		ИначеЕсли ТипЗнч(ЭлементВыборки) = Тип("ЭлементDOM") Тогда
			ПредставлениеЗначения = ЭлементВыборки.ТекстовоеСодержимое;
		Иначе
			ПредставлениеЗначения = ЭлементВыборки.ЗначениеУзла;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеПоУмолчанию) = Тип("Дата") Тогда
			ПредставлениеЗначения = ПодготовитьПредставлениеДаты(ПредставлениеЗначения);
		ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
			ПредставлениеЗначения = ПодготовитьПредставлениеПола(ПредставлениеЗначения);
		ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("СправочникСсылка.СтраныМира") Тогда
			ПредставлениеЗначения = ПодготовитьПредставлениеСтраны(ПредставлениеЗначения);
		ИначеЕсли ТипЗнч(ЗначениеПоУмолчанию) = Тип("СправочникСсылка.ВидыДокументовФизическихЛиц") Тогда
			ПредставлениеЗначения = ПодготовитьПредставлениеВидаДокумента(ПредставлениеЗначения);
		КонецЕсли;
		
		Если ЗначениеПоУмолчанию <> Неопределено Тогда
			МассивТипов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(ЗначениеПоУмолчанию));
			ТекущийТип = Новый ОписаниеТипов(МассивТипов);
			Результат = ТекущийТип.ПривестиЗначение(ПредставлениеЗначения);
		Иначе
			Результат = ПредставлениеЗначения;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьУзелXML(данныеXML, ИмяУзла, ЗначениеУзла, ВыгружатьПустые = Истина)
	
	ПредставлениеЗначения = ЗначениеВыгрузкиXML(ЗначениеУзла);
	
	Если ВыгружатьПустые ИЛИ ЗначениеЗаполнено(ПредставлениеЗначения) Тогда 
		данныеXML.ЗаписатьНачалоЭлемента(ИмяУзла);
		данныеXML.ЗаписатьТекст(ПредставлениеЗначения);
		данныеXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьАтрибутУзлаXML(данныеXML, ИмяАтрибута, ЗначениеАтрибута, ВыгружатьПустые = Истина)
	
	ПредставлениеЗначения = ЗначениеВыгрузкиXML(ЗначениеАтрибута);
	
	Если ВыгружатьПустые ИЛИ ЗначениеЗаполнено(ЗначениеАтрибута) Тогда 
		данныеXML.ЗаписатьНачалоАтрибута(ИмяАтрибута);
		данныеXML.ЗаписатьТекст(ПредставлениеЗначения);
		данныеXML.ЗаписатьКонецАтрибута();
	КонецЕсли;
	
КонецПроцедуры

Функция ЗначениеВыгрузкиXML(ЗначениеВыгрузки)
	
	Результат = Строка(ЗначениеВыгрузки);
	
	Если НЕ ЗначениеЗаполнено(ЗначениеВыгрузки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеВыгрузки) = Тип("Дата") Тогда
		Результат = Формат(ЗначениеВыгрузки, "ДФ=dd.MM.yyyy");
	ИначеЕсли ТипЗнч(ЗначениеВыгрузки) = Тип("СправочникСсылка.СтраныМира") Тогда
		Результат = ЗначениеВыгрузки.Код;
	ИначеЕсли ТипЗнч(ЗначениеВыгрузки) = Тип("СправочникСсылка.ВидыДокументовФизическихЛиц") Тогда
		Если Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодМВД") <> Неопределено Тогда
			Результат = ЗначениеВыгрузки.КодМВД;
		ИначеЕсли Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодФНС") <> Неопределено Тогда
			Результат = ЗначениеВыгрузки.КодФНС;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЗначениеВыгрузки) = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
		Если ЗначениеВыгрузки = Перечисления.ПолФизическогоЛица.Женский Тогда
			Результат = "2";
		Иначе	
			Результат = "1";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаЭлементаСправочника

Функция ЗаписатьЭлементСправочника(ДанныеДоверенности, ОбновлятьСуществующийИлиСсылка)
	
	Результат = Неопределено;
	НашлиЭлемент = Неопределено;
	
	Если ТипЗнч(ОбновлятьСуществующийИлиСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		НашлиЭлемент = ОбновлятьСуществующийИлиСсылка;
		
	ИначеЕсли ОбновлятьСуществующийИлиСсылка = Истина Тогда
		СтрокаПоиска = Новый Структура;
		СтрокаПоиска.Вставить("НомерДоверенности", ДанныеДоверенности.НомерДоверенности);
		СтрокаПоиска.Вставить("ДатаВыдачи", ДанныеДоверенности.ДатаВыдачи);
		СтрокаПоиска.Вставить("Организация", ДанныеДоверенности.Организация);
		НашлиЭлемент = НайтиЭлементИБ(Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ПустаяСсылка(), СтрокаПоиска);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НашлиЭлемент) Тогда
		ОбъектСправочника = Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.СоздатьЭлемент();
	Иначе
		ОбъектСправочника = НашлиЭлемент.ПолучитьОбъект();
		Для Каждого СтрокаРеквизита Из ОбъектСправочника.Метаданные().Реквизиты Цикл
			ОбъектСправочника[СтрокаРеквизита.Имя] = Неопределено;
		КонецЦикла;
	КонецЕсли;
	
	// здесь переносим значения реквизитов
	ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДанныеДоверенности);
	
	ТабличныеЧасти = Новый Структура;
	Если ДанныеДоверенности.Свойство("НалоговыеОрганыДействия") Тогда
		ТабличныеЧасти.Вставить("НалоговыеОрганыДействия", ДанныеДоверенности.НалоговыеОрганыДействия);
	КонецЕсли;
	ТабличныеЧасти.Вставить("ФИО", ДанныеДоверенности.ФИО);
	Если ДанныеДоверенности.Свойство("ПолномочияПредставителя") Тогда
		ТабличныеЧасти.Вставить("ПолномочияПредставителя", ДанныеДоверенности.ПолномочияПредставителя);
	КонецЕсли;
	ТабличныеЧасти.Вставить("УдостоверенияЛичности", ДанныеДоверенности.УдостоверенияЛичности);
	
	Для Каждого СтрокаТЧ Из ТабличныеЧасти Цикл
		ТекущаяТЧ = ОбъектСправочника[СтрокаТЧ.Ключ];
		ТекущаяТЧ.Очистить();
		Для Каждого НоваяСтрока Из СтрокаТЧ.Значение Цикл
			ДобавленнаяСтрока = ТекущаяТЧ.Добавить();
			ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, НоваяСтрока);
		КонецЦикла;
	КонецЦикла;
	
	Если ДанныеДоверенности.Свойство("ДанныеВыгрузки") И ДанныеДоверенности.ДанныеВыгрузки <> Неопределено Тогда
		ОбъектСправочника.ФайлВырузки = Новый ХранилищеЗначения(ДанныеДоверенности.ДанныеВыгрузки, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если ДанныеДоверенности.Свойство("ДанныеПодписи") И ДанныеДоверенности.ДанныеПодписи <> Неопределено Тогда
		ОбъектСправочника.ЭлектроннаяПодпись = Новый ХранилищеЗначения(
			ДанныеДоверенности.ДанныеПодписи, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если ДанныеДоверенности.Свойство("ДанныеЗаявленияНаОтзыв")
		И ДанныеДоверенности.ДанныеЗаявленияНаОтзыв <> Неопределено Тогда
		
		ОбъектСправочника.ФайлЗаявленияНаОтзыв = Новый ХранилищеЗначения(
			ДанныеДоверенности.ДанныеЗаявленияНаОтзыв, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если ДанныеДоверенности.Свойство("ДанныеПодписиЗаявленияНаОтзыв")
		И ДанныеДоверенности.ДанныеПодписиЗаявленияНаОтзыв <> Неопределено Тогда
		
		ОбъектСправочника.ЭлектроннаяПодписьЗаявленияНаОтзыв = Новый ХранилищеЗначения(
			ДанныеДоверенности.ДанныеПодписиЗаявленияНаОтзыв, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если ДанныеДоверенности.Свойство("ДанныеАрхива")
		И ДанныеДоверенности.ДанныеАрхива <> Неопределено Тогда
		
		ОбъектСправочника.Образ = Новый ХранилищеЗначения(ДанныеДоверенности.ДанныеАрхива, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Удачно = Ложь;
	
	Если ОбъектСправочника.ПроверитьЗаполнение() Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр");
		Если НЕ ОбъектСправочника.ЭтоНовый() Тогда
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбъектСправочника.Ссылка);
		КонецЕсли;	
		
		Удачно = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			ОбъектСправочника.Записать();
			
		Исключение
			ИмяСобытия = НСтр("ru = 'Электронный документооборот с контролирующими органами. Ошибка изменения МЧД.'", ОбщегоНазначения.КодОсновногоЯзыка());
			ДанныеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Ошибка,,,
				ДанныеОшибки);
			Удачно = Ложь;
		КонецПопытки;
		
		Если Удачно Тогда
			ЗафиксироватьТранзакцию();
		Иначе
			ОтменитьТранзакцию();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Удачно Тогда
		Результат = ОбъектСправочника.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаЭлементаСправочника

Функция ДанныеИзИнформационнойБазы(СправочникСсылка)

	Результат = Новый Структура;
	Результат.Вставить("КодНО", "");
	Результат.Вставить("КНД", "1110310");
	Результат.Вставить("КодыНО", Новый Массив);
	
	ТекстЗапросаРеквизитовПолномочий = "";
	Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя() Цикл
		ПредставлениеВида = Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");
		ТекстЗапросаРеквизитовПолномочий = ТекстЗапросаРеквизитовПолномочий + "
			|	МашиночитаемыеДоверенностиРаспределенныйРеестрПолномочияПредставителя.Признак" + ПредставлениеВида
			+ " КАК Признак" + ПредставлениеВида + ?(ВидПолномочия = КоличествоВидовПолномочийПредставителя(), "", ",");
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрПолномочияПредставителя.Ссылка КАК Доверенность,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрПолномочияПредставителя.ПризнакПолныеПолномочия КАК ПризнакПолныеПолномочия,"
		+ ТекстЗапросаРеквизитовПолномочий + "
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.ПолномочияПредставителя КАК МашиночитаемыеДоверенностиРаспределенныйРеестрПолномочияПредставителя
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрПолномочияПредставителя.Ссылка = &СправочникСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрФИО.Владелец КАК Владелец,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрФИО.Фамилия КАК Фамилия,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрФИО.Имя КАК Имя,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрФИО.Отчество КАК Отчество
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.ФИО КАК МашиночитаемыеДоверенностиРаспределенныйРеестрФИО
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрФИО.Ссылка = &СправочникСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.НомерДоверенности КАК НомерДоверенности,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаВыдачи КАК ДатаВыдачи,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОкончания КАК ДатаОкончания,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.КодНалоговогоОрганаПредставления КАК КодНалоговогоОрганаПредставления,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.СведенияОбИнформационнойСистеме КАК СведенияОбИнформационнойСистеме,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.СрокДействия КАК СрокДействия,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.НомерРодительскойДоверенности КАК НомерРодительскойДоверенности,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец КАК Владелец,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Организация КАК Организация,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Доверитель КАК Доверитель,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_ИностраннаяОрганизация КАК ДоверительЮЛ_ИностраннаяОрганизация,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_ИНН КАК ДоверительЮЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_КПП КАК ДоверительЮЛ_КПП,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_ОГРН КАК ДоверительЮЛ_ОГРН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_СтрРег КАК ДоверительЮЛ_СтрРег,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_НаимРегОрг КАК ДоверительЮЛ_НаимРегОрг,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_РегНомер КАК ДоверительЮЛ_РегНомер,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_КодНПРег КАК ДоверительЮЛ_КодНПРег,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_АдрРус КАК ДоверительЮЛ_АдрРус,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_АдрЛат КАК ДоверительЮЛ_АдрЛат,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_ИНН КАК ДоверительФЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_ОГРН КАК ДоверительФЛ_ОГРН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_СНИЛС КАК ДоверительФЛ_СНИЛС,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_Гражданство КАК ДоверительФЛ_Гражданство,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_Пол КАК ДоверительФЛ_Пол,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_ДатаРождения КАК ДоверительФЛ_ДатаРождения,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительФЛ_МестоРожд КАК ДоверительФЛ_МестоРожд,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_НаимОрг КАК ДоверительЮЛ_НаимОрг,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовЮЛ КАК ЛицоБезДовЮЛ,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовЮЛ_НаимОрг КАК ЛицоБезДовЮЛ_НаимОрг,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовЮЛ_ИНН КАК ЛицоБезДовЮЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовЮЛ_КПП КАК ЛицоБезДовЮЛ_КПП,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовЮЛ_ОГРН КАК ЛицоБезДовЮЛ_ОГРН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ КАК ЛицоБезДовФЛ,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ_ИНН КАК ЛицоБезДовФЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ_СНИЛС КАК ЛицоБезДовФЛ_СНИЛС,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ_Гражданство КАК ЛицоБезДовФЛ_Гражданство,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ_ДатаРождения КАК ЛицоБезДовФЛ_ДатаРождения,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ЛицоБезДовФЛ_Должность КАК ЛицоБезДовФЛ_Должность,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Представитель КАК Представитель,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительЮЛ_НаимОрг КАК ПредставительЮЛ_НаимОрг,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительЮЛ_ИНН КАК ПредставительЮЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительЮЛ_КПП КАК ПредставительЮЛ_КПП,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительЮЛ_ОГРН КАК ПредставительЮЛ_ОГРН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_ИНН КАК ПредставительФЛ_ИНН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_ОГРН КАК ПредставительФЛ_ОГРН,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_СНИЛС КАК ПредставительФЛ_СНИЛС,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_Гражданство КАК ПредставительФЛ_Гражданство,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_ДатаРождения КАК ПредставительФЛ_ДатаРождения,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительЯвляетсяСотрудником КАК ПредставительЯвляетсяСотрудником,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус КАК Статус
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр КАК МашиночитаемыеДоверенностиРаспределенныйРеестр
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка = &СправочникСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрНалоговыеОрганыДействия.КодНалоговогоОргана КАК КодНалоговогоОргана
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.НалоговыеОрганыДействия КАК МашиночитаемыеДоверенностиРаспределенныйРеестрНалоговыеОрганыДействия
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрНалоговыеОрганыДействия.Ссылка = &СправочникСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.Владелец КАК Владелец,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.ВидДок КАК ВидДок,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.СерДок КАК СерДок,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.НомДок КАК НомДок,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.ДатаДок КАК ДатаДок,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.ВыдДок КАК ВыдДок,
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.КодВыдДок КАК КодВыдДок
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.УдостоверенияЛичности КАК МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестрУдостоверенияЛичности.Ссылка = &СправочникСсылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СправочникСсылка", СправочникСсылка);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаПолномочий = ПакетЗапросов[0].Выгрузить();
	
	ПолномочияПредставителя = Новый Массив;
	Если ВыборкаПолномочий.Количество() = 0 ИЛИ ВыборкаПолномочий[0].ПризнакПолныеПолномочия Тогда
		ПолномочияПредставителя.Добавить("99");
	Иначе
		Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя() Цикл
			ПредставлениеВида = Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");
			Если ВыборкаПолномочий[0]["Признак" + ПредставлениеВида] Тогда
				ПолномочияПредставителя.Добавить(ПредставлениеВида);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Результат.Вставить("ПолномочияПредставителя", ПолномочияПредставителя);
	
	ВыборкаПодписантов = ПакетЗапросов[1].Выгрузить();
	ВыборкаДокументов = ПакетЗапросов[4].Выгрузить();
	
	ПустойПодписант = Новый Структура("Имя, Отчество, Фамилия", "", "", "");
	ПустойДокумент = Новый Структура("ВидДок, СерДок, НомДок, ДатаДок, ВыдДок, КодВыдДок", "", "", "", "", "", "", "");
	
	ВыборкаИнформации = ПакетЗапросов[2].Выбрать();
	Если ВыборкаИнформации.Следующий() Тогда
		Результат.Вставить("КодНО", ВыборкаИнформации.КодНалоговогоОрганаПредставления);
		Результат.Вставить("Номер", ВыборкаИнформации.НомерДоверенности);
		Результат.Вставить("ДатаВыдачи", ВыборкаИнформации.ДатаВыдачи);
		Результат.Вставить("ДатаОкончания", ВыборкаИнформации.ДатаОкончания);
		Результат.Вставить("СведенияОбИнформационнойСистеме", ВыборкаИнформации.СведенияОбИнформационнойСистеме);
		Результат.Вставить("СрокДействия", ВыборкаИнформации.СрокДействия);
		Результат.Вставить("ПредыдующийНомер", ВыборкаИнформации.НомерРодительскойДоверенности);
		
		// информация об доверителе
		Результат.Вставить("Владелец", ВыборкаИнформации.Владелец);
		Результат.Вставить("Организация", ВыборкаИнформации.Организация);
		Результат.Вставить("ИностраннаяОрганизация", ВыборкаИнформации.ДоверительЮЛ_ИностраннаяОрганизация);
		Результат.Вставить("Доверитель", ВыборкаИнформации.Доверитель);
		Результат.Вставить("ДоверительЮЛ_ИНН", ВыборкаИнформации.ДоверительЮЛ_ИНН);
		Результат.Вставить("ДоверительЮЛ_КПП", ВыборкаИнформации.ДоверительЮЛ_КПП);
		Результат.Вставить("ДоверительЮЛ_ОГРН", ВыборкаИнформации.ДоверительЮЛ_ОГРН);
		Результат.Вставить("ДоверительЮЛ_СтрРег", ВыборкаИнформации.ДоверительЮЛ_СтрРег);
		Результат.Вставить("ДоверительЮЛ_НаимРегОрг", ВыборкаИнформации.ДоверительЮЛ_НаимРегОрг);
		Результат.Вставить("ДоверительЮЛ_РегНомер", ВыборкаИнформации.ДоверительЮЛ_РегНомер);
		Результат.Вставить("ДоверительЮЛ_КодНПРег", ВыборкаИнформации.ДоверительЮЛ_КодНПРег);
		Результат.Вставить("ДоверительЮЛ_АдрРус", ВыборкаИнформации.ДоверительЮЛ_АдрРус);
		Результат.Вставить("ДоверительЮЛ_АдрЛат", ВыборкаИнформации.ДоверительЮЛ_АдрЛат);
		Результат.Вставить("ДоверительФЛ_ИНН", ВыборкаИнформации.ДоверительФЛ_ИНН);
		Результат.Вставить("ДоверительФЛ_ОГРН", ВыборкаИнформации.ДоверительФЛ_ОГРН);
		Результат.Вставить("ДоверительФЛ_СНИЛС", ВыборкаИнформации.ДоверительФЛ_СНИЛС);
		Результат.Вставить("ДоверительФЛ_Гражданство", ВыборкаИнформации.ДоверительФЛ_Гражданство);
		Результат.Вставить("ДоверительФЛ_Пол", ВыборкаИнформации.ДоверительФЛ_Пол);
		Результат.Вставить("ДоверительФЛ_ДатаРождения", ВыборкаИнформации.ДоверительФЛ_ДатаРождения);
		Результат.Вставить("ДоверительФЛ_МестоРожд", ВыборкаИнформации.ДоверительФЛ_МестоРожд);
		Результат.Вставить("ДоверительЮЛ_НаимОрг", ВыборкаИнформации.ДоверительЮЛ_НаимОрг);
		
		Если Результат.ИностраннаяОрганизация Тогда
			Результат.Вставить("ДоверительЮЛ_ТипОрганизации", "ИО");
		ИначеЕсли ЗначениеЗаполнено(Результат.ДоверительЮЛ_ИНН) Тогда
			Результат.Вставить("ДоверительЮЛ_ТипОрганизации", "ЮЛ");
		Иначе
			Результат.Вставить("ДоверительЮЛ_ТипОрганизации", "ФЛ");
		КонецЕсли;
		
		// информация об подписанте
		Результат.Вставить("ЛицоБезДовЮЛ", ВыборкаИнформации.ЛицоБезДовЮЛ);
		Результат.Вставить("ЛицоБезДовЮЛ_НаимОрг", ВыборкаИнформации.ЛицоБезДовЮЛ_НаимОрг);
		Результат.Вставить("ЛицоБезДовЮЛ_ИНН", ВыборкаИнформации.ЛицоБезДовЮЛ_ИНН);
		Результат.Вставить("ЛицоБезДовЮЛ_КПП", ВыборкаИнформации.ЛицоБезДовЮЛ_КПП);
		Результат.Вставить("ЛицоБезДовЮЛ_ОГРН", ВыборкаИнформации.ЛицоБезДовЮЛ_ОГРН);
		Результат.Вставить("ЛицоБезДовФЛ", ВыборкаИнформации.ЛицоБезДовФЛ);
		Результат.Вставить("ЛицоБезДовФЛ_ИНН", ВыборкаИнформации.ЛицоБезДовФЛ_ИНН);
		Результат.Вставить("ЛицоБезДовФЛ_СНИЛС", ВыборкаИнформации.ЛицоБезДовФЛ_СНИЛС);
		Результат.Вставить("ЛицоБезДовФЛ_Гражданство", ВыборкаИнформации.ЛицоБезДовФЛ_Гражданство);
		Результат.Вставить("ЛицоБезДовФЛ_ДатаРождения", ВыборкаИнформации.ЛицоБезДовФЛ_ДатаРождения);
		Результат.Вставить("ЛицоБезДовФЛ_Должность", ВыборкаИнформации.ЛицоБезДовФЛ_Должность);
		
		Если Результат.ДоверительЮЛ_ТипОрганизации = "ФЛ" Тогда
			НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ));
		Иначе
			НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительРук));
		КонецЕсли;
		
		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиФИО = ПустойПодписант;
		Иначе
			НашлиФИО = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ЛицоБезДовФЛ_Имя", НашлиФИО.Имя);
		Результат.Вставить("ЛицоБезДовФЛ_Отчество", НашлиФИО.Отчество);
		Результат.Вставить("ЛицоБезДовФЛ_Фамилия", НашлиФИО.Фамилия);
		
		НашлиСтроки = ВыборкаДокументов.НайтиСтроки(Новый Структура("Владелец", Перечисления.СубъектыДоверенностиНалогоплательщика.ДоверительФЛ));
		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиДок = ПустойДокумент;
		Иначе
			НашлиДок = НашлиСтроки[0];
		КонецЕсли;
		
		Результат.Вставить("ЛицоБезДовФЛ_ВидДок", НашлиДок.ВидДок);
		Результат.Вставить("ЛицоБезДовФЛ_СерДок", НашлиДок.СерДок);
		Результат.Вставить("ЛицоБезДовФЛ_НомДок", НашлиДок.НомДок);
		Результат.Вставить("ЛицоБезДовФЛ_ДатаДок", НашлиДок.ДатаДок);
		Результат.Вставить("ЛицоБезДовФЛ_ВыдДок", НашлиДок.ВыдДок);
		Результат.Вставить("ЛицоБезДовФЛ_КодВыдДок", НашлиДок.КодВыдДок);
		
		// информация о представителе
		Результат.Вставить("Представитель", ВыборкаИнформации.Представитель);
		Результат.Вставить("ПредставительЮЛ_НаимОрг", ВыборкаИнформации.ПредставительЮЛ_НаимОрг);
		Результат.Вставить("ПредставительЮЛ_ИНН", ВыборкаИнформации.ПредставительЮЛ_ИНН);
		Результат.Вставить("ПредставительЮЛ_КПП", ВыборкаИнформации.ПредставительЮЛ_КПП);
		Результат.Вставить("ПредставительЮЛ_ОГРН", ВыборкаИнформации.ПредставительЮЛ_ОГРН);
		Результат.Вставить("ПредставительФЛ_ИНН", ВыборкаИнформации.ПредставительФЛ_ИНН);
		Результат.Вставить("ПредставительФЛ_ОГРН", ВыборкаИнформации.ПредставительФЛ_ОГРН);
		Результат.Вставить("ПредставительФЛ_СНИЛС", ВыборкаИнформации.ПредставительФЛ_СНИЛС);
		Результат.Вставить("ПредставительФЛ_Гражданство", ВыборкаИнформации.ПредставительФЛ_Гражданство);
		Результат.Вставить("ПредставительФЛ_ДатаРождения", ВыборкаИнформации.ПредставительФЛ_ДатаРождения);
		Результат.Вставить("ПредставительЯвляетсяСотрудником", ВыборкаИнформации.ПредставительЯвляетсяСотрудником);
		Результат.Вставить("Статус", ВыборкаИнформации.Статус);
		
		ТипВладельца = Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ;
		НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец", ТипВладельца));
		
		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиФИО = ПустойПодписант;
		Иначе
			НашлиФИО = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ПредставительФЛ_Имя", НашлиФИО.Имя);
		Результат.Вставить("ПредставительФЛ_Отчество", НашлиФИО.Отчество);
		Результат.Вставить("ПредставительФЛ_Фамилия", НашлиФИО.Фамилия);
		
		НашлиСтроки = ВыборкаДокументов.НайтиСтроки(Новый Структура("Владелец", ТипВладельца));
		
		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиДок = ПустойДокумент;
		Иначе
			НашлиДок = НашлиСтроки[0];
		КонецЕсли;
		
		Результат.Вставить("ПредставительФЛ_ВидДок", НашлиДок.ВидДок);
		Результат.Вставить("ПредставительФЛ_СерДок", НашлиДок.СерДок);
		Результат.Вставить("ПредставительФЛ_НомДок", НашлиДок.НомДок);
		Результат.Вставить("ПредставительФЛ_ДатаДок", НашлиДок.ДатаДок);
		Результат.Вставить("ПредставительФЛ_ВыдДок", НашлиДок.ВыдДок);
		Результат.Вставить("ПредставительФЛ_КодВыдДок", НашлиДок.КодВыдДок);
		
	КонецЕсли;
	
	ВыборкаНО = ПакетЗапросов[3].Выбрать();
	Пока ВыборкаНО.Следующий() Цикл
		НоваяСтрока = Новый Структура("КодНО", ВыборкаНО.КодНалоговогоОргана);
		Результат.КодыНО.Добавить(НоваяСтрока);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВыгрузитьФИОУзла(ЗаписьXML, Фамилия, Имя, Отчество)
	
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Фамилия", Фамилия);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Имя", Имя);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "Отчество", Отчество, Ложь);
	
КонецПроцедуры

Процедура ВыгрузитьАдресУзла(ЗаписьXML, АдресРФ, АдресЛат, ВыделитьВУзел = "")
	
	Если ЗначениеЗаполнено(ВыделитьВУзел) Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента(ВыделитьВУзел);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресЛат) Тогда
		ЗаписатьУзелXML(ЗаписьXML, "АдрЛат", АдресЛат);
	ИначеЕсли ЗначениеЗаполнено(АдресРФ) Тогда
		ЗаписатьУзелXML(ЗаписьXML, "АдрРус", АдресРФ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыделитьВУзел) Тогда
		ЗаписьXML.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьУдостоверениеУзла(
				ЗаписьXML,
				ВидДокумента,
				СерияДокумента,
				НомерДокумента,
				ДатаВыдачи,
				Выдан,
				КодПодразделения)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("УдЛичн");
	
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КодВидДок", ВидДокумента);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "СерНомДок", СокрЛП(СерияДокумента + " " + НомерДокумента));
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ДатаДок", ДатаВыдачи);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "ВыдДок", Выдан, Ложь);
	ЗаписатьАтрибутУзлаXML(ЗаписьXML, "КодВыдДок", КодПодразделения, Ложь);
		
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
