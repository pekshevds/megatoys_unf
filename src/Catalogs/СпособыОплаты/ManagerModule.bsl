#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция определяет организацию, кассу и валюту выбранного эквайрингового терминала.
//
// Параметры:
//  ЭквайринговыйТерминал - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на эквайринговый терминал
//
// Возвращаемое значение:
//	Структура - Организация, Касса и Валюта эквайрингового терминала
//
Функция ПолучитьРеквизитыЭквайринговогоТерминала(ЭквайринговыйТерминал) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭквайринговыеТерминалы.Организация КАК Организация,
	|	ЭквайринговыеТерминалы.Касса КАК Касса
	|ИЗ
	|	Справочник.СпособыОплаты КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	ЭквайринговыеТерминалы.Ссылка = &ЭквайринговыйТерминал
	|");
	
	Запрос.УстановитьПараметр("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Организация = Выборка.Организация;
		Касса = Выборка.Касса;
	Иначе
		Организация = Неопределено;
		Касса = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Организация, Касса",
		Организация,
		Касса);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции // ПолучитьРеквизитыЭквайринговогоТерминала()

// Функция определяет эквайринговый терминал по выбранной кассе.
//
// Возвращает эквайринговый терминал, если найден один эквайринговый терминал.
// Возвращает Неопределено, если эквайринговый терминал не найден или эквайринговых терминалов больше одного.
//
// Параметры:
//  Организация - СправочникСсылка.Кассы (СправочникСсылка.КассыККМ) - Ссылка на кассу
//
// Возвращаемое значение:
//	СправочникСсылка.ЭквайринговыеТерминалы - Найденный эквайринговый терминал
//
Функция ПолучитьЭквайринговыйТерминалПоУмолчанию(Касса) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал
	|ИЗ
	|	Справочник.СпособыОплаты КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	ЭквайринговыеТерминалы.Касса = &Касса
	|	И НЕ ЭквайринговыеТерминалы.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Касса", Касса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 
	   И Выборка.Следующий()
	Тогда
		ЭквайринговыйТерминал = Выборка.ЭквайринговыйТерминал;
	Иначе
		ЭквайринговыйТерминал = Неопределено;
	КонецЕсли;
	
	Возврат ЭквайринговыйТерминал;

КонецФункции // ПолучитьЭквайринговыйТерминалПоУмолчанию()

// Функция определяет эквайринговый терминал по выбранной кассе.
//
// Возвращает эквайринговый терминал, если найден один эквайринговый терминал.
// Возвращает Неопределено, если эквайринговый терминал не найден или эквайринговых терминалов больше одного.
//
// Параметры:
//  Организация - СправочникСсылка.Кассы (СправочникСсылка.КассыККМ) - Ссылка на кассу
//
// Возвращаемое значение:
//	СправочникСсылка.ЭквайринговыеТерминалы - Найденный эквайринговый терминал
//
Функция ПолучитьЭквайринговыйТерминалПоУмолчаниюДляОперацииЭквайринга(БанковскийСчет, Организация, Эквайрер = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ЭквайринговыеТерминалы.Ссылка КАК ЭквайринговыйТерминал
	|ИЗ
	|	Справочник.СпособыОплаты КАК ЭквайринговыеТерминалы
	|ГДЕ
	|	НЕ ЭквайринговыеТерминалы.ПометкаУдаления
	|	И (&БанковскийСчетЭквайринг = ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
	|			ИЛИ ЭквайринговыеТерминалы.БанковскийСчетЭквайринг = &БанковскийСчетЭквайринг)
	|	И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ЭквайринговыеТерминалы.Организация = &Организация)
	|	И (&Эквайрер = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ &Эквайрер = НЕОПРЕДЕЛЕНО
	|			ИЛИ ЭквайринговыеТерминалы.Эквайрер = &Эквайрер)");
	
	Запрос.УстановитьПараметр("БанковскийСчетЭквайринг", БанковскийСчет);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Эквайрер", Эквайрер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 
	   И Выборка.Следующий()
	Тогда
		ЭквайринговыйТерминал = Выборка.ЭквайринговыйТерминал;
	Иначе
		ЭквайринговыйТерминал = Неопределено;
	КонецЕсли;
	
	Возврат ЭквайринговыйТерминал;

КонецФункции // ПолучитьЭквайринговыйТерминалПоУмолчанию()

// Процедура получения списка видов платежных карт, обслуживаемых по договору эквайринга
//
// Параметры:
//  ЭквайринговыйТерминал - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на эквайринговый терминал
//
// Возвращаемое значение:
//	Массив - Массив видов платежных карт
//
Функция ВидыПлатежныхКарт(ЭквайринговыйТерминал) Экспорт
	
	МассивВидовПлатежныхКарт = Новый Массив;
	
	Если ЗначениеЗаполнено(ЭквайринговыйТерминал) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыПлатежныхКарт.ВидПлатежнойКарты КАК ВидПлатежнойКарты
		|ИЗ
		|	Справочник.СпособыОплаты.ВидыПлатежныхКарт КАК ВидыПлатежныхКарт
		|ГДЕ
		|	ВидыПлатежныхКарт.Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", ЭквайринговыйТерминал);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивВидовПлатежныхКарт.Добавить(Выборка.ВидПлатежнойКарты);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивВидовПлатежныхКарт;
	
КонецФункции // ВидыПлатежныхКарт()

// Процедура получения списка видов платежных карт, обслуживаемых по договору эквайринга
//
// Параметры:
//  ЭквайринговыйТерминал - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на эквайринговый терминал
//
// Возвращаемое значение:
//	Массив - Массив видов платежных карт
//
Функция ВидыПлатежныхКартИПроцентыКомиссии(ЭквайринговыйТерминал) Экспорт
	
	ТаблицаВидовПлатежныхКартИПроцентовКомиссии = Новый ТаблицаЗначений;
	
	Если ЗначениеЗаполнено(ЭквайринговыйТерминал) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыПлатежныхКарт.ВидПлатежнойКарты КАК ВидПлатежнойКарты,
		|	ВидыПлатежныхКарт.ПроцентКомиссии,
		|	ВидыПлатежныхКарт.ПроцентКомиссииПриОтмене,
		|	ВидыПлатежныхКарт.ПроцентКомиссииПриВозврате
		|ИЗ
		|	Справочник.СпособыОплаты.ВидыПлатежныхКарт КАК ВидыПлатежныхКарт
		|ГДЕ
		|	ВидыПлатежныхКарт.Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", ЭквайринговыйТерминал);
		
		ТаблицаВидовПлатежныхКартИПроцентовКомиссии = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Возврат ТаблицаВидовПлатежныхКартИПроцентовКомиссии;
	
КонецФункции // ВидыПлатежныхКарт()

Процедура ПометитьНаУдалениеТерминалыСДоговором(Договор) Экспорт
	
	НачатьТранзакцию();
	
	МассивТерминалов = ЭквайринговыеОперацииСервер.ПолучитьТерминалыПоДоговору(Договор);
	Для Каждого Терминал Из МассивТерминалов Цикл
		Если Не Терминал.ПометкаУдаления Тогда
			ТерминалОбъект = Терминал.ПолучитьОбъект();
			ТерминалОбъект.УстановитьПометкуУдаления(Истина);
		КонецЕсли;
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

// Функция возвращает вид платежной карты по умолчанию
//
// Параметры:
//  ТипСпособаОплаты - ПеречислениеСсылка.ТипыСпособовОплат	 - Тип способа оплаты
// 
// Возвращаемое значение:
//  Строка - Вид платежной карты
//
Функция ВидПлатежнойКартыПоУмолчанию(ТипСпособаОплаты) Экспорт
	
	Если ТипСпособаОплаты = Перечисления.ТипыСпособовОплат.СБП Тогда
		ВидПлатежнойКарты = ИнтеграцияСПлатежнымиСистемамиУНФКлиентСервер.ВидПлатежнойКартыСБП();
	ИначеЕсли ТипСпособаОплаты = Перечисления.ТипыСпособовОплат.ЮKassa Тогда
		ВидПлатежнойКарты = НСтр("ru = 'ЮKassa'");
	ИначеЕсли ТипСпособаОплаты = Перечисления.ТипыСпособовОплат.Кредит Тогда
		ВидПлатежнойКарты = НСтр("ru = 'Кредит'");
	Иначе
		ВидПлатежнойКарты = "";
	КонецЕсли;
	
	Возврат ВидПлатежнойКарты;
	
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И( ЗначениеРазрешено(Касса)
	|	ИЛИ ЗначениеРазрешено(Касса)
	|	)И ЗначениеРазрешено(БанковскийСчетЭквайринг)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// Функция возвращает список имен «ключевых» реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Тип");
	
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти

#КонецОбласти

#КонецЕсли
