
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("АдресНастроекОтбора", АдресНастроекОтбора)	Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЕстьСохраненныеСпособыОбеспечения() Тогда

		Отказ = Истина;
		ОткрытьПомощник();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СохранитьВыбранныйВариантРасчетаПотребностей" Тогда

		ВыбранноеЗначение = Элементы.Список.ТекущаяСтрока;
		
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			СохранитьНастройки(ВыбранноеЗначение, СтандартнаяОбработка);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СоздатьВариантРасчетаПотребностей" Тогда

		СтандартнаяОбработка = Ложь;
		ОткрытьПомощник();
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СохранитьНастройки(ВыбраннаяСтрока, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЕстьСохраненныеСпособыОбеспечения()
	
	Схема = Элементы.Список.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	КомпоновщикМакета 	= Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки 	= КомпоновщикМакета.Выполнить(
		Схема, 
		Настройки, 
		, 
		,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	СхемаЗапроса		= Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(МакетКомпоновки.НаборыДанных[0].Запрос);

	ПакетЗапроса 		= СхемаЗапроса.ПакетЗапросов[0];
	ОператорЗапроса 	= ПакетЗапроса.Операторы[0];	
		
	ОператорЗапроса.КоличествоПолучаемыхЗаписей = 1;

	ТекстЗапроса 		= СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос 				= Новый Запрос(ТекстЗапроса);
	
	Для каждого Параметр Из МакетКомпоновки.ЗначенияПараметров Цикл
		Запрос.Параметры.Вставить(Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПомощник(СсылкаНаСправочник = Неопределено)

	ПараметрыФормы		 = Новый Структура;

	ПараметрыФормы.Вставить("РежимВыбора", 			Истина);
	ПараметрыФормы.Вставить("АдресНастроекОтбора", 	АдресНастроекОтбора);

	Если ЗначениеЗаполнено(СсылкаНаСправочник) Тогда
		ПараметрыФормы.Вставить("Ключ", СсылкаНаСправочник);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ВариантыРасчетаПотребностей.ФормаОбъекта", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьПомощник(ВыбранноеЗначение);

	Закрыть();
	
КонецПроцедуры

#КонецОбласти
