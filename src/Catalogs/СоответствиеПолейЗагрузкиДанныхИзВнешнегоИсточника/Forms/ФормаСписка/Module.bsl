#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Список.Параметры.Элементы.Найти("ВсеПользователи")<>Неопределено Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ВсеПользователи", НСтр("ru='Все пользователи'"));
	КонецЕсли;

	УстановитьОтборы();

	УстановитьОтборНедействительныеСоответствия(ЭтотОбъект);

	УстановитьУсловноеОформление();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьНедействительные(Команда)
	
	Элементы.ПоказыватьНедействительные.Пометка = Не Элементы.ПоказыватьНедействительные.Пометка;
	
	УстановитьОтборНедействительныеСоответствия(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборНедействительныеСоответствия(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Недействителен",
		Ложь,
		,
		,
		Не Форма.Элементы.ПоказыватьНедействительные.Пометка);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Недействительные отображаются серым
	НовоеУсловноеОформление = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Недействителен");
	Отбор.ПравоеЗначение 	= Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборы()
	
	МассивПользователей = Новый Массив;
	МассивПользователей.Добавить(Пользователи.АвторизованныйПользователь());
	МассивПользователей.Добавить(Справочники.Пользователи.ПустаяСсылка());
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Пользователь",
		МассивПользователей, ВидСравненияКомпоновкиДанных.ВСписке, НСтр("ru ='Пользователь'"), Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
КонецПроцедуры

#КонецОбласти