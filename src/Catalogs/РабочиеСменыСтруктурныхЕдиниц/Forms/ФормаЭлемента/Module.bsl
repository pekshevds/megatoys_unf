#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
		И Параметры.Свойство("Склад") Тогда
		
		Объект.Склад = Параметры.Склад;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("СоздатьРабочуюСмену", , ИмяФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПродолжительностьРабочегоВремениПриИзменении(Элемент)
	
	РассчитатьНерабочееВремя();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоИнтервалаПриИзменении(Элемент)
	
	ОбновитьПоказателиВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеИнтервалаПриИзменении(Элемент)
	
	ОбновитьПоказателиВремени();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьНерабочееВремя() 
	
	ЧасНачала = Час(Объект.НачалоИнтервала);
	МинутаНачала = Минута(Объект.НачалоИнтервала);
	Объект.НачалоИнтервала = Дата(1,1,1,ЧасНачала, МинутаНачала, 0);
	
	ЧасОкончания = Час(Объект.ОкончаниеИнтервала);
	МинутаОкончания = Минута(Объект.ОкончаниеИнтервала);
	Объект.ОкончаниеИнтервала = Дата(1,1,1,ЧасОкончания, МинутаОкончания, 0);
	
	Если Объект.ОкончаниеИнтервала < Объект.НачалоИнтервала Тогда
		
		ОкончаниеИнтервала = Объект.ОкончаниеИнтервала + 86400;
		
	Иначе
		
		ОкончаниеИнтервала = Объект.ОкончаниеИнтервала;
		
	КонецЕсли;
	
	РазностьВСекундах = ОкончаниеИнтервала - Объект.НачалоИнтервала;
	
	Если РазностьВСекундах > 0 Тогда
		
		РазностьВСекундахВДате = ЗарплатаИПерсоналКлиентСервер.ПреобразоватьСекундыВДату(РазностьВСекундах);
		ПродолжительностьНерабочегоВремениВСекундах 
			= РазностьВСекундахВДате - Объект.ПродолжительностьРабочегоВремени;
		
		Если ПродолжительностьНерабочегоВремениВСекундах < 0 Тогда
			
			ОбновитьПоказателиВремени();
			
		Иначе
			
			ПродолжительностьНерабочегоВремени 
				= ЗарплатаИПерсоналКлиентСервер.ПреобразоватьСекундыВДату(ПродолжительностьНерабочегоВремениВСекундах);
			
		КонецЕсли;
		
		
	Иначе
		
		Объект.ПродолжительностьРабочегоВремени = Дата("00010101000000");
		ПродолжительностьНерабочегоВремени = Дата("00010101000000");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоказателиВремени()
	
	ЧасНачала = Час(Объект.НачалоИнтервала);
	МинутаНачала = Минута(Объект.НачалоИнтервала);
	Объект.НачалоИнтервала = Дата(1, 1, 1, ЧасНачала, МинутаНачала, 0);
	
	ЧасОкончания = Час(Объект.ОкончаниеИнтервала);
	МинутаОкончания = Минута(Объект.ОкончаниеИнтервала);
	Объект.ОкончаниеИнтервала = Дата(1, 1, 1, ЧасОкончания, МинутаОкончания, 0);
	
	Если Объект.ОкончаниеИнтервала < Объект.НачалоИнтервала Тогда	
		
		ОкончаниеИнтервала = Объект.ОкончаниеИнтервала + 86400;
		
	Иначе
		
		ОкончаниеИнтервала = Объект.ОкончаниеИнтервала;
		
	КонецЕсли;
	
	РазностьВСекундах = ОкончаниеИнтервала - Объект.НачалоИнтервала;
	
	Если РазностьВСекундах > 0 Тогда
		
		Объект.ПродолжительностьРабочегоВремени 
			= ЗарплатаИПерсоналКлиентСервер.ПреобразоватьСекундыВДату(РазностьВСекундах);
		ПродолжительностьНерабочегоВремени = Дата("00010101000000");
		
	Иначе
		
		Объект.ПродолжительностьРабочегоВремени   = Дата("00010101000000");
		ПродолжительностьНерабочегоВремени = Дата("00010101000000");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти