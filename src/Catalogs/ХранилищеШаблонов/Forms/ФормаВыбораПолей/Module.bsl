#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресСКД",АдресСКД) Тогда
		ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек,АдресСКД);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы
&НаКлиенте
Процедура КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПодготовитьДанныеВыбора()
	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанных, АдресНастроекКомпоновкиДанных = Неопределено)
	
	Попытка
		
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
			НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
		КонецЕсли;
		
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
		Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
			КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
		Иначе
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
		КонецЕсли;
		
		КомпоновщикНастроек.Восстановить();
		
	Исключение
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Не удалось инициализировать компоновщик настроек'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеВыбора()
	
	ТекущаяСтрока = Элементы.КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбора.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		
		Закрыть();
		
	Иначе
		
		ПолеВыбора = КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.
			ПолучитьОбъектПоИдентификатору(ТекущаяСтрока);
		
		Если НЕ ПолеВыбора = Неопределено Тогда
		
			ИмяЭлемента = Строка(ПолеВыбора.Поле);
			ПодготовленноеИмяЭлемента = СтрЗаменить(ИмяЭлемента,".","");
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("ИндексКартинки", 4);
			СтруктураСтроки.Вставить("Вычислять", Истина);
			
			Если СтрНайти(ИмяЭлемента, "Штрихкод") > 0 Тогда
				СтруктураСтроки.Вставить("ВыводитьКакШтрихкод", Истина);
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ОписаниеТипа", ПолеВыбора.ТипЗначения);
			
			СтруктураСтроки.Вставить("ИмяПоля", ПодготовленноеИмяЭлемента);
			Закрыть(СтруктураСтроки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
