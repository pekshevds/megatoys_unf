
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидМаркетплейса") И ЗначениеЗаполнено( Параметры.ВидМаркетплейса ) Тогда
		ЭтаФорма.ВидМаркетплейса = Параметры.ВидМаркетплейса;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ВидМаркетплейса", ЭтаФорма.ВидМаркетплейса, ВидСравненияКомпоновкиДанных.Равно);
		
		Если ЭтаФорма.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			ЗаголовокДополнение = "Яндекс маркет";
			Элементы.Организация.Видимость = Ложь;
			Элементы.ИсточникКатегории.Видимость = Ложь;
		ИначеЕсли ЭтаФорма.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			ЗаголовокДополнение = "Ozon";
		КонецЕсли;
		
		Элементы.Код.Видимость = Ложь;
		Элементы.ВидМаркетплейса.Видимость = Ложь;
		Элементы.ИдентификаторКлиента.Видимость = Ложь;
		ЭтаФорма.Заголовок = "Интеграции с " + ЗаголовокДополнение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВидовМаркетплейсовПослеВыбора( Значение, ДополнительныеПараметры ) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранВидМаркетплейса = Значение.Значение;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить( "ВидМаркетплейса", ВыбранВидМаркетплейса );
	ОткрытьФорму( "Справочник.УчетныеЗаписиМаркетплейсов.Форма.ФормаЭлемента", ПараметрыФормы, , ВыбранВидМаркетплейса );
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;

    Если ЗначениеЗаполнено( ЭтаФорма.ВидМаркетплейса ) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить( "ВидМаркетплейса", ЭтаФорма.ВидМаркетплейса );
		ОткрытьФорму( "Справочник.УчетныеЗаписиМаркетплейсов.Форма.ФормаЭлемента", ПараметрыФормы, , ВидМаркетплейса );
	Иначе
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить( ПредопределенноеЗначение( "Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет" ), "Яндекс маркет", , БиблиотекаКартинок.ЯндексМаркетЛоготип );
		СписокВыбора.Добавить( ПредопределенноеЗначение( "Перечисление.ВидыМаркетплейсов.МаркетплейсOzon" ), "Ozon", , БиблиотекаКартинок.ЛоготипOzon2 );
		
		ОповещениеОВыборе = Новый ОписаниеОповещения( "СписокВидовМаркетплейсовПослеВыбора", ЭтаФорма );
		ПоказатьВыборИзМеню( ОповещениеОВыборе, СписокВыбора, Элементы.Список );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "УдалениеИнтеграцииМаркетплейса"
	Или ИмяСобытия = "ЗаписьИнтеграцииМаркетплейса" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры
