#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("Организация", Организация);
		СтруктураНастроек.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
		СтруктураНастроек.Вставить("ИсточникКатегории", ИсточникКатегории);
		СтруктураНастроек.Вставить("ВалютаУчета", ВалютаУчета);
		СтруктураНастроек.Вставить("ВидыЦен", ВидыЦен.Выгрузить());
		
		ХешНастроек = Справочники.УчетныеЗаписиМаркетплейсов.ПолучитьХешНастроек(СтруктураНастроек);

		ИзменениеПометкиУдаления = 0;
		Если ПометкаУдаления = Истина И Ссылка.ПометкаУдаления = Ложь Тогда // Помечается
			ИзменениеПометкиУдаления = 1;
		ИначеЕсли ПометкаУдаления = Ложь И Ссылка.ПометкаУдаления = Истина Тогда // Снимается
			ИзменениеПометкиУдаления = -1;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ИзменениеПометкиУдаления", ИзменениеПометкиУдаления);
	КонецЕсли;

	Если ПометкаУдаления Или НЕ ИспользоватьРегламентныеЗадания Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.УдалитьРегламентноеЗаданиеОбмена(Ссылка);
	КонецЕсли;
	
	Если НЕ ПометкаУдаления И ИспользоватьРегламентныеЗадания Тогда
		Если НЕ ИнтеграцияСМаркетплейсомOzonСервер.ЕстьРегламентноеЗаданиеОбмена(Ссылка, ИдентификаторРегламентногоЗадания) Тогда
			
			ОписаниеНастроек = Новый Структура;
			ОписаниеНастроек.Вставить("Наименование", Наименование);
			ОписаниеНастроек.Вставить("ПометкаУдаления", ПометкаУдаления);
			ОписаниеНастроек.Вставить("ИдентификаторРегламентногоЗадания", ИдентификаторРегламентногоЗадания);
			ОписаниеНастроек.Вставить("ИспользоватьРегламентныеЗадания", ИспользоватьРегламентныеЗадания);
			ОписаниеНастроек.Вставить("РасписаниеРегламентногоЗадания", РасписаниеРегламентногоЗадания);
			
			ИдентификаторРегламентногоЗадания = ИнтеграцияСМаркетплейсомOzonСервер.СоздатьОбновитьРегламентноеЗаданиеОбмена(Ссылка, Ложь, Ложь, ОписаниеНастроек);
			
		ИначеЕсли РасписаниеРегламентногоЗадания <> Ссылка.РасписаниеРегламентногоЗадания Тогда
			ДополнительныеСвойства.Вставить("ОбновитьРасписание", Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если Отказ Или ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("ОбновитьРасписание") И ДополнительныеСвойства.ОбновитьРасписание = Истина Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.СоздатьОбновитьРегламентноеЗаданиеОбмена(Ссылка, Ложь, Истина);
	КонецЕсли;

КонецПроцедуры

Процедура ПередУдалением(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.УдалитьРегламентноеЗаданиеОбмена(Ссылка);
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ИдентификаторРегламентногоЗадания = Неопределено;
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
