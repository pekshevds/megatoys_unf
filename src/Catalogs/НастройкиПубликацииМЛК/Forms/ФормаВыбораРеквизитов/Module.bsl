
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРеквизитов(Параметры.НастройкиВыгрузкиРеквизитов, Параметры.ВыбраннаяНоменклатура);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокРеквизитовПриИзменении(Элемент)
	
	СортироватьПоПометке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВыбранныеРеквизиты = Новый Массив;
	
	Для каждого Реквизит Из СписокРеквизитов Цикл
		Если Реквизит.Пометка Тогда
			СтруктураРеквизита = Новый Структура;
			СтруктураРеквизита.Вставить("Имя", Реквизит.ИмяРеквизита);
			СтруктураРеквизита.Вставить("Представление", Реквизит.Представление);
			Если ЗначениеЗаполнено(Реквизит.Ссылка) Тогда
				СтруктураРеквизита.Вставить("УникальныйИдентификатор", Строка(Реквизит.Ссылка.УникальныйИдентификатор()));
			Иначе
				СтруктураРеквизита.Вставить("УникальныйИдентификатор", "");
			КонецЕсли; 
			ВыбранныеРеквизиты.Добавить(СтруктураРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(ВыбранныеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьСписокРеквизитов(НастройкиВыгрузкиРеквизитов, ВыбраннаяНоменклатура)
	
	ДоступныеРеквизиты = ПолучитьСписокДоступныхРеквизитов();
	ДополнительныеРеквизиты = ПолучитьСписокДополнительныхРеквизитов(ВыбраннаяНоменклатура);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДоступныеРеквизиты, ДополнительныеРеквизиты, Истина);
	
	Если ЗначениеЗаполнено(НастройкиВыгрузкиРеквизитов) Тогда
		СписокВыбранныхРеквизитов = КонструкторМобильногоПриложения.ЧтениеJSONВСтруктуру(НастройкиВыгрузкиРеквизитов);
	Иначе
		СписокВыбранныхРеквизитов = Новый Массив;
	КонецЕсли;
	
	Если ТипЗнч(СписокВыбранныхРеквизитов) = Тип("Массив") Тогда
		ТаблицаСВыбраннымиРеквизитами = ПреобразоватьМассивВТаблицуЗначений(СписокВыбранныхРеквизитов);
		ЕстьВыбранныеРеквизиты = ТаблицаСВыбраннымиРеквизитами.Количество() > 0;
	Иначе
		ЕстьВыбранныеРеквизиты = Ложь;
	КонецЕсли;
	
	Для каждого ТекущийРеквизит Из ДоступныеРеквизиты Цикл
		
		СвойстваРеквизитаСписка = Неопределено;
		// АПК:1036-выкл проверка орфографии не требуется
		ИмяРеквизита = УбратьЗапрещенныеСимволы(ТекущийРеквизит.ИмяРеквизита, "йцукенгшщзхъфывапролджэячсмитьбю_qwertyuiopasdfghjklzxcvbnm01234567890");
		// АПК:1036-вкл
		
		Если ЕстьВыбранныеРеквизиты Тогда
			НайденныеСтроки = ТаблицаСВыбраннымиРеквизитами.НайтиСтроки(Новый Структура("Имя", ИмяРеквизита));
			ЕстьРеквизит = НайденныеСтроки.Количество() > 0;
		Иначе
			ЕстьРеквизит = Ложь;
		КонецЕсли;
		
		Если ЕстьРеквизит Тогда
			ПредставлениеРеквизита = ?(ЗначениеЗаполнено(НайденныеСтроки[0].Представление),
				НайденныеСтроки[0].Представление, ТекущийРеквизит.Представление);
			Пометка = Истина;
		Иначе
			ПредставлениеРеквизита = ТекущийРеквизит.Представление;
			Пометка = Ложь;
		КонецЕсли;
		
		НовСтр = СписокРеквизитов.Добавить();
		НовСтр.ИмяРеквизита = ИмяРеквизита;
		НовСтр.Значение = ТекущийРеквизит.Значение;
		НовСтр.Представление = ПредставлениеРеквизита;
		НовСтр.Пометка = Пометка;
		НовСтр.Ссылка = ТекущийРеквизит.Ссылка;
		
	КонецЦикла;
	
	СортироватьПоПометке();
	
КонецФункции

&НаСервере
Процедура СортироватьПоПометке()
	
	ТЗРеквизитов = РеквизитФормыВЗначение("СписокРеквизитов");
	ТЗРеквизитов.Сортировать("Пометка Убыв, Значение Возр");
	
	ЗначениеВРеквизитФормы(ТЗРеквизитов, "СписокРеквизитов");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокДоступныхРеквизитов()
	
	МассивРеквизитов = Новый Массив;
	МассивИменРеквизитов = Новый Массив;
	
	МассивИменРеквизитов.Добавить("Вес");
	МассивИменРеквизитов.Добавить("Высота");
	МассивИменРеквизитов.Добавить("ГарантийныйСрок");
	МассивИменРеквизитов.Добавить("Артикул");
	МассивИменРеквизитов.Добавить("Весовой");
	МассивИменРеквизитов.Добавить("Длина");
	МассивИменРеквизитов.Добавить("ЕдиницаИзмерения");
	МассивИменРеквизитов.Добавить("НаименованиеПолное");
	МассивИменРеквизитов.Добавить("Объем");
	МассивИменРеквизитов.Добавить("Поставщик");
	МассивИменРеквизитов.Добавить("Производитель");
	МассивИменРеквизитов.Добавить("Склад");
	МассивИменРеквизитов.Добавить("СтранаПроисхождения");
	МассивИменРеквизитов.Добавить("Ширина");
	МассивИменРеквизитов.Добавить("ЭтоНабор");
	МассивИменРеквизитов.Добавить("ЭтоНовинка");
	
	Для каждого ИмяРеквизита Из МассивИменРеквизитов Цикл
		СтруктураРеквизита = Новый Структура("Значение,ИмяРеквизита,Представление, Ссылка");
		СтруктураРеквизита.Значение = ИмяРеквизита;
		СтруктураРеквизита.ИмяРеквизита = ИмяРеквизита;
		СтруктураРеквизита.Представление = ИмяРеквизита;
		МассивРеквизитов.Добавить(СтруктураРеквизита);
	КонецЦикла;
	
	Возврат МассивРеквизитов;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокДополнительныхРеквизитов(ВыбраннаяНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.КатегорияНоменклатуры.НаборСвойств КАК НаборСвойств
		|ПОМЕСТИТЬ ИспользуемыеНаборыСвойств
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&ВыбраннаяНоменклатура)
		|
		|СГРУППИРОВАТЬ ПО
		|	Номенклатура.КатегорияНоменклатуры.НаборСвойств
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НаборСвойств_Справочник_Номенклатура_Общие
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИспользуемыеНаборыСвойств.НаборСвойств КАК НаборСвойств,
		|	ДополнительныеРеквизитыИСведения.Наименование КАК Значение,
		|	ДополнительныеРеквизитыИСведения.Представление КАК Представление,
		|	ДополнительныеРеквизитыИСведения.Имя КАК ИмяРеквизита,
		|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
		|ИЗ
		|	ИспользуемыеНаборыСвойств КАК ИспользуемыеНаборыСвойств
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|		ПО ИспользуемыеНаборыСвойств.НаборСвойств = ДополнительныеРеквизитыИСведения.НаборСвойств";
	
	Запрос.УстановитьПараметр("ВыбраннаяНоменклатура", ВыбраннаяНоменклатура);
	НаборСвойств_Справочник_Номенклатура_Общие = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие");
	Запрос.УстановитьПараметр("НаборСвойств_Справочник_Номенклатура_Общие", НаборСвойств_Справочник_Номенклатура_Общие);
	
	ВыборкаРеквизитов = Запрос.Выполнить().Выбрать();
	
	МассивРеквизитов = Новый Массив;
	
	Пока ВыборкаРеквизитов.Следующий() Цикл
		СтруктураРеквизита = Новый Структура("Значение,ИмяРеквизита,Представление, Ссылка");
		ЗаполнитьЗначенияСвойств(СтруктураРеквизита, ВыборкаРеквизитов);
		МассивРеквизитов.Добавить(СтруктураРеквизита);
	КонецЦикла; 
	
	Возврат МассивРеквизитов;
	
КонецФункции

&НаСервере
Функция УбратьЗапрещенныеСимволы(Текст, Разрешенные)
	
	Результат = Текст;
	Для Сч = 1 По СтрДлина(Текст) Цикл
		ТекСимвол = Сред(Текст, Сч, 1);
		Если СтрНайти(Разрешенные, НРег(ТекСимвол))=0 Тогда
			Результат = СтрЗаменить(Результат, ТекСимвол,"");
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПреобразоватьМассивВТаблицуЗначений(ДанныеМС)
	
	ДанныеТЗ = Новый ТаблицаЗначений;
	Для Каждого ЭлементМассива Из ДанныеМС Цикл
		Если ДанныеТЗ.Колонки.Количество() = 0 Тогда
			Для Каждого ЗначениеСтруктуры Из ЭлементМассива Цикл
				МассивДопустимыеТипы = Новый Массив;
				ТипКолонки = ТипЗнч(ЗначениеСтруктуры.Значение); 
				МассивДопустимыеТипы.Добавить(ТипКолонки);
				Описание_Типов = Новый ОписаниеТипов(МассивДопустимыеТипы);
				ДанныеТЗ.Колонки.Добавить(ЗначениеСтруктуры.Ключ,Описание_Типов);
			КонецЦикла;
		КонецЕсли;
		НоваяСтрока = ДанныеТЗ.Добавить();
		Для Каждого ЗначениеСтруктуры Из ЭлементМассива Цикл
			НоваяСтрока[ЗначениеСтруктуры.Ключ] = ЗначениеСтруктуры.Значение;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДанныеТЗ;
	
КонецФункции

#КонецОбласти
