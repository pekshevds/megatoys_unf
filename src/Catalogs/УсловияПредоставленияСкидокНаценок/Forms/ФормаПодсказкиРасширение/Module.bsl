#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СсылкаУсловие = Параметры.Ссылка;
	Если Параметры.Свойство("АдресСхемыКомпоновкиДанных", АдресСКД) Тогда		
		СКДДляМодификации = ПолучитьИзВременногоХранилища(АдресСКД);
		АдресМодифицированнойСКД = ПоместитьВоВременноеХранилище(СКДДляМодификации, УникальныйИдентификатор);
	КонецЕсли;
	Параметры.Свойство("АдресНастроекКомпоновкиДанных", АдресНастроекСКД);
	ТекстРасширенияПроцедурыУстановкиПараметров =
	"&Вместо(""СписокСхемСКД"")
	|Функция МоеУсловие_СписокСхемСКД()
	|	ОписаниеМакетов = ПродолжитьВызов();
	|	// Вставить имя и представление добавленных макетов СКД
	|	ОписаниеМакетов.СхемыРасширений.Вставить(""МоеУсловие_ДанныеДокумента"", ""Доработанный фильтр по данным документа (МоеУсловие)"");
	|	Возврат ОписаниеМакетов;
	|КонецФункции
	|
	|&Вместо(""ПодготовитьОписаниеОбъектаПроизвольныхУсловий"")
	|Функция МоеУсловие_ПодготовитьОписаниеОбъектаПроизвольныхУсловий(Объект, Товары)
	|	Результат = ПродолжитьВызов(Объект, Товары);
	|	// Если в основе схемы лежит стандартный запрос, то проще сначала выполнить типовую функцию получения параметров
	|	// После чего реализовать необходимую для данной схемы логику заполнения новых/переопределенных параметров.
	|	СтандартныеПараметры = Справочники.УсловияПредоставленияСкидокНаценок.ПодготовитьСтандартноеОписаниеОбъекта(Объект, Товары);
	|	ИзмененныеПараметры = МоеУсловие_ДополнитьПараметры(СтандартныеПараметры);
	|	Результат.Вставить(""МоеУсловие_ДанныеДокумента"", ИзмененныеПараметры);
	|	Возврат Результат;
	|КонецФункции
	|
	|Функция МоеУсловие_ДополнитьПараметры(ИсходныеПараметры)
	|	Возврат ИсходныеПараметры;
	|КонецФункции";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ВидимостьНазадСправка = Ложь;
	ВидимостьНазадСКД = Ложь;
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОписаниеСКД Тогда
		ВидимостьНазадСправка = Истина;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСозданияРасширение Тогда
		ВидимостьНазадСКД = Истина;
	КонецЕсли; 
	Элементы.ПодвалОткрытьСтраницуСправка.Видимость = ВидимостьНазадСправка;
	Элементы.ПодвалОткрытьСтраницуСКД.Видимость = ВидимостьНазадСКД;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	Результат = ПодготовитьРезультат();
	Закрыть(Результат)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СохранитьРасширение(Команда)
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Расширение = "cfe";
	Фильтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расширение (*.%1)|*.%1'"), ВыборФайла.Расширение);
	ВыборФайла.Фильтр = Фильтр;
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Новый ОписаниеОповещения("СохранитьРасширениеПродолжение", ЭтотОбъект), ВыборФайла);

КонецПроцедуры  

&НаКлиенте
Процедура ОткрытьСтраницуСКД(Команда)
	ОткрытьСтраницу("СтраницаОписаниеСКД");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтраницуСправка(Команда)
	ОткрытьСтраницу("СтраницаСправка");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонстуркторСКД(Команда)
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр(
		"ru = 'Настройте схему компоновки данных для условия скидки '");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПомещатьНастройкиВСхемуКомпоновкиДанных", Ложь);
	ПараметрыФормы.Вставить("РедактироватьСхемуКомпоновкиДанных", Истина);
	ПараметрыФормы.Вставить("ЗагрузитьСхемуИзФайла", Истина);
	ПараметрыФормы.Вставить("НастраиватьОтбор", Истина);
	ПараметрыФормы.Вставить("НастраиватьПараметры", Истина);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресМодифицированнойСКД);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", АдресНастроекСКД);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);

	ОписаниеОповещения = Новый ОписаниеОповещения("РедактироватьСхемуКомпоновкиДанныхЗавершение", ЭтотОбъект);

	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", ПараметрыФормы, , , , , ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьРасширениеПродолжение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено
		ИЛИ ВыбранныеФайлы.Количество() = 0
		ИЛИ Не ЗначениеЗаполнено(ВыбранныеФайлы[0]) Тогда
		Возврат;
	КонецЕсли;
	ФайлДляЗаписи = Новый Файл(ВыбранныеФайлы[0]);
	Адрес = ПоместитьДанныеКаркасногоРасширенияВоВременноеХранилище();
	#Если ВебКлиент ИЛИ МобильныйКлиент Тогда
		ПолучитьФайл(Адрес, ФайлДляЗаписи.ИмяБезРасширения + "." + ФайлДляЗаписи.РасширениеБезТочки);
		Возврат;
	#КонецЕсли 
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвоичныеДанные.Записать(ФайлДляЗаписи.ПолноеИмя);
	
	Состояние(НСтр("ru = 'Сохранение успешно завершено'"));
КонецПроцедуры

&НаСервере
Функция ПоместитьДанныеКаркасногоРасширенияВоВременноеХранилище()	
	ДанныеФайла = Справочники.УсловияПредоставленияСкидокНаценок.ПолучитьМакет("КаркасРасширенияПроизвольныхУсловийСкидокНаценок");
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла);
КонецФункции

&НаКлиенте
Процедура ОткрытьСтраницу(ИмяСтраницы = "СтраницаОписаниеСКД")
	ИскомаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.Найти(ИмяСтраницы);
	Если Не ИскомаяСтраница = Неопределено Тогда
		Элементы.Страницы.ТекущаяСтраница = ИскомаяСтраница;
		СтраницыПриСменеСтраницы(Элементы.Страницы, ИскомаяСтраница); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтраницуРасширение(Команда)
	ОткрытьСтраницу("СтраницаСозданияРасширение");
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСхемуКомпоновкиДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт

	АдресНастроекСКД = Результат;

	Если ЗначениеЗаполнено(АдресНастроекСКД) Тогда
		СКДИзменена = Истина;
		Элементы.Закрыть.КнопкаПоУмолчанию = Ложь;
		Элементы.Применить.Видимость = Истина;
		Элементы.Применить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПодготовитьРезультат()

	Если СКДИзменена Тогда
		// Обновим схему СКД по тому же адресу, где она находилась изначально
		МодифицированнаяСКД = ПолучитьИзВременногоХранилища(АдресМодифицированнойСКД);
		ПоместитьВоВременноеХранилище(МодифицированнаяСКД, АдресСКД);
	КонецЕсли;
	
	Возврат АдресНастроекСКД;
	
КонецФункции

#КонецОбласти


