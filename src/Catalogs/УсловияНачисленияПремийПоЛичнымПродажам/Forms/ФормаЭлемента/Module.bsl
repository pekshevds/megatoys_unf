#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Наименование = СформироватьАвтоНаименованиеНаСервере();
	Иначе
		СформироватьАвтоНаименованиеНаСервере();
	КонецЕсли;
	
	Для каждого ВариантНаименования Из Элементы.Наименование.СписокВыбора Цикл
		Если Объект.Наименование = ВариантНаименования.Значение Тогда 
			
			ИспользуетсяАвтоНаименование = Истина;
			
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	НаименованиеИзмененоПользователем = Истина;

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		
		СформироватьАвтоНаименованиеНаКлиенте(); 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УсловиеНачисленияПриИзменении(Элемент)
	
	ОбновитьАвтонаименование(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеУсловияПриИзменении(Элемент) 
	
	ОбновитьАвтонаименование(Истина); 
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьАвтонаименование(Обновить = Истина)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) ИЛИ (Обновить И ИспользуетсяАвтоНаименование И Не НаименованиеИзмененоПользователем) Тогда
		
		Объект.Наименование = СформироватьАвтоНаименованиеНаКлиенте();
		ИспользуетсяАвтоНаименование = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьАвтоНаименованиеНаКлиенте()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	Если Объект.УсловиеНачисленияПремий = ПредопределенноеЗначение("Перечисление.УсловияНачисленияПремийПоЛичнымПродажам.ЗаОбъемПродажСумма") Тогда
		
		СтрокаНаименования = СтрШаблон(НСтр("ru = 'При достижении суммы продаж: %1 %2'"), Объект.ЗначениеУсловияНачисления, ВалютаУчета);;
		Элементы.ЗначениеУсловияНачисленияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru = 'В валюте учета %1'"), ВалютаУчета);
		
	ИначеЕсли Объект.УсловиеНачисленияПремий = ПредопределенноеЗначение("Перечисление.УсловияНачисленияПремийПоЛичнымПродажам.ЗаОбъемПродажКоличество") Тогда
		
		СтрокаНаименования = СтрШаблон(НСтр("ru = 'При достижении объема продаж в количестве: %1 ед. изм. хранения'"), Объект.ЗначениеУсловияНачисления);
		Элементы.ЗначениеУсловияНачисленияРасширеннаяПодсказка.Заголовок = "";
		
	КонецЕсли;
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Возврат СтрокаНаименования;

КонецФункции

&НаСервере
Функция СформироватьАвтоНаименованиеНаСервере()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	Если Объект.УсловиеНачисленияПремий = Перечисления.УсловияНачисленияПремийПоЛичнымПродажам.ЗаОбъемПродажСумма Тогда 
		
		СтрокаНаименования = СтрШаблон(НСтр("ru = 'При достижении суммы продаж: %1 %2'"), Объект.ЗначениеУсловияНачисления, ВалютаУчета);
		Элементы.ЗначениеУсловияНачисленияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru = 'В валюте учета %1'"), ВалютаУчета);
		
	ИначеЕсли Объект.УсловиеНачисленияПремий = Перечисления.УсловияНачисленияПремийПоЛичнымПродажам.ЗаОбъемПродажКоличество Тогда  
		
		СтрокаНаименование = СтрШаблон(НСтр("ru = 'При достижении объема продаж в количестве: %1 ед. изм. хранения'"), Объект.ЗначениеУсловияНачисления);
		Элементы.ЗначениеУсловияНачисленияРасширеннаяПодсказка.Заголовок = "";
		
	КонецЕсли;
	
	Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Возврат СтрокаНаименования;

КонецФункции

#КонецОбласти
