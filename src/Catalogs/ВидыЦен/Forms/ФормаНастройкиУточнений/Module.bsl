
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПереданнаяТаблица 	= ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	ДополнительныеПараметры	= Новый Структура();

	Если Параметры.Свойство("ДополнительныеПараметры", ДополнительныеПараметры)
		И ДополнительныеПараметры.Количество() Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДополнительныеПараметры);
		ВидВызова = Параметры.ДополнительныеПараметры.ВидВызова;
		
	КонецЕсли;
	
	ЗаполнитьТаблицуЗначенийИзПереданнойДанных(ПереданнаяТаблица);
	
	НастроитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПравилаОкругленияЦены

&НаКлиенте
Процедура ПравилаОкругленияЦеныПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Перем МножительГраницы;
	МножительГраницы = 10;
	
	Если НоваяСтрока Тогда
		
		Если ПравилаОкругленияЦены.Количество() > 1 Тогда
			
			БазовоеЗначениеГраницы = ПравилаОкругленияЦены[ПравилаОкругленияЦены.Количество() - 1].НижняяГраницаДиапазонаЦен ;
			
			Если Не ЗначениеЗаполнено(БазовоеЗначениеГраницы) Тогда
				БазовоеЗначениеГраницы = 1;
			КонецЕсли;
			
			Элемент.ТекущиеДанные.НижняяГраницаДиапазонаЦен = БазовоеЗначениеГраницы * МножительГраницы;
			
		КонецЕсли;
		
		НастроитьТаблицуПравилОкругления();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НижняяГраницаДиапазонаЦенПриИзменении(Элемент)
	
	НастроитьТаблицуПравилОкругления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОкругленияЦеныПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ОчиститьСообщения();
	
	Если Не ОтменаРедактирования Тогда
		
		Для Каждого СтрокаПравилОкругления Из ПравилаОкругленияЦены Цикл
			
			Если СтрокаПравилОкругления <> Элемент.ТекущиеДанные 
				И СтрокаПравилОкругления.НижняяГраницаДиапазонаЦен = Элемент.ТекущиеДанные.НижняяГраницаДиапазонаЦен Тогда
				
				Сообщение = НСтр("ru = 'Для диапазона от %НижняяГраница% уже заданы правила округления.'");
				ЗначениеНижнейГраницы = Элемент.ТекущиеДанные.НижняяГраницаДиапазонаЦен;
				
				Если ЗначениеЗаполнено(ВалютаЦены) Тогда
					ЗначениеНижнейГраницы = Строка(ЗначениеНижнейГраницы) + " " + ВалютаЦены;
				КонецЕсли;
				
				Сообщение = СтрЗаменить(Сообщение, "%НижняяГраница%", ЗначениеНижнейГраницы);
				
				НомерСтроки = ПравилаОкругленияЦены.Индекс(Элемент.ТекущиеДанные) + 1;
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ПравилаОкругленияЦены", 
					НомерСтроки , "НижняяГраницаДиапазонаЦен");
						
				ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщение, , , ПутьКДанным, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОкругленияЦеныПослеУдаления(Элемент)
	
	НастроитьТаблицуПравилОкругления();
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаОкругленияЦеныПриИзменении(Элемент)
	
	НастроитьТаблицуПравилОкругления();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОкругленияПриИзменении(Элемент)
	
	НастроитьТаблицуПравилОкругления();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)

	АдресВХранилище = ПоместитьВХранилищеТаблицу();
	
	ДополнительныеПараметры = Новый Структура;	
	
	Если ВидВызова = "УточнениеНастроекОкругления" Тогда		
		ДополнительныеПараметры.Вставить("ВариантОкругления", ОкруглятьВБольшуюСторону);	
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("АдресВХранилище, ВидВызова, ДополнительныеПараметры", 
		АдресВХранилище, ВидВызова, ДополнительныеПараметры);
	
	Закрыть(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуЗначенийИзПереданнойДанных(ПереданнаяТаблица)
		 
	Если ВидВызова = "УточнениеНастроекОкругления" Тогда
		ТекущаяТаблица = ПравилаОкругленияЦены;
	КонецЕсли;
	
	Если ВидВызова = "УточнениеПороговСрабатывания" Тогда
		ТекущаяТаблица = ПорогиСрабатывания;
	КонецЕсли;
	
	ТекущаяТаблица.Загрузить(ПереданнаяТаблица);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьДоступностьЗаголовок()
	
	ЗаголовокФормыНастройки = НСтр("ru = 'Уточните %1'");
	
	Если ВидВызова = "УточнениеПороговСрабатывания" Тогда
		 
		Элементы.ГруппаПорогиСрабатывания.Видимость = Истина;
		
		Подстрока = НСтр("ru = 'пороги срабатывания по ценовым группам'");
		ЗаголовокФормыНастройки = СтрЗаменить(ЗаголовокФормыНастройки, "%1", Подстрока);
		
	ИначеЕсли ВидВызова = "УточнениеНастроекОкругления" Тогда
		 
		Элементы.ГруппаПравилаОкругленияЦены.Видимость = Истина;
		
		Подстрока = НСтр("ru = 'ценовые диапазоны округления'");
		ЗаголовокФормыНастройки = СтрЗаменить(ЗаголовокФормыНастройки, "%1", Подстрока);
			
	Иначе
			
		ЗаголовокФормыНастройки = "";
		
	КонецЕсли;

	Заголовок = ЗаголовокФормыНастройки;
	
КонецПроцедуры

// Помещает во временное хранилище таблицу значений с выбранными изменениями
// и возвращает адрес.
// 
// Возвращаемое значение:
//	Строка - адрес хранилища значений таблицы значений во временном хранилище.
//
&НаСервере 
Функция ПоместитьВХранилищеТаблицу()

	Если ВидВызова = "УточнениеПороговСрабатывания" Тогда
		 
		ТекущаяТаблица = ПорогиСрабатывания.Выгрузить();
		
	ИначеЕсли ВидВызова = "УточнениеНастроекОкругления" Тогда
		 
		ТекущаяТаблица = ПравилаОкругленияЦены.Выгрузить();
		
	Иначе
		
		ТекущаяТаблица = Новый ТаблицаЗначений;
	
	КонецЕсли;
	
	АдресНастроекДоступности = ПоместитьВоВременноеХранилище(ТекущаяТаблица, УникальныйИдентификатор);
	
	Возврат АдресНастроекДоступности;
	
КонецФункции

&НаКлиенте
Процедура ПравилаОкругленияЦеныПриАктивизацииЯчейки(Элемент)
	
	Если Не Элемент.ТекущийЭлемент = Элементы.ТочностьОкругления Тогда
		Возврат;
	КонецЕсли;
	
	// Формирование списка выбора для поля ТочностьОкругления
	Элементы.ТочностьОкругления.СписокВыбора.Очистить();
	ЗначениеОкругления             = 0.01;
	ЧетноеПриращение               = Ложь;
	МаксимальноеЗначениеОкругления = 1000;
	ГраницаДиапазонаЦен            = ВерхняяГраницаДиапазонаЦен(Элементы.ПравилаОкругленияЦены.ТекущиеДанные);
	
	Пока ЗначениеОкругления <= МаксимальноеЗначениеОкругления 
		И (ЗначениеОкругления <= ГраницаДиапазонаЦен Или ГраницаДиапазонаЦен < 0) Цикл
		
		Элементы.ТочностьОкругления.СписокВыбора.Добавить(ЗначениеОкругления);
		ЗначениеОкругления = ЗначениеОкругления * ?(ЧетноеПриращение, 2, 5);
		ЧетноеПриращение   = Не ЧетноеПриращение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ВерхняяГраницаДиапазонаЦен(СтрокаПравилОкругления)
	
	Индекс = ПравилаОкругленияЦены.Индекс(СтрокаПравилОкругления);
	
	Если Индекс < ПравилаОкругленияЦены.Количество() - 1 Тогда
		Возврат ПравилаОкругленияЦены[Индекс + 1].НижняяГраницаДиапазонаЦен;
	КонецЕсли;
	
	Возврат -1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПример(Форма, Индекс)
	
	Число = 987654.321;
	ДиапазонГраниц = 1000;
	ДелительДиапазона = 2;
	ЧислоСПлавающейТочкой = 0.11;
	
	Если Форма.ПравилаОкругленияЦены.Количество() > 1 Тогда
		
		НижняяГраница = Форма.ПравилаОкругленияЦены[Индекс].НижняяГраницаДиапазонаЦен;
		Если Индекс = Форма.ПравилаОкругленияЦены.Количество() - 1 Тогда
			ВерхняяГраница = НижняяГраница + ДиапазонГраниц;
		Иначе
			ВерхняяГраница = Форма.ПравилаОкругленияЦены[Индекс + 1].НижняяГраницаДиапазонаЦен;
		КонецЕсли;
		
		Число = НижняяГраница + Окр((ВерхняяГраница - НижняяГраница) / ДелительДиапазона, 15, 2);
		Если Число + ЧислоСПлавающейТочкой < ВерхняяГраница Тогда
			Число = Число + ЧислоСПлавающейТочкой;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Форма.ПравилаОкругленияЦены[Индекс].ТочностьОкругления <> 0 
		ИЛИ Форма.ПравилаОкругленияЦены[Индекс].ПсихологическоеОкругление <> 0 Тогда
		
		Цена = ЦенообразованиеСервер.ОкруглитьЦену(
			Число, 
			Форма.ПравилаОкругленияЦены[Индекс].ТочностьОкругления, 
			Форма.ОкруглятьВБольшуюСторону);
			
		Значение = ЦенообразованиеКлиентСервер.ПрименитьПсихологическоеОкругление(
			Цена,
			Форма.ПравилаОкругленияЦены[Индекс].ПсихологическоеОкругление);
		
		ФорматнаяСтрока = "ЧДЦ=2; ЧРГ=' '; ЧН=; ЧГ=0";
		
		Параметр1 = Формат(Число, ФорматнаяСтрока);
		Параметр2 = Формат(Значение, ФорматнаяСтрока);
		СтрокаШаблон = НСтр("ru = 'Округлять %1 до %2'");
		Форма.ПравилаОкругленияЦены[Индекс].Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СтрокаШаблон,
			Параметр1,
			Параметр2);
			
	Иначе
		Форма.ПравилаОкругленияЦены[Индекс].Подсказка = НСтр("ru = 'Не округлять'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьТаблицуПравилОкругления()
	
	Если ПравилаОкругленияЦены.Количество() > 1 Тогда
		НастроитьТаблицуПравилОкругленияСервер(ЭтотОбъект);
	Иначе
		
		Если ПравилаОкругленияЦены.Количество() = 1 Тогда
			
			Если ПравилаОкругленияЦены[0].НижняяГраницаДиапазонаЦен = 0 Тогда
				ЦеновойДиапазон = НСтр("ru = 'Все цены'");
			Иначе
				
				ЦеновойДиапазон = НСтр("ru = 'Свыше %НачалоДиапазона%'");
				ЦеновойДиапазон = СтрЗаменить(ЦеновойДиапазон, "%НачалоДиапазона%", 
					ПравилаОкругленияЦены[0].НижняяГраницаДиапазонаЦен);
				
				Если ЗначениеЗаполнено(ВалютаЦены) Тогда
					ЦеновойДиапазон = ЦеновойДиапазон + " " + ВалютаЦены;
				КонецЕсли;
				
			КонецЕсли;
			
			ПравилаОкругленияЦены[0].ЦеновойДиапазон = ЦеновойДиапазон;
			
			УстановитьПример(ЭтотОбъект, 0);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьТаблицуПравилОкругленияСервер(Форма)
	
	Если Форма.ПравилаОкругленияЦены.Количество() > 0 Тогда
		Форма.ПравилаОкругленияЦены.Сортировать("НижняяГраницаДиапазонаЦен");
	КонецЕсли;
	
	МаксимальныйИндекс = Форма.ПравилаОкругленияЦены.Количество() - 1;
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		
		Если Форма.ПравилаОкругленияЦены.Количество() = 1 
			И Форма.ПравилаОкругленияЦены[Индекс].НижняяГраницаДиапазонаЦен = 0 Тогда
			
			ЦеновойДиапазон = НСтр("ru = 'Все цены'");
			
		Иначе
			
			Если Индекс = МаксимальныйИндекс Тогда
				
				ЦеновойДиапазон = НСтр("ru = 'Свыше %НачалоДиапазона%'");
				ЦеновойДиапазон = СтрЗаменить(ЦеновойДиапазон, 
					"%НачалоДиапазона%", Форма.ПравилаОкругленияЦены[Индекс].НижняяГраницаДиапазонаЦен);
				
			Иначе
				
				ЦеновойДиапазон = НСтр("ru = 'От %НачалоДиапазона% до %ОкончаниеДиапазона%'");
				ЦеновойДиапазон = СтрЗаменить(ЦеновойДиапазон, "%НачалоДиапазона%",
					Форма.ПравилаОкругленияЦены[Индекс].НижняяГраницаДиапазонаЦен);
					
				ЦеновойДиапазон = СтрЗаменить(ЦеновойДиапазон, "%ОкончаниеДиапазона%", 
					Форма.ПравилаОкругленияЦены[Индекс + 1].НижняяГраницаДиапазонаЦен);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Форма.ВалютаЦены) Тогда
				ЦеновойДиапазон = ЦеновойДиапазон + " " + Форма.ВалютаЦены;
			КонецЕсли;
			
		КонецЕсли;
		
		Форма.ПравилаОкругленияЦены[Индекс].ЦеновойДиапазон = ЦеновойДиапазон;
		
		УстановитьПример(Форма, Индекс);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	НастроитьВидимостьДоступностьЗаголовок();
	
КонецПроцедуры

#КонецОбласти