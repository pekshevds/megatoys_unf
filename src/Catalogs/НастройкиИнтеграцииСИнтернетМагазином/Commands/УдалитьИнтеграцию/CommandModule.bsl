
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ИмяФормы = ПараметрыВыполненияКоманды.Источник.ИмяФормы;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Источник", ПараметрыВыполненияКоманды.Источник);
	ДополнительныеПараметры.Вставить("ЭтоФормаНастройки", СтрНайти(ИмяФормы, "ФормаСписка") = 0);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалениеНастройкиИнтеграцииЗавершение",
	ЭтотОбъект,
	ДополнительныеПараметры
	);
	
	ПараметрыФормы = Новый Структура("НастройкаИнтеграции", ПараметрКоманды);
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииСИнтернетМагазином.Форма.УдалениеНастройкиИнтеграции",
	ПараметрыФормы,
	ПараметрыВыполненияКоманды.Источник,
	ПараметрыВыполненияКоманды.Уникальность,
	ПараметрыВыполненияКоманды.Окно,
	ПараметрыВыполненияКоманды.НавигационнаяСсылка,
	ОписаниеОповещения,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалениеНастройкиИнтеграцииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		
		Если ДополнительныеПараметры.ЭтоФормаНастройки Тогда
			ФормаНастройки = ДополнительныеПараметры.Источник;
			ФормаНастройки.Модифицированность = Ложь;
			ФормаНастройки.Закрыть();
		КонецЕсли;
		
		Оповестить("ОбновитьИнтеграцияСИнтернетМагазином");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
