#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаИнтеграции = Параметры.НастройкаИнтеграции;
	ВыгружатьТолькоИзменения = Параметры.ВыгружатьТолькоИзменения;
	
	Если Параметры.Свойство("ОбменЗаказами") Тогда
		ОбменЗаказами = Параметры.ОбменЗаказами;
	КонецЕсли;
	Если Параметры.Свойство("ОбменТоварами") Тогда
		ОбменТоварами = Параметры.ОбменТоварами;
	КонецЕсли;
	
	Элементы.НадписьОбменПрогресс.Заголовок = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВыполняетсяОбмен;
	ПодключитьОбработчикОжидания("ЗапуститьВыполнениеОбмена", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьВыполнениеОбмена()
	
	ПараметрыОбмена = Новый Структура;
	ПараметрыОбмена.Вставить("НастройкаИнтеграции", НастройкаИнтеграции);
	ПараметрыОбмена.Вставить("ВыгружатьТолькоИзменения", ВыгружатьТолькоИзменения);
	
	ДлительнаяОперация = ВыполнитьОбменССайтомНаСервере(ПараметрыОбмена, ЭтотОбъект.УникальныйИдентификатор);
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания( ЭтотОбъект );
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения( "ОповещениеОПрогрессеВыполнения", ЭтотОбъект );
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗавершенияОбменаССайтом", ЭтотОбъект.ВладелецФормы, ЭтотОбъект );
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессеВыполнения( Результат, ДополнительныеПараметры ) Экспорт
	
	Если ТипЗнч( Результат.Прогресс ) = Тип( "Структура" ) И Результат.Прогресс.Свойство( "Текст" ) Тогда
		Текст = Результат.Прогресс.Текст;
		Элементы.НадписьОбменПрогресс.Заголовок = "" + Текст;
	КонецЕсли;
	
	Если Результат.Свойство( "Сообщения" )
	И ТипЗнч( Результат.Сообщения ) = Тип( "ФиксированныйМассив" )
	И Результат.Сообщения.Количество() > 0 Тогда

			// группировка сообщений
			Сообщения = Новый Массив;
			Для Каждого СообщениеЭлемент Из Результат.Сообщения Цикл
				Если Сообщения.Найти( СообщениеЭлемент.Текст ) = Неопределено Тогда
					Сообщения.Добавить( СокрЛП( СообщениеЭлемент.Текст ) );
				КонецЕсли;
			КонецЦикла;
			
			// вывод сообщений
			СообщениеВФорму = Новый СообщениеПользователю;
			СообщениеВФорму.ИдентификаторНазначения = ЭтотОбъект.ВладелецФормы.УникальныйИдентификатор;
			Для Каждого СообщениеТекст Из Сообщения Цикл
				СообщениеВФорму.Текст = СообщениеТекст;
				СообщениеВФорму.Сообщить();
			КонецЦикла;
			
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьОбменССайтомНаСервере(ПараметрыОбмена, ИдентификаторФормы )
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполнение обмена с сайтом'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("ИнтеграцияСИнтернетМагазиномСервер.ВыполнитьОбменИнтерактивно",
	ПараметрыОбмена, ПараметрыВыполнения);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьЖурналНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("Данные", НастройкаИнтеграции);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
