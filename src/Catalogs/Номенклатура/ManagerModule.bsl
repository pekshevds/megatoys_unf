#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	Возврат;
КонецПроцедуры

// Функция возвращает основной счет учета запасов
// 
// Возвращаемое значение:
//  ПланСчетовСсылка.Управленческий - Счет учета запасов
//
Функция СчетУчетаЗапасов() Экспорт
	
	СчетПоУмолчанию = Константы.СчетУчетаЗапасовДляНоменклатурыПоУмолчанию.Получить();
	
	Если НЕ ЗначениеЗаполнено(СчетПоУмолчанию) Тогда
		СчетПоУмолчанию = ПланыСчетов.Управленческий.СырьеИМатериалы;
	КонецЕсли;
	
	Возврат СчетПоУмолчанию;
	
КонецФункции

#КонецОбласти

// Функция возвращает список имен «ключевых» реквизитов.
//
// Возвращаемое значение:
// 	Массив - ключевые реквизиты
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ЕдиницаИзмерения");
	Результат.Добавить("ТипНоменклатуры");
	
	Если Справочники.ВидыЦен.ИспользуютсяЦеновыеГруппыВДинамическихВидахЦен() Тогда
		
		Результат.Добавить("ЦеноваяГруппа");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает список реквизитов, которые разрешается редактировать
// с помощью обработки группового изменения объектов.
//
// Возвращаемое значение:
// 	Массив - реквизиты, которые разрешается редактировать в обработке группового изменения.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	
	РедактируемыеРеквизиты.Добавить("Артикул");
	РедактируемыеРеквизиты.Добавить("Вес");
	РедактируемыеРеквизиты.Добавить("Весовой");
	РедактируемыеРеквизиты.Добавить("ВыписыватьГарантийныйТалон");
	РедактируемыеРеквизиты.Добавить("Высота");
	РедактируемыеРеквизиты.Добавить("ГарантийныйСрок");
	РедактируемыеРеквизиты.Добавить("ДатаОкончанияДействия");
	РедактируемыеРеквизиты.Добавить("Длина");
	РедактируемыеРеквизиты.Добавить("ИсключитьИзПрайсЛистов");
	РедактируемыеРеквизиты.Добавить("ИспользоватьДатуПроизводстваПартии");
	РедактируемыеРеквизиты.Добавить("ИспользоватьПартии");
	РедактируемыеРеквизиты.Добавить("ИспользоватьХарактеристики");
	РедактируемыеРеквизиты.Добавить("КатегорияНоменклатуры");
	РедактируемыеРеквизиты.Добавить("Комментарий");
	РедактируемыеРеквизиты.Добавить("МетодОценки");
	РедактируемыеРеквизиты.Добавить("НаправлениеДеятельности");
	РедактируемыеРеквизиты.Добавить("Недействителен");
	РедактируемыеРеквизиты.Добавить("НижняяГраницаОстатков");
	РедактируемыеРеквизиты.Добавить("Номинал");
	РедактируемыеРеквизиты.Добавить("Объем");
	РедактируемыеРеквизиты.Добавить("Периодичность");
	РедактируемыеРеквизиты.Добавить("Поставщик");
	РедактируемыеРеквизиты.Добавить("ПроверятьЗаполнениеПартий");
	РедактируемыеРеквизиты.Добавить("ПроверятьЗаполнениеХарактеристики");
	РедактируемыеРеквизиты.Добавить("Производитель");
	РедактируемыеРеквизиты.Добавить("РеквизитДопУпорядочиванияУНФ");
	РедактируемыеРеквизиты.Добавить("Склад");
	РедактируемыеРеквизиты.Добавить("Изготовитель");
	РедактируемыеРеквизиты.Добавить("СпособПополнения");
	РедактируемыеРеквизиты.Добавить("СрокДействияФлагаНовинка");
	РедактируемыеРеквизиты.Добавить("СрокИсполненияЗаказа");
	РедактируемыеРеквизиты.Добавить("СрокПополнения");
	РедактируемыеРеквизиты.Добавить("ВидСтавкиНДС");
	РедактируемыеРеквизиты.Добавить("СтранаПроисхождения");
	РедактируемыеРеквизиты.Добавить("СчетУчетаДоходов");
	РедактируемыеРеквизиты.Добавить("СчетУчетаЗапасов");
	РедактируемыеРеквизиты.Добавить("СчетУчетаЗатрат");
	РедактируемыеРеквизиты.Добавить("Ширина");
	РедактируемыеРеквизиты.Добавить("ЭтоНовинка");
	РедактируемыеРеквизиты.Добавить("Ячейка");
	РедактируемыеРеквизиты.Добавить("ПризнакПредметаРасчета");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	
	РедактируемыеРеквизиты.Добавить("ЕдиницаИзмерения");
	РедактируемыеРеквизиты.Добавить("ТипНоменклатуры");
	РедактируемыеРеквизиты.Добавить("ЦеноваяГруппа");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

// Возвращает основную цену продажи для указанной номенклатуры, по указанному виду цен.
//
// Параметры:
//  ВидЦен - СправочникСсылка.ВидыЦен - вид цен,  
//        - Неопределено - основной вид цен вычисляется см. Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи,
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура по которой необходимо вычислить цену (обязательна к заполнению);
//  ЕдиницаИзмерения - СправочникСсылка.КлассификаторЕдиницИзмерения, СправочникСсылка.ЕдиницыИзмерения, Неопределено - .
// Возвращаемое значение:
// 	Произвольный, Число - Описание
Функция ПолучитьОсновнуюЦенуПродажи(ВидЦен, Номенклатура, ЕдиницаИзмерения = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) 
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ЦеныНоменклатуры) Тогда
		
		Возврат 0;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК ОсновнаяЦенаПродажи
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			,
	|			ВидЦен = &ВидЦен
	|				И Номенклатура = &Номенклатура
	|				И Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				И Актуальность
	|				И &ПараметрЕдиницаИзмерения) КАК ЦеныНоменклатурыСрезПоследних");
	
	Запрос.УстановитьПараметр("ВидЦен", 
		?(ЗначениеЗаполнено(ВидЦен), ВидЦен, Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи()));
	
	Запрос.УстановитьПараметр("Номенклатура", 
		Номенклатура);
		
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрЕдиницаИзмерения", "ЕдиницаИзмерения = &ЕдиницаИзмерения");
		Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрЕдиницаИзмерения", "ИСТИНА");
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.ОсновнаяЦенаПродажи, 0);
	
КонецФункции

Функция ЕстьХарактеристики(Номенклатура) Экспорт
	
	Характеристики = Справочники.ХарактеристикиНоменклатуры.Выбрать(, Номенклатура);
	Если Характеристики.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Формирует строку представления номенклатуры с учетом характеристики и серий.
//
// Параметры:
//  НоменклатураПредставление - Строка - представление номенклатуры,
//  ХарактеристикаПредставление - Строка - представление характеристики,
//  ПартияПредставление - Строка - представление партии,
//  ЗаказПокупателяПредставление - Строка - представление партии.
//
// Возвращаемое значение:
//  Строка - строка с представлением номенклатуры.
Функция Представление(НоменклатураПредставление, ХарактеристикаПредставление = "", ПартияПредставление = "",
	ЗаказПокупателяПредставление = "") Экспорт
	
	КомпонентыПредставления = Новый Массив;
	КомпонентыПредставления.Добавить(СокрЛП(НоменклатураПредставление));
	
	Если ЗначениеЗаполнено(ХарактеристикаПредставление)Тогда
		КомпонентыПредставления.Добавить(СокрЛП(ХарактеристикаПредставление))
	КонецЕсли;
	
	Если  ЗначениеЗаполнено(ПартияПредставление) Тогда
		КомпонентыПредставления.Добавить(СокрЛП(ПартияПредставление));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказПокупателяПредставление) Тогда
		КомпонентыПредставления.Добавить(СокрЛП(ЗаказПокупателяПредставление));
	КонецЕсли;
	
	Возврат СтрСоединить(КомпонентыПредставления, " / ");
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.ВыборГруппИЭлементов <> ИспользованиеГруппИЭлементов.Группы
		И Не Параметры.Отбор.Свойство("Недействителен") Тогда
		
		Параметры.Отбор.Вставить("Недействителен", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	Возврат;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ЗагрузкаДанныхИзВнешнегоИсточника

Процедура ПриОпределенииЗначенияПоУмолчанию(СправочникСсылка, ИмяРеквизита, ВходящиеДанные, СтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию)
	
	Если СтрокаСопоставлена 
		И НЕ ЗначениеЗаполнено(ВходящиеДанные) Тогда
		
		ЗначениеПоУмолчанию = СправочникСсылка[ИмяРеквизита];
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииОбразцовЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	Образец_xlsx = ПолучитьМакет("ОбразецЗагрузкиДанных_xlsx");
	ОбразецЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Образец_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_xlsx", ОбразецЗагрузкиДанных_xlsx);
	
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_mxl", "ОбразецЗагрузкиДанных_mxl");
	
	Образец_csv = ПолучитьМакет("ОбразецЗагрузкиДанных_csv");
	ОбразецЗагрузкиДанных_csv = ПоместитьВоВременноеХранилище(Образец_csv, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_csv", ОбразецЗагрузкиДанных_csv);
	
	Шаблон_xlsx = ПолучитьМакет("ШаблонЗагрузкиДанных_xlsx");
	ШаблонЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Шаблон_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ШаблонЗагрузкиДанных_xlsx", ШаблонЗагрузкиДанных_xlsx);
	
КонецПроцедуры

// Поля загрузки данных из внешнего источника.
// 
// Параметры:
//  ТаблицаПолейЗагрузки - см. ЗагрузкаДанныхИзВнешнегоИсточника.СоздатьТаблицуПолейОписанияЗагрузки
//  НастройкиЗагрузкиДанных - см. ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере
//
Процедура ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	ОписанияТиповПолей = ЗагрузкаДанныхИзВнешнегоИсточника.НовыйОписанияТиповПолейЗагрузки();
	
	ЭтоФиксированныйШаблон = НастройкиЗагрузкиДанных.Свойство("ФиксированныйШаблон") И НастройкиЗагрузкиДанных.ФиксированныйШаблон;
	ЗначениеЗагружатьДопРеквизитыВХарактеристики = 2;
	
	ПроверитьИДобавитьКолонкиФиксированногоШаблона(ТаблицаПолейЗагрузки, ЭтоФиксированныйШаблон, 
		ОписанияТиповПолей.ОписаниеТиповСтрока50, ОписанияТиповПолей.ОписаниеТиповБулево); 
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПоляНоменклатуры(ТаблицаПолейЗагрузки, ОписаниеТиповКолонка,
		ОписанияТиповПолей, НастройкиЗагрузкиДанных);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПоляГруппыНоменклатуры(ТаблицаПолейЗагрузки, ОписанияТиповПолей,
		ОписаниеТиповКолонка);
	
	ПредставлениеЭтоГруппа = НСтр("ru = 'Это группа'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЭтоГруппа", 
		ПредставлениеЭтоГруппа, ОписанияТиповПолей.ОписаниеТиповБулево, ОписанияТиповПолей.ОписаниеТиповБулево);

	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры");
	ПредставлениеТипНоменклатуры = НСтр("ru = 'Тип номенклатуры'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ТипНоменклатуры", 
		ПредставлениеТипНоменклатуры, ОписанияТиповПолей.ОписаниеТиповСтрока11, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения");
	ПредставлениеЕдиницаИзмерения = НСтр("ru = 'Ед. измерения'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЕдиницаИзмерения", 
		ПредставлениеЕдиницаИзмерения, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ВидыЦен");
	ПредставлениеВидЦенНаименование = НСтр("ru = 'Вид цен (наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВидЦен", 
		ПредставлениеВидЦенНаименование, ОписанияТиповПолей.ОписаниеТиповСтрока100, ОписаниеТиповКолонка, , , , , Ложь);
		
	ПредставлениеВыписыватьГарантийныйТалон = НСтр("ru='Выписывать гарантийный талон'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВыписыватьГарантийныйТалон",
		ПредставлениеВыписыватьГарантийныйТалон, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповБулево);
		
	ПредставлениеГарантийныйСрок = НСтр("ru='Гарантийный срок, мес.'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ГарантийныйСрок",
		ПредставлениеГарантийныйСрок, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);	

	ПредставлениеЦена = НСтр("ru = 'Цена'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Цена", 
		ПредставлениеЦена, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло15_2);
		
	Если ЭтоФиксированныйШаблон Тогда
		ПредставлениеКоличество = НСтр("ru = 'Количество'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Количество", 
			ПредставлениеКоличество, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);
	КонецЕсли;
	
	ПредставлениеКартинка = НСтр("ru='Картинка номенклатуры'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Картинка", 
		ПредставлениеКартинка, ОписанияТиповПолей.ОписаниеТиповСтрока1000, ОписанияТиповПолей.ОписаниеТиповСтрока1000);
		
	ПредставлениеОсновнаяКартинка = НСтр("ru='Основная картинка номенклатуры'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "КартинкаПоУмолчанию", 
		ПредставлениеОсновнаяКартинка, ОписанияТиповПолей.ОписаниеТиповСтрока1000, ОписанияТиповПолей.ОписаниеТиповСтрока1000);
		
	ПредставлениеАдресКартинки = НСтр("ru='Адрес картинки'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "АдресКартинки", 
		ПредставлениеАдресКартинки, ОписанияТиповПолей.ОписаниеТиповСтрока1000, ОписанияТиповПолей.ОписаниеТиповСтрока1000, 
		, , , , Ложь);
		
	ПредставлениеКартинка = НСтр("ru='Картинка характеристики'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "КартинкаХарактеристики",
		ПредставлениеКартинка, ОписанияТиповПолей.ОписаниеТиповСтрока1000,
		ОписанияТиповПолей.ОписаниеТиповСтрока1000);
		
	ПроверитьИДобавитьКолонкиЕГАИС(ТаблицаПолейЗагрузки, ОписанияТиповПолей.ОписаниеТиповСтрока19);
	
	ПроверитьИДобавитьКолонкиУчетаГТД(ТаблицаПолейЗагрузки, ОписанияТиповПолей.ОписаниеТиповСтрока10);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	ПредставлениеПоставщикИНН = НСтр("ru = 'Поставщик (ИНН или наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Поставщик", 
		ПредставлениеПоставщикИНН, ОписанияТиповПолей.ОписаниеТиповСтрока100, ОписаниеТиповКолонка);
	
	ПроверитьИДобавитьКолонкиСерий(ТаблицаПолейЗагрузки, ОписанияТиповПолей.ОписаниеТиповСтрока150, 
		ОписанияТиповПолей.ОписаниеТиповСтрока25);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы");
	ПредставлениеСкладНаименование = НСтр("ru = 'Склад (наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Склад", 
		ПредставлениеСкладНаименование, ОписанияТиповПолей.ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы");
	ПредставлениеИзготовительНаименование = НСтр("ru = 'Изготовитель (наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Изготовитель", 
		ПредставлениеИзготовительНаименование, ОписанияТиповПолей.ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
		
	ПредставлениеСрокПополнения = НСтр("ru = 'Срок пополнения'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СрокПополнения", 
		ПредставлениеСрокПополнения, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_0);
		
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС");
	ПредставлениеСтавкаНДС = НСтр("ru = 'Ставка НДС'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтавкаНДС", 
		ПредставлениеСтавкаНДС, ОписанияТиповПолей.ОписаниеТиповСтрока11, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
	ПредставлениеФлагНовинка = НСтр("ru = 'Флаг ""Новинка""'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЭтоНовинка", 
		ПредставлениеФлагНовинка, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ПредставлениеСрокНовинки = НСтр("ru = 'Дата (срок ""Новинки"")'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СрокДействияФлагаНовинка", 
		ПредставлениеСрокНовинки, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповДата);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
	ПредставлениеИсключитьИзПрайсЛистов = НСтр("ru = 'Флаг ""Исключить из прайс-листов""'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ИсключитьИзПрайсЛистов", 
		ПредставлениеИсключитьИзПрайсЛистов, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ПредставлениеНижняяГраницаОстатков = НСтр("ru = 'Нижняя граница остатков'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НижняяГраницаОстатков", 
		ПредставлениеНижняяГраницаОстатков, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло15_2);
		
	ПредставлениеВерхняяГраницаОстатков = НСтр("ru = 'Верхняя граница остатков'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВерхняяГраницаОстатков", 
		ПредставлениеВерхняяГраницаОстатков, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло15_2);
	
	ПроверитьИДобавитьКолонкиЯчеек(ТаблицаПолейЗагрузки, ОписанияТиповПолей.ОписаниеТиповСтрока50);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ЦеновыеГруппы");
	ПредставлениеЦеноваяГруппа = НСтр("ru = 'Ценовая группа (наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЦеноваяГруппа", 
		ПредставлениеЦеноваяГруппа, ОписанияТиповПолей.ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		
		ПредставлениеИспользоватьХарактеристики = НСтр("ru = 'Использовать характеристики'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, 
			"ИспользоватьХарактеристики", ПредставлениеИспользоватьХарактеристики, 
			ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
			
		ПредставлениеОбязательноеЗаполнениеХарактеристик = НСтр("ru = 'Обязательное заполнение характеристики'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, 
			"ПроверятьЗаполнениеХарактеристики", ПредставлениеОбязательноеЗаполнениеХарактеристик, 
			ОписанияТиповПолей.ОписаниеТиповСтрока100, ОписаниеТиповКолонка);
			
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПоляХарактеристики(ТаблицаПолейЗагрузки, ОписанияТиповПолей);

	КонецЕсли;
	
	ПроверитьИДобавитьКолонкиПартий(ТаблицаПолейЗагрузки, ОписаниеТиповКолонка, ОписанияТиповПолей.ОписаниеТиповСтрока25);
	
	ПроверитьИДобавитьКолонкиНаборов(ТаблицаПолейЗагрузки, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ПредставлениеОписание = НСтр("ru = 'Описание'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Комментарий", 
		ПредставлениеОписание, ОписанияТиповПолей.ОписаниеТиповСтрока0000, ОписанияТиповПолей.ОписаниеТиповСтрока0000);
	
	ПредставлениеСрокИсполненияЗаказа = НСтр("ru = 'Срок исполнения заказа'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СрокИсполненияЗаказа", 
		ПредставлениеСрокИсполненияЗаказа, ОписанияТиповПолей.ОписаниеТиповСтрока11, ОписанияТиповПолей.ОписаниеТиповЧисло10_0);
	
	ПредставлениеНормаВремени = НСтр("ru = 'Норма времени'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НормаВремени", 
		ПредставлениеНормаВремени, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);
	
	ПредставлениеВесБрутто = НСтр("ru = 'Вес брутто, кг'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Вес",
		ПредставлениеВесБрутто, ОписанияТиповПолей.ОписаниеТиповСтрока25, Метаданные.ОпределяемыеТипы.Вес.Тип);
	
	ПредставлениеОбъем = НСтр("ru = 'Объем, м³'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Объем", 
		ПредставлениеОбъем, ОписанияТиповПолей.ОписаниеТиповСтрока25, Метаданные.ОпределяемыеТипы.Объем.Тип);
		
	ПредставлениеДлина = НСтр("ru = 'Длина'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Длина", 
		ПредставлениеДлина, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);
		
	ПредставлениеШирина = НСтр("ru = 'Ширина'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Ширина", 
		ПредставлениеШирина, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);
		
	ПредставлениеВысота = НСтр("ru = 'Высота'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Высота", 
		ПредставлениеВысота, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписанияТиповПолей.ОписаниеТиповЧисло10_3);
		
	ПредставлениеОсновнойШтрихкод = НСтр("ru = 'Основной штрихкод'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ШтрихкодПоУмолчанию",
		ПредставлениеОсновнойШтрихкод, ОписанияТиповПолей.ОписаниеТиповСтрока200, ОписанияТиповПолей.ОписаниеТиповСтрока25);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
	ПредставлениеСтоимостьДляРабот = НСтр("ru = 'Фикс. стоимость (для работ)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ФиксированнаяСтоимость", 
		ПредставлениеСтоимостьДляРабот, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтраныМира");
	ПредставлениеСтранаПроисхождения = НСтр("ru = 'Страна происхождения (код или наименование)'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтранаПроисхождения", 
		ПредставлениеСтранаПроисхождения, ОписанияТиповПолей.ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ПроверитьИДобавитьКолонкиДополнительныхРеквизитов(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных, 
		ОписанияТиповПолей.ОписаниеТиповСтрока11, ОписанияТиповПолей.ОписаниеТиповСтрока150, ЗначениеЗагружатьДопРеквизитыВХарактеристики);
	
	ПредставлениеРеквизитыШаблона = НСтр("ru = 'ДопРеквизитыШаблона'");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ДопРеквизитыШаблона", 
		ПредставлениеРеквизитыШаблона, ОписанияТиповПолей.ОписаниеТиповСтрока0000, ОписанияТиповПолей.ОписаниеТиповСтрока0000, 
		, , , , Ложь);
		
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиФиксированногоШаблона(ТаблицаПолейЗагрузки, 
	Знач ЭтоФиксированныйШаблон, ОписаниеТиповСтрока50, ОписаниеТиповБулево)
	Если ЭтоФиксированныйШаблон Тогда
		ПредставлениеУИД = НСтр("ru = 'УИД'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "УИД", 
			ПредставлениеУИД, ОписаниеТиповСтрока50, ОписаниеТиповСтрока50);
		ПредставлениеЭтоУслуга = НСтр("ru = 'Это услуга'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЭтоУслуга", 
			ПредставлениеЭтоУслуга, ОписаниеТиповБулево, ОписаниеТиповБулево);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиЕГАИС(ТаблицаПолейЗагрузки, ОписаниеТиповСтрока19)
	Если ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС");
		ПредставлениеКодПоЕГАИС = НСтр("ru = 'Код по ЕГАИС'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ПродукцияЕГАИС", 
			ПредставлениеКодПоЕГАИС, ОписаниеТиповСтрока19, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиДополнительныхРеквизитов(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных, 
	ОписаниеТиповСтрока11, ОписаниеТиповСтрока150, Знач ЗначениеЗагружатьДопРеквизитыВХарактеристики)

	Перем ПредставлениеГруппыДополнительныхРеквизитов;
	Перем ИмяПоля;
	
	ВладелецНоменклатура = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура");
	ВладелецХарактеристики = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_ХарактеристикиНоменклатуры");

	ЗагрузкаДанныхИзВнешнегоИсточника.ПодготовитьСоответствиеПоДополнительнымРеквизитам(НастройкиЗагрузкиДанных,
		ВладелецНоменклатура, "ОписаниеДополнительныхРеквизитовНоменклатура");
		
	Если НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитовНоменклатура.Количество() > 0 Тогда

		ПредставлениеГруппыДополнительныхРеквизитов = НСтр("ru = 'Дополнительные реквизиты номенклатуры'"); 
		ИмяПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяПоляДобавленияДополнительныхРеквизитов();

		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, ИмяПоля, 
			ПредставлениеГруппыДополнительныхРеквизитов, ОписаниеТиповСтрока150, 
			ОписаниеТиповСтрока11, , , , , , Истина, ВладелецНоменклатура);
		
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ПодготовитьСоответствиеПоДополнительнымРеквизитам(НастройкиЗагрузкиДанных,
		ВладелецХарактеристики, "ОписаниеДополнительныхРеквизитовХарактеристики");
		
	Если НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитовХарактеристики.Количество() > 0 Тогда
			
		ПредставлениеГруппыДополнительныхРеквизитов = НСтр("ru = 'Дополнительные реквизиты характеристик'");
		ИмяПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяПоляДобавленияДополнительныхРеквизитов();

		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, ИмяПоля, 
			ПредставлениеГруппыДополнительныхРеквизитов, ОписаниеТиповСтрока150, 
			ОписаниеТиповСтрока11, , , , , , Истина, ВладелецХарактеристики);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиНаборов(ТаблицаПолейЗагрузки, ОписаниеТиповСтрока25, ОписаниеТиповКолонка)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНаборы") Тогда
		
		ПредставлениеФлагНабор = НСтр("ru = 'Флаг ""Набор""'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЭтоНабор", 
			ПредставлениеФлагНабор, ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиПартий(ТаблицаПолейЗагрузки, ОписаниеТиповКолонка, ОписаниеТиповСтрока25)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
		
		ПредставлениеИспользоватьПартии = НСтр("ru = 'Использовать партии'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ИспользоватьПартии", 
			ПредставлениеИспользоватьПартии, ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиЯчеек(ТаблицаПолейЗагрузки, ОписаниеТиповСтрока50)
	Если ПолучитьФункциональнуюОпцию("УчетПоЯчейкам") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Ячейки");
		ПредставлениеЯчейкаНаименование = НСтр("ru = 'Ячейка (наименование)'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Ячейка", 
			ПредставлениеЯчейкаНаименование, ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиСерий(ТаблицаПолейЗагрузки, ОписаниеТиповСтрока150, ОписаниеТиповСтрока25)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
		ПредставлениеИспользоватьСерии = НСтр("ru = 'Использовать Серии номенклатуры'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ИспользоватьСерииНоменклатуры", 
			ПредставлениеИспользоватьСерии, ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры");
		ПредставлениеСерия = НСтр("ru = 'Серия'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Серия", 
			ПредставлениеСерия, ОписаниеТиповСтрока150, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИДобавитьКолонкиУчетаГТД(ТаблицаПолейЗагрузки, ОписаниеТиповСтрока10)
	Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КлассификаторТНВЭД");
		ПредставлениеТНВЭД = НСтр("ru = 'ТН ВЭД'");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ТоварнаяНоменклатураВЭД", 
			ПредставлениеТНВЭД, ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
		
	КонецЕсли;
КонецПроцедуры

// АПК:299-выкл используется в модуле обработки ПомощникЗагрузкиДанныхИзВнешнегоИсточника

// Сопоставляет данные из ДанныеФормыКоллекция
//
// Параметры:
//  ПараметрыСопоставления - Структура:
//    * ТаблицаСопоставленияДанных - ДанныеФормыКоллекция - таблица для обработки
//    * НастройкиЗагрузкиДанных - Структура - настройки загрузки
//  АдресРезультата - УникальныйИдентификатор - адрес, по которому будет помещен результат обработки
//  АдресДополнительногоРезультата - УникальныйИдентификатор - адрес по которому будет помещен
//                                                             дополнительный результат
//
Процедура СопоставитьЗагружаемыеДанныеИзВнешнегоИсточника(
		ПараметрыСопоставления, 
		АдресРезультата = "", 
		АдресДополнительногоРезультата = "") Экспорт
	
	ТаблицаСопоставленияДанных	= ПараметрыСопоставления.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных		= ПараметрыСопоставления.НастройкиЗагрузкиДанных;	
	ОбновлятьДанные				= НастройкиЗагрузкиДанных.ОбновлятьСуществующие;
	ФиксированныйШаблон			= НастройкиЗагрузкиДанных.ФиксированныйШаблон;
	НастройкиПоиска				= НастройкиЗагрузкиДанных.НастройкиПоиска;
	ТаблицаДублирующихСтрок		= ПустаяТаблицаДублирующихСтрок();
	ИспользоватьХарактеристики	= НастройкиЗагрузкиДанных.ИспользоватьХарактеристики;
	ВидЦен						= НастройкиЗагрузкиДанных.ВидЦен;
	
	НастройкиПоиска.Вставить("ТаблицаДублирующихСтрок", ТаблицаДублирующихСтрок);
	
	СоответствиеИерархииКэш = Новый Соответствие;
	ДеревоИерархии = Новый ДеревоЗначений;
	ДеревоИерархии.Колонки.Добавить("Каталог", Новый ОписаниеТипов("Строка", ,
											   Новый КвалификаторыСтроки(150, ДопустимаяДлина.Переменная)));
	ДеревоИерархии.Колонки.Добавить("ГруппаСсылка", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ДеревоИерархии.Колонки.Добавить("УИД", Новый ОписаниеТипов("Строка", ,
											   Новый КвалификаторыСтроки(40, ДопустимаяДлина.Переменная)));
	
	// ТаблицаСопоставленияДанных - Тип ДанныеФормыКоллекция
	Для каждого СтрокаТаблицыФормы Из ТаблицаСопоставленияДанных Цикл
		
		ЭтоГруппа_ВходящиеДанные = СокрЛП(НРег(СтрокаТаблицыФормы.ЭтоГруппа_ВходящиеДанные));
		СтрокаТаблицыФормы.ЭтоГруппа = ЭтоГруппа_ВходящиеДанные = "да" 
			ИЛИ ЭтоГруппа_ВходящиеДанные = "true" 
			ИЛИ ЭтоГруппа_ВходящиеДанные = "1";
		
		// Номенклатура по ШтрихКоду, Артикулу, Наименованию, НаименованиеПолное
		ПроверитьИСопоставитьЭлементСправочникаПриЗагрузке(СтрокаТаблицыФормы, НастройкиПоиска); 
		
		ЭтаСтрокаСопоставлена = ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура);
		
		// Родитель может быть задан как строка иерархии с разделителями
		СтрокаИерархии = СокрЛП(СтрокаТаблицыФормы.РодительНаименование);
		
		ПроверитьИЗаполнитьПолеРодительПоИерархии(СтрокаТаблицыФормы, ДеревоИерархии, 
			СоответствиеИерархииКэш, СтрокаИерархии);
		
		ПроверитьИЗаполнитьПолеРодительПоКодуИлиНаименованию(ЭтаСтрокаСопоставлена, 
			СтрокаТаблицыФормы, ОбновлятьДанные);
		
		Если НЕ СтрокаТаблицыФормы.ЭтоГруппа Тогда
			
			СопоставитьДанныеНоменклатуры(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных, ИспользоватьХарактеристики, 
				ОбновлятьДанные, ФиксированныйШаблон, ВидЦен, ЭтаСтрокаСопоставлена);
			
		КонецЕсли; 
		
		ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы);
		
		ЗагрузкаДанныхИзВнешнегоИсточника.
			ПрогрессСопоставленияДанных(ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицыФормы), РазмерТаблицыДанных);
		
	КонецЦикла;
	
	ТаблицаСопоставленияДанных.ЗагрузитьКолонку(ТаблицаДублирующихСтрок.ВыгрузитьКолонку("КлючСвязи"), "_КлючСвязи");
	
	ПоместитьВоВременноеХранилище(ТаблицаСопоставленияДанных, АдресРезультата);
	
	Если ДеревоИерархии.Строки.Количество() > 0 Тогда
		ПоместитьВоВременноеХранилище(ДеревоИерархии, АдресДополнительногоРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьДанныеНоменклатуры(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных, Знач ИспользоватьХарактеристики, 
	ОбновлятьДанные, Знач ФиксированныйШаблон, ВидЦен, ЭтаСтрокаСопоставлена)
	
	Перем ЗначениеПоУмолчанию;
	Перем ОрганизацияПоУмолчанию;
	Перем ВидСтавкиНДСПоУмолчанию;
	ПроверитьИСопоставитьЭтоУслуга(ФиксированныйШаблон, СтрокаТаблицыФормы);
	
	// Тип номенклатуры (реквизиты, закрытые для редактирования не правим)
	ПроверитьИСопоставитьТипНоменклатуры(СтрокаТаблицыФормы, ЭтаСтрокаСопоставлена);
	
	// ЕдиницыИзмерения по Наименованию (так же рассмотреть возможность прикрутить пользовательские ЕИ)
	ЗначениеПоУмолчанию = Справочники.КлассификаторЕдиницИзмерения.шт;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "ЕдиницаИзмерения", 
		СтрокаТаблицыФормы.ЕдиницаИзмерения_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьЕдиницыИзмерения(СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.ЕдиницаИзмерения, 
		СтрокаТаблицыФормы.ЕдиницаИзмерения_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	
	// МетодОценки
	ЗначениеПоУмолчанию = Перечисления.МетодОценкиЗапасов.ПоСредней;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "МетодОценки", 
		СтрокаТаблицыФормы.МетодОценки_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьМетодОценки(СтрокаТаблицыФормы.МетодОценки, 
		СтрокаТаблицыФормы.МетодОценки_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// НаправлениеДеятельности по наименованию
	ЗначениеПоУмолчанию = Справочники.НаправленияДеятельности.ОсновноеНаправление;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "НаправлениеДеятельности", 
		СтрокаТаблицыФормы.НаправлениеДеятельности_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьНаправлениеДеятельности(СтрокаТаблицыФормы.НаправлениеДеятельности, 
		СтрокаТаблицыФормы.НаправлениеДеятельности_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// КатегорияНоменклатуры по наименованию
	ЗначениеПоУмолчанию = ПроверитьИСопоставитьКатегориюПоУмолчанию(ФиксированныйШаблон);
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "КатегорияНоменклатуры", 
		СтрокаТаблицыФормы.КатегорияНоменклатуры_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьКатегориюНоменклатуры(СтрокаТаблицыФормы.КатегорияНоменклатуры, 
		СтрокаТаблицыФормы.КатегорияНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// ВидыЦен по наименованию
	ПроверитьИСопоставитьВидЦенПоУмолчанию(ВидЦен, СтрокаТаблицыФормы);

	// ПродукцияЕГАИС по коду
	ПроверитьИСопоставитьПродукциюЕГАИСПоКоду(СтрокаТаблицыФормы);
	
	ПроверитьИСопоставитьНоменклатуруВЭД(СтрокаТаблицыФормы);
	
	// Поставщик по ИНН, Наименованию
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьПоставщика(СтрокаТаблицыФормы.Поставщик, 
		СтрокаТаблицыФормы.Поставщик_ВходящиеДанные);
	
	// Серии номенклатуры
	ПроверитьИСопоставитьСерииНоменклатуры(СтрокаТаблицыФормы, ЭтаСтрокаСопоставлена);
	
	// Склад по наименованию
	ЗначениеПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "Склад", 
		СтрокаТаблицыФормы.Склад_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтруктурнуюЕдиницу(СтрокаТаблицыФормы.Склад, 
		СтрокаТаблицыФормы.Склад_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// Изготовитель по наименованию
	ЗначениеПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "Изготовитель", 
		СтрокаТаблицыФормы.Изготовитель_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСтруктурнуюЕдиницу(СтрокаТаблицыФормы.Изготовитель, 
			СтрокаТаблицыФормы.Изготовитель_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// СпособПополнения по наименованию
	ЗначениеПоУмолчанию = Перечисления.СпособыПополненияЗапасов.Закупка;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СпособПополнения", 
		СтрокаТаблицыФормы.СпособПополнения_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСпособПополнения(СтрокаТаблицыФормы.СпособПополнения, 
		СтрокаТаблицыФормы.СпособПополнения_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// СрокПополнения
	ЗначениеПоУмолчанию = 1;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СрокПополнения", 
		СтрокаТаблицыФормы.СрокПополнения_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.СрокПополнения, 
		СтрокаТаблицыФормы.СрокПополнения_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// СтавкаНДС по наименованию
	ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
	ВидСтавкиНДСПоУмолчанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияПоУмолчанию,
		"ВидСтавкиНДСПоУмолчанию");
	ЗначениеПоУмолчанию = ?(СтрокаТаблицыФормы.ТипНоменклатуры 
		= ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат"),
	УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС(),
	Справочники.СтавкиНДС.СтавкаНДС(ВидСтавкиНДСПоУмолчанию));
	
	ПроверитьИСопоставитьСтавкуНДС(ЭтаСтрокаСопоставлена, ЗначениеПоУмолчанию, СтрокаТаблицыФормы);
	
	// Прайс-лист
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ЭтоНовинка, 
		СтрокаТаблицыФормы.ЭтоНовинка_ВходящиеДанные);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ИсключитьИзПрайсЛистов, 
			СтрокаТаблицыФормы.ИсключитьИзПрайсЛистов_ВходящиеДанные);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВДату(СтрокаТаблицыФормы.СрокДействияФлагаНовинка, 
			СтрокаТаблицыФормы.СрокДействияФлагаНовинка_ВходящиеДанные);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.НижняяГраницаОстатков, 
			СтрокаТаблицыФормы.НижняяГраницаОстатков_ВходящиеДанные);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.ВерхняяГраницаОстатков, 
			СтрокаТаблицыФормы.ВерхняяГраницаОстатков_ВходящиеДанные);
			
	// Гарантийный срок
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(
		СтрокаТаблицыФормы.ВыписыватьГарантийныйТалон, СтрокаТаблицыФормы.ВыписыватьГарантийныйТалон_ВходящиеДанные);
		
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(
		СтрокаТаблицыФормы.ГарантийныйСрок, СтрокаТаблицыФормы.ГарантийныйСрок_ВходящиеДанные);
	
	// СчетУчетаЗапасов по коду, наименованию
	ЗначениеПоУмолчанию = Справочники.Номенклатура.СчетУчетаЗапасов();
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СчетУчетаЗапасов", 
		СтрокаТаблицыФормы.СчетУчетаЗапасов_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСчетУчетаЗапасов(СтрокаТаблицыФормы.СчетУчетаЗапасов, 
			СтрокаТаблицыФормы.СчетУчетаЗапасов_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// СчетУчетаЗатрат по коду, наименованию
	ЗначениеПоУмолчанию = ?(ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуПроизводство"), 
		ПланыСчетов.Управленческий.НезавершенноеПроизводство, ПланыСчетов.Управленческий.КоммерческиеРасходы);
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СчетУчетаЗатрат", 
		СтрокаТаблицыФормы.СчетУчетаЗатрат_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСчетУчетаЗатрат(СтрокаТаблицыФормы.СчетУчетаЗатрат, 
			СтрокаТаблицыФормы.СчетУчетаЗатрат_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// СчетУчетаДоходов по коду, наименованию
	ЗначениеПоУмолчанию = ?(СтрокаТаблицыФормы.ТипНоменклатуры 
		= ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат"), 
		ПланыСчетов.Управленческий.ПрочиеДоходы, ПланыСчетов.Управленческий.ПустаяСсылка());
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СчетУчетаДоходов", 
		СтрокаТаблицыФормы.СчетУчетаДоходов_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСчетУчетаДоходов(СтрокаТаблицыФормы.СчетУчетаДоходов, 
			СтрокаТаблицыФормы.СчетУчетаДоходов_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	ПроверитьИСопоставитьЯчейки(ЭтаСтрокаСопоставлена, ОбновлятьДанные, СтрокаТаблицыФормы);
	
	// ЦеноваяГруппа по наименованию
	ЗначениеПоУмолчанию = Справочники.ЦеновыеГруппы.ПустаяСсылка();
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "ЦеноваяГруппа", 
		СтрокаТаблицыФормы.ЦеноваяГруппа_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьЦеновуюГруппу(СтрокаТаблицыФормы.ЦеноваяГруппа, 
		СтрокаТаблицыФормы.ЦеноваяГруппа_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// ИспользоватьХарактеристики
	ПроверитьИСопоставитьПараметрыХарактеристик(СтрокаТаблицыФормы, ИспользоватьХарактеристики);
	
	Если ИспользоватьХарактеристики Тогда
		// Характеристика
		ВладелецХарактеристики = СтрокаТаблицыФормы.КатегорияНоменклатуры;
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристикуПоНаименованиюАртикулу(
				СтрокаТаблицыФормы, ВладелецХарактеристики);
				
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицыФормы.Характеристика) Тогда
				ВладелецХарактеристики = СтрокаТаблицыФормы.Номенклатура;
				ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристикуПоНаименованиюАртикулу(
				СтрокаТаблицыФормы, ВладелецХарактеристики);
			КонецЕсли; 
			
	КонецЕсли;
		
	// ИспользоватьПартии
	ПроверитьИСопоставитьИспользоватьПартии(СтрокаТаблицыФормы);
	
	ПроверитьИСопоставитьЭтоНабор(СтрокаТаблицыФормы);
	
	// Комментарий как строка
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.Комментарий, 
		СтрокаТаблицыФормы.Комментарий_ВходящиеДанные);
	
	// СрокИсполненияЗаказа
	ЗначениеПоУмолчанию = 1;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, 
		"СрокИсполненияЗаказа", СтрокаТаблицыФормы.СрокИсполненияЗаказа_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.СрокИсполненияЗаказа, 
		СтрокаТаблицыФормы.СрокИсполненияЗаказа_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// НормаВремени
	ЗначениеПоУмолчанию = 0;
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "НормаВремени", 
		СтрокаТаблицыФормы.НормаВремени_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.НормаВремени, 
			СтрокаТаблицыФормы.НормаВремени_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	//Вес
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Вес, 
		СтрокаТаблицыФормы.Вес_ВходящиеДанные, 0);
	
	//Объем
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Объем, 
		СтрокаТаблицыФормы.Объем_ВходящиеДанные, 0);
	
	// Длина
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Длина, 
		СтрокаТаблицыФормы.Длина_ВходящиеДанные, 0);
	
	// Ширина
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Ширина, 
		СтрокаТаблицыФормы.Ширина_ВходящиеДанные, 0);
	
	// Высота
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Высота, 
		СтрокаТаблицыФормы.Высота_ВходящиеДанные, 0);
	
	// Штрихкод по умолчанию
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.ШтрихкодПоУмолчанию, 
			СтрокаТаблицыФормы.ШтрихкодПоУмолчанию_ВходящиеДанные);
	
	// ФиксированнаяСтоимость
	ПроверитьИСопоставитьФиксированнаяСтоимость(СтрокаТаблицыФормы);
	
	// СтранаПроисхождения по коду
	ЗначениеПоУмолчанию = Справочники.СтраныМира.ПустаяСсылка();
	ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "СтранаПроисхождения", 
		СтрокаТаблицыФормы.СтранаПроисхождения_ВходящиеДанные, 
		ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
		СопоставитьСтрануПроисхождения(СтрокаТаблицыФормы.СтранаПроисхождения, 
			СтрокаТаблицыФормы.СтранаПроисхождения_ВходящиеДанные, ЗначениеПоУмолчанию);
	
	// Цена
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Цена, 
		СтрокаТаблицыФормы.Цена_ВходящиеДанные);
	
	// Количество
	ПроверитьИСопоставитьКоличество(НастройкиЗагрузкиДанных, СтрокаТаблицыФормы);
	
	// Дополнительные реквизиты
	ПроверитьИСопоставитьДополнительныеРеквизиты(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных);
КонецПроцедуры

Процедура ПроверитьИСопоставитьВидЦенПоУмолчанию(ВидЦен, СтрокаТаблицыФормы)
	ЗначениеПоУмолчанию = ?(ЗначениеЗаполнено(ВидЦен), ВидЦен, Справочники.Контрагенты.ПолучитьОсновнойВидЦенПродажи());
	ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьВидЦен(СтрокаТаблицыФормы.ВидЦен, 
		СтрокаТаблицыФормы.ВидЦен_ВходящиеДанные, ЗначениеПоУмолчанию);
КонецПроцедуры

Функция ПроверитьИСопоставитьКатегориюПоУмолчанию(Знач ФиксированныйШаблон)
	Если ФиксированныйШаблон Тогда
		ЗначениеПоУмолчанию = Справочники.КатегорииНоменклатуры.ПустаяСсылка();
	Иначе
		ЗначениеПоУмолчанию = Справочники.КатегорииНоменклатуры.БезКатегории;
	КонецЕсли;
	Возврат ЗначениеПоУмолчанию;
КонецФункции

Процедура ПроверитьИСопоставитьТипНоменклатуры(СтрокаТаблицыФормы, Знач ЭтаСтрокаСопоставлена)
	Если ЭтаСтрокаСопоставлена Тогда
		СтрокаТаблицыФормы.ТипНоменклатуры = СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры;
	Иначе
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			СопоставитьТипНоменклатуры(СтрокаТаблицыФормы.ТипНоменклатуры, 
				СтрокаТаблицыФормы.ТипНоменклатуры_ВходящиеДанные, Перечисления.ТипыНоменклатуры.Запас);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьЭтоУслуга(Знач ФиксированныйШаблон, СтрокаТаблицыФормы)
	Если ФиксированныйШаблон Тогда
		СтрокаТаблицыФормы.ЭтоУслуга = ?(СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры 
			= Перечисления.ТипыНоменклатуры.Услуга, 
			Истина, 
			Ложь);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьДополнительныеРеквизиты(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных)
	Если НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьДополнительныеРеквизиты(СтрокаТаблицыФормы, 
			НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьКоличество(НастройкиЗагрузкиДанных, СтрокаТаблицыФормы)
	Если НастройкиЗагрузкиДанных.ФиксированныйШаблон Тогда
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Количество, 
			СтрокаТаблицыФормы.Количество_ВходящиеДанные);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьФиксированнаяСтоимость(СтрокаТаблицыФормы)
	Если ПустаяСтрока(СтрокаТаблицыФормы.ФиксированнаяСтоимость_ВходящиеДанные) Тогда
		
		СтрокаТаблицыФормы.ФиксированнаяСтоимость = Истина;
		
	Иначе
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ФиксированнаяСтоимость, 
				СтрокаТаблицыФормы.ФиксированнаяСтоимость_ВходящиеДанные);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьЭтоНабор(СтрокаТаблицыФормы)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНаборы") Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ЭтоНабор, 
			СтрокаТаблицыФормы.ЭтоНабор_ВходящиеДанные);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьИспользоватьПартии(СтрокаТаблицыФормы)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ИспользоватьПартии, 
				СтрокаТаблицыФормы.ИспользоватьПартии_ВходящиеДанные);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьПараметрыХарактеристик(СтрокаТаблицыФормы, Знач ИспользоватьХарактеристики)
	Если ИспользоватьХарактеристики Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ИспользоватьХарактеристики, 
				СтрокаТаблицыФормы.ИспользоватьХарактеристики_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ПроверятьЗаполнениеХарактеристики, 
				СтрокаТаблицыФормы.ПроверятьЗаполнениеХарактеристики_ВходящиеДанные);			
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьЯчейки(ЭтаСтрокаСопоставлена, ОбновлятьДанные, СтрокаТаблицыФормы)
	Если ПолучитьФункциональнуюОпцию("УчетПоЯчейкам") Тогда
		
		// Ячейка по наименованию
		ЗначениеПоУмолчанию = Справочники.Ячейки.ПустаяСсылка();
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "Ячейка", 
			СтрокаТаблицыФормы.Ячейка_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЯчейку(СтрокаТаблицыФормы.Ячейка, 
			СтрокаТаблицыФормы.Ячейка_ВходящиеДанные, ЗначениеПоУмолчанию);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьСтавкуНДС(Знач ЭтаСтрокаСопоставлена, ЗначениеПоУмолчанию, СтрокаТаблицыФормы)
	Если Не ЭтаСтрокаСопоставлена Или СтрокаТаблицыФормы.СтавкаНДС_ВходящиеДанные <> "" Тогда
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтавкуНДС(СтрокаТаблицыФормы.СтавкаНДС, 
			СтрокаТаблицыФормы.СтавкаНДС_ВходящиеДанные, ЗначениеПоУмолчанию);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьСерииНоменклатуры(СтрокаТаблицыФормы, Знач ЭтаСтрокаСопоставлена)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ИспользоватьСерииНоменклатуры, 
				СтрокаТаблицыФормы.ИспользоватьСерииНоменклатуры_ВходящиеДанные);
		Если ЭтаСтрокаСопоставлена
			И СтрокаТаблицыФормы.ИспользоватьСерииНоменклатуры Тогда
			
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСерия(СтрокаТаблицыФормы.Номенклатура, 
				СтрокаТаблицыФормы.Серия, СтрокаТаблицыФормы.Серия_ВходящиеДанные);
			
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьНоменклатуруВЭД(СтрокаТаблицыФормы)
	Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			СопоставитьТоварнуюНоменклатуруВЭД(СтрокаТаблицыФормы.ТоварнаяНоменклатураВЭД, 
				СтрокаТаблицыФормы.ТоварнаяНоменклатураВЭД_ВходящиеДанные);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьПродукциюЕГАИСПоКоду(СтрокаТаблицыФормы)
	Если ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьПродукциюПоЕГАИС(СтрокаТаблицыФормы.ПродукцияЕГАИС,
			СтрокаТаблицыФормы.ПродукцияЕГАИС_ВходящиеДанные);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСопоставитьЭлементСправочникаПриЗагрузке(СтрокаТаблицыФормы, НастройкиПоиска)
	Если СтрокаТаблицыФормы.ЭтоГруппа Тогда
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.
			СопоставитьНоменклатуруГруппу(СтрокаТаблицыФормы, НастройкиПоиска);
		
	Иначе
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНоменклатуру(СтрокаТаблицыФормы, НастройкиПоиска);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗаполнитьПолеРодительПоКодуИлиНаименованию(ЭтаСтрокаСопоставлена, 
	СтрокаТаблицыФормы, ОбновлятьДанные)
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицыФормы.Родитель) Тогда
	
		// Родитель по наименованию или коду
		ЗначениеПоУмолчанию = Справочники.Номенклатура.ПустаяСсылка();
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Номенклатура, "Родитель", 
			СтрокаТаблицыФормы.РодительНаименование, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьРодителяНоменклатуры(СтрокаТаблицыФормы.Родитель,
			СтрокаТаблицыФормы.РодительКод, СтрокаТаблицыФормы.РодительНаименование, ЗначениеПоУмолчанию);
	
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗаполнитьПолеРодительПоИерархии(СтрокаТаблицыФормы, 
	ДеревоИерархии, СоответствиеИерархииКэш, СтрокаИерархии)
	
	Перем ГруппаРодитель;
	Перем ПоследнийРодитель;
	Если НЕ ПустаяСтрока(СтрокаИерархии) Тогда
		// поиск по иерархии
		МассивИерархии = СтрРазделить(СтрокаИерархии, СимволыИерархии(), Ложь);
		
		Если МассивИерархии.Количество() > 0 Тогда
			
			// сначала ищем по кэшированным значениям в СоответствиеИерархииКэш
			ГруппаРодитель = СоответствиеИерархииКэш.Получить(СтрокаИерархии);
			Если ГруппаРодитель = Неопределено Тогда
				
				ПоследнийРодитель = ЗаполнитьИерархию(МассивИерархии, ДеревоИерархии);
				СоответствиеИерархииКэш.Вставить(СтрокаИерархии, ПоследнийРодитель);
				
				Если ЗначениеЗаполнено(ПоследнийРодитель) Тогда
					СтрокаТаблицыФормы.Родитель = ПоследнийРодитель;	
				КонецЕсли;
			Иначе
				// Родитель существует, подставляем из кэша
				СтрокаТаблицыФормы.Родитель = ГруппаРодитель;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
// АПК:299-вкл

Процедура ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения = "") Экспорт
	
	СтрокаТаблицыФормы._СтрокаСопоставлена = ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура);
	
	ИмяСлужебногоПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();
	
	НаименованиеЗаполнено = НЕ ПустаяСтрока(СтрокаТаблицыФормы.НоменклатураНаименование) ИЛИ НЕ ПустаяСтрока(СтрокаТаблицыФормы.НоменклатураНаименованиеПолное);
	
	СтрокаТаблицыФормы[ИмяСлужебногоПоля] = СтрокаТаблицыФормы._СтрокаСопоставлена
											ИЛИ (НЕ СтрокаТаблицыФормы._СтрокаСопоставлена И НаименованиеЗаполнено);
	
КонецПроцедуры

Процедура ОбработатьПодготовленныеДанные(СтруктураДанных, ФоновоеЗаданиеАдресХранилища = "") Экспорт
	
	ФОУчетГТД						= ПолучитьФункциональнуюОпцию("УчетГТД");
	НастройкиОбновленияСвойств		= СтруктураДанных.НастройкиЗагрузкиДанных.НастройкиОбновленияСвойств;
	ОбновлятьСуществующие			= СтруктураДанных.НастройкиЗагрузкиДанных.ОбновлятьСуществующие;
	СоздаватьЕслиНеСопоставлено		= СтруктураДанных.НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено;
	ФиксированныйШаблон				= СтруктураДанных.НастройкиЗагрузкиДанных.ФиксированныйШаблон;
	ТаблицаСопоставленияДанных		= СтруктураДанных.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных				= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных			= СтруктураДанных.НастройкиЗагрузкиДанных;
	КоличествоЗаписейТранзакции		= 0;
	ТранзакцияОткрыта				= Ложь;

	Если СтруктураДанных.Свойство("СоответствиеКартинок") Тогда
		СоответствиеКартинок = СтруктураДанных.СоответствиеКартинок;
	КонецЕсли;
	
	ВременныеДанныеЗагрузки = Новый Структура;
	ВременныеДанныеЗагрузки.Вставить("СозданныеГруппы", Новый Соответствие);
	
	Если СтруктураДанных.Свойство("ДеревоИерархии") Тогда
		ДеревоИерархии = СтруктураДанных.ДеревоИерархии;
		ВременныеДанныеЗагрузки.Вставить("ДеревоИерархии", ДеревоИерархии);
	КонецЕсли;
	
	Если ФОУчетГТД Тогда
		ТаблицаКлассификатораТНВЭД = Справочники.КлассификаторТНВЭД.ТаблицаКлассификатораТНВЭД();
		ВременныеДанныеЗагрузки.Вставить("ТаблицаКлассификатораТНВЭД", ТаблицаКлассификатораТНВЭД);
	КонецЕсли;
	
	Если ФиксированныйШаблон Тогда
		
		ВводНачальныхОстатков = Документы.ВводНачальныхОстатков.СоздатьДокумент();
		ВводНачальныхОстатков.Дата = НачалоДня(ТекущаяДатаСеанса());
		ВводНачальныхОстатков.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		ВводНачальныхОстатков.РазделУчета = Перечисления.РазделыУчета.Запасы;
		
		ВременныеДанныеЗагрузки.Вставить("ВводНачальныхОстатков", ВводНачальныхОстатков);
		
	КонецЕсли;
	
	НастройкиОбновленияСвойств.ИменаПолейОбновляемые = ЗагрузкаДанныхИзВнешнегоИсточника.УдалитьНесуществующиеСвойства(
		СтрРазделить(НастройкиОбновленияСвойств.ИменаПолейОбновляемые, ", "), 
		Метаданные.Справочники.Номенклатура.Реквизиты);
	
	Попытка
		
		Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			Если НЕ ТранзакцияОткрыта 
				И КоличествоЗаписейТранзакции = 0 Тогда
				
				НачатьТранзакцию();
				ТранзакцияОткрыта = Истина;
				
			КонецЕсли;
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			
			МожноЗагрузитьИлиОбновить = (СтрокаТаблицы._СтрокаСопоставлена И ОбновлятьСуществующие) 
				ИЛИ (НЕ СтрокаТаблицы._СтрокаСопоставлена И СоздаватьЕслиНеСопоставлено)
				ИЛИ ФиксированныйШаблон;
				
				Если ЗагрузкаВПриложениеВозможна И МожноЗагрузитьИлиОбновить Тогда
					
					ВременныеДанныеЗагрузки.Вставить("СоздатьСоответствиеЕГАИС", Ложь);
					КоличествоЗаписейТранзакции = КоличествоЗаписейТранзакции + 1;
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ЭтоГруппа") 
						И СтрокаТаблицы.ЭтоГруппа Тогда
						
						ЗагрузитьГруппу(СтрокаТаблицы, ВременныеДанныеЗагрузки);
						
					Иначе	
						
						ЭлементСправочника = ЗагрузитьЭлемент(СтрокаТаблицы, СтруктураДанных, ВременныеДанныеЗагрузки);
						
						ЗагрузитьСвязанныеДанные(ЭлементСправочника, СтрокаТаблицы, НастройкиЗагрузкиДанных, ВременныеДанныеЗагрузки);
						
					КонецЕсли;
					
				КонецЕсли;
				
				ИндексТекущейСтроки	= ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицы);
				ТекстПрогресса		= СтрШаблон(НСтр("ru ='Обработано %1 из %2 строк...'"), ИндексТекущейСтроки, РазмерТаблицыДанных);
				
				ДлительныеОперации.СообщитьПрогресс(, ТекстПрогресса);
				
				Если ТранзакцияОткрыта
					И КоличествоЗаписейТранзакции > ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.МаксимумЗаписейВОднойТранзакции() Тогда
					
					ЗафиксироватьТранзакцию();
					ТранзакцияОткрыта = Ложь;
					КоличествоЗаписейТранзакции = 0;
					
				КонецЕсли;
			
		КонецЦикла;
		
		Если ФиксированныйШаблон И ВводНачальныхОстатков.Запасы.Количество() > 0 Тогда
			ВводНачальныхОстатков.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли; 
		
		Если ТранзакцияОткрыта 
			И КоличествоЗаписейТранзакции > 0 Тогда
			
			ЗафиксироватьТранзакцию();
			ТранзакцияОткрыта = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		НастройкиЗагрузкиДанных.Вставить("ТекстОшибки", ТекстОшибки);
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка данных'", "ru"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура, , ТекстОшибки);
		ОтменитьТранзакцию();
		Возврат;
		
	КонецПопытки;
	
	Если ТранзакцияОткрыта Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
	// Картинки загружаем без транзакции
	ЗагрузитьКартинки(ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных, 
		СоответствиеКартинок, "Картинка_ВходящиеДанные", Ложь);
		
	ЗагрузитьКартинки(ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных, 
		СоответствиеКартинок, "КартинкаПоУмолчанию_ВходящиеДанные", Истина);
		
	ЗагрузитьКартинки(ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных, 
		СоответствиеКартинок, "КартинкаХарактеристики_ВходящиеДанные", Ложь);
	
	Если ЗначениеЗаполнено(ФоновоеЗаданиеАдресХранилища) Тогда
		СтруктураРезультата = Новый Структура("ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных", 
			ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных);
		ПоместитьВоВременноеХранилище(СтруктураРезультата, ФоновоеЗаданиеАдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьГруппу(СтрокаТаблицы, ВременныеДанныеЗагрузки)
	
	НаименованиеИзВходящихДанных = ?(ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименование), СтрокаТаблицы.НоменклатураНаименованиеПолное, СтрокаТаблицы.НоменклатураНаименование);	
	
	Если ПустаяСтрока(НаименованиеИзВходящихДанных) Тогда
		Возврат;
	КонецЕсли;
		
	Если СтрокаТаблицы._СтрокаСопоставлена Тогда
		ЭлементСправочника = СтрокаТаблицы.Номенклатура.ПолучитьОбъект();
		ЭлементСправочника.Заблокировать();
	Иначе
		ЭлементСправочника = Справочники.Номенклатура.СоздатьГруппу();
		УстановитьУИДНового(СтрокаТаблицы, ЭлементСправочника);
		// Код не обновляем, можно установить только при создании
	КонецЕсли;
	
	ЭлементСправочника.Наименование = НаименованиеИзВходящихДанных;
	Если ЗначениеЗаполнено(СтрокаТаблицы.Родитель) Тогда
		ЭлементСправочника.Родитель = СтрокаТаблицы.Родитель;
	ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицы.РодительНаименование) Тогда
		ЭлементСправочника.Родитель = ВременныеДанныеЗагрузки.СозданныеГруппы.Получить(СтрокаТаблицы.РодительНаименование);
	КонецЕсли;
	
	ЭлементСправочника.Записать();
	
	ВременныеДанныеЗагрузки.СозданныеГруппы.Вставить(СтрокаТаблицы.НоменклатураНаименование, ЭлементСправочника.Ссылка);
	
КонецПроцедуры

Функция ЗагрузитьЭлемент(СтрокаТаблицы, СтруктураДанных, ВременныеДанныеЗагрузки)

	ЗначениеЗагружатьДопРеквизитыВХарактеристики = 2;
	ФОУчетГТД								= ПолучитьФункциональнуюОпцию("УчетГТД");
	НастройкиОбновленияСвойств				= СтруктураДанных.НастройкиЗагрузкиДанных.НастройкиОбновленияСвойств;
	ФиксированныйШаблон						= СтруктураДанных.НастройкиЗагрузкиДанных.ФиксированныйШаблон;
	НастройкиЗагрузкиДанных					= СтруктураДанных.НастройкиЗагрузкиДанных;
	ФОИспользоватьХарактеристики			= ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики");
	ФОИспользоватьСерииНоменклатуры			= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	НастройкиПоиска							= НастройкиЗагрузкиДанных.НастройкиПоиска;
	ЗагружатьДопРеквизитыВХарактеристики	= ?(НастройкиЗагрузкиДанных.КудаЗагружаемДопРеквизиты
		= ЗначениеЗагружатьДопРеквизитыВХарактеристики, Истина, Ложь);
	ЭтоЗагрузкаТабличнойЧасти				= СтруктураДанных.НастройкиЗагрузкиДанных.ЭтоЗагрузкаТабличнойЧасти;

	Если СтрокаТаблицы._КлючСвязи <> -1 Тогда
		РанееЗаполненнаяСтрока = СтруктураДанных.ТаблицаСопоставленияДанных[СтрокаТаблицы._КлючСвязи];
		СтрокаТаблицы.Номенклатура = РанееЗаполненнаяСтрока.Номенклатура;
		СтрокаТаблицы._СтрокаСопоставлена = Истина;
	КонецЕсли;

	Если СтрокаТаблицы._СтрокаСопоставлена Тогда

		ЭлементСправочника = СтрокаТаблицы.Номенклатура.ПолучитьОбъект();

		МассивСвойств = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			НастройкиОбновленияСвойств.ИменаПолейОбновляемые);
		Для Каждого Свойство Из МассивСвойств Цикл
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементСправочника, Свойство) Тогда

				ЗаполнитьЗначенияСвойств(ЭлементСправочника, СтрокаТаблицы, Свойство);

			КонецЕсли;
		КонецЦикла;

		ЗаполнитьЗначенияСвойств(ЭлементСправочника, СтрокаТаблицы, НастройкиОбновленияСвойств.ИменаПолейОбновляемые);

		Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
			ЭлементСправочника.ВидСтавкиНДС = СтрокаТаблицы.СтавкаНДС.ВидСтавкиНДС;
		ИначеЕсли Не ЗначениеЗаполнено(ЭлементСправочника.ВидСтавкиНДС) Тогда
			ЭлементСправочника.ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Справочники.Организации.ОрганизацияПоУмолчанию(), "ВидСтавкиНДСПоУмолчанию", Истина);
		КонецЕсли;

	Иначе

		ЭлементСправочника = Справочники.Номенклатура.СоздатьЭлемент();

		УстановитьУИДНового(СтрокаТаблицы, ЭлементСправочника, НастройкиЗагрузкиДанных);

		ИменаПолейНеподлежащихОбновлению = ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СтандартныеИменаПолейНеподлежащихОбновлению(
			НастройкиЗагрузкиДанных);
		ЗаполнитьЗначенияСвойств(ЭлементСправочника, СтрокаТаблицы, , ИменаПолейНеподлежащихОбновлению);

		Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
			ЭлементСправочника.ВидСтавкиНДС = СтрокаТаблицы.СтавкаНДС.ВидСтавкиНДС;
		Иначе
			ЭлементСправочника.ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Справочники.Организации.ОрганизацияПоУмолчанию(), "ВидСтавкиНДСПоУмолчанию", Истина);
		КонецЕсли;

	КонецЕсли;
		
	// Родитель, если он заполнен
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Родитель") И ЗначениеЗаполнено(
		СтрокаТаблицы.Родитель) Тогда

		ЭлементСправочника.Родитель = СтрокаТаблицы.Родитель;

	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "РодительНаименование")
		И ЗначениеЗаполнено(СтрокаТаблицы.РодительНаименование) Тогда

		СтрокаИерархии = СтрокаТаблицы.РодительНаименование;

		МассивИерархии = СтрРазделить(СтрокаИерархии, СимволыИерархии(), Ложь);

		Если МассивИерархии.Количество() > 0 И ВременныеДанныеЗагрузки.Свойство("ДеревоИерархии") Тогда

			ЭлементСправочника.Родитель = ЗагрузитьИерархиюРодителей(МассивИерархии,
				ВременныеДанныеЗагрузки.ДеревоИерархии);

		КонецЕсли;
		Если Не ЗначениеЗаполнено(ЭлементСправочника.Родитель) Тогда

			ЭлементСправочника.Родитель = ВременныеДанныеЗагрузки.СозданныеГруппы.Получить(СтрокаИерархии);

		КонецЕсли;

		НастройкиЗагрузкиДанных.Вставить("ОповеститьОСозданииГруппы", Истина);

	КонецЕсли;
		
	// Наименование
	Если Не ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименование) Тогда
		ЭлементСправочника.Наименование = СтрокаТаблицы.НоменклатураНаименование;
	КонецЕсли;
	Если Не ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименованиеПолное) Тогда
		ЭлементСправочника.НаименованиеПолное = СтрокаТаблицы.НоменклатураНаименованиеПолное;
	КонецЕсли;

	НаименованиеИзВходящихДанных = ?(ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименование),
		СтрокаТаблицы.НоменклатураНаименованиеПолное, СтрокаТаблицы.НоменклатураНаименование);
	Если ПустаяСтрока(ЭлементСправочника.Наименование) Тогда
		ЭлементСправочника.Наименование = НаименованиеИзВходящихДанных;
	КонецЕсли;
	Если ПустаяСтрока(ЭлементСправочника.НаименованиеПолное) Тогда
		ЭлементСправочника.НаименованиеПолное = НаименованиеИзВходящихДанных;
	КонецЕсли;

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ТипНоменклатуры")
		И ЗначениеЗаполнено(СтрокаТаблицы.ТипНоменклатуры) Тогда
		ТипНоменклатуры = СтрокаТаблицы.ТипНоменклатуры;
	Иначе
		ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас;
	КонецЕсли;
		
	// Тип номенклатуры при начальной загрузке
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ЭтоУслуга")
		И СтрокаТаблицы.ЭтоУслуга Тогда
		ЭлементСправочника.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ЭтоРабота")
		И СтрокаТаблицы.ЭтоРабота Тогда
		ЭлементСправочника.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭлементСправочника.ТипНоменклатуры) Тогда
		ЭлементСправочника.ТипНоменклатуры = ТипНоменклатуры;;
	КонецЕсли;

	Если Не ЭлементСправочника.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда

		ЭлементСправочника.НижняяГраницаОстатков = 0;
		ЭлементСправочника.ВерхняяГраницаОстатков = 0;

	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ПродукцияЕГАИС")
		И ЗначениеЗаполнено(СтрокаТаблицы.ПродукцияЕГАИС) Тогда

		ПродукцияЕГАИС = СтрокаТаблицы.ПродукцияЕГАИС;

		ЭлементСправочника.ОбъемДАЛ = ПродукцияЕГАИС.Объем / 100;
		ЭлементСправочника.Крепость = ПродукцияЕГАИС.Крепость;
		ЭлементСправочника.ВидАлкогольнойПродукции = ПродукцияЕГАИС.ВидПродукции;

		ЭлементСправочника.ИмпортнаяАлкогольнаяПродукция = ЗначениеЗаполнено(ПродукцияЕГАИС.Производитель)
			И ЗначениеЗаполнено(ПродукцияЕГАИС.Импортер);

		СопоставленныеДанные = ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПолучитьКонтрагентовПоСоответствиюЕГАИС(
			ПродукцияЕГАИС.Производитель, ПродукцияЕГАИС.Импортер);
		Если ЭлементСправочника.ИмпортнаяАлкогольнаяПродукция Тогда

			ЭлементСправочника.Производитель = СопоставленныеДанные.Производитель;
			ЭлементСправочника.ПроизводительИмпортерАлкогольнойПродукции = СопоставленныеДанные.Импортер;

		Иначе

			ЭлементСправочника.ПроизводительИмпортерАлкогольнойПродукции = СопоставленныеДанные.Производитель;

		КонецЕсли;

		ВременныеДанныеЗагрузки.Вставить("СоздатьСоответствиеЕГАИС", Истина);

	КонецЕсли;

	Если ФОУчетГТД И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы,
		"ТоварнаяНоменклатураВЭД_ВходящиеДанные") И Не ПустаяСтрока(СтрокаТаблицы.ТоварнаяНоменклатураВЭД_ВходящиеДанные)
		И Не ЗначениеЗаполнено(ЭлементСправочника.ТоварнаяНоменклатураВЭД) Тогда

		КодТНВЭД = СокрЛП(СтрокаТаблицы.ТоварнаяНоменклатураВЭД_ВходящиеДанные);

		ЭлементКлассификатора = Справочники.КлассификаторТНВЭД.НайтиПоКоду(КодТНВЭД, Ложь);
		Если ЗначениеЗаполнено(ЭлементКлассификатора) Тогда

			ЭлементСправочника.ТоварнаяНоменклатураВЭД = ЭлементКлассификатора;

		Иначе

			ОтборПоКоду = Новый Структура("Код", КодТНВЭД);
			МассивСтрок = ВременныеДанныеЗагрузки.ТаблицаКлассификатораТНВЭД.НайтиСтроки(ОтборПоКоду);

			Если МассивСтрок.Количество() > 0 Тогда

				ЭлементКлассификатора = Справочники.КлассификаторТНВЭД.СоздатьЭлемент();
				ЗаполнитьЗначенияСвойств(ЭлементКлассификатора, МассивСтрок[0]);
				ЭлементКлассификатора.НаименованиеПолное = МассивСтрок[0].Наименование;
				ЭлементКлассификатора.Записать();

				ЭлементСправочника.ТоварнаяНоменклатураВЭД = ЭлементКлассификатора.Ссылка;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;
	
	// категория
	Если Не ЗначениеЗаполнено(ЭлементСправочника.КатегорияНоменклатуры) 
		И ПустаяСтрока(СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные) Тогда

		ЭлементСправочника.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.БезКатегории;
		
	КонецЕсли;

	// вид продукции ИС
	Если Не ПустаяСтрока(СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные) Тогда
	
		ЭлементСправочника.ВидПродукцииИС = ЭлементСправочника.КатегорияНоменклатуры.ВидПродукцииИС;
		
	КонецЕсли;
	
	// характеристики
	Если ФОИспользоватьХарактеристики И (Не ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНаименование)) Тогда

		ЭлементСправочника.ИспользоватьХарактеристики = Истина;

	КонецЕсли;
	
	// Серии номенклатуры
	Если ФОИспользоватьСерииНоменклатуры И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы,
		"Серия") И Не ПустаяСтрока(СтрокаТаблицы.Серия_ВходящиеДанные) Тогда

		ЭлементСправочника.ИспользоватьСерииНоменклатуры = Истина;

	КонецЕсли; 
	
	// доп. реквизиты
	Если ФиксированныйШаблон Тогда

		ЗагрузитьДополнительныеРеквизитыФиксированногоШаблона(СтрокаТаблицы, ЭлементСправочника,
			НастройкиЗагрузкиДанных);

	ИначеЕсли НастройкиЗагрузкиДанных.Свойство("ВыбранныеДополнительныеРеквизиты")
		И НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда

		ЗагрузкаДанныхИзВнешнегоИсточника.ОбработатьВыбранныеДополнительныеРеквизиты(ЭлементСправочника,
			СтрокаТаблицы._СтрокаСопоставлена, СтрокаТаблицы, НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты,
			"Справочник_Номенклатура");

	КонецЕсли;
	
	// объем
	РассчитатьОбъем = Не ЗначениеЗаполнено(ЭлементСправочника.Объем) И (ЗначениеЗаполнено(ЭлементСправочника.Длина)
		Или ЗначениеЗаполнено(ЭлементСправочника.Ширина) Или ЗначениеЗаполнено(ЭлементСправочника.Высота));
	Если РассчитатьОбъем Тогда

		ЭлементСправочника.Объем = ЭлементСправочника.Длина * ЭлементСправочника.Ширина * ЭлементСправочника.Высота;

	КонецЕсли;

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ШтрихкодПоУмолчанию")
		И ЗначениеЗаполнено(СтрокаТаблицы.ШтрихкодПоУмолчанию) Тогда

		ЭлементСправочника.Штрихкод = СтрокаТаблицы.ШтрихкодПоУмолчанию;

	КонецЕсли;
	
	ЭлементСправочника.Заполнить(Неопределено);
	ЭлементСправочника.Записать();

	СтрокаТаблицы.Номенклатура = ЭлементСправочника.Ссылка;

	Возврат ЭлементСправочника.Ссылка;

КонецФункции

Функция ПреобразоватьСтрокуШтрихкодовВМассив(Знач Штрихкод)
	
	СтрокаШтрихкодов = Штрихкод;
	МассивШтрихкодов = Новый Массив;
	Пока Истина Цикл
		ПозицияРазделителя = СтрНайти(СтрокаШтрихкодов, Символы.ПС);
		Если ПозицияРазделителя = 0 Тогда
			МассивШтрихкодов.Добавить(СокрЛП(СтрокаШтрихкодов));
			Прервать;
		Иначе
			МассивШтрихкодов.Добавить(СокрЛП(Лев(СтрокаШтрихкодов, ПозицияРазделителя - 1)));
			СтрокаШтрихкодов = Сред(СтрокаШтрихкодов, ПозицияРазделителя + 1);
		КонецЕсли;
		Если ПустаяСтрока(СтрокаШтрихкодов) Тогда // Для случая, когда разделитель есть в конце строки.
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивШтрихкодов;
	
КонецФункции

Процедура ЗагрузитьСвязанныеДанные(ЭлементСправочника, СтрокаТаблицы, НастройкиЗагрузкиДанных, ВременныеДанныеЗагрузки)
	
	ЗначениеЗагружатьХарактеристикиВКатегории = 2;
	ЗначениеЗагружатьДопРеквизитыВХарактеристики = 2;
	
	ВидЦен = ?(ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ВидЦен"), 
		СтрокаТаблицы.ВидЦен, 
		НастройкиЗагрузкиДанных.ВидЦен);
	СкладОстатков = НастройкиЗагрузкиДанных.СкладОстатков;
	ФОИспользоватьСерииНоменклатуры			= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ИспользоватьХарактеристики				= ЭлементСправочника.ИспользоватьХарактеристики 
												И (ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ХарактеристикаНаименование")
													И ЗначениеЗаполнено(СтрокаТаблицы.ХарактеристикаНаименование));
	ЗагружатьХарактеристикиВКатегории		= ?(НастройкиЗагрузкиДанных.КудаЗагружаемХарактеристики = 
													ЗначениеЗагружатьХарактеристикиВКатегории, Истина, Ложь);
	ЗагружатьДопРеквизитыВХарактеристики	= ?(НастройкиЗагрузкиДанных.КудаЗагружаемДопРеквизиты = 
													ЗначениеЗагружатьДопРеквизитыВХарактеристики, Истина, Ложь) 
													И ИспользоватьХарактеристики;
	ЭтоЗагрузкаТабличнойЧасти				= НастройкиЗагрузкиДанных.ЭтоЗагрузкаТабличнойЧасти;

	ФиксированныйШаблон = НастройкиЗагрузкиДанных.ФиксированныйШаблон;
	
	ПроверитьИЗагрузитьХарактеристику(ЭлементСправочника, СтрокаТаблицы, НастройкиЗагрузкиДанных, 
		ЗагружатьДопРеквизитыВХарактеристики, ИспользоватьХарактеристики, ФиксированныйШаблон, 
		ЗагружатьХарактеристикиВКатегории);
	
	ПроверитьИЗагрузитьШтрихкод(ЭлементСправочника, СтрокаТаблицы, ИспользоватьХарактеристики);
	
	ПроверитьИЗагрузитьЦену(ЭлементСправочника, СтрокаТаблицы, ВидЦен, ИспользоватьХарактеристики, 
		ЭтоЗагрузкаТабличнойЧасти, НастройкиЗагрузкиДанных.ДатаОбработки);
	
	ПроверитьИЗагрузитьВводНачальныхОстатков(ЭлементСправочника, СтрокаТаблицы, ВременныеДанныеЗагрузки, 
		ФиксированныйШаблон, СкладОстатков, ИспользоватьХарактеристики);				
	
	ПроверитьИЗагрузитьДанныеЕГАИС(ЭлементСправочника, СтрокаТаблицы, ВременныеДанныеЗагрузки, 
		ИспользоватьХарактеристики, ЭтоЗагрузкаТабличнойЧасти);
	
	ПроверитьИЗагрузитьСерииНоменклатуры(ЭлементСправочника, СтрокаТаблицы, ФОИспользоватьСерииНоменклатуры);
	
КонецПроцедуры

Процедура ПроверитьИЗагрузитьСерииНоменклатуры(ЭлементСправочника, СтрокаТаблицы, Знач ФОИспользоватьСерииНоменклатуры)
	Перем СерияСправочник;
	Если ФОИспользоватьСерииНоменклатуры Тогда

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Серия") И Не ЗначениеЗаполнено(
			СтрокаТаблицы.Серия) И Не ПустаяСтрока(СтрокаТаблицы.Серия_ВходящиеДанные) Тогда

			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСерия(ЭлементСправочника.Ссылка,
				СтрокаТаблицы.Серия, СтрокаТаблицы.Серия_ВходящиеДанные);

			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Серия) Тогда

				СерияСправочник = Справочники.СерииНоменклатуры.СоздатьЭлемент();
				СерияСправочник.Владелец = ЭлементСправочника.Ссылка;
				СерияСправочник.Наименование = СтрокаТаблицы.Серия_ВходящиеДанные;

				СерияСправочник.Записать();

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗагрузитьДанныеЕГАИС(ЭлементСправочника, СтрокаТаблицы, ВременныеДанныеЗагрузки,
	Знач ИспользоватьХарактеристики, Знач ЭтоЗагрузкаТабличнойЧасти)
	Перем СоответствиеНоменклатурыЕГАИС;
	Если ВременныеДанныеЗагрузки.СоздатьСоответствиеЕГАИС И Не ЭтоЗагрузкаТабличнойЧасти Тогда

		СоответствиеНоменклатурыЕГАИС = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьМенеджерЗаписи();
		СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = СтрокаТаблицы.ПродукцияЕГАИС;
		СоответствиеНоменклатурыЕГАИС.Номенклатура = ЭлементСправочника.Ссылка;
		СоответствиеНоменклатурыЕГАИС.Характеристика = ?(ИспользоватьХарактеристики, СтрокаТаблицы.Характеристика,
			Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		СоответствиеНоменклатурыЕГАИС.Записать(Истина);

	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗагрузитьВводНачальныхОстатков(ЭлементСправочника, СтрокаТаблицы, ВременныеДанныеЗагрузки,
	Знач ФиксированныйШаблон, Знач СкладОстатков, Знач ИспользоватьХарактеристики)
	Перем ЗапасыСтрока;
	Если ФиксированныйШаблон И ЗначениеЗаполнено(СтрокаТаблицы.Количество) И ЭлементСправочника.ТипНоменклатуры
		= Перечисления.ТипыНоменклатуры.Запас Тогда

		ЗапасыСтрока = ВременныеДанныеЗагрузки.ВводНачальныхОстатков.Запасы.Добавить();
		ЗапасыСтрока.Номенклатура 		= ЭлементСправочника.Ссылка;
		ЗапасыСтрока.Характеристика		= ?(ИспользоватьХарактеристики, СтрокаТаблицы.Характеристика,
			Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		ЗапасыСтрока.СтруктурнаяЕдиница = СкладОстатков;
		ЗапасыСтрока.Цена 				= СтрокаТаблицы.Цена;
		ЗапасыСтрока.Количество 		= СтрокаТаблицы.Количество;
		ЗапасыСтрока.Сумма				= ЗапасыСтрока.Цена * ЗапасыСтрока.Количество;
		ЗапасыСтрока.ЕдиницаИзмерения	= ?(ЗначениеЗаполнено(СтрокаТаблицы.ЕдиницаИзмерения),
			СтрокаТаблицы.ЕдиницаИзмерения, ЭлементСправочника.ЕдиницаИзмерения);

	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗагрузитьЦену(ЭлементСправочника, СтрокаТаблицы, Знач ВидЦен, Знач ИспользоватьХарактеристики,
	Знач ЭтоЗагрузкаТабличнойЧасти, Знач ДатаОбработки)
	Характеристика = ?(ИспользоватьХарактеристики, СтрокаТаблицы.Характеристика,
		Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());

	ХарактеристикаУказана = ?(ЭлементСправочника.ПроверятьЗаполнениеХарактеристики, ЗначениеЗаполнено(Характеристика),
		Истина);

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "Цена") И ЗначениеЗаполнено(
		СтрокаТаблицы.Цена) И Не ЭтоЗагрузкаТабличнойЧасти И ХарактеристикаУказана Тогда

		Цены = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();
		Цены.Период = ?(ЗначениеЗаполнено(ДатаОбработки), ДатаОбработки, ТекущаяДатаСеанса());
		Цены.ВидЦен = ВидЦен;
		Цены.Цена	= СтрокаТаблицы.Цена;
		Цены.Номенклатура = ЭлементСправочника.Ссылка;
		Цены.Характеристика = Характеристика;
		Цены.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(СтрокаТаблицы.ЕдиницаИзмерения), СтрокаТаблицы.ЕдиницаИзмерения,
			ЭлементСправочника.ЕдиницаИзмерения);

		Цены.Записать(Истина);

	КонецЕсли;

	Если Не ХарактеристикаУказана Тогда

		СообщениеЖурнала = СтрШаблон(НСтр(
			"ru='Для номенклатуры %1 установлено обязательное заполнение характеристик. Загрузка цены невозможна'"),
			ЭлементСправочника);
		ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка цены для номенклатуры'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , , СообщениеЖурнала);

	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗагрузитьШтрихкод(ЭлементСправочника, СтрокаТаблицы, Знач ИспользоватьХарактеристики)
	Перем ШК;
	Перем ЗначениеМассиваШтрихкодов;
	Перем МассивШтрихкодов;
	Если НЕ ПустаяСтрока(СтрокаТаблицы.ШтрихКод) Тогда
		
		МассивШтрихкодов = ПреобразоватьСтрокуШтрихкодовВМассив(СтрокаТаблицы.ШтрихКод);
		
		Для Каждого ЗначениеМассиваШтрихкодов Из МассивШтрихкодов Цикл

			Если Не ПустаяСтрока(ЗначениеМассиваШтрихкодов) Тогда
				ШК = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
				ШК.Период = ТекущаяДата();
				ШК.Штрихкод = ЗначениеМассиваШтрихкодов;
				ШК.Номенклатура = ЭлементСправочника.Ссылка;
				ШК.Характеристика = ?(ИспользоватьХарактеристики, СтрокаТаблицы.Характеристика,
					Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
				ШК.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(СтрокаТаблицы.ЕдиницаИзмерения),
					СтрокаТаблицы.ЕдиницаИзмерения, ЭлементСправочника.ЕдиницаИзмерения);

				ШК.Записать(Истина);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИЗагрузитьХарактеристику(ЭлементСправочника, СтрокаТаблицы, НастройкиЗагрузкиДанных, Знач ЗагружатьДопРеквизитыВХарактеристики, Знач ИспользоватьХарактеристики, Знач ФиксированныйШаблон, Знач ЗагружатьХарактеристикиВКатегории)
	Перем СтрокиСвойства;
	Перем СвойствоПВХ;
	Перем НовСтр;
	Перем СтруктураОтбора;
	Перем СтрСимволы;
	Перем СвойствоНаименование;
	Перем СоответствиеСвойства;
	Перем ДанныеСвойства;
	Перем РазрешенныеСимволыИмениРеквизита;
	Перем ВладелецХарактеристики;
	Перем НаборСвойствРодитель;
	Перем ЭлементСправочникаХарактеристика;
	Перем НаименованиеДляПечати;
	Перем ЗначениеСвойства;
	Перем ДопРеквизиты;
	Перем СсылкаНаборСвойств;
	Перем ЧтениеJSON;
	Перем н;
	Если ИспользоватьХарактеристики Тогда
				
		Если ЗагружатьХарактеристикиВКатегории Тогда
			ВладелецХарактеристики = ЭлементСправочника.КатегорияНоменклатуры;
		Иначе
			ВладелецХарактеристики = ЭлементСправочника;
		КонецЕсли;    
		
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристикуПоНаименованиюАртикулу(
			СтрокаТаблицы, ВладелецХарактеристики);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Характеристика) Тогда  			
			ЭлементСправочникаХарактеристика = СтрокаТаблицы.Характеристика.ПолучитьОбъект();
			ЭлементСправочникаХарактеристика.Заблокировать();
		Иначе    			
			ЭлементСправочникаХарактеристика = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();	 					

		КонецЕсли;   		   		
		
		ЭлементСправочникаХарактеристика.Владелец = ВладелецХарактеристики;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ХарактеристикаНаименование) Тогда
			ЭлементСправочникаХарактеристика.Наименование = СтрокаТаблицы.ХарактеристикаНаименование;
		КонецЕсли; 
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
				СтрокаТаблицы, "ХарактеристикаНаименованиеДляПечати") Тогда
			НаименованиеДляПечати = ?(ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНаименованиеДляПечати), 
									СтрокаТаблицы.ХарактеристикаНаименование, 
									СтрокаТаблицы.ХарактеристикаНаименованиеДляПечати);
		Иначе
			НаименованиеДляПечати = СтрокаТаблицы.ХарактеристикаНаименование;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НаименованиеДляПечати) Тогда
			ЭлементСправочникаХарактеристика.НаименованиеДляПечати = НаименованиеДляПечати;
		КонецЕсли; 
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ХарактеристикаАртикул") 
			И ЗначениеЗаполнено(СтрокаТаблицы.ХарактеристикаАртикул) Тогда
			ЭлементСправочникаХарактеристика.Артикул = СтрокаТаблицы.ХарактеристикаАртикул;
		КонецЕсли;
		
		// дополнительные свойства
		Если ФиксированныйШаблон Тогда  
			Если НЕ ПустаяСтрока(СтрокаТаблицы.ДопРеквизитыШаблона_ВходящиеДанные) 
				И ЗагружатьДопРеквизитыВХарактеристики Тогда  			
				
				НаборСвойствРодитель = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_ХарактеристикиНоменклатуры");
				
				СтруктураОтбора = Новый Структура("Наименование", СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные);
				СтруктураОтбора.Вставить("Родитель", НаборСвойствРодитель);
				ДопРеквизиты = Новый Структура;
				ДопРеквизиты.Вставить("Используется", Истина);
				НаборыДополнительныхРеквизитовИСведенийПустаяСсылка = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
				СсылкаНаборСвойств = СоздатьИлиНайтиСуществующий(СтруктураОтбора, 
						НаборыДополнительныхРеквизитовИСведенийПустаяСсылка, ДопРеквизиты);
				
				// Свойство и значение свойства
				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.УстановитьСтроку(СтрокаТаблицы.ДопРеквизитыШаблона_ВходящиеДанные);
				СоответствиеСвойства = ПрочитатьJSON(ЧтениеJSON, Истина);
				
				РазрешенныеСимволыИмениРеквизита = Новый Соответствие;
				// АПК:1036-выкл
				СтрСимволы = "йцукенгшщзхъфывапролджэячсмитьбю_qwertyuiopasdfghjklzxcvbnm01234567890";
				// АПК:1036-вкл
				Для н = 1 По СтрДлина(СтрСимволы) Цикл
					РазрешенныеСимволыИмениРеквизита.Вставить(Сред(СтрСимволы, н, 1), Истина);
				КонецЦикла;	
				
				ПроверитьИСоздатьСвойстваДолонительныхРеквизитов(ЭлементСправочника, СоответствиеСвойства, 
					РазрешенныеСимволыИмениРеквизита, ЭлементСправочникаХарактеристика, СсылкаНаборСвойств);
				
			КонецЕсли;
		ИначеЕсли НастройкиЗагрузкиДанных.Свойство("ВыбранныеДополнительныеРеквизиты") 
			И НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
			
			ЗагрузкаДанныхИзВнешнегоИсточника.ОбработатьВыбранныеДополнительныеРеквизиты(
				ЭлементСправочникаХарактеристика, СтрокаТаблицы._СтрокаСопоставлена, 
				СтрокаТаблицы, НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты, 
				"Справочник_ХарактеристикиНоменклатуры");
			
		КонецЕсли;
				
		ЭлементСправочникаХарактеристика.Записать();
		СтрокаТаблицы.Характеристика = ЭлементСправочникаХарактеристика.Ссылка;
		
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьИСоздатьСвойстваДолонительныхРеквизитов(ЭлементСправочника, Знач СоответствиеСвойства, РазрешенныеСимволыИмениРеквизита, ЭлементСправочникаХарактеристика, СсылкаНаборСвойств)
	Для каждого ДанныеСвойства Из СоответствиеСвойства Цикл
			
			Если ЗначениеЗаполнено(ДанныеСвойства.Значение) Тогда
				
				СвойствоНаименование = ДанныеСвойства.Ключ;
				СвойствоПВХ = НайтиСоздатьПВХ(СвойствоНаименование, 
					ЭлементСправочника.КатегорияНоменклатуры, РазрешенныеСимволыИмениРеквизита);
					
				Если СвойствоПВХ <> Неопределено Тогда
						
					ЗначениеСвойства = НайтиСоздатьЗначениеСвойства(СвойствоПВХ, СсылкаНаборСвойств, ДанныеСвойства.Значение);
					
					СтруктураОтбора = Новый Структура("Свойство,Значение", СвойствоПВХ, ЗначениеСвойства);
					СтрокиСвойства = ЭлементСправочникаХарактеристика.ДополнительныеРеквизиты.НайтиСтроки(СтруктураОтбора);
					Если СтрокиСвойства.Количество() = 0 Тогда
						НовСтр = ЭлементСправочникаХарактеристика.ДополнительныеРеквизиты.Добавить();
						НовСтр.Свойство = СвойствоПВХ;
						НовСтр.Значение = ЗначениеСвойства;
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЕсли;						
		КонецЦикла;
КонецПроцедуры

#Область ЗагрузкаДанныхИзВнешнегоИсточникаСлужебные

Функция СимволыИерархии()
	
	Возврат ";>/\";	
	
КонецФункции

Процедура УстановитьУИДНового(СтрокаТаблицы, ЭлементСправочника, НастройкиЗагрузкиДанных=Неопределено)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "УИД_ВходящиеДанные") 
		И ЗначениеЗаполнено(СтрокаТаблицы.УИД_ВходящиеДанные) Тогда
		НовыйУИД = Новый УникальныйИдентификатор(СтрокаТаблицы.УИД_ВходящиеДанные);
		ЭлементСправочника.УстановитьСсылкуНового(Справочники.Номенклатура.ПолучитьСсылку(НовыйУИД));
	КонецЕсли;  
	
КонецПроцедуры

Функция ЗаполнитьИерархию(МассивИерархии, ДеревоИерархии)
	
	таблицаИерархии = Новый ТаблицаЗначений;
	таблицаИерархии.Колонки.Добавить("Каталог", Новый ОписаниеТипов("Строка", ,
											   Новый КвалификаторыСтроки(150, ДопустимаяДлина.Переменная)));
	таблицаИерархии.Колонки.Добавить("ГруппаСсылка", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	Для каждого каталог Из МассивИерархии Цикл
		НовСтр = таблицаИерархии.Добавить();
		НовСтр.Каталог = СокрЛП(каталог);
	КонецЦикла;
	
	Индекс = 0;
	ТекущийРодительНаименование = "";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =  
		"ВЫБРАТЬ
		|	таблицаИерархии.Каталог КАК Каталог,
		|	таблицаИерархии.ГруппаСсылка КАК ГруппаСсылка
		|ПОМЕСТИТЬ ВТИерархия
		|ИЗ
		|	&таблицаИерархии КАК таблицаИерархии";
		Запрос.УстановитьПараметр("таблицаИерархии", таблицаИерархии);
		Запрос.Выполнить();
		
	Запрос.Текст = "";
	Для каждого строкаТаблицы Из таблицаИерархии Цикл
		
		// проверяется имя Номенклатуры и имя Родителя на два уровня вверх
		Запрос.Текст = Запрос.Текст + 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК ГруппаСсылка,
		|	таблицаИерархии.Каталог КАК Каталог
		|ИЗ
		|	ВТИерархия КАК таблицаИерархии
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО (таблицаИерархии.Каталог = Номенклатура.Наименование
		|    		И Номенклатура.ЭтоГруппа 
		|			И НЕ Номенклатура.ПометкаУдаления
		|			И ЕСТЬNULL(Номенклатура.Родитель.Наименование, """") = &РодительНаименование" + Индекс;
		Если Индекс > 1 Тогда
		    Запрос.Текст = Запрос.Текст + "
			|			И ЕстьNull(Номенклатура.Родитель.Родитель.Наименование, """") = &РодительНаименование" + (Индекс-1);
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + " )  
		|ГДЕ
		|	таблицаИерархии.Каталог = &Наименование" + Индекс;
		
		Запрос.УстановитьПараметр("Наименование" + Индекс, строкаТаблицы.Каталог);
		Запрос.УстановитьПараметр("РодительНаименование" + Индекс, ТекущийРодительНаименование);
		
		Индекс = Индекс + 1;
		ТекущийРодительНаименование = строкаТаблицы.Каталог;
		
		Если Индекс < таблицаИерархии.Количество() Тогда
			Запрос.Текст = Запрос.Текст + " 
			|
			| ОБЪЕДИНИТЬ ВСЕ 
			|
			|";
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаИерархии = Запрос.Выполнить().Выгрузить();
	
	ПоследняяСтрокаДерева = ДеревоИерархии;
	ПоследнийРодитель = "";
	Для каждого строкаИерархии Из ТаблицаИерархии Цикл
		
		Если ПоследняяСтрокаДерева.Строки.Количество() > 0 Тогда
			
			НайденныеСтроки = ПоследняяСтрокаДерева.Строки.НайтиСтроки(Новый Структура("Каталог", строкаИерархии.Каталог));
			Если НайденныеСтроки.Количество() = 0 Тогда
				НовСтр = ПоследняяСтрокаДерева.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, строкаИерархии);
				Если ЗначениеЗаполнено(строкаИерархии.ГруппаСсылка) Тогда
					НовСтр.УИД = строкаИерархии.ГруппаСсылка.УникальныйИдентификатор();
				КонецЕсли; 
				
				ПоследняяСтрокаДерева = НовСтр;
			Иначе
				ПоследняяСтрокаДерева = НайденныеСтроки[0];
			КонецЕсли;
		Иначе
			НовСтр = ПоследняяСтрокаДерева.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, строкаИерархии);
			Если ЗначениеЗаполнено(строкаИерархии.ГруппаСсылка) Тогда
				НовСтр.УИД = строкаИерархии.ГруппаСсылка.УникальныйИдентификатор();
			КонецЕсли; 
			
			ПоследняяСтрокаДерева = НовСтр;
		КонецЕсли;
		
		ПоследнийРодитель = строкаИерархии.ГруппаСсылка;
		
	КонецЦикла;
	
	Возврат ПоследнийРодитель;
	
КонецФункции

Процедура ЗагрузитьДополнительныеРеквизитыФиксированногоШаблона(СтрокаТаблицы, ЭлементСправочника, НастройкиЗагрузкиДанных)
	
	ПолучательДополнительныхРеквизитовХарактеристика = 2;
	
	ЗагружатьДопРеквизитыВХарактеристики = ?(НастройкиЗагрузкиДанных.КудаЗагружаемДопРеквизиты 
		= ПолучательДополнительныхРеквизитовХарактеристика, Истина, Ложь);
	
	// Категория и набор свойств
	Если ПустаяСтрока(СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные) Тогда
		
		Если НЕ СтрокаТаблицы._СтрокаСопоставлена Тогда
			ЭлементСправочника.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.БезКатегории;
		КонецЕсли;
		Возврат;
		
	Иначе
		
		НаборСвойствРодитель = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура");
		
		СтруктураОтбора = Новый Структура("Наименование", СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные);
		СтруктураОтбора.Вставить("Родитель", НаборСвойствРодитель);
		ДопРеквизиты = Новый Структура;
		ДопРеквизиты.Вставить("Используется", Истина);
		СсылкаНаборСвойств = СоздатьИлиНайтиСуществующий(СтруктураОтбора, Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка(), ДопРеквизиты);
		
		// Создание/поиск Категории
		СтруктураОтбора = Новый Структура("Наименование", СтрокаТаблицы.КатегорияНоменклатуры_ВходящиеДанные);
		СтруктураОтбора.Вставить("НаборСвойств", СсылкаНаборСвойств);
		ДопРеквизиты = Новый Структура;
		ДопРеквизиты.Вставить("ТипНоменклатурыПоУмолчанию", ЭлементСправочника.ТипНоменклатуры);
		ДопРеквизиты.Вставить("ЕдиницаИзмерения", ?(ЗначениеЗаполнено(СтрокаТаблицы.ЕдиницаИзмерения), СтрокаТаблицы.ЕдиницаИзмерения, ЭлементСправочника.ЕдиницаИзмерения));
		ДопРеквизиты.Вставить("ИспользоватьХарактеристики", ?(НастройкиЗагрузкиДанных.КудаЗагружаемХарактеристики 
			= ПолучательДополнительныхРеквизитовХарактеристика, Истина, Ложь));
		СсылкаКатегория = СоздатьИлиНайтиСуществующий(СтруктураОтбора, Справочники.КатегорииНоменклатуры.ПустаяСсылка(), ДопРеквизиты);
		
		ЭлементСправочника.КатегорияНоменклатуры = СсылкаКатегория;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "ДопРеквизитыШаблона_ВходящиеДанные") 
		И НЕ ПустаяСтрока(СтрокаТаблицы.ДопРеквизитыШаблона_ВходящиеДанные) 
		И НЕ ЗагружатьДопРеквизитыВХарактеристики Тогда
		
		// Свойство и значение свойства
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаТаблицы.ДопРеквизитыШаблона_ВходящиеДанные);
		СоответствиеСвойства = ПрочитатьJSON(ЧтениеJSON, Истина);
		
		РазрешенныеСимволыИмениРеквизита = Новый Соответствие;
		// АПК:1036-выкл
		СтрСимволы = "йцукенгшщзхъфывапролджэячсмитьбю_qwertyuiopasdfghjklzxcvbnm01234567890";
		// АПК:1036-вкл
		Для н=1 По СтрДлина(СтрСимволы) Цикл
			РазрешенныеСимволыИмениРеквизита.Вставить(Сред(СтрСимволы, н, 1), Истина);
		КонецЦикла;
		
		ПроверитьИСоздатьСвойстваДолонительныхРеквизитов(ЭлементСправочника, СоответствиеСвойства,
			РазрешенныеСимволыИмениРеквизита, ЭлементСправочника, СсылкаНаборСвойств);
			
	КонецЕсли;
	
КонецПроцедуры

Функция ЗагрузитьССайта(АдресКартинки)
	
	ОписаниеОшибки = "";
	НастройкиПодключения = Новый Структура("АдресСайта", АдресКартинки);
	ОбменССайтом.ПолучитьНастройкиПодключения(НастройкиПодключения, ОписаниеОшибки);
	
	Если НРег(Лев(АдресКартинки, 7)) = "http://" Тогда
		Соединение = Новый HTTPСоединение(НастройкиПодключения.Сервер);
	Иначе
		Соединение = Новый HTTPСоединение(НастройкиПодключения.Сервер,,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	КонецЕсли;
	
	Ответ = Соединение.Получить(Новый HTTPЗапрос(НастройкиПодключения.АдресСкрипта));
	
	Если Ответ.КодСостояния <> 200 Тогда
		Возврат Неопределено;		                          	
	КонецЕсли;

	Возврат Ответ.ПолучитьТелоКакДвоичныеДанные();
	
КонецФункции

Процедура ЗагрузитьКартинки(ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных, СоответствиеКартинок, 
	ИмяПоляКартинки, КартинкаПоУмолчанию)

	Если ИмяПоляКартинки = "КартинкаХарактеристики_ВходящиеДанные" Тогда
		ЗагружатьКартинкиВХарактеристики = Истина;
	Иначе
		ЗагружатьКартинкиВХарактеристики = Ложь;
	КонецЕсли;
	
	ОбновлятьСуществующие			= НастройкиЗагрузкиДанных.ОбновлятьСуществующие;
	СоздаватьЕслиНеСопоставлено		= НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено;
	КартинкиЕсть 					= ОбщегоНазначенияКлиентСервер.
		ЕстьРеквизитИлиСвойствоОбъекта(ТаблицаСопоставленияДанных.Колонки, ИмяПоляКартинки);
	РазмерТаблицыДанных				= ТаблицаСопоставленияДанных.Количество();
	
	Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
		
		ЗагрузкаВПриложениеВозможна 
			= СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
		
		МожноЗагрузитьИлиОбновить = (СтрокаТаблицы._СтрокаСопоставлена И ОбновлятьСуществующие) 
			ИЛИ (НЕ СтрокаТаблицы._СтрокаСопоставлена И СоздаватьЕслиНеСопоставлено);
		
		Если ЗагрузкаВПриложениеВозможна И МожноЗагрузитьИлиОбновить И КартинкиЕсть Тогда
		
			ХарактеристикаЗаполнена = НастройкиЗагрузкиДанных.ИспользоватьХарактеристики 
				И ЗначениеЗаполнено(СтрокаТаблицы.Характеристика);
			Если НЕ ПустаяСтрока(СтрокаТаблицы[ИмяПоляКартинки]) Тогда
				
				ЗагрузкаССайта = Ложь;
				
				Попытка
					// если путь неправильный, пропускаем
					Если СтрНайти(СтрокаТаблицы[ИмяПоляКартинки], "http") > 0 Тогда
						ДвоичныеДанныеКартинки = ЗагрузитьССайта(СтрокаТаблицы[ИмяПоляКартинки]);	
						ЗагрузкаССайта = Истина;
					Иначе
						Если СоответствиеКартинок <> Неопределено Тогда
							Файл = Новый Файл(СтрокаТаблицы[ИмяПоляКартинки]);
							ДвоичныеДанныеКартинки = СоответствиеКартинок.Получить(Файл.ПолноеИмя);
						КонецЕсли;
					КонецЕсли;
				Исключение
					ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка картинок'", "ru"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура, , ОписаниеОшибки());
					Продолжить;
				КонецПопытки;
				
				Если ДвоичныеДанныеКартинки = Неопределено Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка картинок'", "ru"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура, , 
						СтрШаблон(НСтр("ru='Не удалось загрузить картинку с адресом %1'", "ru"), СтрокаТаблицы[ИмяПоляКартинки]));
					Продолжить;
				КонецЕсли;
				
				РасширениеБезТочки = "";
				ИмяБезРасширения = "";
				
				Если ЗагрузкаССайта Тогда
					СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(СтрокаТаблицы[ИмяПоляКартинки]);
					ЧастиКаталогов = СтрРазделить(СтруктураАдреса.ПутьНаСервере, "/", Ложь);
					ИмяФайла = ЧастиКаталогов[ЧастиКаталогов.Количество() - 1];
					
					ЧастиФайла = СтрРазделить(ИмяФайла, ".", Ложь);
					Если ЧастиФайла.Количество() > 0 Тогда
						РасширениеБезТочки = ЧастиФайла[ЧастиФайла.Количество() - 1];
						ИмяБезРасширения = ЧастиФайла[0];
					Иначе
						// не удалось разобрать путь к файлу
						ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка картинок'", "ru"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура, , 
							СтрШаблон(НСтр("ru='Не удалось загрузить картинку с адресом %1'", "ru"), СтрокаТаблицы[ИмяПоляКартинки]));
						Продолжить;
					КонецЕсли;
				Иначе
					Попытка
						Файл = Новый Файл(СтрокаТаблицы[ИмяПоляКартинки]);
						РасширениеБезТочки = СтрЗаменить(Файл.Расширение, ".", "");
						ИмяБезРасширения = Файл.ИмяБезРасширения;
						ЧастиКаталогов = СтрРазделить(Файл.Путь, ПолучитьРазделительПути(), Ложь);
					
					Исключение
						
						ТекстОшибки = НСтр("ru='Загрузка картинок'", ОбщегоНазначения.КодОсновногоЯзыка());
						ИнформацияОбОшибкеЗагрузки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						ЗаписьЖурналаРегистрации(ТекстОшибки,
							УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Номенклатура, ,
							ИнформацияОбОшибкеЗагрузки);
						Продолжить;
					
					КонецПопытки;
				КонецЕсли; 
				
				ВладелецФайлов = ОпределитьВладельцаКартинок(ЗагружатьКартинкиВХарактеристики, 
					ХарактеристикаЗаполнена, СтрокаТаблицы);

				ПараметрыФайла = Новый Структура;
				ПараметрыФайла.Вставить("Автор", Пользователи.АвторизованныйПользователь());
				ПараметрыФайла.Вставить("ВладелецФайлов", ВладелецФайлов);
				ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяБезРасширения);
				ПараметрыФайла.Вставить("РасширениеБезТочки", РасширениеБезТочки);
				ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", ТекущаяУниверсальнаяДата());
				
				// ищем сначала по имени картинки
				Запрос = Новый Запрос;
				ТекстЗапроса = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ПрисоединенныеФайлы.Ссылка КАК Ссылка
				|ИЗ
				|	&ТаблицаСправочника КАК ПрисоединенныеФайлы
				|ГДЕ
				|	ПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
				|	И ПрисоединенныеФайлы.Наименование = &Наименование";
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаСправочника", 
					ОбщегоНазначения.ИмяТаблицыПоСсылке(ВладелецФайлов) + "ПрисоединенныеФайлы");
				Запрос.Текст = ТекстЗапроса;
				Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайлов);
				Запрос.УстановитьПараметр("Наименование", ИмяБезРасширения);
				Результат = Запрос.Выполнить();
				Выборка = Результат.Выбрать();
				
				СохраненнаяКартинка = Неопределено;
				Пока Выборка.Следующий() Цикл
					СохраненнаяКартинка = Выборка.Ссылка;
				КонецЦикла;
				
				// затем по значению
				ХешСохраненного = Неопределено;
				Если ЗначениеЗаполнено(СохраненнаяКартинка) Тогда
					ДвоичныеДанныеСохраненногоФайла = РаботаСФайлами.ДвоичныеДанныеФайла(СохраненнаяКартинка);
					
					ХешЗагруженного = ХешФайла(ДвоичныеДанныеКартинки);
					ХешСохраненного = ХешФайла(ДвоичныеДанныеСохраненногоФайла);
					
					Если ХешСохраненного <> ХешЗагруженного Тогда
						АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки);
						ПараметрыФайла.Вставить("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
						ПараметрыФайла.Вставить("АдресВременногоХранилищаТекста", Неопределено);
						
						РаботаСФайлами.ОбновитьФайл(СохраненнаяКартинка, ПараметрыФайла);
					КонецЕсли;
					
					Если КартинкаПоУмолчанию Тогда
						
						ЭлементСправочника = ВладелецФайлов.ПолучитьОбъект();
						ЭлементСправочника.ФайлКартинки = Выборка.Ссылка;
						ЭлементСправочника.Записать();
						
					КонецЕсли;
					
				Иначе
					АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки);
					Если КартинкаПоУмолчанию Тогда
						
						ЭлементСправочника = ВладелецФайлов.ПолучитьОбъект();
						ЭлементСправочника.ФайлКартинки = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВоВременномХранилище);
						ЭлементСправочника.Записать();
						
					Иначе
						
						РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресФайлаВоВременномХранилище);
						
					КонецЕсли;
					
				КонецЕсли; 
				ИндексТекущейСтроки	= ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицы);
				ТекстПрогресса		= СтрШаблон(НСтр("ru ='Картинок обработано %1 из %2...'"), ИндексТекущейСтроки, РазмерТаблицыДанных);
				ДлительныеОперации.СообщитьПрогресс(, ТекстПрогресса);
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределитьВладельцаКартинок(Знач ЗагружатьКартинкиВХарактеристики, 
	Знач ХарактеристикаЗаполнена, СтрокаТаблицы)
	Перем ВладелецФайлов;
	
	Если ЗагружатьКартинкиВХарактеристики И ХарактеристикаЗаполнена Тогда
		
		ВладелецФайлов = СтрокаТаблицы.Характеристика;
		
	Иначе
		
		ВладелецФайлов = СтрокаТаблицы.Номенклатура;
		
	КонецЕсли;
	
	Возврат ВладелецФайлов;
	
КонецФункции

Функция ЗагрузитьИерархиюРодителей(МассивИерархии, ДеревоИерархии)
	
	ПоследнийРодитель = Справочники.Номенклатура.ПустаяСсылка();
	ПоследняяСтрокаДерева = ДеревоИерархии; // Корневой элемент
	 
	Для каждого строкаКаталог Из МассивИерархии Цикл
	
		НайденныеСтроки = ПоследняяСтрокаДерева.Строки.НайтиСтроки(Новый Структура("Каталог", СокрЛП(строкаКаталог)));
		
		Для каждого строкаДерева Из НайденныеСтроки Цикл
			Если НЕ ЗначениеЗаполнено(строкаДерева.ГруппаСсылка) Тогда
				РодительГруппа = Справочники.Номенклатура.СоздатьГруппу();
				
				УстановитьУИДНового(строкаДерева, РодительГруппа);
				
				РодительГруппа.Наименование = СокрЛП(строкаКаталог);
				РодительГруппа.Родитель = ПоследнийРодитель;
				
				РодительГруппа.Записать();
				
				строкаДерева.ГруппаСсылка = РодительГруппа.Ссылка;
			КонецЕсли; 
			
			ПоследнийРодитель = строкаДерева.ГруппаСсылка;
			ПоследняяСтрокаДерева = НайденныеСтроки[0];
			Прервать;
		КонецЦикла;
				
	КонецЦикла; 
	
	Возврат ПоследнийРодитель;
	
КонецФункции

Функция ХешФайла(ДвоичныеДанныеФайла)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанных.Добавить(ДвоичныеДанныеФайла);
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

Функция НайтиСоздатьПВХ(СвойствоНаименование, Категория, РазрешенныеСимволыИмениРеквизита)
	
	СсылкаНаборСвойств = Категория.НаборСвойств;
	
	ДопРеквизиты = Новый Структура;
	СтруктураОтбора = Новый Структура("Наименование", СвойствоНаименование);
	
	// Создать/найти Набор свойств
	СтруктураОтбора.Вставить("НаборСвойств", СсылкаНаборСвойств);
	
	ДопРеквизиты.Вставить("Заголовок", СвойствоНаименование);
	ДопРеквизиты.Вставить("Представление", СвойствоНаименование);
	ДопРеквизиты.Вставить("ТипЗначения", Новый ОписаниеТипов("СправочникСсылка.ЗначенияСвойствОбъектов"));
	
	
	УИД = Новый УникальныйИдентификатор();
	СтрокаУИД = СтрЗаменить(Строка(УИД), "-", "");
	
	ИмяРеквизита = "";
	номерСимвола = 1;
	
	Пока НомерСимвола <= СтрДлина(СвойствоНаименование) Цикл
		ПроверяемыйСимвол = Сред(СвойствоНаименование, НомерСимвола, 1);
		
		Если ПустаяСтрока(ИмяРеквизита)
			И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПроверяемыйСимвол) Тогда
			
			НомерСимвола = НомерСимвола + 1;
			Продолжить;
			
		КонецЕсли;
		
		Если РазрешенныеСимволыИмениРеквизита.Получить(НРег(ПроверяемыйСимвол)) <> Неопределено Тогда
			
			ИмяРеквизита = ИмяРеквизита + ПроверяемыйСимвол;
			
		КонецЕсли;
		НомерСимвола = НомерСимвола + 1;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ИмяРеквизита) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДопРеквизиты.Вставить("Имя", ИмяРеквизита + СтрокаУИД);
	
	ДопРеквизиты.Вставить("Доступен", Истина);
	ДопРеквизиты.Вставить("Виден", Истина);
	ДопРеквизиты.Вставить("ДополнительныеЗначенияИспользуются", Истина);
	
	СсылкаПВХ = СоздатьИлиНайтиСуществующий(СтруктураОтбора, ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка(), ДопРеквизиты);
	
	Возврат СсылкаПВХ;
	
КонецФункции

Функция НайтиСоздатьЗначениеСвойства(СсылкаПВХ, СсылкаНаборСвойств, СвойствоНаименование)
		
	// Заполнить ПВХ в наборе свойств
	обНаборСвойств = СсылкаНаборСвойств.ПолучитьОбъект();
	НайденнаяСтрока = обНаборСвойств.ДополнительныеРеквизиты.Найти(СсылкаПВХ, "Свойство");
	Если НайденнаяСтрока = Неопределено Тогда
		НовСтр = обНаборСвойств.ДополнительныеРеквизиты.Добавить();
		НовСтр.Свойство = СсылкаПВХ;
		
		Попытка
			обНаборСвойств.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	// Создать/найти значения свойства
	СтруктураОтбора = Новый Структура("Наименование, Владелец", СвойствоНаименование, СсылкаПВХ);
	ДопРеквизиты = Новый Структура;	
	СсылкаЗначениеСвойства = СоздатьИлиНайтиСуществующий(СтруктураОтбора, Справочники.ЗначенияСвойствОбъектов.ПустаяСсылка(), ДопРеквизиты);
	
	Возврат СсылкаЗначениеСвойства;
	
КонецФункции

Функция СоздатьИлиНайтиСуществующий(СтруктураОтбора, СсылкаТип, ДопРеквизиты=Неопределено)
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СсылкаТип);
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(СсылкаТип);
	
	Если ДопРеквизиты = Неопределено Тогда
		ДопРеквизиты = Новый Структура;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИмяСпр.Ссылка
	|ИЗ
	|	&ИмяТаблицы КАК ИмяСпр
	|ГДЕ
	|	&УсловияЗапроса");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ИмяТаблицы);
	
	УсловияЗапроса = Новый Массив;
	Для каждого ТекОтбор Из СтруктураОтбора Цикл
		
		УсловияЗапроса.Добавить(СтрШаблон("ИмяСпр.%1 = &%1", ТекОтбор.Ключ));
		Запрос.УстановитьПараметр(ТекОтбор.Ключ, ТекОтбор.Значение);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияЗапроса", СтрСоединить(УсловияЗапроса, " И "));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Если СтруктураОтбора.Свойство("ЭтоГруппа") Тогда
			Об = МенеджерОбъекта.СоздатьГруппу();
		Иначе
			Об = МенеджерОбъекта.СоздатьЭлемент();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Об, СтруктураОтбора);
		ЗаполнитьЗначенияСвойств(Об, ДопРеквизиты);
		
		Попытка
			Об.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
		Возврат Об.Ссылка;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
		
КонецФункции

Функция ПустаяТаблицаДублирующихСтрок()
			
	Возврат ЗагрузкаДанныхИзВнешнегоИсточника.ПустаяТаблицаДублирующихСтрокНоменклатуры();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ПечатьЭтикетокИЦенников") Тогда
		Возврат;
	КонецЕсли;

	Если Не ПравоДоступа("Просмотр", Метаданные.Обработки.ПечатьЭтикетокИЦенников) Тогда
		Возврат;
	КонецЕсли;

	Если УправлениеДоступомУНФ.ЕстьПрофильРабочееМестоКассира() Тогда
		Возврат;
	КонецЕсли;

	Если Не ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.ЦеныНоменклатуры) Тогда
		Возврат;
	КонецЕсли;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьЭтикетокИЦенниковИзДокументов";
	КомандаПечати.Идентификатор = "ПечатьЭтикетокИзПриходнойНакладной";
	КомандаПечати.Представление = НСтр("ru = 'Печать этикеток'");
	КомандаПечати.Порядок = 14;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьЭтикетокИЦенниковИзДокументов";
	КомандаПечати.Идентификатор = "ПечатьЦенниковИзПриходнойНакладной";
	КомандаПечати.Представление = НСтр("ru = 'Печать ценников'");
	КомандаПечати.Порядок = 17;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьВидыСтавокНДС(Параметры = Неопределено, ТолькоНезаполненные = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтавкиНДС.Ссылка КАК Ссылка,
	|	СтавкиНДС.ВидСтавкиНДС КАК ВидСтавкиНДС
	|ПОМЕСТИТЬ ВТСТАВКИ
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	Номенклатура.Ссылка КАК Ссылка,
	|	СправочникСтавки.ВидСтавкиНДС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСТАВКИ КАК СправочникСтавки
	|		ПО (СправочникСтавки.Ссылка = Номенклатура.УдалитьСтавкаНДС)
	|ГДЕ
	|	Номенклатура.УдалитьСтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|	И Номенклатура.ВидСтавкиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПустаяСсылка)
	|	И НЕ Номенклатура.ЭтоГруппа";
	
	Если Параметры = Неопределено Тогда
		Запрос.Текст =СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
		Если Не ТолькоНезаполненные Тогда
			Запрос.Текст =СтрЗаменить(Запрос.Текст, "И Номенклатура.ВидСтавкиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.ПустаяСсылка)", "");
		КонецЕсли;
		
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЕстьОшибки = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НоменклатураОбъект.ВидСтавкиНДС = Выборка.ВидСтавкиНДС;
		НоменклатураОбъект.ДополнительныеСвойства.Вставить("ОбновлениеВидовСтавокНДС");
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатураОбъект);
		Исключение
			ЕстьОшибки = Истина;
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				НСтр("ru='Ошибка заполнения вида ставки НДС'", "ru"),
				УровеньЖурналаРегистрации.Ошибка,,
				НоменклатураОбъект.Ссылка,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если Параметры <> Неопределено Тогда
		Параметры.ОбработкаЗавершена = НЕ ЕстьОшибки И Выборка.Количество() < 1000;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДанныхФоновогоЗадания

Процедура ДанныеДиаграмм(Параметры, ВременноеХранилищеРезультата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = Параметры.ТекстЗапроса;
	Для каждого парамЗапроса Из Параметры.ПараметрыЗапроса Цикл
		Запрос.Параметры.Вставить(парамЗапроса.Ключ, парамЗапроса.Значение);
	КонецЦикла;
	Результат = Запрос.ВыполнитьПакет();
	
	ПоместитьВоВременноеХранилище(Новый Структура("РезультатЗапроса, ПараметрыВывода", Результат, Параметры.ПараметрыВывода), ВременноеХранилищеРезультата);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли