&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Требование = Параметры.Требование;

	ИнициализироватьДанные();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СостояниеОтветаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспознатьАвтоматическиРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОткрытьКалендарь(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НеЗаполненКалендарьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОткрытьКалендарь(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИзменениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КонтекстЭДОКлиент.ПодготвоитьУведомлениеОНевозможностиОтветитьВСрок(Требование);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОтветаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подтвердить(Команда)
	
	Успешно = Истина;
	
	Если Модифицированность Тогда
		Успешно = РегистрацияДанных();
	КонецЕсли;
	
	Если Успешно Тогда
		Оповестить("Запись_ДокументыРеализацииПолномочийНалоговыхОрганов", , Требование);
		Закрыть(Модифицированность);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспознатьАвтоматически(Команда)
	
	РаспознатьАвтоматическиНаСервере();
	
	Оповестить("Запись_ДокументыРеализацииПолномочийНалоговыхОрганов", , Требование);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
		
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Состояние = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Требование);
	
	ЭтоТребованиеДокументов = Требование.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов;
	
	Элементы.ПодсказкаПроОтсрочку.Видимость = ЭтоТребованиеДокументов;
	
	Элементы.РаспознатьАвтоматически.Видимость = 
		ЭтоТребованиеДокументов
		И Состояние.ОтветитьДо <> СрокОтвета;
		
	Элементы.ГруппаТребуетсяОтвет.Доступность = СостояниеОтвета = НСтр("ru = 'Требуется ответ'");
	
	Элементы.НеУдалосьРазобратьPDF.Видимость = 
		НЕ Состояние.УдалосьРазобратьPDF
		И ЭтоТребованиеДокументов
		И Состояние.ОтветитьДо = СрокОтвета
		И НЕ ЗначениеЗаполнено(Состояние.ДатаОтветаИзмененная);
	
	Элементы.НеЗаполненКалендарь.Видимость = Состояние.КалендарьБылНезаполнен;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДанные()
	
	Состояние = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Требование);
	
	Если ЗначениеЗаполнено(Состояние.ДатаОтветаИзмененная) Тогда
		СрокОтвета = Состояние.ДатаОтветаИзмененная;
	Иначе
		СрокОтвета = Состояние.ОтветитьДо;
	КонецЕсли;
	
	Если Состояние.ОтветУжеДан Тогда // В другой программе или другим способом
		СостояниеОтвета = НСтр("ru = 'Ответ уже дан'");
	ИначеЕсли Состояние.ТребуетОтвета Тогда
		СостояниеОтвета = НСтр("ru = 'Требуется ответ'");
	Иначе
		СостояниеОтвета = НСтр("ru = 'Ответ не требуется'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РегистрацияДанных()
	
	Данные = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Требование);
	
	ПоляРегистра = Новый Структура;
	
	ТребуетОтвета = СостояниеОтвета = НСтр("ru = 'Требуется ответ'");
	ПоляРегистра.Вставить("ТребуетОтвета", ТребуетОтвета);
	
	ОтветУжеДан = СостояниеОтвета = НСтр("ru = 'Ответ уже дан'"); // В другой программе или другим способом
	ПоляРегистра.Вставить("ОтветУжеДан", ОтветУжеДан);
	
	ОбновитьДату = 
		ТребуетОтвета
		И Данные.ДатаОтветаИзмененная <> СрокОтвета 
		И Данные.ДатаОтвета <> СрокОтвета;
	
	Если ОбновитьДату Тогда
		ПоляРегистра.Вставить("ДатаОтветаИзмененная", СрокОтвета);
	КонецЕсли;
	
	Результат = ТребованияФНС.ОбновитьИнформациюВРегистреИзвлеченныеДанныеТребований(Требование, ПоляРегистра);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура РаспознатьАвтоматическиНаСервере()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Ссылка", Требование);
	
	ТребованияФНС.ИзвлечьДанныеТребованияИзPDF(ПараметрыОперации);
	
	Состояние = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Требование);
	СрокОтвета = Состояние.ДатаОтветаИзмененная;
	
	Модифицированность = Ложь;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКалендарь(СтандартнаяОбработка)
	
	ТребованияФНСКлиент.ОткрытьКалендарь(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти