&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = Параметры.Ключ.Пустая();
	Если ЭтоНовый Тогда
		ТекстПредупреждения = НСтр("ru = 'Копирование входящих сообщений запрещено!'");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// инициализируем контекст ЭДО - модуль обработки
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
	ПрименяетсяФорматОтветаНаТребованиеПояснений_5_02 = КонтекстЭДОСервер.ПрименяетсяФорматОтветаНаТребованиеПояснений_5_02();
	
	ОпределитьВидДокумента(КонтекстЭДОСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда 
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// При записи ответа необходимо перерисовать количество ответов в панели требования. 
	Если ИмяСобытия = "Запись_ОписиИсходящихДокументовВНалоговыеОрганы" 
		И ТипЗнч(Источник) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы")
		ИЛИ	ИмяСобытия = "Запись_ПоясненияКДекларацииПоНДС" 
		И ТипЗнч(Источник) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС")
		ИЛИ	ИмяСобытия = "Запись_ПерепискаСКонтролирующимиОрганами" 
		И ТипЗнч(Источник) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами")
		ИЛИ	ТипЗнч(Источник) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения")
		ИЛИ	СтрНайти(Врег(ИмяСобытия), НСтр("ru = 'ОТПРАВ'")) Тогда
		
		ОбновитьТекущееСостояниеДокумента();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДокументыРеализацииПолномочийНалоговыхОрганов", , Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НеЗаполненКалендарьРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ТребованияФНСКлиент.ОткрытьКалендарь(СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НеУдалосьРазобратьPDFРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ТребованияФНСКлиент.ОткрытьКалендарь(СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтветыНажатие(Элемент)
	
	КонтекстЭДОКлиент.НажатиеНаКнопкуПоказатьОтветыПоТребованию(Объект.Ссылка, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаСрокиНажатие(Элемент)
	
	ТребованияФНСКлиент.ПоказатьИнформациюоСроках();
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяФайла = НавигационнаяСсылкаФорматированнойСтроки;
	
	Результат = ПолучитьВложениеНаСервере(ИмяФайла);
	Если ЗначениеЗаполнено(Результат.ТекстПредупреждения) Тогда 
		ПоказатьПредупреждение(, Результат.ТекстПредупреждения);
	ИначеЕсли Результат.ВАрхиве Тогда 
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 9, 3, Истина);
		Возврат;
	ИначеЕсли ЭтоXML(ИмяФайла) Тогда
		Текст = ТекстСообщения(Результат.АдресДанных);
		ДокументооборотСКОКлиент.ПоказатьВходящееИзвещениеФНС(ЭтотОбъект, Текст);
	Иначе
		ОперацииСФайламиЭДКОКлиент.ОткрытьФайл(Результат.АдресДанных, ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоXML(ИмяФайла) Экспорт
	
	Возврат Прав(ВРег(ИмяФайла), 4) = ".XML";
					
КонецФункции

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОтветыНажатие(Элемент)
	КонтекстЭДОКлиент.НажатиеНаКнопкуПоказатьОтветыПоТребованию(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДокументаОснованияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Объект.ДокументОснование);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКСверкеИмущественногоНалога(Команда)
	
	ДокументооборотСКОКлиентПереопределяемый.ПерейтиКСверкеПоИмущественномуНалогу(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПояснениеКИзвещениюОбИсчисленныхНалогах(Команда)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.СоздатьПояснениеКИзвещениюОбИсчисленныхНалогах(
		ЭтотОбъект, 
		Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьРеестрДокументов(Команда)
	
	КонтекстЭДОКлиент.ПодготовитьРеестрДокументов(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьРеестрДокументовИмущество(Команда)
	
	КонтекстЭДОКлиент.ПодготовитьРеестрДокументовИмущество(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПакетСДопДокументами(Команда)
	
	КонтекстЭДОКлиент.СоздатьПакетСДопДокуменами(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготвоитьУведомлениеОНевозможностиОтветитьВСрок(Команда)
	
	КонтекстЭДОКлиент.ПодготвоитьУведомлениеОНевозможностиОтветитьВСрок(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьОтветНаТребованиеДокументов(Команда)
	
	КонтекстЭДОКлиент.СоздатьОтветНаТребованиеДокументов(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ПечатнаяФормаДокументаНО = ТабличныйДокументНО(Объект.Ссылка);
	КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаДокументаНО, Объект.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьВложения(Команда)
	
	СохранитьВложения();

КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьОтвет(Команда)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЗапретитьСозданиеПисьмаВОтветНаТребованиеПоДекларации", Истина);
			
	КонтекстЭДОКлиент.СоздатьОтветНаДокументРеализацииПолномочий(Объект.Ссылка, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПодтвердитьПрием(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПодтвержденияПриемаИлиОтказаВПриеме", 
		ЭтотОбъект);
		
	КонтекстЭДОКлиент.СоздатьРезультатПриемаВходящейОписиИнтерактивноПоДокументуОписи(Объект.Ссылка, Истина, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтказатьВПриеме(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПодтвержденияПриемаИлиОтказаВПриеме", 
		ЭтотОбъект);
	
	КонтекстЭДОКлиент.СоздатьРезультатПриемаВходящейОписиИнтерактивноПоДокументуОписи(Объект.Ссылка, Ложь, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьПоясненияПоНДС(Команда)
	КонтекстЭДОКлиент.СоздатьПоясненияКДекларацииПоНДС(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьПоясненияПисьмом(Команда)
	КонтекстЭДОКлиент.СоздатьПисьмоВОтветНаДокументРеализацииПолномочий(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросАктаСверкиРасчетов(Команда)
	
	КонтекстЭДОКлиент.СоздатьЗапросНаСверку(
		Объект.Организация, 
		ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов"));
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросВыпискиОперацийИзКарточкиРасчетыСБюджетом(Команда)
	
	КонтекстЭДОКлиент.СоздатьЗапросНаСверку(
		Объект.Организация, 
		ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом"));
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросСпискаПредставленнойОтчетности(Команда)
	
	КонтекстЭДОКлиент.СоздатьЗапросНаСверку(
		Объект.Организация, 
		ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности"));
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросСправкиОбИсполненииОбязанностейПоУплате(Команда)
	
	КонтекстЭДОКлиент.СоздатьЗапросНаСверку(
		Объект.Организация, 
		ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате"));
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросСправкиОСостоянииРасчетовСБюджетом(Команда)
	
	КонтекстЭДОКлиент.СоздатьЗапросНаСверку(
		Объект.Организация, 
		ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом"));
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСогласиеСМотивированнымМнением(Команда)
	
	КонтекстЭДОКлиент.ПодготвоитьУведомлениеПоТребованию(Объект.Ссылка, "СогласиеСМотивированнымМнением");

КонецПроцедуры

&НаКлиенте
Процедура СоздатьРазногласиеСМотивированнымМнением(Команда)
	
	КонтекстЭДОКлиент.ПодготвоитьУведомлениеПоТребованию(Объект.Ссылка, "РазногласиеСМотивированнымМнением");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВзаимосогласительнаяПроцедура(Команда)
	
	КонтекстЭДОКлиент.ПодготвоитьУведомлениеПоТребованию(Объект.Ссылка, "ВзаимосогласительнаяПроцедура");
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуОтвета(Команда)
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ИзменитьДатуОтветаЗавершение", ЭтотОбъект);
	ТребованияФНСКлиент.ИзменитьСрокТребования(Объект.Ссылка, ОповещениеЗавершения);
	ТекущийЭлемент = Элементы.Комментарий;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьВложениеНаСервере(ИмяФайла)
	
	Результат = Новый Структура("ТекстПредупреждения, АдресДанных, ВАрхиве", "",, Ложь);
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДОСервер = Неопределено Тогда 
		Результат.ТекстПредупреждения = ТекстСообщения;
		Возврат Результат;
	КонецЕсли;
	
	// получаем вложение
	СтрВложения = КонтекстЭДОСервер.ПолучитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Объект.Ссылка, ИмяФайла);
	Если СтрВложения.Количество() = 0 Тогда
		Результат.ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Вложение с именем  %1 не обнаружено.'"), Символ(34) + ИмяФайла + Символ(34));
		Возврат Результат;
	КонецЕсли;
	
	Вложение = СтрВложения[0];
	
	Если Вложение.ВАрхиве Тогда 
		Результат.ВАрхиве = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Адрес = ПоместитьВоВременноеХранилище(Вложение.Данные.Получить(), УникальныйИдентификатор);
	Результат.АдресДанных = Адрес;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПроверитьВложения(Всего, ВАрхиве)
	
	КонтекстМодуля = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ВложенияОснования = КонтекстМодуля.ПолучитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Объект.Ссылка);
	
	Всего = 0;
	ВАрхиве = 0;
	
	Для Каждого Вложение Из ВложенияОснования Цикл 
		ВАрхиве = ВАрхиве + ?(Вложение.ВАрхиве, 1, 0);
		Всего = Всего + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВложения()
	
	МассивИменВложений = Новый Массив;
	Для Каждого Элемент Из Вложения Цикл
		МассивИменВложений.Добавить(Элемент.Значение.ИмяФайла);
	КонецЦикла;
	
	Если МассивИменВложений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Всего = 0;
	ВАрхиве = 0;
	ПроверитьВложения(Всего, ВАрхиве);
	
	ВхПараметры = Новый Структура("МассивИменВложений", МассивИменВложений);
	
	Если ВАрхиве > 0 Тогда 
		ВсеВАрхиве = ?(ВАрхиве = Всего, 1, 0);		
		Описание = Новый ОписаниеОповещения("СохранитьВложенияПослеПроверкиЗавершение", ЭтотОбъект, ВхПараметры);
		КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(Описание, 18 + ВсеВАрхиве, 0, ?(ВсеВАрхиве = 1, Истина, Ложь));
		Возврат;
	КонецЕсли;
	
	СохранитьВложенияПослеПроверкиЗавершение(КодВозвратаДиалога.Да, ВхПараметры);
			
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВложенияПослеПроверкиЗавершение(Результат, ВхПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		
		МассивОписанийПолучаемыеФайлы = ПолучитьМассивОписанийФайловВложений(ВхПараметры.МассивИменВложений);
		ОперацииСФайламиЭДКОКлиент.СохранитьФайлы(МассивОписанийПолучаемыеФайлы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда 
		Закрыть();
	КонецЕсли;

	Сейчас = ТекущаяДата();
	УправлениеЭУ();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивОписанийФайловВложений(МассивИменВложений)
	
	МассивОписаний = Новый Массив;
	
	Для Каждого ИмяВложения Из МассивИменВложений Цикл 
		Результат = ПолучитьВложениеНаСервере(ИмяВложения);
		Если ЗначениеЗаполнено(Результат.АдресДанных) Тогда 
			ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ИмяВложения, Результат.АдресДанных); 
			МассивОписаний.Добавить(ОписаниеФайла);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат МассивОписаний;
	
КонецФункции

&НаСервере
Функция ПрорисоватьСтатус()
	
	ПараметрыПрорисовкиПанелиОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(Объект.Ссылка, Объект.Организация, "ФНС");
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПрименитьПараметрыПрорисовкиПанелиОтправки(ЭтаФорма, ПараметрыПрорисовкиПанелиОтправки);
	ПрорисоватьПанельПриема();
			
	Возврат ПараметрыПрорисовкиПанелиОтправки;
	
КонецФункции

&НаСервере
Процедура ПрорисоватьПанельПриема() Экспорт
	
	Элементы.ГиперссылкаОтветы.Видимость = ВидимостьСсылкиОтветов();
	Элементы.ГруппаПанельПриема.Видимость = ВидимостьКнопкиПриема();
	Элементы.ОтступПередКнопкойОбновитьОтправку.Видимость 	= Истина;
	
	Если ЭтоТребованиеФНС Тогда
		
		Элементы.ЗаголовокНеотправленныхСообщений.Заголовок = 
			НСтр("ru = 'Оператору не отправлено извещение о получении документа'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВидимостьСсылкиОтветов() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Ответы = КонтекстЭДОСервер.ОтветыНаТребование(Объект.Ссылка);
	Видимость = Ответы.Количество() > 0;
	
	Возврат Видимость;
	
КонецФункции

&НаСервере
Функция ВидимостьКнопкиПриема() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ТекущееСостояниеОтправки = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Объект.Ссылка, Перечисления.ТипыКонтролирующихОрганов.ФНС);

	Видимость = Ложь;
	
	Если ТекущееСостояниеОтправки <> Неопределено Тогда
		ТекущийЭтапОтправки = ТекущееСостояниеОтправки.ТекущийЭтапОтправки;
		Если ТекущийЭтапОтправки <> Неопределено Тогда
			
			СостояниеСдачиОтчетности = ТекущийЭтапОтправки.СостояниеСдачиОтчетности;
			
			Видимость = (СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ТребуетсяПодтверждениеПриема);
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Видимость;
	
КонецФункции

&НаСервере
Процедура ОпределитьВидДокумента(КонтекстЭДОСервер)

	ЭтоТребованиеОПредставленииДокументов = 
		Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов;
		
	ЭтоТребованиеОПредставленииПояснений = 
		Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииПоясненийКДекларацииНДС;
		
	ЭтоТребованиеФНС = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоТребованиеФНС(Объект.Ссылка);
	ЭтоРешениеПоЖалобе = Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.РешениеПоЖалобе;
	ЭтоКвитанцияОПрисвоенииРНПТ = (Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.КвитанцияОПрисвоенииРНПТ);
	
	ВТребованииЕстьXMLФайл = КонтекстЭДОСервер.ЕстьТребованияКРазделам8_12(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУ()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	Заголовок = Объект.Наименование;
	
	Элементы.Тема.Заголовок = Заголовок;
	Элементы.ТекстПисьма.Заголовок = "Приложенные файлы:";
	Элементы.ВидРешенияНаЖалобу.Заголовок = Объект.ВидРешенияНаЖалобу;
	Элементы.ВидРешенияНаЖалобу.Видимость = ЗначениеЗаполнено(Объект.ВидРешенияНаЖалобу);
	Элементы.ГруппаКвитанцияОПрисвоенииРНПТ.Видимость = ЭтоКвитанцияОПрисвоенииРНПТ;
	Элементы.ГруппаРеквизитыСобственникаТовараЮЛ.Видимость = ЗначениеЗаполнено(Объект.НаименованиеСобственника);
	Элементы.ГруппаРеквизитыСобственникаТовараФЛ.Видимость = ЗначениеЗаполнено(Объект.ФИОСобственника);
	
	ПрорисоватьПриложения();
	
	ОписьВходящихДокументов = ДокументооборотСКОВызовСервера.ПолучитьОписьВходящихДокументовПоТребованию(Объект.Ссылка);
	
	Элементы.ГруппаСрокиПредставления.Видимость = ЭтоТребованиеФНС;
	
	Если ЭтоТребованиеОПредставленииДокументов Тогда
		Элементы.СсылкаСроки.Видимость = Ложь;
		Элементы.ИнформацияСроки.Заголовок = 
			НСтр("ru = 'Обратите внимание на срок представления документов, указанный в приложенном файле'");
	КонецЕсли;
		
	Если ЭтоТребованиеОПредставленииПояснений Тогда
		
		Элементы.ИнформацияСроки.Заголовок = 
			НСтр("ru = 'Ответ на требование должен быть направлен в течение 5 дней с момента получения'");
			
	КонецЕсли;
	
	УправлениеКнопкамиОтвета(КонтекстЭДОСервер);
	
	Элементы.ЗапроситьСверку.Видимость =
		Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОбУплатеНалогаСбораПениШтрафа;
	Элементы.Печать.Видимость = ЭтоКвитанцияОПрисвоенииРНПТ;
	
	ПрорисоватьПодписанта();
	УстановитьПредставлениеДокументаОснования(КонтекстЭДОСервер);
	
	ОбновитьТекущееСостояниеДокумента();
	ОформитьСрокиПредставленияСервер();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеКнопкамиОтвета(КонтекстЭДОСервер)
	
	ТребуетсяОтправкаДопДокументов = КонтекстЭДОСервер.ТребуетсяОтправкаПакетаСДопДокументами(Объект.Ссылка);
	
	Элементы.ПодготовитьОтветНаТребованиеДокументов.Видимость            = ЭтоТребованиеОПредставленииДокументов;
	Элементы.ПодготвоитьУведомлениеОНевозможностиОтветитьВСрок.Видимость = ЭтоТребованиеОПредставленииДокументов;
	Элементы.ПодготовитьПоясненияПоНДС.Видимость                         = ЭтоТребованиеОПредставленииПояснений;
	Элементы.ПодготовитьПояснениеКДругимОтчетам.Видимость                = ЭтоТребованиеОПредставленииПояснений И НЕ ВТребованииЕстьXMLФайл;
	Элементы.ОтветитьПисьмомНаУведомление.Видимость                      = НЕ ЭтоТребованиеФНС;
	Элементы.СоздатьПакетСДопДокументами.Видимость                       = ТребуетсяОтправкаДопДокументов;
	
	ДобавитьКнопкиОтветаРеестрами();
	ДобавитьКнопкиОтветаНаЭтоМотивированноеМнение();
	ДобавитьКнопкиОтветаНаСообщениеОНалоге();
	
	КоличествоВидимых = 0;
	Для каждого Подменю Из Элементы.ГруппаОтветовНаТребованиеПояснений.ПодчиненныеЭлементы Цикл
		Если Подменю.Видимость Тогда
			КоличествоВидимых = КоличествоВидимых + 1;
		КонецЕсли;
	КонецЦикла; 
	
	Элементы.КомандаСоздатьОтвет.Видимость                = КоличествоВидимых = 1; 
	Элементы.ГруппаОтветовНаТребованиеПояснений.Видимость = КоличествоВидимых > 1;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкиОтветаНаЭтоМотивированноеМнение()
	
	ЭтоМотивированноеМнение = 
		Объект.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.МотивированноеМнение;
	
	ЕстьУведомление = ДокументооборотСКОВызовСервера.ЕстьУведомление("СогласиеСМотивированнымМнением");
	Элементы.СоздатьСогласиеСМотивированнымМнением.Видимость = ЕстьУведомление И ЭтоМотивированноеМнение;
	
	ЕстьУведомление = ДокументооборотСКОВызовСервера.ЕстьУведомление("РазногласиеСМотивированнымМнением");
	Элементы.СоздатьРазногласиеСМотивированнымМнением.Видимость = ЕстьУведомление И ЭтоМотивированноеМнение;
	
	ЕстьУведомление = ДокументооборотСКОВызовСервера.ЕстьУведомление("ВзаимосогласительнаяПроцедура");
	Элементы.СоздатьВзаимосогласительнаяПроцедура.Видимость = ЕстьУведомление И ЭтоМотивированноеМнение;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкиОтветаРеестрами()
	
	ЕстьУведомление = ДокументооборотСКОВызовСервера.ЕстьУведомление("РеестрЛьготИмущество");
	Элементы.ПодготовитьРеестрДокументовИмущество.Видимость = ЕстьУведомление И ЭтоТребованиеОПредставленииПояснений И НЕ ВТребованииЕстьXMLФайл;
	
	ЕстьУведомление = ДокументооборотСКОВызовСервера.ЕстьУведомление("РеестрДокументовПодтверждающихЛьготы");	
	Элементы.ПодготовитьРеестрДокументов.Видимость = ЕстьУведомление И ЭтоТребованиеОПредставленииПояснений;
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьКнопкиОтветаНаСообщениеОНалоге()

	Виды = Перечисления.ВидыНалоговыхДокументов;
	Вид  = Объект.ВидДокумента;

	ЭтоСообщениеОбИсчисленииИмущественныхНалогов = 
		(Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоНалога
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоИмущественногоЗемельногоНалогов
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеЗемельногоНалога);

	Элементы.СоздатьПояснениеКИзвещениюОбИсчисленныхНалогах.Видимость = ЭтоСообщениеОбИсчисленииИмущественныхНалогов;
	РеализованаСверкаИмущественныхНалогов = Ложь;
	ДокументооборотСКОПереопределяемый.ПроверитьРеализованаСверкаИмущественныхНалогов(РеализованаСверкаИмущественныхНалогов);

	Если ЭтоСообщениеОбИсчисленииИмущественныхНалогов И РеализованаСверкаИмущественныхНалогов Тогда
		Элементы.ПерейтиКСверкеИмущественногоНалога.Видимость = Истина;
		Элементы.ПерейтиКСверкеИмущественногоНалога.КнопкаПоУмолчанию = Истина;
		// Скроем стандартную кнопку ответа
		Элементы.КомандаСоздатьОтвет.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПрорисоватьПриложения()
	
	ВсеВложенияТребования = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьВложенияДокументовРеализацииПолномочийНалоговыхОрганов(Объект.Ссылка);
	
	Если ЭтоТребованиеОПредставленииПояснений ИЛИ ЭтоРешениеПоЖалобе ИЛИ ЭтоКвитанцияОПрисвоенииРНПТ Тогда
		// Не показываем xml-файлы, если это требование о представлении пояснений к декларации по НДС или Решение по жалобе или
		// Квитанция о присвоении РНПТ
		Для каждого ПриложениеТребования Из ВсеВложенияТребования Цикл
			
			СвойстваФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПриложениеТребования.ИмяФайла);
			
			Если ВРЕГ(СвойстваФайла.Расширение) <> ВРЕГ(".xml") Тогда
				Вложения.Добавить(ПриложениеТребования);
			КонецЕсли;
		
		КонецЦикла;
	Иначе
		Вложения.ЗагрузитьЗначения(ВсеВложенияТребования);
	КонецЕсли;
	
	Элементы.Вложения.Заголовок = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ФорматированноеПредставлениеСпискаВложений(Вложения.ВыгрузитьЗначения());
	
КонецФункции

&НаКлиенте
Процедура ПослеПодтвержденияПриемаИлиОтказаВПриеме(Результат, ВходящийКонтекст) Экспорт
	
	ОбновитьТекущееСостояниеДокумента();
	
	// Перерисовываем другие формы при необходимости
	ОповеститьОбОкончанииОтправки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбОкончанииОтправки()
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	ПараметрыОповещения.Вставить("Ссылка", 		Объект.Ссылка);
	
	Оповестить("Завершение отправки", ПараметрыОповещения, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПрорисоватьПодписанта()
	
	// Текст подсказки
	НалоговыйОрганПодсказка = СокрЛП(Объект.ПодписантДолжность + " " + Объект.ПодписантФИО);
	
	Если ЗначениеЗаполнено(Объект.ПодписантТелефон) Тогда
		ПодписантТелефон = НСтр("ru = 'тел: '") + Объект.ПодписантТелефон;
	Иначе
		ПодписантТелефон = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПодписантПочта) Тогда
		ПодписантПочта = НСтр("ru = 'e-mail: '") + Объект.ПодписантПочта;
	Иначе
		ПодписантПочта = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПодписантТелефон) И ЗначениеЗаполнено(ПодписантПочта) Тогда
		КонтактныеДанныеПодписанта = ПодписантТелефон + ", " + ПодписантПочта;
	ИначеЕсли ЗначениеЗаполнено(ПодписантТелефон) Тогда
		КонтактныеДанныеПодписанта = ПодписантТелефон;
	Иначе
		КонтактныеДанныеПодписанта = ПодписантПочта;
	КонецЕсли;
	
	НалоговыйОрганПодсказка = СокрЛП(НалоговыйОрганПодсказка + Символы.ПС + КонтактныеДанныеПодписанта);
	
	Элементы.НалоговыйОрган.РасширеннаяПодсказка.Заголовок = НалоговыйОрганПодсказка;
	
	// Отображение подсказки
	Если ЗначениеЗаполнено(НалоговыйОрганПодсказка) Тогда
		Элементы.НалоговыйОрган.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.НалоговыйОрган.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеДокументаОснования(КонтекстЭДОСервер)
	
	// Представление
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		СведенияПоОбъекту = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Объект.ДокументОснование);
		ПредставлениеДокументаОснования = СведенияПоОбъекту.Наименование;
		
		Если НЕ ЗначениеЗаполнено(ПредставлениеДокументаОснования) Тогда
			ПредставлениеДокументаОснования = Строка(Объект.ДокументОснование);
		КонецЕсли;
	Иначе
		ПредставлениеДокументаОснования = "";
	КонецЕсли;
	
	// Видимость
	Элементы.ПредставлениеДокументаОснования.Видимость = 
		ЗначениеЗаполнено(Объект.ДокументОснование)
		И ЗначениеЗаполнено(ПредставлениеДокументаОснования);

КонецПроцедуры

&НаСервере
Функция ТабличныйДокументНО(ДокументНО)
	
	Возврат Справочники.ДокументыРеализацииПолномочийНалоговыхОрганов.ПечатнаяФорма(ДокументНО);
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстСообщения(Адрес)

	Возврат Справочники.ДокументыРеализацииПолномочийНалоговыхОрганов.ТекстСообщения(Адрес);

КонецФункции

#Область ИнформацияПоТребованиям

&НаКлиенте
Процедура ИзменитьДатуОтветаЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Истина Тогда
		ОформитьСрокиПредставленияСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьСрокиПредставления()
	
	Состояние = ТребованияФНС.РасширенноеСостояниеПодтвержденияИОтвета(Объект.Ссылка, Сейчас);
	
	Если Состояние.ЕстьДанные Тогда
	
		Если ЭтоТребованиеФНС Тогда
			ОформитьПредставлениеПодтверждения(Состояние);
			ОформитьПредставлениеОтвета(Состояние);
		КонецЕсли;
		
		Элементы.НеУдалосьРазобратьPDF.Видимость = 
			НЕ Состояние.УдалосьРазобратьPDF
			И Элементы.ИзменитьДатуОтвета.Видимость
			И Не ЗначениеЗаполнено(Состояние.ДатаОтветаИзмененная)
			И ЭтоТребованиеОПредставленииДокументов
			И Состояние.ЕстьДанные;
		
		Элементы.НеЗаполненКалендарь.Видимость = Состояние.КалендарьБылНезаполнен;
		
	ИначеЕсли ЭтоТребованиеФНС Тогда
		
		Если ЭтоТребованиеОПредставленииДокументов Тогда
			Элементы.СсылкаСроки.Видимость = Ложь;
			Элементы.ИнформацияСроки.Заголовок = 
				НСтр("ru = 'Обратите внимание на срок представления документов, указанный в приложенном файле'");
		КонецЕсли;
			
		Если ЭтоТребованиеОПредставленииПояснений Тогда
			Элементы.ИнформацияСроки.Заголовок = 
				НСтр("ru = 'Ответ на требование должен быть направлен в течение 5 дней с момента получения'");
				
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ГруппаНаличиеДаты.Видимость = ЭтоТребованиеФНС И Состояние.ЕстьДанные;
	Элементы.ГруппаБезПарсинга.Видимость = НЕ ЭтоТребованиеФНС ИЛИ НЕ Состояние.ЕстьДанные;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьПредставлениеОтвета(Состояние)
	
	Результат = ТребованияФНС.ПредставлениеОтвета(Состояние, Сейчас);
	
	Элементы.ПоказатьОтветы.Видимость = Результат.ПоказатьОтветы;
	Элементы.ПоказатьОтветы.Заголовок = Результат.ПредставлениеСсылки;
	Элементы.ДатаОтветаПредставление.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Результат.Представление);
	Элементы.ИзменитьДатуОтвета.Видимость = Состояние.Подтвержден И НЕ Состояние.ЕстьОтвет;
	Элементы.ОгонекОтвета.Видимость = Результат.ДобавитьОгонек;
	Элементы.ГруппаОшибокОтвета.Видимость = Состояние.ЕстьКритическаяОшибкаОтвета;

КонецПроцедуры

&НаСервере
Процедура ОформитьПредставлениеПодтверждения(Состояние)
	
	Результат = ТребованияФНС.ПредставлениеПодтверждения(Состояние, Сейчас);
	
	Элементы.ДатаПриемаПредставление.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Результат.Представление);
	Элементы.ОгонекПодтверждения.Видимость = Результат.ДобавитьОгонек;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьСрокиПредставленияСервер()
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	СтатусОтправки = ДокументооборотСКОВызовСервера.ПараметрыПрорисовкиПанелиОтправки(Объект.Ссылка, Объект.Организация, "ФНС");
	ОформитьСрокиПредставления();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекущееСостояниеДокумента()
	
	СтатусОтправки = ПрорисоватьСтатус();
	ОформитьСрокиПредставления();
	
КонецПроцедуры

&НаКлиенте
Процедура НеЗаполненКалендарьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Календарь = ТребованияФНСВызовСервера.ПроизводственныйКалендарь();
	ПоказатьЗначение(, Календарь);
	
КонецПроцедуры

&НаКлиенте
Процедура НеУдалосьРазобратьPDFОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ИзменитьДатуОтветаЗавершение", ЭтотОбъект);
	ТребованияФНСКлиент.ИзменитьСрокТребования(Объект.Ссылка, ОповещениеЗавершения);
	ТекущийЭлемент = Элементы.Комментарий;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСвойстваОтветов(Команда)
	
	Парам = Новый Структура;
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", Объект.Ссылка);
	Парам.Вставить("Отбор", Отбор);
	ОткрытьФорму("РегистрСведений.СвойстваОтветовНаТребования.ФормаСписка", Парам);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСвойстваТребования(Команда)
	
	Парам = Новый Структура;
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", Объект.Ссылка);
	Парам.Вставить("Отбор", Отбор);
	ОткрытьФорму("РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы.ФормаСписка", Парам);
	
КонецПроцедуры



#КонецОбласти

#КонецОбласти
