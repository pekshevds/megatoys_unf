
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей") Тогда
		Если  ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.СостоянияЗаказовПокупателей) Тогда
			Элементы.ФормаСоздать.Видимость = Ложь;
		КонецЕсли;
 	Иначе
 		Элементы.ФормаДобавитьСостояние.Видимость = Ложь;
 	КонецЕсли;
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеСостоянияЗавершен(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
		ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен"));
	
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();

	ВидЗаказа = Параметры.ВидЗаказа;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей") Тогда
		Заголовок = СтрШаблон(Заголовок, Строка(ВидЗаказа));
	Иначе
		Заголовок = НСтр("ru='Состояния заказа покупателя'");				
	КонецЕсли;
		
	СписокСостояний.Параметры.УстановитьЗначениеПараметра("ВидЗаказа", ВидЗаказа);
	
	СписокСостояний.Порядок.Элементы.Очистить();
	
	Порядок = СписокСостояний.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Порядок.Использование = Истина;
	Порядок.Поле = Новый ПолеКомпоновкиДанных("ЗавершенПоследним");
	Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	Порядок = СписокСостояний.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Порядок.Использование = Истина;
	Порядок.Поле = Новый ПолеКомпоновкиДанных("НомерСтроки");
	Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	Элементы.ГруппаПодсказка.Видимость = ОбщегоНазначенияУНФ.ЭтоУНФ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СостоянияЗаказовПокупателей" Тогда
		Если Параметр.Свойство("ЭтоНовый") И Параметр.Свойство("Ссылка") И Параметр.ЭтоНовый Тогда
			Если Источник.ВладелецФормы = Элементы.СписокЭтапов Тогда
				ДобавитьСостояниеЗаказа(ВидЗаказа, Параметр.Ссылка);
			КонецЕсли;
		КонецЕсли;
		УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЭтапов

&НаКлиенте
Процедура СписокЭтаповОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ДобавитьСостояниеЗаказа(ВидЗаказа, ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокЭтаповПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого СтрокаСписка Из Строки Цикл
		Данные = СтрокаСписка.Значение.Данные;
		СтрокаПредставления = Строка(Данные.НомерСтроки) + ". " + Данные.Наименование;  
		Данные.ПредставлениеНаименования = СтрокаПредставления;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСостояние(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
		
	ОткрытьФорму("Справочник.СостоянияЗаказовПокупателей.ФормаВыбора", ПараметрыФормы, Элементы.СписокЭтапов);

КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	ПереместитьЭлемент(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	ПереместитьЭлемент(1);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеПоЦветамСостоянийСервер()
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеПоЦветамСостояний(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
		Метаданные.Справочники.СостоянияЗаказовПокупателей.ПолноеИмя(),
		"Ссылка");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСостояниеЗаказа(ВидЗаказа, СостояниеЗаказа)
	
	Если ВидЗаказа.ПорядокСостояний.НайтиСтроки(Новый Структура("Состояние", СостояниеЗаказа)).Количество() > 0 Тогда				
		Возврат;		
	КонецЕсли;
	
	ВидЗаказаОбъект = ВидЗаказа.ПолучитьОбъект();
	ВидЗаказаОбъект.Заблокировать();
	НоваяСтрока = ВидЗаказаОбъект.ПорядокСостояний.Добавить();
	НоваяСтрока.Состояние = СостояниеЗаказа;
	ВидЗаказаОбъект.Записать();
		
	Элементы.СписокЭтапов.Обновить();
			
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлемент(Смещение)
	
	Если Не ЗначениеЗаполнено(Элементы.СписокЭтапов.ТекущаяСтрока)
		Или Элементы.СписокЭтапов.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен") Тогда
		
		Возврат;
	КонецЕсли;
	
	СостоянияЗаказовВызовСервера.ПереместитьСостояние(ВидЗаказа,
		Элементы.СписокЭтапов.ТекущаяСтрока,
		Смещение);
	Элементы.СписокЭтапов.Обновить();
	
	Оповестить("Запись_ВидыЗаказовПокупателей", ВидЗаказа, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНастройкеПроцессов(Команда) 
	ПараметрыПроцессов = Новый Структура;
	ПараметрыПроцессов.Вставить("НастройкаПроцессов", Истина);
	ПараметрыПроцессов.Вставить("Ключ", ВидЗаказа);
	ОткрытьФорму("Справочник.ВидыЗаказовПокупателей.Форма.ФормаЭлемента", ПараметрыПроцессов);
КонецПроцедуры

#КонецОбласти
