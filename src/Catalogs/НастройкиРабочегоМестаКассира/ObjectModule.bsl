
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РабочееМесто) Тогда
		
		ТекущееРабочееМесто = ПараметрыСеанса.РабочееМестоКлиента;
		
		Если НЕ ЗначениеЗаполнено(ТекущееРабочееМесто) Тогда
			ТекущееРабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
		КонецЕсли; 
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НастройкиРабочегоМестаКассира.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиРабочегоМестаКассира КАК НастройкиРабочегоМестаКассира
		|ГДЕ
		|	НЕ НастройкиРабочегоМестаКассира.ПометкаУдаления
		|	И НастройкиРабочегоМестаКассира.РабочееМесто = &ТекущееРабочееМесто
		|	И НЕ НастройкиРабочегоМестаКассира.РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМеста.ПустаяСсылка)");
		Запрос.УстановитьПараметр("ТекущееРабочееМесто", ТекущееРабочееМесто);
		
		НастройкиТекущегоРабочегоМеста = Запрос.Выполнить().Выбрать();
		
		Если НастройкиТекущегоРабочегоМеста.Количество() = 0 Тогда 
			РабочееМесто = ТекущееРабочееМесто;
		КонецЕсли;
	
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ИспользоватьНовыйПодборТоваров = Истина;
	КонецЕсли;
	
	ТаблицыГорячихКлавиш = ПодготовитьТаблицыГорячихКлавишДляПроверки();
	ПроверитьЗапрещенныеКомбинацииГорячихКлавиш(ТаблицыГорячихКлавиш, Отказ);
	Если Не Отказ Тогда
		ПроверитьУникальностьКомбинацийГорячихКлавиш(ТаблицыГорячихКлавиш, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЗначениеЗаполнено(ОбъектКопирования.РабочееМесто) Тогда
		РабочееМесто = Справочники.РабочиеМеста.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	ПроверитьУникальностьНабораИзбранныхТоваров(Отказ, ТекстОшибки);
	
	ИспользоватьХарактеристики = Ложь;
	ОбщегоНазначенияРМКПереопределяемый.ЗаполнитьИспользованиеХарактеристик(ИспользоватьХарактеристики);
	Если ИспользоватьХарактеристики Тогда
		ПроверитьЗаполнениеХарактеристикБыстрыхТоваров(Отказ, ТекстОшибки);
	КонецЕсли;
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьТаблицыГорячихКлавишДляПроверки()
	
	РезультатФункции = Новый Структура;
	
	РезультатФункции.Вставить("ГорячиеКлавиши", ГорячиеКлавиши.Выгрузить());
	РезультатФункции.Вставить("ГорячиеКлавишиКупюр", ПолучитьГорячиеКлавишиКупюр());
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПолучитьТаблицуЗапрещенныхКлавиш()
	
	ЗапрещенныеКлавиши = ГорячиеКлавиши.ВыгрузитьКолонки();
	ЗапрещенныеКлавиши.Колонки.Удалить("ИмяКнопки");
	ЗапрещенныеКлавиши.Колонки.Удалить("ЗаголовокКнопки");
	
	ДобавитьЗапрещеннуюКомбинациюКлавиш(ЗапрещенныеКлавиши, "F10", Истина, Ложь, Истина);
	
	Возврат ЗапрещенныеКлавиши;
	
КонецФункции

Процедура ДобавитьЗапрещеннуюКомбинациюКлавиш(ЗапрещенныеКлавиши, КлавишаСтрокой,
		АкселераторAlt, АкселераторCtrl, АкселераторShift)
		
	НоваяСтрока = ЗапрещенныеКлавиши.Добавить();
	НоваяСтрока.Клавиша = КлавишаСтрокой;
	НоваяСтрока.АкселераторAlt = АкселераторAlt;
	НоваяСтрока.АкселераторCtrl = АкселераторCtrl;
	НоваяСтрока.АкселераторShift = АкселераторShift;
	
КонецПроцедуры

Функция ПолучитьГорячиеКлавишиКупюр()
	
	ПустаяКлавиша = ИнтерфейсРМКСлужебныйКлиентСервер.ПредставлениеПустойКлавиши();
	
	ГорячиеКлавишиКупюр = ГорячиеКлавиши.ВыгрузитьКолонки();
	ГорячиеКлавишиКупюр.Колонки.Добавить("Номинал", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	ГорячиеКлавишиКупюр.Колонки.Удалить("НомерСтроки");
	ГорячиеКлавишиКупюр.Колонки.Удалить("ИмяКнопки");
	ГорячиеКлавишиКупюр.Колонки.Удалить("ЗаголовокКнопки");
	
	Для Каждого СтрокаНаличных Из ВводНаличных Цикл
		Если ЗначениеЗаполнено(СтрокаНаличных.Клавиша) И Не СтрокаНаличных.Клавиша = ПустаяКлавиша Тогда
			НоваяСтрока = ГорячиеКлавишиКупюр.Добавить();
			НоваяСтрока.Номинал = СтрокаНаличных.Номинал;
			НоваяСтрока.Клавиша = СтрокаНаличных.Клавиша;
			НоваяСтрока.АкселераторAlt = ВводНаличныхАкселераторAlt;
			НоваяСтрока.АкселераторCtrl = ВводНаличныхАкселераторCtrl;
			НоваяСтрока.АкселераторShift = ВводНаличныхАкселераторShift;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ГорячиеКлавишиКупюр;
	
КонецФункции

Процедура ПроверитьУникальностьКомбинацийГорячихКлавиш(ТаблицыГорячихКлавиш, Отказ)

	Если НЕ Отказ Тогда
		
		КоличествоВведенныхКомбинаций = ТаблицыГорячихКлавиш.ГорячиеКлавиши.Количество();
		
		ЗарезервированныеГорячиеКлавишиИзбранного = ГорячиеКлавиши.ВыгрузитьКолонки();
		ГорячиеКлавишиКупюр = ТаблицыГорячихКлавиш.ГорячиеКлавишиКупюр;
		
		КомбинацияОткрытияПанелиИзбранного = ГорячиеКлавиши.Найти("ОткрытьПанельИзбранныхТоваров", "ИмяКнопки");
		СтроковоеПредставлениеКомбинацииОткрытияПанелиИзбранного = "";
		
		Если НЕ КомбинацияОткрытияПанелиИзбранного = Неопределено Тогда
			
			СоставляющаяCtrl = ?(КомбинацияОткрытияПанелиИзбранного.АкселераторCtrl, "Ctrl +", "");
			СоставляющаяAlt = ?(КомбинацияОткрытияПанелиИзбранного.АкселераторAlt, "Alt +", "");
			СоставляющаяShift = ?(КомбинацияОткрытияПанелиИзбранного.АкселераторShift, "Shift +", "");
			
			СтроковоеПредставлениеКомбинацииОткрытияПанелиИзбранного = СтрШаблон("%1%2%3%4", СоставляющаяCtrl,
				СоставляющаяAlt, СоставляющаяShift);
			
			ПозицияВиртуальнойСтроки = КоличествоВведенныхКомбинаций + 1;
			
			НомерПП = 0;
			
			Пока НомерПП < 10 Цикл
				
				НоваяКомбинация = ЗарезервированныеГорячиеКлавишиИзбранного.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяКомбинация, КомбинацияОткрытияПанелиИзбранного);
				НоваяКомбинация.НомерСтроки = ПозицияВиртуальнойСтроки + НомерПП;
				НоваяКомбинация.ИмяКнопки = СтрШаблон(НСтр("ru = 'ИзбранныйТовар%1'"), НомерПП);
				НоваяКомбинация.ЗаголовокКнопки = СтрШаблон(НСтр("ru = 'ИзбранныйТовар%1'"), НомерПП);
				НоваяКомбинация.Клавиша = СтрШаблон("_%1", НомерПП);
				
				НомерПП = НомерПП + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Избранное.НомерСтроки КАК НомерСтроки,
		|	Избранное.ИмяКнопки КАК ИмяКнопки,
		|	Избранное.ЗаголовокКнопки КАК ЗаголовокКнопки,
		|	Избранное.Клавиша КАК Клавиша,
		|	Избранное.АкселераторAlt КАК АкселераторAlt,
		|	Избранное.АкселераторCtrl КАК АкселераторCtrl,
		|	Избранное.АкселераторShift КАК АкселераторShift
		|ПОМЕСТИТЬ ЗарезервированныеГорячиеКлавишиИзбранного
		|ИЗ
		|	&Избранное КАК Избранное
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГорячиеКлавиши.НомерСтроки КАК НомерСтроки,
		|	ГорячиеКлавиши.ИмяКнопки КАК ИмяКнопки,
		|	ГорячиеКлавиши.ЗаголовокКнопки КАК ЗаголовокКнопки,
		|	ГорячиеКлавиши.Клавиша КАК Клавиша,
		|	ГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
		|	ГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
		|	ГорячиеКлавиши.АкселераторShift КАК АкселераторShift
		|ПОМЕСТИТЬ РанееОпределенныеГорячиеКлавиши
		|ИЗ
		|	&ГорячиеКлавиши КАК ГорячиеКлавиши
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГорячиеКлавишиКупюр.Номинал КАК Номинал,
		|	ГорячиеКлавишиКупюр.Клавиша КАК Клавиша,
		|	ГорячиеКлавишиКупюр.АкселераторAlt КАК АкселераторAlt,
		|	ГорячиеКлавишиКупюр.АкселераторCtrl КАК АкселераторCtrl,
		|	ГорячиеКлавишиКупюр.АкселераторShift КАК АкселераторShift
		|ПОМЕСТИТЬ ГорячиеКлавишиВводНаличных
		|ИЗ
		|	&ГорячиеКлавишиКупюр КАК ГорячиеКлавишиКупюр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РанееОпределенныеГорячиеКлавиши.НомерСтроки КАК НомерСтроки,
		|	"""" КАК Номинал,
		|	РанееОпределенныеГорячиеКлавиши.ИмяКнопки КАК ИмяКнопки,
		|	РанееОпределенныеГорячиеКлавиши.ЗаголовокКнопки КАК ЗаголовокКнопки,
		|	РанееОпределенныеГорячиеКлавиши.Клавиша КАК Клавиша,
		|	РанееОпределенныеГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
		|	РанееОпределенныеГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
		|	РанееОпределенныеГорячиеКлавиши.АкселераторShift КАК АкселераторShift
		|ПОМЕСТИТЬ ТаблицаГорячиеКлавиши
		|ИЗ
		|	РанееОпределенныеГорячиеКлавиши КАК РанееОпределенныеГорячиеКлавиши
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗарезервированныеГорячиеКлавишиИзбранного.НомерСтроки,
		|	"""",
		|	ЗарезервированныеГорячиеКлавишиИзбранного.ИмяКнопки,
		|	ЗарезервированныеГорячиеКлавишиИзбранного.ЗаголовокКнопки,
		|	ЗарезервированныеГорячиеКлавишиИзбранного.Клавиша,
		|	ЗарезервированныеГорячиеКлавишиИзбранного.АкселераторAlt,
		|	ЗарезервированныеГорячиеКлавишиИзбранного.АкселераторCtrl,
		|	ЗарезервированныеГорячиеКлавишиИзбранного.АкселераторShift
		|ИЗ
		|	ЗарезервированныеГорячиеКлавишиИзбранного КАК ЗарезервированныеГорячиеКлавишиИзбранного
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	ГорячиеКлавишиВводНаличных.Номинал,
		|	"""",
		|	"""",
		|	ГорячиеКлавишиВводНаличных.Клавиша,
		|	ГорячиеКлавишиВводНаличных.АкселераторAlt,
		|	ГорячиеКлавишиВводНаличных.АкселераторCtrl,
		|	ГорячиеКлавишиВводНаличных.АкселераторShift
		|ИЗ
		|	ГорячиеКлавишиВводНаличных КАК ГорячиеКлавишиВводНаличных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаГорячиеКлавиши.Клавиша КАК Клавиша,
		|	ТаблицаГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
		|	ТаблицаГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
		|	ТаблицаГорячиеКлавиши.АкселераторShift КАК АкселераторShift,
		|	СУММА(1) КАК КоличествоЗаписей
		|ПОМЕСТИТЬ НаборРазличныхСочетаний
		|ИЗ
		|	ТаблицаГорячиеКлавиши КАК ТаблицаГорячиеКлавиши
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаГорячиеКлавиши.Клавиша,
		|	ТаблицаГорячиеКлавиши.АкселераторAlt,
		|	ТаблицаГорячиеКлавиши.АкселераторCtrl,
		|	ТаблицаГорячиеКлавиши.АкселераторShift
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаГорячиеКлавиши.НомерСтроки КАК НомерСтроки,
		|	ТаблицаГорячиеКлавиши.Номинал КАК Номинал,
		|	ТаблицаГорячиеКлавиши.ИмяКнопки КАК ИмяКнопки,
		|	ТаблицаГорячиеКлавиши.ЗаголовокКнопки КАК ЗаголовокКнопки,
		|	ТаблицаГорячиеКлавиши.Клавиша КАК Клавиша,
		|	ТаблицаГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
		|	ТаблицаГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
		|	ТаблицаГорячиеКлавиши.АкселераторShift КАК АкселераторShift,
		|	ВЫБОР
		|		КОГДА НаборРазличныхСочетаний.КоличествоЗаписей > 1
		|				И НЕ ТаблицаГорячиеКлавиши.Клавиша = &КлавишаНет
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЗаписьУникальна
		|ИЗ
		|	ТаблицаГорячиеКлавиши КАК ТаблицаГорячиеКлавиши
		|		ЛЕВОЕ СОЕДИНЕНИЕ НаборРазличныхСочетаний КАК НаборРазличныхСочетаний
		|		ПО ТаблицаГорячиеКлавиши.Клавиша = НаборРазличныхСочетаний.Клавиша
		|			И ТаблицаГорячиеКлавиши.АкселераторAlt = НаборРазличныхСочетаний.АкселераторAlt
		|			И ТаблицаГорячиеКлавиши.АкселераторCtrl = НаборРазличныхСочетаний.АкселераторCtrl
		|			И ТаблицаГорячиеКлавиши.АкселераторShift = НаборРазличныхСочетаний.АкселераторShift");
		
		Запрос.УстановитьПараметр("ГорячиеКлавиши", ТаблицыГорячихКлавиш.ГорячиеКлавиши);
		Запрос.УстановитьПараметр("Избранное", ЗарезервированныеГорячиеКлавишиИзбранного);
		Запрос.УстановитьПараметр("ГорячиеКлавишиКупюр", ГорячиеКлавишиКупюр);
		Запрос.УстановитьПараметр("КлавишаНет", ИнтерфейсРМКСлужебныйКлиентСервер.ПредставлениеПустойКлавиши());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		СписокПовторяющихсяСтрок = "";
		СписокПовторяющихсяКупюр = "";
		
		Пока Выборка.Следующий() Цикл
			
			Если Не Выборка.ЗаписьУникальна Тогда 
				
				// зарезервированные комбинации клавиш для избранного
				Если Выборка.НомерСтроки > КоличествоВведенныхКомбинаций Тогда
					Продолжить;
				КонецЕсли;
				
				Если Выборка.НомерСтроки = 0 Тогда
					СписокПовторяющихсяКупюр = СтрШаблон("%1%2, ", СписокПовторяющихсяКупюр, Выборка.Номинал);
				Иначе
					СписокПовторяющихсяСтрок = СтрШаблон("%1%2,", СписокПовторяющихсяСтрок, Выборка.НомерСтроки);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ (ПустаяСтрока(СписокПовторяющихсяСтрок) И ПустаяСтрока(СписокПовторяющихсяКупюр)) Тогда
			
			ДополнениеТекстаСообщения = "";
			
			Если НЕ ПустаяСтрока(СтроковоеПредставлениеКомбинацииОткрытияПанелиИзбранного) Тогда 
				
				ДополнениеТекстаСообщения = СтрШаблон(
					НСтр("ru = '%1Для избранных товаров зарезервированы комбинации клавиш ""%2"" [0-9] цифрового ряда.'"),
					Символы.ПС, СтроковоеПредставлениеКомбинацииОткрытияПанелиИзбранного);
					
			КонецЕсли;
			
			ТекстСообщения = "";
			ТекстКупюр = "";
			Если НЕ ПустаяСтрока(СписокПовторяющихсяСтрок) Тогда
				СписокПовторяющихсяСтрок = Лев(СписокПовторяющихсяСтрок, СтрДлина(СписокПовторяющихсяСтрок) - 1);
				Если Не ПустаяСтрока(СписокПовторяющихсяКупюр) Тогда
					ТекстКупюр = НСтр("ru = ' и для купюр: '");
				КонецЕсли;
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строках %1'"), СписокПовторяющихсяСтрок);
			ИначеЕсли Не ПустаяСтрока(СписокПовторяющихсяКупюр) Тогда
				ТекстКупюр = НСтр("ru = 'Для купюр: '");
			КонецЕсли;
			Если Не ПустаяСтрока(СписокПовторяющихсяКупюр) Тогда
				СписокПовторяющихсяКупюр = Лев(СписокПовторяющихсяКупюр, СтрДлина(СписокПовторяющихсяКупюр) - 2);
				ТекстСообщения = СтрШаблон("%1%2%3", ТекстСообщения, ТекстКупюр, СписокПовторяющихсяКупюр);
			КонецЕсли;
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1 неуникальные сочетания горячих клавиш.%2'"),
				ТекстСообщения, ДополнениеТекстаСообщения);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗапрещенныеКомбинацииГорячихКлавиш(ТаблицыГорячихКлавиш, Отказ)
	
	ЗапрещенныеКлавиши = ПолучитьТаблицуЗапрещенныхКлавиш();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ГорячиеКлавиши.НомерСтроки КАК НомерСтроки,
	|	ГорячиеКлавиши.ИмяКнопки КАК ИмяКнопки,
	|	ГорячиеКлавиши.ЗаголовокКнопки КАК ЗаголовокКнопки,
	|	ГорячиеКлавиши.Клавиша КАК Клавиша,
	|	ГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
	|	ГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
	|	ГорячиеКлавиши.АкселераторShift КАК АкселераторShift
	|ПОМЕСТИТЬ РанееОпределенныеГорячиеКлавиши
	|ИЗ
	|	&ГорячиеКлавиши КАК ГорячиеКлавиши
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГорячиеКлавишиКупюр.Номинал КАК Номинал,
	|	ГорячиеКлавишиКупюр.Клавиша КАК Клавиша,
	|	ГорячиеКлавишиКупюр.АкселераторAlt КАК АкселераторAlt,
	|	ГорячиеКлавишиКупюр.АкселераторCtrl КАК АкселераторCtrl,
	|	ГорячиеКлавишиКупюр.АкселераторShift КАК АкселераторShift
	|ПОМЕСТИТЬ ГорячиеКлавишиВводНаличных
	|ИЗ
	|	&ГорячиеКлавишиКупюр КАК ГорячиеКлавишиКупюр
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапрещенныеКлавиши.Клавиша КАК Клавиша,
	|	ЗапрещенныеКлавиши.АкселераторAlt КАК АкселераторAlt,
	|	ЗапрещенныеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
	|	ЗапрещенныеКлавиши.АкселераторShift КАК АкселераторShift
	|ПОМЕСТИТЬ ЗапрещенныеГорячиеКлавиши
	|ИЗ
	|	&ЗапрещенныеКлавиши КАК ЗапрещенныеКлавиши
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееОпределенныеГорячиеКлавиши.НомерСтроки КАК НомерСтроки,
	|	"""" КАК Номинал,
	|	РанееОпределенныеГорячиеКлавиши.Клавиша КАК Клавиша,
	|	РанееОпределенныеГорячиеКлавиши.АкселераторAlt КАК АкселераторAlt,
	|	РанееОпределенныеГорячиеКлавиши.АкселераторCtrl КАК АкселераторCtrl,
	|	РанееОпределенныеГорячиеКлавиши.АкселераторShift КАК АкселераторShift
	|ИЗ
	|	РанееОпределенныеГорячиеКлавиши КАК РанееОпределенныеГорячиеКлавиши
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗапрещенныеГорячиеКлавиши КАК ЗапрещенныеКлавиши
	|		ПО РанееОпределенныеГорячиеКлавиши.Клавиша = ЗапрещенныеКлавиши.Клавиша
	|			И РанееОпределенныеГорячиеКлавиши.АкселераторAlt = ЗапрещенныеКлавиши.АкселераторAlt
	|			И РанееОпределенныеГорячиеКлавиши.АкселераторCtrl = ЗапрещенныеКлавиши.АкселераторCtrl
	|			И РанееОпределенныеГорячиеКлавиши.АкселераторShift = ЗапрещенныеКлавиши.АкселераторShift
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ГорячиеКлавишиВводНаличных.Номинал,
	|	ГорячиеКлавишиВводНаличных.Клавиша,
	|	ГорячиеКлавишиВводНаличных.АкселераторAlt,
	|	ГорячиеКлавишиВводНаличных.АкселераторCtrl,
	|	ГорячиеКлавишиВводНаличных.АкселераторShift
	|ИЗ
	|	ГорячиеКлавишиВводНаличных КАК ГорячиеКлавишиВводНаличных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗапрещенныеГорячиеКлавиши КАК ЗапрещенныеКлавиши
	|		ПО ГорячиеКлавишиВводНаличных.Клавиша = ЗапрещенныеКлавиши.Клавиша
	|			И ГорячиеКлавишиВводНаличных.АкселераторAlt = ЗапрещенныеКлавиши.АкселераторAlt
	|			И ГорячиеКлавишиВводНаличных.АкселераторCtrl = ЗапрещенныеКлавиши.АкселераторCtrl
	|			И ГорячиеКлавишиВводНаличных.АкселераторShift = ЗапрещенныеКлавиши.АкселераторShift";
	
	Запрос.УстановитьПараметр("ГорячиеКлавиши", ТаблицыГорячихКлавиш.ГорячиеКлавиши);
	Запрос.УстановитьПараметр("ГорячиеКлавишиКупюр", ТаблицыГорячихКлавиш.ГорячиеКлавишиКупюр);
	Запрос.УстановитьПараметр("ЗапрещенныеКлавиши", ЗапрещенныеКлавиши);
	
	ШаблонПозицииВСтроке = НСтр("ru = 'В строке %1'");
	ШаблонПозицииДляКупюры = НСтр("ru = 'Для купюры номиналом %1'");
	ТекстПредупреждения = НСтр("ru = 'используется недопустимая комбинация клавиш ""'");
	ТекстПредложения = НСтр("ru = '"". Выберите другую комбинацию.'");
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = "";
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.НомерСтроки = 0 Тогда
				ТекстПозиции = СтрШаблон(ШаблонПозицииДляКупюры, Выборка.Номинал);
			Иначе
				ТекстПозиции = СтрШаблон(ШаблонПозицииВСтроке, Выборка.НомерСтроки);
			КонецЕсли;
			
			СоставляющаяCtrl = ?(Выборка.АкселераторCtrl, "Ctrl + ", "");
			СоставляющаяAlt = ?(Выборка.АкселераторAlt, "Alt + ", "");
			СоставляющаяShift = ?(Выборка.АкселераторShift, "Shift + ", "");
			СтроковоеПредставлениеКомбинации = СтрШаблон("%1%2%3%4",
				СоставляющаяCtrl, СоставляющаяAlt, СоставляющаяShift, Выборка.Клавиша);
			
			ТекстСообщения = СтрШаблон("%1%2%3 %4%5%6", ТекстСообщения, ?(ТекстСообщения = "", "", Символы.ПС), ТекстПозиции,
				ТекстПредупреждения, СтроковоеПредставлениеКомбинации, ТекстПредложения);
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьУникальностьНабораИзбранныхТоваров(Отказ, ТекстОшибки)

	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		ОбщегоНазначенияРМКПереопределяемый.СформироватьЗапросДублейСтрокВБыстрыхТоварах(Запрос, Ссылка);
		Если Не ЗначениеЗаполнено(Запрос.Текст) Тогда
			Отказ = Истина;
			ТекстОшибки = НСтр("ru = 'Не сформирован запрос для проверки дублей строк в быстрых товарах.'");
			Возврат;
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НомераСтрок = "";
		Пока Выборка.Следующий() Цикл
			НомераСтрок = СтрШаблон(НСтр("ru = '%1%2,'"), НомераСтрок, Выборка.НомерСтроки);
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(НомераСтрок) Тогда
			НомераСтрок = Лев(НомераСтрок, СтрДлина(НомераСтрок) - 1);
			ТекстОшибки = СтрШаблон(НСтр("ru = 'В строках %1 содержатся дубли комбинаций ""Номенклатура - Характеристика"".
												|Пожалуйста, удалите дубли.'"), НомераСтрок);
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ Отказ И БыстрыеТовары.Количество() > 12 Тогда
			ТекстОшибки = НСтр("ru ='Программа поддерживает не более 12 товарных позиций в разделе ""Избранное"".
				|Пожалуйста, удалите лишние позиции.'");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполненность характеристик у товаров, в виде которых указан признак использования характеристик
//
// Параметры:
//  Отказ - Булево - признак отказа
//
Процедура ПроверитьЗаполнениеХарактеристикБыстрыхТоваров(Отказ, ТекстОшибки)
	
	Если Не Отказ Тогда
		
		Запрос = Новый Запрос;
		ОбщегоНазначенияРМКПереопределяемый.СформироватьЗапросИспользованияХарактеристикВБыстрыхТоварах(Запрос, Ссылка);
		Если Не ЗначениеЗаполнено(Запрос.Текст) Тогда
			Отказ = Истина;
			ТекстОшибки = НСтр("ru = 'Не сформирован запрос для проверки заполнения характеристик.'");
			Возврат;
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		НомераСтрок = "";
		Пока Выборка.Следующий() Цикл
			НомераСтрок = СтрШаблон(НСтр("ru = '%1%2,'"), НомераСтрок, Выборка.НомерСтроки);
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(НомераСтрок) Тогда
			НомераСтрок = Лев(НомераСтрок, СтрДлина(НомераСтрок) - 1);
			ТекстОшибки = СтрШаблон(НСтр("ru = 'В строках %1 не заполнены характеристики.
				|Пожалуйста, заполните их.'"), НомераСтрок);
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли