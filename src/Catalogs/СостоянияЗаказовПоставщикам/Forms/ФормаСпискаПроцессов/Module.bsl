
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеСостоянияЗавершен(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
		ПредопределенноеЗначение("Справочник.СостоянияЗаказовПоставщикам.Завершен"));
		
	ИспользоватьПроцессы = ПолучитьФункциональнуюОпцию("ИспользоватьПроцессыСостояний");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"СтраницаПроцессы", 
		"Видимость", 
		ИспользоватьПроцессы);
		
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	
	СписокСостояний.Порядок.Элементы.Очистить();
	
	Если Параметры.Свойство("НастройкаПроцессов") Тогда
		Элементы.СтраницыСостояний.ТекущаяСтраница = Элементы.СтраницаПроцессы;
	КонецЕсли;
	
	Порядок = СписокСостояний.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Порядок.Использование = Истина;
	Порядок.Поле = Новый ПолеКомпоновкиДанных("РеквизитДопУпорядочивания");
	Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	Порядок = СписокСостояний.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Порядок.Использование = Истина;
	Порядок.Поле = Новый ПолеКомпоновкиДанных("ЗавершенПоследним");
	Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	
	// Установим настройки формы для случая открытия в режиме выбора
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = КлючНазначенияИспользования + "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = КлючНазначенияИспользования + "Список";
	КонецЕсли;
	
	ИндексСозданныхЭлементов = 2;
	ЗаполнитьПорядокСостояний();
	
	ЗаполнитьТаблицуПроцессов();
	ОбновитьЭлементыПроцессов();
	УстановитьФлагиПроцессов();
		
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	Массив = Новый Массив;
	Массив.Добавить(Метаданные.Справочники.СостоянияЗаказовПоставщикам);
	
	ПараметрыРазмещения.Вставить("Источники", Массив);
	ПараметрыРазмещения.Вставить("КоманднаяПанель", Элементы.ГруппаКоманднаяПанель);
	ПараметрыРазмещения.Вставить("ПрефиксГрупп", "Список");
	ПараметрыРазмещения.Вставить("ВладелецКоманд", Элементы.Список);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СостоянияЗаказовПоставщикам" Тогда
		УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработатьОтветНаВопрос", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Данные были изменены.Сохранить изменения?'");
	ПоказатьВопрос(ОписаниеОповещенияОЗакрытии, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаКоманд = Элементы.Найти("НастройкаПорядкаЭлементов");
	Если ГруппаКоманд <> Неопределено Тогда
		ГруппаКоманд.Доступность =
			Элементы.Список.ТекущаяСтрока <> ПредопределенноеЗначение("Справочник.СостоянияЗаказовПоставщикам.Завершен");
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	СохранитьДанныеПроцессов();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьДанныеПроцессов();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеЭлементамиПроцессов

&НаСервере
Процедура ОбновитьЭлементыПроцессов()
	
	Если НЕ ИспользоватьПроцессы Тогда
		Возврат;
	КонецЕсли;
	
	Если ПорядокСостояний.Количество() = 0 Тогда
		Элементы.ГруппаЗаголовкиСостояний.Видимость = Ложь;
	Иначе
		Элементы.ГруппаЗаголовкиСостояний.Видимость = Истина;
	КонецЕсли;
	
	УдалитьПодчиненныеЭлементы();
	ЗаполнитьЗаголовкиСостояний();
	
	ГруппыПроцессов = Процессы.НайтиСтроки(Новый Структура("ЭтоГруппа", Истина));
	
	Для Каждого Группа Из ГруппыПроцессов Цикл
		
		ИндексГруппы = Группа.ИндексГруппы;
		Если ИндексГруппы >= ИндексСозданныхЭлементов Тогда
			
			ГруппаЗаголовка = Элементы.Добавить("ГруппаДействий_" + ИндексГруппы, Тип("ГруппаФормы"), Элементы.ДействияПоЗаказу);
			УстановитьОформлениеГруппы(ГруппаЗаголовка, Элементы.ГруппаДействий_0);
			
			ДекорацияНадпись = Элементы.Добавить("ЗаголовокДействия_" + ИндексГруппы, Тип("ДекорацияФормы"), ГруппаЗаголовка);
			ДекорацияНадпись.Вид = Элементы.ЗаголовокДействия_0.Вид;
			ДекорацияНадпись.ГоризонтальноеПоложение = Элементы.ЗаголовокДействия_0.ГоризонтальноеПоложение;
			ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.ЗаголовокДействия_0.ГоризонтальноеПоложениеВГруппе;
			ДекорацияНадпись.Заголовок = Группа.Наименование;
			ДекорацияНадпись.Ширина = Элементы.ЗаголовокДействия_0.Ширина;
			ДекорацияНадпись.Видимость = Элементы.ЗаголовокДействия_0.Видимость;
			ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.ЗаголовокДействия_0.РастягиватьПоГоризонтали;
			ДекорацияНадпись.Высота = Элементы.ЗаголовокДействия_0.Высота;
			ДекорацияНадпись.Подсказка = Группа.ПодсказкаГруппы;
			ДекорацияНадпись.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			
			ГруппаОтступа = Элементы.Добавить("ОтступыГруппыДействий_" + ИндексГруппы, Тип("ГруппаФормы"), ГруппаЗаголовка);
			УстановитьОформлениеГруппы(ГруппаОтступа, Элементы.ОтступыГруппыДействий_0); 
			
		Иначе
			ГруппаЗаголовка = Элементы["ГруппаДействий_"+ИндексГруппы];
			ГруппаОтступа = Элементы["ОтступыГруппыДействий_"+ИндексГруппы];
		КонецЕсли;
				
		ИндексСостояния = 0;
		
		Для Каждого Состояние Из ПорядокСостояний Цикл
			
			ДекорацияНадпись = Элементы.Добавить("ГруппаДействия_"+ ИндексГруппы + "_Состояние_"+ИндексСостояния, Тип("ДекорацияФормы"), ГруппаОтступа);
			ДекорацияНадпись.Вид = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.Вид;
			ДекорацияНадпись.ГоризонтальноеПоложение = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.ГоризонтальноеПоложение;
			ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
			ДекорацияНадпись.Заголовок = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.Заголовок;
			ДекорацияНадпись.Ширина = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.Ширина;
			ДекорацияНадпись.Видимость = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.Видимость;
			ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.ГруппаДействия_0_Состояние_ЗавершенУспешно.РастягиватьПоГоризонтали;
			
			ИндексСостояния = ИндексСостояния + 1;
			
		КонецЦикла;
		
		ДействияГруппы = Процессы.НайтиСтроки(Новый Структура("КлючГруппы", Группа.Ключ));
		
		Для Каждого Действия Из ДействияГруппы Цикл
			
			ИндексДействия = Действия.ИндексДействия;
			
			Если ИндексГруппы >= ИндексСозданныхЭлементов Тогда
				
				ГруппаЗаголовка = Элементы.Добавить("ГруппаДействий_" + ИндексГруппы + "_Действие_" + ИндексДействия, Тип("ГруппаФормы"), Элементы.ДействияПоЗаказу);
				УстановитьОформлениеГруппы(ГруппаЗаголовка, Элементы.ГруппаДействий_0_Действие_0);
				
				ДекорацияНадпись = Элементы.Добавить("ГруппаДействий_" + ИндексГруппы + "_Действие_"+ИндексДействия+"_Заголовок", Тип("ДекорацияФормы"), ГруппаЗаголовка);
				ДекорацияНадпись.Вид = Элементы.ГруппаДействий_0_Действие_0_Заголовок.Вид;
				ДекорацияНадпись.ГоризонтальноеПоложение = Элементы.ГруппаДействий_0_Действие_0_Заголовок.ГоризонтальноеПоложение;
				ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.ГруппаДействий_0_Действие_0_Заголовок.ГоризонтальноеПоложениеВГруппе;
				ДекорацияНадпись.Заголовок = Действия.Наименование;
				ДекорацияНадпись.Ширина = Элементы.ГруппаДействий_0_Действие_0_Заголовок.Ширина;
				ДекорацияНадпись.Видимость = Элементы.ГруппаДействий_0_Действие_0_Заголовок.Видимость;
				ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.ГруппаДействий_0_Действие_0_Заголовок.РастягиватьПоГоризонтали;
				ДекорацияНадпись.Гиперссылка = Действия.ЭтоЗадачаАссистента;
				ДекорацияНадпись.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
				Если Действия.ЭтоЗадачаАссистента Тогда
					ДекорацияНадпись.УстановитьДействие("Нажатие", "Подключаемый_ОткрытьФормуЗадачи");
				КонецЕсли;
				
				ГруппаОтступа = Элементы.Добавить("ФлагиГруппыДействий_" + ИндексГруппы + "_Действие_"+ ИндексДействия, Тип("ГруппаФормы"), ГруппаЗаголовка);
				УстановитьОформлениеГруппы(ГруппаОтступа, Элементы.ФлагиГруппаДействий_0_Действие_0); 
				
			Иначе
				ГруппаЗаголовка = Элементы["ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия];
				ГруппаОтступа = Элементы["ФлагиГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия];
			КонецЕсли;
	
			ИндексСостояния = 0;
			
			Для Каждого Состояние Из ПорядокСостояний Цикл
				
				Если Состояние.Состояние = Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
					
					Если ИндексГруппы < ИндексСозданныхЭлементов Тогда
						
						ДекорацияНадпись = Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно"];
						ДекорацияНадпись.Картинка = Элементы.ДекорацияНетКартинки.Картинка;
						ДекорацияНадпись.Гиперссылка = НЕ Действия.ЭтоЗадачаАссистента;
						Если ДекорацияНадпись.Гиперссылка Тогда
							ДекорацияНадпись.УстановитьДействие("Нажатие", Действия.ОбработкаНажатия);
						КонецЕсли; 
						
						ДекорацияНадпись = Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенОтменен"];
						ДекорацияНадпись.Картинка = Элементы.ДекорацияНетКартинки.Картинка;
						ДекорацияНадпись.Гиперссылка = НЕ Действия.ЭтоЗадачаАссистента;
						Если ДекорацияНадпись.Гиперссылка Тогда
							ДекорацияНадпись.УстановитьДействие("Нажатие", Действия.ОбработкаНажатия);
						КонецЕсли;
						
						ИндексСостояния = ИндексСостояния + 1;
						Продолжить;
					КонецЕсли;
					
					ДекорацияНадпись = Элементы.Добавить("Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно", Тип("ДекорацияФормы"), ГруппаОтступа);
					ДекорацияНадпись.Вид = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Вид;
					ДекорацияНадпись.Картинка = Элементы.ДекорацияНетКартинки.Картинка;
					ДекорацияНадпись.Гиперссылка =  НЕ Действия.ЭтоЗадачаАссистента;
					ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
					ДекорацияНадпись.Заголовок = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Заголовок;
					ДекорацияНадпись.Ширина = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Ширина;
					ДекорацияНадпись.Видимость = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Видимость;
					ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.РастягиватьПоГоризонтали;
					ДекорацияНадпись.Высота = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Высота;
					Если ДекорацияНадпись.Гиперссылка Тогда
						ДекорацияНадпись.УстановитьДействие("Нажатие", Действия.ОбработкаНажатия);
					КонецЕсли;
					
					ДекорацияНадпись = Элементы.Добавить("Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенОтменен", Тип("ДекорацияФормы"), ГруппаОтступа);
					ДекорацияНадпись.Вид = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Вид;
					ДекорацияНадпись.Картинка = Элементы.ДекорацияНетКартинки.Картинка;
					ДекорацияНадпись.Гиперссылка =  НЕ Действия.ЭтоЗадачаАссистента;
					ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
					ДекорацияНадпись.Заголовок = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Заголовок;
					ДекорацияНадпись.Ширина = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Ширина;
					ДекорацияНадпись.Видимость = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Видимость;
					ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.РастягиватьПоГоризонтали;
					ДекорацияНадпись.Высота = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Высота;
					Если ДекорацияНадпись.Гиперссылка Тогда
						ДекорацияНадпись.УстановитьДействие("Нажатие", Действия.ОбработкаНажатия);
					КонецЕсли;
				
					ИндексСостояния = ИндексСостояния + 1;
					Продолжить;
					
				КонецЕсли;
				
				ДекорацияНадпись = Элементы.Добавить("Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_Состояние_"+ ИндексСостояния, Тип("ДекорацияФормы"), ГруппаОтступа);
				ДекорацияНадпись.Вид = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Вид;
				ДекорацияНадпись.Картинка = Элементы.ДекорацияНетКартинки.Картинка;
				ДекорацияНадпись.Гиперссылка =  НЕ Действия.ЭтоЗадачаАссистента;
				ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
				ДекорацияНадпись.Заголовок = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Заголовок;
				ДекорацияНадпись.Ширина = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Ширина;
				ДекорацияНадпись.Видимость = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Видимость;
				ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.РастягиватьПоГоризонтали;
				ДекорацияНадпись.Высота = Элементы.Флаг_ГруппаДействий_0_Действие_0_СостояниеЗавершенУспешно.Высота;
				Если ДекорацияНадпись.Гиперссылка Тогда
					ДекорацияНадпись.УстановитьДействие("Нажатие", Действия.ОбработкаНажатия);
				КонецЕсли;
				
				ИндексСостояния = ИндексСостояния + 1;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПереместитьЭлементыЗавершения();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПроцессов()
	
	Процессы.Очистить();
	
	Если НЕ ИспользоватьПроцессы Тогда
		Возврат;
	КонецЕсли;
	
	НоваяГруппа = Процессы.Добавить();
	НоваяГруппа.ЭтоГруппа = Истина;
	НоваяГруппа.ИндексГруппы	  = 0;
	НоваяГруппа.Ключ	  = "КорректировкаЗаказа";
	НоваяГруппа.Наименование = НСтр("ru = 'Корректировка заказа'");
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 0;
	НовоеДействие.ИндексДействия	  = 0;
	НовоеДействие.Ключ	  = "ОбязательноеЗаполнениеПричиныОтмены";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.НаФорме;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'Обязательно заполнять поле ""Причина отмены товара""'");
	НовоеДействие.КлючГруппы   = "КорректировкаЗаказа";
	НовоеДействие.ОбработкаНажатия = "Подключаемый_НажатиеФлагОбязательноЗаполнение";
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 0;
	НовоеДействие.ИндексДействия = 1;
	НовоеДействие.Ключ	  = "Предупреждать о неотгруженных товарах";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.НаФорме;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ТолькоВПоследнемСостоянии;
	НовоеДействие.Наименование = НСтр("ru = 'Предпреждать о неотгруженных товарах при завершении заказа'");
	НовоеДействие.КлючГруппы   = "КорректировкаЗаказа";	
	НовоеДействие.ОбработкаНажатия = "Подключаемый_НажатиеЗапрещатьЗавершениеНеотгруженныхЗаказов";
	
	НоваяГруппа = Процессы.Добавить();
	НоваяГруппа.ЭтоГруппа = Истина;
	НоваяГруппа.ИндексГруппы = 1;
	НоваяГруппа.Ключ	  = "ОбщиеНастройки";
	НоваяГруппа.Наименование = НСтр("ru = 'Общие настройки (для всех видов)'");
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 1;
	НовоеДействие.ИндексДействия = 0;
	НовоеДействие.Ключ	  = "ЗапрещатьИзмененияПрошлыхПериодов";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.НаФорме;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'Запрещать изменения прошлых периодов'");
	НовоеДействие.КлючГруппы   = "ОбщиеНастройки";
	НовоеДействие.ОбработкаНажатия = "Подключаемый_НажатиеЗапрещатьИзменениеПрошлыхПериодов";
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 1;
	НовоеДействие.ИндексДействия = 1;
	НовоеДействие.Ключ	  = "Версионирование";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.НаФорме;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ТолькоВПервомСостоянии;
	НовоеДействие.Наименование = НСтр("ru = 'Хранить историю изменений (заказ покупателя, заказ-наряд)'");
	НовоеДействие.КлючГруппы   = "ОбщиеНастройки";
	НовоеДействие.ОбработкаНажатия = "Подключаемый_НажатиеХранитьИсториюИзменений";
	
	НоваяГруппа = Процессы.Добавить();
	НоваяГруппа.ЭтоГруппа = Истина;
	НоваяГруппа.ИндексГруппы = 2;
	НоваяГруппа.Ключ	  = "СвязанныеЗаказы";
	НоваяГруппа.Наименование = НСтр("ru = 'Даша, измени состояние связанных заказов'");
	НоваяГруппа.ПодсказкаГруппы = 
	НСтр("ru = 'В программе реализован «Ассистент управления нашей фирмой» – Даша (CRM - Сервис - Ассистент управления нашей фирмой). 
	|Даша, как и обычный сотрудник, выполняет поручения в 1С:УНФ. 
	|Например, обрабатывает заказы при получении оплаты или отгрузке.
	|
	|При изменении состояния заказа Даша может изменить состояние связанных заказов. '");
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 2;
	НовоеДействие.ИндексДействия = 0;
	НовоеДействие.Ключ	  = "ИзменитьЗаказПокупателя";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.ВнешняяНастройка;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'Измени состояние заказа покупателя'");
	НовоеДействие.КлючГруппы   = "СвязанныеЗаказы";
	НовоеДействие.ЭтоЗадачаАссистента   = Истина;
	НовоеДействие.ИмяФормыЗадачиАссистента   = "Обработка.АссистентУправления.Форма.ДействияСЗаказомПокупателяПоСостояниюСвязанныхЗаказов";
	НовоеДействие.ИдентификаторГруппыАссистента   = "ИзменениеСостоянияЗаказаПокупателяПоСостояниюЗаказовПоставщикам";

	
	НоваяГруппа = Процессы.Добавить();
	НоваяГруппа.ЭтоГруппа = Истина;
	НоваяГруппа.ИндексГруппы	  = 3;
	НоваяГруппа.Ключ	  = "ПереводЗаказаВСостояние";
	НоваяГруппа.Наименование = НСтр("ru = 'Даша, переведи заказ в состояние'");
	НоваяГруппа.ПодсказкаГруппы = 
	НСтр("ru = 'В программе реализован «Ассистент управления нашей фирмой» – Даша (CRM - Сервис - Ассистент управления нашей фирмой). 
	|Даша, как и обычный сотрудник, выполняет поручения в 1С:УНФ. 
	|Например, обрабатывает заказы при получении оплаты или отгрузке.
	|
	|При выполнении определенных действий в программе Даша может перевести заказ в указанное состояние. '");
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 3;
	НовоеДействие.ИндексДействия = 0;
	НовоеДействие.Ключ	  = "ИзменениеСтатусаОплаты";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.ВнешняяНастройка;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'При изменении статуса оплаты'");
	НовоеДействие.КлючГруппы   = "ПереводЗаказаВСостояние";
	НовоеДействие.ЭтоЗадачаАссистента   = Истина;
	НовоеДействие.ИмяФормыЗадачиАссистента   = "Обработка.АссистентУправления.Форма.ДействияСЗаказомПоставщикуПриОплатеИПоставке";
	НовоеДействие.ИдентификаторГруппыАссистента   = "ИзменениеСостоянияЗаказаПоставщикуПриОплате";
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 3;
	НовоеДействие.ИндексДействия = 1;
	НовоеДействие.Ключ	  = "ИзменениеСтатусаПоставки";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.ВнешняяНастройка;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'При изменении статуса поставки'");
	НовоеДействие.КлючГруппы   = "ПереводЗаказаВСостояние";
	НовоеДействие.ЭтоЗадачаАссистента   = Истина;
	НовоеДействие.ИмяФормыЗадачиАссистента   = "Обработка.АссистентУправления.Форма.ДействияСЗаказомПоставщикуПриОплатеИПоставке";
	НовоеДействие.ИдентификаторГруппыАссистента   = "ИзменениеСостоянияЗаказаПоставщикуПриПоставке";
	
	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 3;
	НовоеДействие.ИндексДействия = 2;
	НовоеДействие.Ключ	  = "ИзменениеСтатусаОплатыИОтгрузки";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.ВнешняяНастройка;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'При изменении статуса оплаты и поставке'");
	НовоеДействие.КлючГруппы   = "ПереводЗаказаВСостояние";
	НовоеДействие.ЭтоЗадачаАссистента   = Истина;
	НовоеДействие.ИмяФормыЗадачиАссистента   = "Обработка.АссистентУправления.Форма.ДействияСЗаказомПоставщикуПриОплатеИПоставке";
	НовоеДействие.ИдентификаторГруппыАссистента   = "ИзменениеСостоянияЗаказаПоставщикуПриОплатеИПоставке";

	НовоеДействие = Процессы.Добавить();
	НовоеДействие.ИндексГруппы	  = 3;
	НовоеДействие.ИндексДействия = 3;
	НовоеДействие.Ключ	  = "ИзменениеСостоянияЗаказаПоставщику";
	НовоеДействие.ТипНастройки = Перечисления.ТипыНастройкиПроцессовСостояний.ВнешняяНастройка;
	НовоеДействие.ВидНастройки = Перечисления.ВидыНастройкиПроцессовСостояний.ВоВсехСостояниях;
	НовоеДействие.Наименование = НСтр("ru = 'При изменении состояния заказа покупателя'");
	НовоеДействие.КлючГруппы   = "ПереводЗаказаВСостояние";
	НовоеДействие.ЭтоЗадачаАссистента   = Истина;
	НовоеДействие.ИмяФормыЗадачиАссистента   = "Обработка.АссистентУправления.Форма.ДействияСЗаказомПоставщикуПоСостояниюСвязанныхЗаказов";
	НовоеДействие.ИдентификаторГруппыАссистента   = "ИзменениеСостоянияЗаказаПоставщикуПоСостояниюЗаказовПокупателей";
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаголовкиСостояний()
	
	ИндексСостояния = 0;
	ШиринаСостояния = 19;
	ЦветЗавершенногоСостояния = Неопределено;
	ГруппаСостояний = Элементы.ГруппаЗаголовкиСостояний;
	
	Для Каждого Состояние Из ПорядокСостояний Цикл
		
		Если Состояние.Состояние = Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ЦветЗавершенногоСостояния = Состояние.Состояние.Цвет.Получить();
			Продолжить;
		КонецЕсли;
		
		ГруппаЗаголовка = Элементы.Добавить("Состояние_" + ИндексСостояния, Тип("ГруппаФормы"), ГруппаСостояний);
		УстановитьОформлениеГруппы(ГруппаЗаголовка, Элементы.Состояние_ЗавершенУспешно);
		
		ДекорацияНадпись = Элементы.Добавить("ЗаголовокСостояние_" + ИндексСостояния, Тип("ДекорацияФормы"), ГруппаЗаголовка);
		ДекорацияНадпись.Вид = Элементы.ЗаголовокСостояние_ЗавершенУспешно.Вид;
		ДекорацияНадпись.ГоризонтальноеПоложение = Элементы.ЗаголовокСостояние_ЗавершенУспешно.ГоризонтальноеПоложение;
		ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.ЗаголовокСостояние_ЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
		ДекорацияНадпись.Заголовок = Строка(Состояние.Состояние);
		ДекорацияНадпись.Ширина = ШиринаСостояния;
		ДекорацияНадпись.Видимость = Элементы.ЗаголовокСостояние_ЗавершенУспешно.Видимость;
		ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.ЗаголовокСостояние_ЗавершенУспешно.РастягиватьПоГоризонтали;
		
		ДекорацияНадпись = Элементы.Добавить("ЛинияСостояние_" + ИндексСостояния, Тип("ДекорацияФормы"), ГруппаЗаголовка);
		ДекорацияНадпись.Вид = Элементы.ЛинияСостояние_ЗавершенУспешно.Вид;
		ДекорацияНадпись.Заголовок = Элементы.ЛинияСостояние_ЗавершенУспешно.Заголовок;
		ДекорацияНадпись.Ширина = ШиринаСостояния;
		ДекорацияНадпись.Видимость = Элементы.ЛинияСостояние_ЗавершенУспешно.Видимость;
		ДекорацияНадпись.РастягиватьПоГоризонтали = Элементы.ЛинияСостояние_ЗавершенУспешно.РастягиватьПоГоризонтали;
		ДекорацияНадпись.ГоризонтальноеПоложение = Элементы.ЛинияСостояние_ЗавершенУспешно.ГоризонтальноеПоложение;
		ДекорацияНадпись.ГоризонтальноеПоложениеВГруппе = Элементы.ЛинияСостояние_ЗавершенУспешно.ГоризонтальноеПоложениеВГруппе;
		ЦветСостояния = Состояние.Состояние.Цвет.Получить();
		
		Если ЦветСостояния <> Неопределено Тогда
			ДекорацияНадпись.ЦветТекста = ЦветСостояния;
		КонецЕсли;
		
		ИндексСостояния = ИндексСостояния + 1; 
		
	КонецЦикла;
	
	Элементы.Переместить(Элементы.Состояние_ЗавершенУспешно, Элементы.ГруппаЗаголовкиСостояний);
	Элементы.Переместить(Элементы.Состояние_ЗавершенОтменен, Элементы.ГруппаЗаголовкиСостояний);
	
	Если ЦветЗавершенногоСостояния <> Неопределено Тогда
		Элементы.ЛинияСостояние_ЗавершенУспешно.ЦветТекста = ЦветЗавершенногоСостояния;
		Элементы.ЛинияСостояние_ЗавершенОтменен.ЦветТекста = ЦветЗавершенногоСостояния;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеГруппы(ДобавляемаяГруппа, ГруппаФормы)
	
	ДобавляемаяГруппа.Вид = ГруппаФормы.Вид;
	ДобавляемаяГруппа.Отображение = ГруппаФормы.Отображение;
	ДобавляемаяГруппа.Группировка = ГруппаФормы.Группировка;
	ДобавляемаяГруппа.ОтображатьЗаголовок = ГруппаФормы.ОтображатьЗаголовок;
	ДобавляемаяГруппа.ЦветФона = ГруппаФормы.ЦветФона;
	ДобавляемаяГруппа.СквозноеВыравнивание = ГруппаФормы.СквозноеВыравнивание;
	ДобавляемаяГруппа.Объединенная = ГруппаФормы.Объединенная;
	ДобавляемаяГруппа.Ширина = ГруппаФормы.Ширина;
	
КонецПроцедуры

&НаСервере
Функция КартинкаДействия(Используется)
	
	Если Используется Тогда
		Возврат БиблиотекаКартинок.ФлагВключен;
	Иначе
		Возврат БиблиотекаКартинок.ФлагОтключен;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПереместитьЭлементыЗавершения()
	
	ИндексГруппы = 0;
	Пока ИндексГруппы < ИндексСозданныхЭлементов Цикл
		
		ИндексДействия = 0;
		ПроцессыГруппы = Процессы.НайтиСтроки(Новый Структура("ИндексГруппы, ЭтоГруппа", ИндексГруппы, Ложь));
		Количество = ПроцессыГруппы.Количество();
		
		Пока ИндексДействия < Количество Цикл
			ЭлементУспешно = Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно"];
			ЭлементОтменен = Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенОтменен"];
			Группа = Элементы["ФлагиГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия];
		
			Элементы.Переместить(ЭлементУспешно, Группа);
			Элементы.Переместить(ЭлементОтменен, Группа);
			ИндексДействия = ИндексДействия + 1;
		КонецЦикла;
		
		ИндексГруппы = ИндексГруппы + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеДействияПроцессов

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуЗадачи(Элемент)
	
	ИмяГруппы = Элемент.Родитель.Имя;
	ИндексПервогоЧисла = СтрНайти(ИмяГруппы, "_",НаправлениеПоиска.СКонца);
	ИмяГруппыДляПоиска = Сред(ИмяГруппы, СтрДлина("ГруппаДействий_")+1);
	ИндексПервойГруппы = СтрНайти(ИмяГруппыДляПоиска, "_",НаправлениеПоиска.СНачала);
	
	ИндексДействия = Число(Прав(ИмяГруппы, СтрДлина(ИмяГруппы) - ИндексПервогоЧисла));
	ИндексГруппы = Число(Лев(ИмяГруппыДляПоиска, ИндексПервойГруппы - 1)); 
	
	Действие = Процессы.НайтиСтроки(Новый Структура("ИндексГруппы, ИндексДействия, ЭтоГруппа", ИндексГруппы, ИндексДействия, Ложь));
	
	ИмяФормыАссистента = Действие[0].ИмяФормыЗадачиАссистента;
	ИдентификаторГруппы = Действие[0].ИдентификаторГруппыАссистента;;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьЗакрытиеФормыЗадачи",ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторГруппы", ИдентификаторГруппы);
	ОткрытьФорму(ИмяФормыАссистента, ПараметрыФормы ,,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеФлагОбязательноЗаполнение(Элемент)
	
	Группа = Элемент.Родитель;
	ИндексЭлемента = Группа.ПодчиненныеЭлементы.Индекс(Элемент);
	ЭлементВключается = Элемент.Картинка = БиблиотекаКартинок.ФлагОтключен;
	Элемент.Картинка = КартинкаДействия(ЭлементВключается);
	Модифицированность = Истина;
	ИзмененыНастройкиОбязательностиЗаполнения = Истина;
	
	Если НЕ ЭлементВключается Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементГалочки ИЗ Группа.ПодчиненныеЭлементы Цикл
		ИндексГалочки = Группа.ПодчиненныеЭлементы.Индекс(ЭлементГалочки);
		Если ИндексГалочки > ИндексЭлемента Тогда
			ЭлементГалочки.Картинка = Элемент.Картинка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеЗапрещатьЗавершениеНеотгруженныхЗаказов(Элемент)
	
	ЭлементВключается = Элемент.Картинка = БиблиотекаКартинок.ФлагОтключен;
	Элемент.Картинка = КартинкаДействия(ЭлементВключается);
	Модифицированность = Истина;
	ИзмененыНастройкиЗапретаЗавершения = Истина; 
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеХранитьИсториюИзменений(Элемент)
	
	ИзмененыНастройкиВерсионирования = Истина;
	ГруппаВерсионирования = Элемент.Родитель;
	
	Если Элемент <> ГруппаВерсионирования.ПодчиненныеЭлементы[0] Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.Картинка = БиблиотекаКартинок.ФлагВключен Тогда
		Элемент.Картинка = БиблиотекаКартинок.ФлагОтключен;
	Иначе
		Элемент.Картинка = БиблиотекаКартинок.ФлагВключен;
	КонецЕсли;
	
	Для Каждого ЭлементГруппы Из ГруппаВерсионирования.ПодчиненныеЭлементы Цикл
		Если ЭлементГруппы = Элемент Тогда
			Продолжить;
		КонецЕсли;
		ЭлементГруппы.Картинка = КартинкаЗадачиАссистента(Элемент.Картинка = БиблиотекаКартинок.ФлагВключен);
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеЗапрещатьИзменениеПрошлыхПериодов(Элемент)
	
	Элемент.Картинка = КартинкаДействия(Элемент.Картинка = БиблиотекаКартинок.ФлагОтключен);
	Модифицированность = Истина;
	ИзмененыНастройкиДатыЗапретаИзменений = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область АссистентУправления

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыЗадачи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьЗакрытиеФормыЗадачиСервер(Результат);
		
КонецПроцедуры 

&НаСервере
Процедура ОбработатьЗакрытиеФормыЗадачиСервер(Результат)
	
	ИнформационнаяБазаПодключена = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	АссистентПодключен = АссистентУправления.Подключен();
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьЗадачиВРаботе(Результат.ГруппаЗадач);
	
	Если НЕ ИнформационнаяБазаПодключена Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ АссистентПодключен Тогда
		Возврат;
	КонецЕсли;
	
	ОбсуждениеСоздано = АссистентУправления.ПолучитьОбсуждениеЖурналРаботыАссистента() <> Неопределено;
	
	Если НЕ ОбсуждениеСоздано Тогда
		Попытка
			СоздатьОбсуждение();
		Исключение
		КонецПопытки;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Результат.АвторИзменений) И Результат.НужноДобавитьВОбсуждение Тогда
		Попытка
			АссистентУправления.ОбновитьУчастниковЖурналРаботыАссистента(Результат.АвторИзменений);
		Исключение
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция КартинкаЗадачиАссистента(Используется)
	
	Если Используется Тогда
		Возврат БиблиотекаКартинок.Готово_16;
	Иначе
		Возврат Элементы.ДекорацияНетКартинки.Картинка;
	КонецЕсли; 
	
КонецФункции

&НаСервере
Процедура ОпределитьЗадачиВРаботе(ИдентификаторГруппы = Неопределено)
	
	ОтборПоГруппе = Ложь;
	Если ИдентификаторГруппы = Неопределено Тогда
		ОтборПоГруппе = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиАссистента.Ссылка КАК Ссылка,
	|	ЗадачиАссистента.Родитель.ИдентификаторГруппы КАК ИдентификаторГруппы,
	|	ЗадачиАссистента.Используется КАК Используется,
	|	ЗадачиАссистента.ТипПредмета КАК ТипПредмета
	|ПОМЕСТИТЬ ИспользуемыеЗадачиАссистента
	|ИЗ
	|	Справочник.ЗадачиАссистентаУправления КАК ЗадачиАссистента
	|ГДЕ
	|	(&ОтборПоГруппе
	|			ИЛИ ЗадачиАссистента.Родитель.ИдентификаторГруппы = &ИдентификаторГруппы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИспользуемыеЗадачиАссистента.ИдентификаторГруппы КАК ИдентификаторГруппы,
	|	ИспользуемыеЗадачиАссистента.Используется КАК Используется,
	|	ИспользуемыеЗадачиАссистента.ТипПредмета КАК ТипПредмета,
	|	ЗначениеЗаполнения.Ссылка КАК Ссылка,
	|	ЗначениеЗаполнения.Параметр КАК Параметр,
	|	ЗначениеЗаполнения.Значение КАК Значение
	|ИЗ
	|	ИспользуемыеЗадачиАссистента КАК ИспользуемыеЗадачиАссистента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиАссистентаУправления.ЗначенияЗаполнения КАК ЗначениеЗаполнения
	|		ПО (ЗначениеЗаполнения.Ссылка = ИспользуемыеЗадачиАссистента.Ссылка)
	|ГДЕ
	|	(ЗначениеЗаполнения.Параметр = ""СостояниеЗаказа""
	|			ИЛИ ЗначениеЗаполнения.Параметр = ""СостояниеЗаказаПоставщику""
	|			ИЛИ ЗначениеЗаполнения.Параметр = ""ВариантЗавершения""
	|			ИЛИ ЗначениеЗаполнения.Параметр = ""ВариантЗавершенияЗаказаПоставщику"")
	|	И (ИспользуемыеЗадачиАссистента.ТипПредмета = ""ЗаказПоставщику""
	|			ИЛИ ИспользуемыеЗадачиАссистента.ТипПредмета = ""ЗаказПоставщикуЗаказНаПроизводство"")
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИспользуемыеЗадачиАссистента.ИдентификаторГруппы,
	|	ИспользуемыеЗадачиАссистента.Используется,
	|	ИспользуемыеЗадачиАссистента.ТипПредмета,
	|	ПараметрыУсловияЗадач.Ссылка,
	|	ПараметрыУсловияЗадач.Параметр,
	|	ПараметрыУсловияЗадач.Значение
	|ИЗ
	|	ИспользуемыеЗадачиАссистента КАК ИспользуемыеЗадачиАссистента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗадачиАссистентаУправления.ПараметрыУсловия КАК ПараметрыУсловияЗадач
	|		ПО (ПараметрыУсловияЗадач.Ссылка = ИспользуемыеЗадачиАссистента.Ссылка)
	|ГДЕ
	|	(ПараметрыУсловияЗадач.Параметр = ""СостояниеЗаказа""
	|			ИЛИ ПараметрыУсловияЗадач.Параметр = ""СостояниеЗаказаПоставщику""
	|			ИЛИ ПараметрыУсловияЗадач.Параметр = ""ВариантЗавершения""
	|			ИЛИ ПараметрыУсловияЗадач.Параметр = ""ВариантЗавершенияЗаказаПоставщику"")
	|	И (ИспользуемыеЗадачиАссистента.ТипПредмета = ""ЗаказПоставщику""
	|			ИЛИ ИспользуемыеЗадачиАссистента.ТипПредмета = ""ЗаказПоставщикуЗаказНаПроизводство"")";
	
	Запрос.УстановитьПараметр("ОтборПоГруппе", ОтборПоГруппе);
	Запрос.УстановитьПараметр("ИдентификаторГруппы", ИдентификаторГруппы);
	
	Результат = Запрос.Выполнить();
	
	ТаблицаЗадач = Результат.Выгрузить();
	ТаблицаЗадач.Колонки.Добавить("Состояние", Новый ОписаниеТипов("СправочникСсылка.СостоянияЗаказовПоставщикам, ПеречислениеСсылка.ВариантыЗавершенияЗаказа"));
	
	Для Каждого Строка Из ТаблицаЗадач Цикл
		
		СостояниеЗаказа = Строка.Значение.Получить();
		Строка.Состояние = СостояниеЗаказа;
		
		ДействиеЗадачи = Процессы.НайтиСтроки(Новый Структура("ИдентификаторГруппыАссистента", Строка.ИдентификаторГруппы));
		
		Если ДействиеЗадачи.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексГруппы = ДействиеЗадачи[0].ИндексГруппы;
		ИндексДействия = ДействиеЗадачи[0].ИндексДействия;

		Для Каждого Состояние Из ПорядокСостояний Цикл
			
			ИндексСостояния = ПорядокСостояний.Индекс(Состояние); 
			Если Состояние.Состояние = Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
				Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно"].Картинка = КартинкаЗадачиАссистента(Ложь);
				Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенОтменен"].Картинка = КартинкаЗадачиАссистента(Ложь);
				ИндексСостояния = ИндексСостояния + 1;
				Продолжить;
			КонецЕсли;
			Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_Состояние_"+ ИндексСостояния].Картинка = КартинкаЗадачиАссистента(Ложь);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаЗадач Цикл
		
		ДействиеЗадачи = Процессы.НайтиСтроки(Новый Структура("ИдентификаторГруппыАссистента", Строка.ИдентификаторГруппы));
		
		Если ДействиеЗадачи.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексГруппы = ДействиеЗадачи[0].ИндексГруппы;
		ИндексДействия = ДействиеЗадачи[0].ИндексДействия;
		
		СостояниеЗадачи = Строка.Состояние;
		
		Если СтрНайти(Строка.Параметр, "СостояниеЗаказа") = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексыСостояний = ПорядокСостояний.НайтиСтроки(Новый Структура("Состояние", СостояниеЗадачи));
		
		Если ИндексыСостояний.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИндексСостояния = ПорядокСостояний.Индекс(ИндексыСостояний[0]);
		Если СостояниеЗадачи = Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			
			ВариантЗавершенияУспешно = ТаблицаЗадач.НайтиСтроки(Новый Структура("Ссылка, Состояние", Строка.Ссылка, Перечисления.ВариантыЗавершенияЗаказа.Успешно));
			ВариантЗавершенияОтменен = ТаблицаЗадач.НайтиСтроки(Новый Структура("Ссылка, Состояние", Строка.Ссылка, Перечисления.ВариантыЗавершенияЗаказа.Отменен));
					
			Если ВариантЗавершенияУспешно.Количество() = 0 И ВариантЗавершенияОтменен.Количество() = 0 Тогда
				Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно"].Картинка = КартинкаЗадачиАссистента(Строка.Используется);
				Продолжить;
			КонецЕсли;
			
			Если ВариантЗавершенияУспешно.Количество() <> 0 Тогда
				Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенУспешно"].Картинка = КартинкаЗадачиАссистента(Строка.Используется);
			КонецЕсли;
			
			Если ВариантЗавершенияОтменен.Количество() <> 0 Тогда
				Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_СостояниеЗавершенОтменен"].Картинка = КартинкаЗадачиАссистента(Строка.Используется);
			КонецЕсли;
			
		Иначе
			Элементы["Флаг_ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия+"_Состояние_"+ ИндексСостояния].Картинка = КартинкаЗадачиАссистента(Строка.Используется);			
		КонецЕсли;
		
	КонецЦикла;


КонецПроцедуры

&НаСервере
Процедура СоздатьОбсуждение()
	
	Если АссистентУправления.ПолучитьОбсуждениеЖурналРаботыАссистента() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АссистентУправления.СоздатьОбсуждениеЖурналРаботыАссистента();
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеДанныхПроцессов

&НаСервере
Процедура СохранитьДанныеПроцессов()
	
	ЗаписатьНастройкиВерсионирования();
	ЗаписатьНастройкиДатыЗапрета();
	ЗаполнитьДанныеОбязательностиЗаполнения();
	ЗаполнитьДанныеЗапретаЗавершения();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиВерсионирования()
	
	Если НЕ ИзмененыНастройкиВерсионирования Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользоватьВерсионированиеОбъектов = Константы.ИспользоватьВерсионированиеОбъектов.Получить();
	
	Если Элементы.ФлагиГруппаДействий_1_Действие_1.ПодчиненныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ИспользоватьВерсионированиеОбъектов Тогда
		Константы.ИспользоватьВерсионированиеОбъектов.Установить(Истина);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТипОбъектаВерсионирования) Тогда
		ТипОбъектаВерсионирования = ТипОбъектаВерсионирования();
	КонецЕсли;
	
	Если Элементы.ФлагиГруппаДействий_1_Действие_1.ПодчиненныеЭлементы[0].Картинка = БиблиотекаКартинок.ФлагВключен Тогда
		ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(ТипОбъектаВерсионирования, 
			Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении,
			Перечисления.СрокиХраненияВерсий.Бессрочно);
	Иначе
		ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(ТипОбъектаВерсионирования, 
			Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать,
			Перечисления.СрокиХраненияВерсий.Бессрочно);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиДатыЗапрета()
	
	Если НЕ ИзмененыНастройкиДатыЗапретаИзменений Тогда
		Возврат;
	КонецЕсли;
	
	Группа = Элементы.ФлагиГруппаДействий_1_Действие_0;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел КАК Раздел,
	|	ДатыЗапретаИзменения.Объект КАК Объект,
	|	ДатыЗапретаИзменения.Пользователь КАК Пользователь,
	|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета,
	|	ДатыЗапретаИзменения.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Раздел.Наименование = ""Заказы поставщиков""";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	РазделДатыЗапрета = РазделДатыЗапрета();
	
	ИндексСостояния = 0;
	Для Каждого Флаг ИЗ Группа.ПодчиненныеЭлементы Цикл
		
		Состояние = ПорядокСостояний.Получить(ИндексСостояния).Состояние;
		
		Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
		КонецЕсли;
		
		ЗаписиВРегистре = Таблица.НайтиСтроки(Новый Структура("Объект, Пользователь, ДатаЗапрета", 
			Состояние, 
			Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей, 
			Дата('39991231')));
			
		
		Если ЗаписиВРегистре.Количество() = 0 И Флаг.Картинка = БиблиотекаКартинок.ФлагВключен Тогда
			ЗаписатьДатуЗапретаСОписанием(РазделДатыЗапрета, Состояние, Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей, Дата('39991231'),,);
		КонецЕсли;
		
		Если ЗаписиВРегистре.Количество() <> 0 И Флаг.Картинка = БиблиотекаКартинок.ФлагОтключен Тогда
			Запись = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписиВРегистре[0]); 
			Запись.Удалить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОбязательностиЗаполнения()
	
	Если НЕ ИзмененыНастройкиОбязательностиЗаполнения Тогда 
		Возврат;
	КонецЕсли;
	
	ГруппаНастроек = Элементы.ФлагиГруппаДействий_0_Действие_0;
	
	ПроцессыВида = ПроцессыВида("ОбработкаПроверкиЗаполнения");
	
	Если ПроцессыВида.Количество() = 0 Тогда
		ПроцессыВидаОбъект = Справочники.ПроцессыСостоянийЗаказов.СоздатьЭлемент();
		ПроцессыВидаОбъект.Предмет = "ЗаказПоставщику";
	Иначе
		ПроцессыВидаОбъект = ПроцессыВида[0].Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	СтрокиПричиныОтмены = ПроцессыВидаОбъект.ОбработкаПроверкиЗаполнения.НайтиСтроки(Новый Структура("ИмяРеквизита", "ОтмененныеЗапасы.ПричинаОтмены"));
	Для Каждого Строка Из СтрокиПричиныОтмены Цикл
		ПроцессыВидаОбъект.ОбработкаПроверкиЗаполнения.Удалить(Строка);
	КонецЦикла;
	
	ИндексСостояния = 0;
	
	Для Каждого Элемент Из ГруппаНастроек.ПодчиненныеЭлементы Цикл
		Состояние = ПорядокСостояний.Получить(ИндексСостояния).Состояние;
		
		Если Элемент.Картинка = БиблиотекаКартинок.ФлагОтключен Тогда
			Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
				ИндексСостояния = ИндексСостояния + 1;
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		НоваяПроверка = ПроцессыВидаОбъект.ОбработкаПроверкиЗаполнения.Добавить();
		НоваяПроверка.ИмяРеквизита = "ОтмененныеЗапасы.ПричинаОтмены";
		НоваяПроверка.Состояние = Состояние;
		
		Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
		Иначе
			ВариантЗавершения = ВариантЗавершения(Элемент.Имя);
			НоваяПроверка.ВариантЗавершения = ВариантЗавершения;
		КонецЕсли;
		
	КонецЦикла;
	
	ПроцессыВидаОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеЗапретаЗавершения()
	
	Если НЕ ИзмененыНастройкиЗапретаЗавершения Тогда 
		Возврат;
	КонецЕсли;
	
	ГруппаНастроек = Элементы.ФлагиГруппаДействий_0_Действие_1;
	
	ПроцессыВида = ПроцессыВида("ОбработкаПроведения");
	
	Если ПроцессыВида.Количество() = 0 Тогда
		ПроцессыВидаОбъект = Справочники.ПроцессыСостоянийЗаказов.СоздатьЭлемент();
		ПроцессыВидаОбъект.Предмет = "ЗаказПоставщику";
	Иначе
		ПроцессыВидаОбъект = ПроцессыВида[0].Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	СтрокиПричиныОтмены = ПроцессыВидаОбъект.ОбработкаПроведения.НайтиСтроки(Новый Структура("Свойство", "ПолностьюОтгружен"));
	
	Для Каждого Строка Из СтрокиПричиныОтмены Цикл
		ПроцессыВидаОбъект.ОбработкаПроведения.Удалить(Строка);
	КонецЦикла;
	
	ИндексСостояния = 0;
	
	Для Каждого Элемент Из ГруппаНастроек.ПодчиненныеЭлементы Цикл
		
		Состояние = ПорядокСостояний.Получить(ИндексСостояния).Состояние;
		
		Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
			Продолжить;
		КонецЕсли;
		
		Если Элемент.Картинка <> БиблиотекаКартинок.ФлагВключен Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяПроверка = ПроцессыВидаОбъект.ОбработкаПроведения.Добавить();
		НоваяПроверка.Свойство = "ПолностьюОтгружен";
		НоваяПроверка.Состояние = Состояние;
		ВариантЗавершения = ВариантЗавершения(Элемент.Имя);
		НоваяПроверка.ВариантЗавершения = ВариантЗавершения;
		
	КонецЦикла;
	
	ПроцессыВидаОбъект.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		СохранитьДанныеПроцессов();
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеФлаговПроцессов

Процедура УстановитьФлагиПроцессов()
	
	Если ПорядокСостояний.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ОпределитьЗадачиВРаботе();
	УстановитьФлагиВерсионирования();
	УстановитьФлагиДатыЗапретаИзменений();
	УстановитьФлагиОбязательноеЗаполнениеПричины();
	УстановитьФлагиЗапретЗавершения(); 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлагиДатыЗапретаИзменений()
	
	УстановитьПривилегированныйРежим(Истина);
	ДатыЗапретаВключены = Константы.ИспользоватьДатыЗапретаИзменения.Получить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел КАК Раздел,
	|	ДатыЗапретаИзменения.Объект КАК Объект,
	|	ДатыЗапретаИзменения.Пользователь КАК Пользователь,
	|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета,
	|	ДатыЗапретаИзменения.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Раздел.Наименование = ""Заказы поставщиков""";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Таблица = Запрос.Выполнить().Выгрузить(); 
	
	ИндексСостояния = 0;

	Для Каждого Флаг Из Элементы.ФлагиГруппаДействий_1_Действие_0.ПодчиненныеЭлементы Цикл
		
		Если НЕ ДатыЗапретаВключены Тогда
			Флаг.Картинка = БиблиотекаКартинок.ФлагОтключен;
			Если  ПорядокСостояний[ИндексСостояния].Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
				ИндексСостояния = ИндексСостояния + 1;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		ЕстьНастройкаСостояния = Таблица.НайтиСтроки(Новый Структура("Объект", ПорядокСостояний[ИндексСостояния].Состояние));
		
		Если ЕстьНастройкаСостояния.Количество() <> 0 Тогда
			Флаг.Картинка = БиблиотекаКартинок.ФлагВключен;
		Иначе 
			Флаг.Картинка = БиблиотекаКартинок.ФлагОтключен;
		КонецЕсли; 
		
		Если ПорядокСостояний[ИндексСостояния].Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлагиЗапретЗавершения()
	
	ГруппаНастроек = Элементы.ФлагиГруппаДействий_0_Действие_1;
	ИндексСостояния = 0;
	
	ПроцессыВида = ПроцессыВида("ОбработкаПроведения");
	
	Для Каждого Элемент Из ГруппаНастроек.ПодчиненныеЭлементы Цикл
		
		Состояние = ПорядокСостояний.Получить(ИндексСостояния).Состояние;
		Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
			Продолжить;
		КонецЕсли;
		
		ВариантЗавершения = ВариантЗавершения(Элемент.Имя);
		СтрокаПравила = ПроцессыВида.НайтиСтроки(Новый Структура("Состояние, ВариантЗавершения", Состояние, ВариантЗавершения));
		Элемент.Картинка = КартинкаДействия(СтрокаПравила.Количество() > 0);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлагиВерсионирования()
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользоватьВерсионированиеОбъектов = Константы.ИспользоватьВерсионированиеОбъектов.Получить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НастройкиВерсионированияОбъектов.ТипОбъекта КАК ТипОбъекта,
	|	НастройкиВерсионированияОбъектов.Вариант КАК ВариантВерсионирования,
	|	НастройкиВерсионированияОбъектов.СрокХраненияВерсий КАК СрокХраненияВерсий
	|ИЗ
	|	РегистрСведений.НастройкиВерсионированияОбъектов КАК НастройкиВерсионированияОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО (ИдентификаторыОбъектовМетаданных.Ссылка = НастройкиВерсионированияОбъектов.ТипОбъекта)
	|ГДЕ ПолноеИмя = ""Документ.ЗаказПоставщику""";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Таблица = Запрос.Выполнить().Выгрузить(); 
	
	Для Каждого Флаг Из Элементы.ФлагиГруппаДействий_1_Действие_1.ПодчиненныеЭлементы Цикл
		
		ВерсионированиеИспользуется = Таблица.Количество() <> 0 И Таблица[0].ВариантВерсионирования <> Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
		ВерсионированиеИспользуется = ВерсионированиеИспользуется И ИспользоватьВерсионированиеОбъектов;
		
		Индекс = Элементы.ФлагиГруппаДействий_1_Действие_1.ПодчиненныеЭлементы.Индекс(Флаг);
		
		Если Индекс = 0 Тогда
			Флаг.Картинка = КартинкаДействия(ВерсионированиеИспользуется);
			Продолжить;
		КонецЕсли;
		
		Флаг.Картинка = КартинкаЗадачиАссистента(ВерсионированиеИспользуется);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлагиОбязательноеЗаполнениеПричины()
	
	ГруппаНастроек = Элементы.ФлагиГруппаДействий_0_Действие_0;
	ИндексСостояния = 0;
	
	ПроцессыВида = ПроцессыВида("ОбработкаПроверкиЗаполнения");
	
	Для Каждого Элемент Из ГруппаНастроек.ПодчиненныеЭлементы Цикл
		
		Состояние = ПорядокСостояний.Получить(ИндексСостояния).Состояние;
		ВариантЗавершения = ВариантЗавершения(Элемент.Имя);
		
		СтрокаПравила = ПроцессыВида.НайтиСтроки(Новый Структура("Состояние, ВариантЗавершения", Состояние, ВариантЗавершения));
		Элемент.Картинка = КартинкаДействия(СтрокаПравила.Количество() > 0);
				
		Если Состояние <> Справочники.СостоянияЗаказовПоставщикам.Завершен Тогда
			ИндексСостояния = ИндексСостояния + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформлениеПоЦветамСостоянийСервер()
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеПоЦветамСостояний(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
		Метаданные.Справочники.СостоянияЗаказовПоставщикам.ПолноеИмя(),
		"Ссылка");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПорядокСостояний()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникСостоянияЗаказовПоставщикам.Ссылка КАК Состояние
	|ИЗ
	|	Справочник.СостоянияЗаказовПоставщикам КАК СправочникСостоянияЗаказовПоставщикам
	|
	|УПОРЯДОЧИТЬ ПО
	|	СправочникСостоянияЗаказовПоставщикам.РеквизитДопУпорядочивания";
	
	ПорядокСостоянийЗапрос = Запрос.Выполнить().Выгрузить();
	
	ПорядокСостояний.Загрузить(ПорядокСостоянийЗапрос);
	
КонецПроцедуры

&НаСервере
Функция ТипОбъектаВерсионирования()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка КАК ТипОбъекта
	|ИЗ
	|	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.ПолноеИмя = ""Документ.ЗаказПоставщику""";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.ТипОбъекта;
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция РазделДатыЗапрета()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазделыДатыЗапретаИзменений.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.РазделыДатЗапретаИзменения КАК РазделыДатыЗапретаИзменений
	|ГДЕ
	|	РазделыДатыЗапретаИзменений.Наименование = ""Заказы поставщиков""";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
КонецФункции

&НаСервере
Процедура ЗаписатьДатуЗапретаСОписанием(Знач Раздел, Знач Объект, Знач Пользователь, Знач ДатаЗапрета, Знач ВнутреннееОписаниеДатыЗапрета, Знач Комментарий)
	
	Если Не ЗначениеЗаполнено(ДатаЗапрета) Тогда
		ДатаЗапрета = '39991231';
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Раздел              = Раздел;
	МенеджерЗаписи.Объект              = Объект;
	МенеджерЗаписи.Пользователь        = Пользователь;
	МенеджерЗаписи.ДатаЗапрета         = ДатаЗапрета;
	МенеджерЗаписи.ОписаниеДатыЗапрета = ВнутреннееОписаниеДатыЗапрета;
	МенеджерЗаписи.Комментарий = Комментарий;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Функция ПроцессыВида(ИмяТаблицы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПроцессов(ИмяТаблицы);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаПроцессов(ИмяТаблицы)
	
	Если ИмяТаблицы = "ОбработкаПроверкиЗаполнения" Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроцессыСостоянийЗаказов.Ссылка КАК Ссылка,
		|	ПроцессыОбработкаПроверкиЗаполнения.НомерСтроки КАК НомерСтроки,
		|	ПроцессыОбработкаПроверкиЗаполнения.Состояние КАК Состояние,
		|	ПроцессыОбработкаПроверкиЗаполнения.ИмяРеквизита КАК ИмяРеквизита,
		|	ПроцессыОбработкаПроверкиЗаполнения.ВариантЗавершения КАК ВариантЗавершения
		|ИЗ
		|	Справочник.ПроцессыСостоянийЗаказов КАК ПроцессыСостоянийЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроцессыСостоянийЗаказов.ОбработкаПроверкиЗаполнения КАК ПроцессыОбработкаПроверкиЗаполнения
		|		ПО ПроцессыСостоянийЗаказов.Ссылка = ПроцессыОбработкаПроверкиЗаполнения.Ссылка
		|ГДЕ
		|	ПроцессыСостоянийЗаказов.Предмет = ""ЗаказПоставщику""";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроцессыСостоянийЗаказов.Ссылка КАК Ссылка,
		|	ПроцессыОбработкаПроведения.НомерСтроки КАК НомерСтроки,
		|	ПроцессыОбработкаПроведения.Состояние КАК Состояние,
		|	ПроцессыОбработкаПроведения.ВариантЗавершения КАК ВариантЗавершения,
		|	ПроцессыОбработкаПроведения.Свойство КАК Свойство
		|ИЗ
		|	Справочник.ПроцессыСостоянийЗаказов КАК ПроцессыСостоянийЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПроцессыСостоянийЗаказов.ОбработкаПроведения КАК ПроцессыОбработкаПроведения
		|		ПО ПроцессыСостоянийЗаказов.Ссылка = ПроцессыОбработкаПроведения.Ссылка
		|ГДЕ
		|	ПроцессыСостоянийЗаказов.Предмет = ""ЗаказПоставщику""";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ВариантЗавершения(ИмяЭлемента)
	
	ИмяВариантаЗавершения = Прав(ИмяЭлемента, СтрДлина(ИмяЭлемента) - СтрНайти(ИмяЭлемента, "_", НаправлениеПоиска.СКонца));
	Если ИмяВариантаЗавершения = "СостояниеЗавершенУспешно" Тогда
		Возврат Перечисления.ВариантыЗавершенияЗаказа.Успешно;
	ИначеЕсли ИмяВариантаЗавершения = "СостояниеЗавершенОтменен" Тогда
		Возврат Перечисления.ВариантыЗавершенияЗаказа.Отменен;
	Иначе
		Возврат Перечисления.ВариантыЗавершенияЗаказа.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьПроцессы()
	
	ЗаполнитьТаблицуПроцессов();
	ОбновитьЭлементыПроцессов();
	УстановитьФлагиПроцессов();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПодчиненныеЭлементы()
	
	УдаляемыеЭлементы = Новый Массив;
	
	УдалитьЭлементыЗаголовков(УдаляемыеЭлементы);
	УдалитьЭлементыДействий(УдаляемыеЭлементы);
	
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыЗаголовков(УдаляемыеЭлементы)
	
	Для Каждого Элемент Из Элементы.ГруппаЗаголовкиСостояний.ПодчиненныеЭлементы Цикл
		Если СтрНайти(Элемент.Имя, "Завершен") <> 0 Тогда
			Продолжить;
		КонецЕсли;
		УдаляемыеЭлементы.Добавить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыДействий(УдаляемыеЭлементы)
	
	ГруппыПроцессов = Процессы.НайтиСтроки(Новый Структура("ЭтоГруппа", Истина));
	
	Для Каждого Группа Из ГруппыПроцессов Цикл
		
		ИндексГруппы = Группа.ИндексГруппы;
		ДействияГруппы = Процессы.НайтиСтроки(Новый Структура("КлючГруппы", Группа.Ключ));
		
		ГруппаОтступа = Элементы.Найти("ОтступыГруппыДействий_"+ИндексГруппы);
		
		Если ГруппаОтступа <> Неопределено Тогда
			
			Для ИндексЭлемента = 0 По ГруппаОтступа.ПодчиненныеЭлементы.Количество()-1 Цикл
				
				ЭлементУдаления = ГруппаОтступа.ПодчиненныеЭлементы[ИндексЭлемента];
				Если СтрНайти(ЭлементУдаления.Имя, "Завершен") <> 0 Тогда
					Продолжить;
				КонецЕсли;
				УдаляемыеЭлементы.Добавить(ГруппаОтступа.ПодчиненныеЭлементы[ИндексЭлемента]);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ИндексГруппы >= ИндексСозданныхЭлементов Тогда
			ГруппаЗаголовкаДействия = Элементы.Найти("ГруппаДействий_"+ИндексГруппы);
			Если ГруппаЗаголовкаДействия <> Неопределено Тогда
				УдаляемыеЭлементы.Добавить(ГруппаЗаголовкаДействия);
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого Действие Из ДействияГруппы Цикл
			
			ИндексДействия = Действие.ИндексДействия;
			ГруппаДействия = Элементы.Найти("ФлагиГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия);
			
			Если ГруппаДействия <> Неопределено Тогда
				Для ИндексЭлемента = 0 По ГруппаДействия.ПодчиненныеЭлементы.Количество()-1 Цикл
					ЭлементУдаления = ГруппаДействия.ПодчиненныеЭлементы[ИндексЭлемента];
					Если СтрНайти(ЭлементУдаления.Имя, "Завершен") <> 0 Тогда
						Продолжить;
					КонецЕсли;
					
					УдаляемыеЭлементы.Добавить(ГруппаДействия.ПодчиненныеЭлементы[ИндексЭлемента]);
					
				КонецЦикла;
			КонецЕсли;
			
					
			Если ИндексГруппы < ИндексСозданныхЭлементов Тогда
				Продолжить;
			КонецЕсли;
			
			ГруппаУдаления = Элементы.Найти("ГруппаДействий_"+ИндексГруппы+"_Действие_"+ИндексДействия);
				
			Если ГруппаУдаления <> Неопределено Тогда
				УдаляемыеЭлементы.Добавить(ГруппаУдаления);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	ОбновитьПроцессы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
