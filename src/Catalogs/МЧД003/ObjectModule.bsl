// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Наименование = МашиночитаемыеДоверенности.ПредставлениеОбъекта(ЭтотОбъект);

	Если Не ЗначениеЗаполнено(ХешФайла) Тогда
		ДанныеФайла = ФайлМЧД.Получить();
		Если ТипЗнч(ДанныеФайла) = Тип("ДвоичныеДанные") Тогда
			ХешФайла = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ДанныеФайла, ХешФункция.SHA256);
		КонецЕсли;
	КонецЕсли;

	ПараметрыЗаписиВЖурналМЧД = МашиночитаемыеДоверенности.НовыеПараметрыЗаписиВЖурналМЧД();
	ДополнительныеСвойства.Вставить("ПараметрыЗаписиВЖурналМЧД", ПараметрыЗаписиВЖурналМЧД);
	Если Не ЭтоНовый() Тогда
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ХешФайла");
		Если ПредыдущиеЗначенияРеквизитов.ХешФайла <> ХешФайла Тогда
			ПараметрыЗаписиВЖурналМЧД.ИзменилсяХеш = Истина;
			ПараметрыЗаписиВЖурналМЧД.ПредыдущееЗначениеХеша = ПредыдущиеЗначенияРеквизитов.ХешФайла;
		КонецЕсли;
	КонецЕсли;

	Если ВариантЗаполненияПолномочий = Перечисления.ВариантыЗаполненияПолномочийМЧД.Текст Тогда
		Полномочия.Очистить();
	ИначеЕсли ВариантЗаполненияПолномочий = Перечисления.ВариантыЗаполненияПолномочийМЧД.КлассификаторФНС Тогда
		ТекстовоеПолномочие = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область События

// Добавляет или обновляет запись в журнале МЧД. Возникает в подписке ПриЗаписи.
// См. МашиночитаемыеДоверенности.ЗаписатьМашиночитаемуюДоверенностьВЖурнал
// 
// Параметры:
//  Отказ - Булево
Процедура ЗаписатьВЖурнал(Отказ) Экспорт
	ДанныеДоверенности = ФайлМЧД.Получить(); // ДвоичныеДанные
	Если Не ЗначениеЗаполнено(ХешФайла) Или Не ЗначениеЗаполнено(Подписи) Или ДанныеДоверенности = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Запись = РегистрыСведений.ЖурналМашиночитаемыхДоверенностей.НоваяЗаписьЖурнала();
	Запись.Хеш = ХешФайла;
	Запись.Номер = НомерДоверенности;
	СведенияФайла = МашиночитаемыеДоверенности.ДанныеИзФайлаОбмена(ДанныеДоверенности).ДанныеДоверенности; // См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.Доверенность
	ЗаполнитьДоверителейВЗаписиЖурнала(СведенияФайла, Запись);
	ЗаполнитьПредставителейВЗаписиЖурнала(СведенияФайла, Запись);
	Запись.ДатаВыдачи = ДатаВыдачи;
	Запись.Доверенность = Ссылка;
	Запись.ДатаОкончанияДействия = ДатаОкончанияДействия();
	Запись.Верна = Верна;
	Запись.Действует = Действует();
	Запись.ПометкаУдаления = ПометкаУдаления;
	РегистрыСведений.ЖурналМашиночитаемыхДоверенностей.ДобавитьЗапись(Запись);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Перечисления

// Классификатор типов доверителей
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура :
// * ЮридическоеЛицо - Строка
// * ИностраннаяОрганизация - Строка
// * ИндивидуальныйПредприниматель - Строка 
// * ФизическоеЛицо - Строка
Функция ТипыДоверителей()
	ТипыДоверителей = Новый Структура;
	ТипыДоверителей.Вставить("ЮридическоеЛицо", "1");
	ТипыДоверителей.Вставить("ИностраннаяОрганизация", "2");
	ТипыДоверителей.Вставить("ИндивидуальныйПредприниматель", "3");
	ТипыДоверителей.Вставить("ФизическоеЛицо", "4");
	Возврат Новый ФиксированнаяСтруктура(ТипыДоверителей);
КонецФункции

// Классификатор типов представителей
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * ЮридическоеЛицо - Строка
// * ИндивидуальныйПредприниматель - Строка
// * ФизическоеЛицо - Строка
// * ФилиалЮридическогоЛица - Строка
// * ФилиалИностраннойОрганизации - Строка
Функция ТипыПредставителей()
	ТипыПредставителей = Новый Структура;
	ТипыПредставителей.Вставить("ЮридическоеЛицо", "1");
	ТипыПредставителей.Вставить("ИндивидуальныйПредприниматель", "2");
	ТипыПредставителей.Вставить("ФизическоеЛицо", "3");
	ТипыПредставителей.Вставить("ФилиалЮридическогоЛица", "4");
	ТипыПредставителей.Вставить("ФилиалИностраннойОрганизации", "5");
	Возврат Новый ФиксированнаяСтруктура(ТипыПредставителей);
КонецФункции

#КонецОбласти

#Область ЗаполнениеЖурнала

Процедура ЗаполнитьДоверителейВЗаписиЖурнала(СведенияФайла, Запись)
	Журнал = РегистрыСведений.ЖурналМашиночитаемыхДоверенностей;
	КлассификаторЖурнала = Журнал.ТипыДоверителей();
	КлассификаторФормата = ТипыДоверителей();
	СписокДоверителей = СведенияФайла.Документ.Довер.СвДоверит; // СписокXDTO Из См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвДоверитТип
	Для Каждого СведенияОДоверителе Из СписокДоверителей Цикл
		Если СведенияОДоверителе.ТипДоверит = КлассификаторФормата.ЮридическоеЛицо Тогда
			Доверитель = Журнал.НовыйДоверитель(КлассификаторЖурнала.ЮридическоеЛицо);
			СведенияОбОрганизации = СведенияОДоверителе.Доверит.РосОргДовер.СвРосОрг; // См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвОргТип
			Доверитель.ДанныеЮридическогоЛица.Наименование = Строка(СведенияОбОрганизации.НаимОрг);
			Доверитель.ДанныеЮридическогоЛица.ИНН = Строка(СведенияОбОрганизации.ИННЮЛ);
			Доверитель.ДанныеЮридическогоЛица.КПП = Строка(СведенияОбОрганизации.КПП);
			Доверитель.ДанныеЮридическогоЛица.ОГРН = Строка(СведенияОбОрганизации.ОГРН);
			СписокЛицБезДоверенности = СведенияОДоверителе.Доверит.РосОргДовер.ЛицоБезДов; // СписокXDTO Из См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.ЛицоБезДовТип
			Для Каждого ЛицоБезДоверенности Из СписокЛицБезДоверенности Цикл
				Если ЛицоБезДоверенности.СВЮЛ <> Неопределено Тогда
					УправляющаяКомпания = Журнал.НовоеЮридическоеЛицо();
					Доверитель.ДанныеЮридическогоЛица.ЛицаБезДоверенности.Добавить(УправляющаяКомпания);
					УправляющаяКомпания.Наименование = Строка(ЛицоБезДоверенности.СВЮЛ.СвЮЛЕИО.НаимОрг);
					УправляющаяКомпания.ИНН = Строка(ЛицоБезДоверенности.СВЮЛ.СвЮЛЕИО.ИННЮЛ);
					УправляющаяКомпания.КПП = Строка(ЛицоБезДоверенности.СВЮЛ.СвЮЛЕИО.КПП);
					УправляющаяКомпания.ОГРН = Строка(ЛицоБезДоверенности.СВЮЛ.СвЮЛЕИО.ОГРН);
					СписокФизЛицБезДоверенности = ЛицоБезДоверенности.СВЮЛ.СвФЛ; // СписокXDTO Из См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвФЛТип
					Для Каждого ФизЛицоБезДоверенности Из СписокФизЛицБезДоверенности Цикл
						ФизЛицо = Журнал.НовоеФизическоеЛицо();
						УправляющаяКомпания.ЛицаБезДоверенности.Добавить(ФизЛицо);
						ФизЛицо.ФИО = ОбщегоНазначенияБЭДКлиентСервер.ФИОСтрокой(ФизЛицоБезДоверенности.СведФЛ.ФИО);
						ФизЛицо.ИНН = Строка(ФизЛицоБезДоверенности.ИННФЛ);
						ФизЛицо.СНИЛС = Строка(ФизЛицоБезДоверенности.СНИЛС);
					КонецЦикла;
				КонецЕсли;
				Если ЛицоБезДоверенности.СвИП <> Неопределено Тогда
					Предприниматель = Журнал.НовыйИндивидуальныйПредприниматель();
					Доверитель.ДанныеЮридическогоЛица.ЛицаБезДоверенности.Добавить(Предприниматель);
					Предприниматель.Наименование = Строка(ЛицоБезДоверенности.СвИП.НаимИП);
					Предприниматель.ИНН  = Строка(ЛицоБезДоверенности.СвИП.ИННФЛ);
					Предприниматель.СНИЛС = Строка(ЛицоБезДоверенности.СвИП.СНИЛС);
					Предприниматель.ОГРН = Строка(ЛицоБезДоверенности.СвИП.ОГРНИП);
				КонецЕсли;
				Если ЛицоБезДоверенности.СвФЛ <> Неопределено Тогда
					ФизЛицо = Журнал.НовоеФизическоеЛицо();
					Доверитель.ДанныеЮридическогоЛица.ЛицаБезДоверенности.Добавить(ФизЛицо);
					ФизЛицо.ФИО = ОбщегоНазначенияБЭДКлиентСервер.ФИОСтрокой(ЛицоБезДоверенности.СвФЛ.СведФЛ.ФИО);
					ФизЛицо.ИНН = Строка(ЛицоБезДоверенности.СвФЛ.ИННФЛ);
					ФизЛицо.СНИЛС = Строка(ЛицоБезДоверенности.СвФЛ.СНИЛС);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли СведенияОДоверителе.ТипДоверит = КлассификаторФормата.ИностраннаяОрганизация Тогда
			Доверитель = Журнал.НовыйДоверитель(КлассификаторЖурнала.ИностраннаяОрганизация);
			СведенияОбОрганизации = СведенияОДоверителе.Доверит.ИнОргДовер.СвИнОрг; // См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвИнОргТип
			Доверитель.ДанныеИностраннойОрганизации.Наименование = Строка(СведенияОбОрганизации.НаимИО);
			Доверитель.ДанныеИностраннойОрганизации.ИНН = Строка(СведенияОбОрганизации.ИННЮЛ);
			Доверитель.ДанныеИностраннойОрганизации.КПП = Строка(СведенияОбОрганизации.КПП);
			СписокРуководителей = СведенияОДоверителе.Доверит.ИнОргДовер.СвРукОП; // СписокXDTO Из См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвФЛТип
			Для Каждого Руководитель Из СписокРуководителей Цикл
				ФизЛицо = Журнал.НовоеФизическоеЛицо();
				Доверитель.ДанныеИностраннойОрганизации.Руководители.Добавить(ФизЛицо);
				ФизЛицо.ФИО = ОбщегоНазначенияБЭДКлиентСервер.ФИОСтрокой(Руководитель.СведФЛ.ФИО);
				ФизЛицо.ИНН = Строка(Руководитель.ИННФЛ);
				ФизЛицо.СНИЛС = Строка(Руководитель.СНИЛС);
			КонецЦикла;
		ИначеЕсли СведенияОДоверителе.ТипДоверит = КлассификаторФормата.ИндивидуальныйПредприниматель Тогда
			Доверитель = Журнал.НовыйДоверитель(КлассификаторЖурнала.ИндивидуальныйПредприниматель);
			СведенияОПредпринимателей = СведенияОДоверителе.Доверит.ИПДовер;
			Доверитель.ДанныеИндивидуальногоПредпринимателя.Наименование = Строка(СведенияОПредпринимателей.НаимИП);
			Доверитель.ДанныеИндивидуальногоПредпринимателя.ИНН = Строка(СведенияОПредпринимателей.ИННФЛ);
			Доверитель.ДанныеИндивидуальногоПредпринимателя.СНИЛС = Строка(СведенияОПредпринимателей.СНИЛС);
			Доверитель.ДанныеИндивидуальногоПредпринимателя.ОГРН = Строка(СведенияОПредпринимателей.ОГРНИП);
		ИначеЕсли СведенияОДоверителе.ТипДоверит = КлассификаторФормата.ФизическоеЛицо Тогда
			Доверитель = Журнал.НовыйДоверитель(КлассификаторЖурнала.ФизическоеЛицо);
			СведенияОФизЛице = СведенияОДоверителе.Доверит.ФЛДовер; // См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвФЛТип
			Доверитель.ДанныеФизическогоЛица.ФИО = ОбщегоНазначенияБЭДКлиентСервер.ФИОСтрокой(
				СведенияОФизЛице.СведФЛ.ФИО);
			Доверитель.ДанныеФизическогоЛица.ИНН = Строка(СведенияОФизЛице.ИННФЛ);
			Доверитель.ДанныеФизическогоЛица.СНИЛС = Строка(СведенияОФизЛице.СНИЛС);
		Иначе
			ШаблонОшибки = НСтр("ru='Не удалось заполнить доверителя, недопустимое значение поля %1: %2';");
			ВызватьИсключение СтрШаблон(ШаблонОшибки, "ТипДоверит", СведенияОДоверителе.ТипДоверит);
		КонецЕсли;
		Запись.Доверители.Добавить(Доверитель);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПредставителейВЗаписиЖурнала(СведенияФайла, Запись)
	Журнал = РегистрыСведений.ЖурналМашиночитаемыхДоверенностей;
	КлассификаторЖурнала = Журнал.ТипыПредставителей();
	КлассификаторФормата = ТипыПредставителей();
	СписокПредставителей = СведенияФайла.Документ.Довер.СвУпПред; // СписокXDTO Из См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвУпПредТип
	Для Каждого СведенияПредставителя Из СписокПредставителей Цикл
		Если СведенияПредставителя.ТипПред = КлассификаторФормата.ЮридическоеЛицо Тогда
			Представитель = Журнал.НовыйПредставитель(КлассификаторЖурнала.ЮридическоеЛицо);
			СведенияОбОрганизации = СведенияПредставителя.Пред.СведОрг;
			Представитель.ДанныеЮридическогоЛица.Наименование = Строка(СведенияОбОрганизации.НаимОрг);
			Представитель.ДанныеЮридическогоЛица.ИНН = Строка(СведенияОбОрганизации.ИННЮЛ);
			Представитель.ДанныеЮридическогоЛица.КПП = Строка(СведенияОбОрганизации.КПП);
			Представитель.ДанныеЮридическогоЛица.ОГРН = Строка(СведенияОбОрганизации.ОГРН);
		ИначеЕсли СведенияПредставителя.ТипПред = КлассификаторФормата.ИндивидуальныйПредприниматель Тогда
			Представитель = Журнал.НовыйПредставитель(КлассификаторЖурнала.ИндивидуальныйПредприниматель);
			СведенияОПредпринимателе = СведенияПредставителя.Пред.СведИП;
			Представитель.ДанныеИндивидуальногоПредпринимателя.Наименование = Строка(СведенияОПредпринимателе.НаимИП);
			Представитель.ДанныеИндивидуальногоПредпринимателя.ИНН = Строка(СведенияОПредпринимателе.ИННФЛ);
			Представитель.ДанныеИндивидуальногоПредпринимателя.ОГРН = Строка(СведенияОПредпринимателе.ОГРНИП);
			Представитель.ДанныеИндивидуальногоПредпринимателя.СНИЛС = Строка(СведенияОПредпринимателе.СНИЛС);
		ИначеЕсли СведенияПредставителя.ТипПред = КлассификаторФормата.ФизическоеЛицо Тогда
			Представитель = Журнал.НовыйПредставитель(КлассификаторЖурнала.ФизическоеЛицо);
			СведенияОФизЛице = СведенияПредставителя.Пред.СведФизЛ; // См. XDTOПакет.ON_EMCHD_1_928_00_01_01_01.СвФЛТип
			Представитель.ДанныеФизическогоЛица.ФИО = ОбщегоНазначенияБЭДКлиентСервер.ФИОСтрокой(
				СведенияОФизЛице.СведФЛ.ФИО);
			Представитель.ДанныеФизическогоЛица.ИНН = Строка(СведенияОФизЛице.ИННФЛ);
			Представитель.ДанныеФизическогоЛица.СНИЛС = Строка(СведенияОФизЛице.СНИЛС);
		ИначеЕсли СведенияПредставителя.ТипПред = КлассификаторФормата.ФилиалЮридическогоЛица Тогда
			Представитель = Журнал.НовыйПредставитель(КлассификаторЖурнала.ФилиалЮридическогоЛица);
			СведенияОбОрганизации = СведенияПредставителя.Пред.СведФилиал;
			Представитель.ДанныеФилиалаЮридическогоЛица.Наименование = Строка(СведенияОбОрганизации.НаимОрг);
			Представитель.ДанныеФилиалаЮридическогоЛица.ИНН = Строка(СведенияОбОрганизации.ИННЮЛ);
			Представитель.ДанныеФилиалаЮридическогоЛица.КПП = Строка(СведенияОбОрганизации.КПП);
			Представитель.ДанныеФилиалаЮридическогоЛица.ОГРН = Строка(СведенияОбОрганизации.ОГРН);
		ИначеЕсли СведенияПредставителя.ТипПред = КлассификаторФормата.ФилиалИностраннойОрганизации Тогда
			Представитель = Журнал.НовыйПредставитель(КлассификаторЖурнала.ФилиалИностраннойОрганизации);
			СведенияОбОрганизации = СведенияПредставителя.Пред.СведИО;
			Представитель.ДанныеФилиалаИностраннойОрганизации.Наименование = Строка(СведенияОбОрганизации.НаимИО);
			Представитель.ДанныеФилиалаИностраннойОрганизации.ИНН = Строка(СведенияОбОрганизации.ИННЮЛ);
			Представитель.ДанныеФилиалаИностраннойОрганизации.КПП = Строка(СведенияОбОрганизации.КПП);
		Иначе
			ШаблонОшибки = НСтр("ru='Не удалось заполнить представителя, недопустимое значение поля %1: %2';");
			ВызватьИсключение СтрШаблон(ШаблонОшибки, "ТипПред", СведенияПредставителя.ТипПред);
		КонецЕсли;
		Запись.Представители.Добавить(Представитель);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ДатаОкончанияДействия()
	Если ЗначениеЗаполнено(СрокДействия) И Не ЗначениеЗаполнено(ДатаПрекращения) Тогда
		Возврат СрокДействия;
	ИначеЕсли ЗначениеЗаполнено(ДатаПрекращения) И Не ЗначениеЗаполнено(СрокДействия) Тогда
		Возврат ДатаПрекращения;
	Иначе
		Возврат Мин(СрокДействия, ДатаПрекращения);
	КонецЕсли;
КонецФункции

Функция Действует()
	СвойстваДоверенности = МашиночитаемыеДоверенности.НовыеСвойстваДоверенности();
	ЗаполнитьЗначенияСвойств(СвойстваДоверенности, ЭтотОбъект);
	СвойстваДоверенности.ДатаОкончания = СрокДействия;
	Возврат МашиночитаемыеДоверенности.ДоверенностьДействительнаПоСвойствам(СвойстваДоверенности, ТекущаяДатаСеанса());
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли