
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказНарядов") Тогда
		Если  ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.СостоянияЗаказНарядов) Тогда
			Элементы.ФормаСоздать.Видимость = Ложь;
		КонецЕсли;
 	Иначе
 		Элементы.ФормаДобавитьСостояние.Видимость = Ложь;
 	КонецЕсли;
 	
	СостоянияЗаказов.УстановитьУсловноеОформлениеСостоянияЗавершен(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
																   ПредопределенноеЗначение("Справочник.СостоянияЗаказНарядов.Завершен"));
	
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	
	ВидЗаказа = Параметры.ВидЗаказа;

	ИспользоватьВидыЗаказНарядов = ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказНарядов");
	
    Если НЕ ИспользоватьВидыЗаказНарядов Тогда
		СостояниеВыполнения =  ВидЗаказа.СостояниеВыполнения;
		ЗаполнитьСписокВыбораСостоянияВыполнения(ВидЗаказа);
	Иначе
		Элементы.СостояниеВыполнения.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораСостоянияВыполнения(ВидЗаказа);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказНарядов") Тогда
		Заголовок = СтрШаблон(Заголовок, Строка(ВидЗаказа));
	Иначе
		Заголовок = НСтр("ru='Состояния заказа-наряда'");				
	КонецЕсли;
			
	СписокСостояний.Параметры.УстановитьЗначениеПараметра("ВидЗаказа", ВидЗаказа);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СостоянияЗаказНарядов" Тогда
		Если Параметр.Свойство("ЭтоНовый") И Параметр.Свойство("Ссылка") И Параметр.ЭтоНовый Тогда
			Если Источник.ВладелецФормы = Элементы.СписокЭтапов Тогда
				ДобавитьСостояниеЗаказа(ВидЗаказа, Параметр.Ссылка);
			КонецЕсли;
		КонецЕсли;		
		УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
		Если НЕ ИспользоватьВидыЗаказНарядов Тогда
			ЗаполнитьСписокВыбораСостоянияВыполнения(ВидЗаказа);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СостояниеВыполненияПриИзменении(Элемент)
	
	СохранитьСостояниеВыполнения(ВидЗаказа, СостояниеВыполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЭтапов

&НаКлиенте
Процедура СписокЭтаповОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ДобавитьСостояниеЗаказа(ВидЗаказа, ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокЭтаповПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого СтрокаСписка Из Строки Цикл
		Данные = СтрокаСписка.Значение.Данные;
		СтрокаПредставления = Строка(Данные.НомерСтроки) + ". " + Данные.Наименование;  
		Данные.ПредставлениеНаименования = СтрокаПредставления;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСостояние(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
		
	ОткрытьФорму("Справочник.СостоянияЗаказНарядов.ФормаВыбора", ПараметрыФормы, Элементы.СписокЭтапов);

КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	ПереместитьЭлемент(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	ПереместитьЭлемент(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНастройкеПроцессов(Команда) 
	ПараметрыПроцессов = Новый Структура;
	ПараметрыПроцессов.Вставить("НастройкаПроцессов", Истина);
	ПараметрыПроцессов.Вставить("Ключ", ВидЗаказа);
	ОткрытьФорму("Справочник.ВидыЗаказНарядов.Форма.ФормаЭлемента", ПараметрыПроцессов);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеПоЦветамСостоянийСервер()
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеПоЦветамСостояний(СписокСостояний.КомпоновщикНастроек.Настройки.УсловноеОформление,
																   Метаданные.Справочники.СостоянияЗаказНарядов.ПолноеИмя(),
																   "Ссылка");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСостояниеЗаказа(ВидЗаказа, СостояниеЗаказа)
	
	Если ВидЗаказа.ПорядокСостояний.НайтиСтроки(Новый Структура("Состояние", СостояниеЗаказа)).Количество() > 0 Тогда				
		Возврат;		
	КонецЕсли;
	
	ВидЗаказаОбъект = ВидЗаказа.ПолучитьОбъект();	
	ВидЗаказаОбъект.Заблокировать();
	НоваяСтрока = ВидЗаказаОбъект.ПорядокСостояний.Добавить();
	НоваяСтрока.Состояние = СостояниеЗаказа;
	ВидЗаказаОбъект.Записать();
		
	Элементы.СписокЭтапов.Обновить();
			
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлемент(Смещение)
	
	Если Не ЗначениеЗаполнено(Элементы.СписокЭтапов.ТекущаяСтрока)
		Или Элементы.СписокЭтапов.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.СостоянияЗаказНарядов.Завершен") Тогда
		
		Возврат;
	КонецЕсли;
	
	СостоянияЗаказовВызовСервера.ПереместитьСостояние(ВидЗаказа,
		Элементы.СписокЭтапов.ТекущаяСтрока,
		Смещение);
	Элементы.СписокЭтапов.Обновить();
	
	Оповестить("Запись_ВидыЗаказНарядов", ВидЗаказа, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьСостояниеВыполнения(ВидЗаказа, СостояниеВыполнения)
	
	ВидОбъект = ВидЗаказа.ПолучитьОбъект();
	ВидОбъект.Заблокировать();
	ВидОбъект.СостояниеВыполнения = СостояниеВыполнения;
	ВидОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораСостоянияВыполнения(ВидЗаказа)
	
	Элементы.СостояниеВыполнения.СписокВыбора.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыЗаказНарядовПорядокСостояний.Состояние КАК Состояние
		|ИЗ
		|	Справочник.ВидыЗаказНарядов.ПорядокСостояний КАК ВидыЗаказНарядовПорядокСостояний
		|ГДЕ
		|	ВидыЗаказНарядовПорядокСостояний.Ссылка = &ВидЗаказа
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыЗаказНарядовПорядокСостояний.НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидЗаказа", ВидЗаказа);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Элементы.СостояниеВыполнения.СписокВыбора.Добавить(Выборка.Состояние);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
