#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПочтовыеЯщикиПоИменам(УчетнаяЗапись, ИменаПочтовыхЯщиковНаСервере) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПочтовыеЯщикиУчетныхЗаписей.Ссылка КАК Ссылка,
	|	ПочтовыеЯщикиУчетныхЗаписей.НаименованиеНаСервере КАК НаименованиеНаСервере
	|ИЗ
	|	Справочник.ПочтовыеЯщикиУчетныхЗаписей КАК ПочтовыеЯщикиУчетныхЗаписей
	|ГДЕ
	|	ПочтовыеЯщикиУчетныхЗаписей.НаименованиеНаСервере В(&ИменаПочтовыхЯщиковНаСервере)
	|	И ПочтовыеЯщикиУчетныхЗаписей.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИменаПочтовыхЯщиковНаСервере", ИменаПочтовыхЯщиковНаСервере);
	ПочтовыеЯщики = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Соответствие;
	Пока ПочтовыеЯщики.Следующий() Цикл
		Результат.Вставить(ПочтовыеЯщики.НаименованиеНаСервере, ПочтовыеЯщики.Ссылка);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ПочтовыйЯщикПоИмени(УчетнаяЗапись, ИмяПочтовогоЯщикаНаСервере) Экспорт
	
	ИменаПочтовыхЯщиковНаСервере = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИмяПочтовогоЯщикаНаСервере);
	ПочтовыеЯщикиПоИменам = ПочтовыеЯщикиПоИменам(УчетнаяЗапись, ИменаПочтовыхЯщиковНаСервере);
	Возврат ПочтовыеЯщикиПоИменам.Получить(ИмяПочтовогоЯщикаНаСервере);
	
КонецФункции

Функция ПочтовыйЯщикПоТипу(УчетнаяЗапись, ТипПочтовогоЯщика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПочтовыеЯщикиУчетныхЗаписей.Ссылка КАК Ссылка,
	|	ПочтовыеЯщикиУчетныхЗаписей.НаименованиеНаСервере КАК НаименованиеНаСервере
	|ИЗ
	|	Справочник.ПочтовыеЯщикиУчетныхЗаписей КАК ПочтовыеЯщикиУчетныхЗаписей
	|ГДЕ
	|	ПочтовыеЯщикиУчетныхЗаписей.Владелец = &Владелец
	|	И ПочтовыеЯщикиУчетныхЗаписей.ВидПапки = &ТипПочтовогоЯщика";
	Запрос.УстановитьПараметр("Владелец", УчетнаяЗапись);
	Запрос.УстановитьПараметр("ТипПочтовогоЯщика", ТипПочтовогоЯщика);
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат Справочники.ПочтовыеЯщикиУчетныхЗаписей.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ТипПочтовогоЯщика(ПочтовыйЯщик) Экспорт
	
	ТипыПочтовыхЯщиков = ТипыПочтовыхЯщиков(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПочтовыйЯщик));
	Возврат ТипыПочтовыхЯщиков.Получить(ПочтовыйЯщик);
	
КонецФункции

Функция ТипыПочтовыхЯщиков(ПочтовыеЯщики) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПочтовыеЯщикиУчетныхЗаписей.Ссылка КАК Ссылка,
	|	ПочтовыеЯщикиУчетныхЗаписей.ВидПапки КАК ВидПапки,
	|	ПочтовыеЯщикиУчетныхЗаписей.Родитель.ВидПапки КАК Родитель1ВидПапки,
	|	ПочтовыеЯщикиУчетныхЗаписей.Родитель.Родитель.ВидПапки КАК Родитель2ВидПапки,
	|	ПочтовыеЯщикиУчетныхЗаписей.Родитель.Родитель.Родитель.ВидПапки КАК Родитель3ВидПапки,
	|	ПочтовыеЯщикиУчетныхЗаписей.Родитель.Родитель.Родитель.Родитель.ВидПапки КАК Родитель4ВидПапки,
	|	ПочтовыеЯщикиУчетныхЗаписей.Родитель.Родитель.Родитель.Родитель.Родитель.ВидПапки КАК Родитель5ВидПапки
	|ИЗ
	|	Справочник.ПочтовыеЯщикиУчетныхЗаписей КАК ПочтовыеЯщикиУчетныхЗаписей
	|ГДЕ
	|	ПочтовыеЯщикиУчетныхЗаписей.Ссылка В(&ПочтовыеЯщики)";
	Запрос.УстановитьПараметр("ПочтовыеЯщики", ПочтовыеЯщики);
	ВидыПапок = Запрос.Выполнить().Выбрать();
	Пока ВидыПапок.Следующий() Цикл
		Если ЗначениеЗаполнено(ВидыПапок.ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.ВидПапки;
		ИначеЕсли ЗначениеЗаполнено(ВидыПапок.Родитель1ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.Родитель1ВидПапки;
		ИначеЕсли ЗначениеЗаполнено(ВидыПапок.Родитель2ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.Родитель2ВидПапки;
		ИначеЕсли ЗначениеЗаполнено(ВидыПапок.Родитель3ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.Родитель3ВидПапки;
		ИначеЕсли ЗначениеЗаполнено(ВидыПапок.Родитель4ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.Родитель4ВидПапки;
		ИначеЕсли ЗначениеЗаполнено(ВидыПапок.Родитель5ВидПапки) Тогда
			ТипПочтовогоЯщика = ВидыПапок.Родитель5ВидПапки;
		Иначе
			ТипПочтовогоЯщика = Перечисления.ТипыПочтовыхЯщиков.ПустаяСсылка();
		КонецЕсли;
		Результат.Вставить(ВидыПапок.Ссылка, ТипПочтовогоЯщика);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли

