#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область ЗаполнениеОбъектов

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеВерсииИБ

// Определяет настройки начального заполнения элементов.
//
// Параметры:
//  Настройки - Структура - настройки заполнения
//   * ПриНачальномЗаполненииЭлемента - Булево - Если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт

	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;

КонецПроцедуры

// Вызывается при начальном заполнении справочника Организации.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника Организации.
//  ТабличныеЧасти - Структура - описание табличных частей объекта, где:
//   * Ключ - Строка - имя табличной части;
//   * Значение - ТаблицаЗначений - табличная часть в виде таблицы значений, структуру которой
//                                  необходимо скопировать перед заполнением. Например:
//                                  Элемент.Ключи = ТабличныеЧасти.Ключи.Скопировать();
//                                  ЭлементТЧ = Элемент.Ключи.Добавить();
//                                  ЭлементТЧ.ИмяКлюча = "Первичный";
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	ЭлементНДС = Элементы.Добавить();
	ЭлементНДС.ИмяПредопределенныхДанных = "НДС";
	ЭлементНДС.Наименование = НСтр("ru = 'НДС'", КодОсновногоЯзыка);
	ЭлементНДС.Код = "01-000019";
	ЭлементНДС.СчетУчета = ПланыСчетов.Управленческий.Налоги;
	ЭлементНДС.СчетУчетаКВозмещению = ПланыСчетов.Управленческий.НалогиКВозмещению;
	ЭлементНДС.ВидНалога = Перечисления.ВидыНалогов.НДС;
	ЗаполнитьСчетаУчета(ЭлементНДС);
	
	ЭлементНалогНаПрибыль = Элементы.Добавить();
	ЭлементНалогНаПрибыль.Наименование = НСтр("ru = 'Налог на прибыль'", КодОсновногоЯзыка);
	ЭлементНалогНаПрибыль.ИмяПредопределенныхДанных = "НалогНаПрибыль";
	ЭлементНалогНаПрибыль.Код = "01-000020";
	ЭлементНалогНаПрибыль.СчетУчета = ПланыСчетов.Управленческий.Налоги;
	ЭлементНалогНаПрибыль.СчетУчетаКВозмещению = ПланыСчетов.Управленческий.НалогиКВозмещению;
	ЭлементНалогНаПрибыль.ВидНалога = Перечисления.ВидыНалогов.НалогНаПрибыль_ФедеральныйБюджет;
	ЗаполнитьСчетаУчета(ЭлементНалогНаПрибыль);
	
	ЭлементАвансовыйПлатежПриУСН = Элементы.Добавить();
	ЭлементАвансовыйПлатежПриУСН.ИмяПредопределенныхДанных = "АвансовыйПлатежПриУСН";
	ЭлементАвансовыйПлатежПриУСН.Наименование = НСтр("ru = 'Авансовый платеж УСН (доходы)'",
		КодОсновногоЯзыка);
	ЭлементАвансовыйПлатежПриУСН.Код = "00-000001";
	ЭлементАвансовыйПлатежПриУСН.КодБК = "18210501011011000110";
	ЭлементАвансовыйПлатежПриУСН.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
	ЗаполнитьСчетаУчета(ЭлементАвансовыйПлатежПриУСН);
	
	ЭлементЕдиныйНалогПриУСН = Элементы.Добавить();
	ЭлементЕдиныйНалогПриУСН.ИмяПредопределенныхДанных = "ЕдиныйНалогПриУСН";
	ЭлементЕдиныйНалогПриУСН.Наименование = НСтр("ru = 'Налог при УСН (доходы)'", КодОсновногоЯзыка);
	ЭлементЕдиныйНалогПриУСН.Код = "00-000002";
	ЭлементЕдиныйНалогПриУСН.КодБК = "18210501011011000110";
	ЭлементЕдиныйНалогПриУСН.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
	ЗаполнитьСчетаУчета(ЭлементЕдиныйНалогПриУСН);
	
	ЭлементАвансовыйПлатежПриУСНРасходы = Элементы.Добавить();
	ЭлементАвансовыйПлатежПриУСНРасходы.ИмяПредопределенныхДанных = "АвансовыйПлатежПриУСНРасходы";
	ЭлементАвансовыйПлатежПриУСНРасходы.Наименование = НСтр("ru = 'Авансовый платеж УСН (доходы минус расходы)'",
	КодОсновногоЯзыка);
	ЭлементАвансовыйПлатежПриУСНРасходы.Код = "00-000024";
	ЭлементАвансовыйПлатежПриУСНРасходы.КодБК = "18210501021011000110";
	ЭлементАвансовыйПлатежПриУСНРасходы.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
	ЗаполнитьСчетаУчета(ЭлементАвансовыйПлатежПриУСНРасходы);
	
	ЭлементЕдиныйНалогПриУСНРасходы = Элементы.Добавить();
	ЭлементЕдиныйНалогПриУСНРасходы.ИмяПредопределенныхДанных = "ЕдиныйНалогПриУСНРасходы";
	ЭлементЕдиныйНалогПриУСНРасходы.Наименование = НСтр("ru = 'Налог при УСН (доходы минус расходы)'", КодОсновногоЯзыка);
	ЭлементЕдиныйНалогПриУСНРасходы.Код = "00-000025";
	ЭлементЕдиныйНалогПриУСНРасходы.КодБК = "18210501021011000110";
	ЭлементЕдиныйНалогПриУСНРасходы.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
	ЗаполнитьСчетаУчета(ЭлементЕдиныйНалогПриУСНРасходы);
	
	ЭлементПФРНакопительная = Элементы.Добавить();
	ЭлементПФРНакопительная.ИмяПредопределенныхДанных = "ПФРНакопительная";
	ЭлементПФРНакопительная.Наименование = НСтр("ru = 'Фиксированные взносы в ПФР, накопительная часть (до 2014 г)'",
		КодОсновногоЯзыка);
	ЭлементПФРНакопительная.Код = "00-000003";
	ЗаполнитьСчетаУчета(ЭлементПФРНакопительная);
	ЭлементПФРНакопительная.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_НакопительнаяЧасть;
	ЗаполнитьИзКлассификатора(ЭлементПФРНакопительная);

	ЭлементПФРСтраховая = Элементы.Добавить();
	ЭлементПФРСтраховая.ИмяПредопределенныхДанных = "ПФРСтраховая";
	ЭлементПФРСтраховая.Наименование = НСтр("ru = 'Фиксированные взносы в ПФР (до 2023 года)'", КодОсновногоЯзыка);
	ЭлементПФРСтраховая.Код = "00-000004";
	ЗаполнитьСчетаУчета(ЭлементПФРСтраховая);     
	ЭлементПФРСтраховая.КодБК = "18210214010061005160";
	ЭлементПФРСтраховая.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть;
	ЗаполнитьИзКлассификатора(ЭлементПФРСтраховая);

	ЭлементТФОМС = Элементы.Добавить();
	ЭлементТФОМС.ИмяПредопределенныхДанных = "ТФОМС";
	ЭлементТФОМС.Наименование = НСтр("ru = 'Фиксированные взносы в ТФОМС'", КодОсновногоЯзыка);
	ЭлементТФОМС.Код = "00-000005"; 
	ЭлементТФОМС.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС;
	ЗаполнитьСчетаУчета(ЭлементТФОМС);

	ЭлементФФОМС = Элементы.Добавить();
	ЭлементФФОМС.ИмяПредопределенныхДанных = "ФФОМС";
	ЭлементФФОМС.Наименование = НСтр("ru = 'Фиксированные взносы в ФФОМС (до 2023 года)'", КодОсновногоЯзыка);
	ЭлементФФОМС.Код = "00-000006";
	ЗаполнитьСчетаУчета(ЭлементФФОМС);
	ЭлементФФОМС.КодБК = "18210214030081002160";
	ЭлементФФОМС.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС;
	ЗаполнитьИзКлассификатора(ЭлементФФОМС);

	ЭлементЕНВД = Элементы.Добавить();
	ЭлементЕНВД.ИмяПредопределенныхДанных = "ЕНВД";
	ЭлементЕНВД.Наименование = НСтр("ru = 'Единый налог (ЕНВД)'", КодОсновногоЯзыка);
	ЭлементЕНВД.Код = "00-000007";
	ЗаполнитьСчетаУчета(ЭлементЕНВД);
	ЭлементЕНВД.ВидНалога = Перечисления.ВидыНалогов.ЕНВД;
	ЗаполнитьИзКлассификатора(ЭлементЕНВД);

	ЭлементНДФЛСотрудники = Элементы.Добавить();
	ЭлементНДФЛСотрудники.ИмяПредопределенныхДанных = "НДФЛСотрудники";
	ЭлементНДФЛСотрудники.Наименование = НСтр("ru = 'НДФЛ исчисленный налоговым агентом'",
		КодОсновногоЯзыка);
	ЭлементНДФЛСотрудники.Код = "00-000008";
	ЗаполнитьСчетаУчета(ЭлементНДФЛСотрудники);
	ЭлементНДФЛСотрудники.ВидНалога = Перечисления.ВидыНалогов.НДФЛ_ДоходыСвышеПредельнойВеличины;;
	ЗаполнитьИзКлассификатора(ЭлементНДФЛСотрудники);
	
	ЭлементНДФЛСотрудникиСвышеПредельнойВеличины = Элементы.Добавить();
	ЭлементНДФЛСотрудникиСвышеПредельнойВеличины.ИмяПредопределенныхДанных = "НДФЛДоходыСвышеПредельнойВеличины";
	ЭлементНДФЛСотрудникиСвышеПредельнойВеличины.Наименование = НСтр("ru = 'НДФЛ с доходов свыше предельной величины, исчисленный налоговым агентом'",
	КодОсновногоЯзыка);
	ЭлементНДФЛСотрудникиСвышеПредельнойВеличины.Код = "00-000017";
	ЗаполнитьСчетаУчета(ЭлементНДФЛСотрудникиСвышеПредельнойВеличины);
	ЭлементНДФЛСотрудникиСвышеПредельнойВеличины.ВидНалога = Перечисления.ВидыНалогов.НДФЛ;
	ЗаполнитьИзКлассификатора(ЭлементНДФЛСотрудникиСвышеПредельнойВеличины);  

	ЭлементПФРНакопительнаяСотрудники = Элементы.Добавить();
	ЭлементПФРНакопительнаяСотрудники.ИмяПредопределенныхДанных = "ПФРНакопительнаяСотрудники";
	ЭлементПФРНакопительнаяСотрудники.Наименование = НСтр(
		"ru = 'Страховые взносы в ПФР, накопительная часть по сотрудникам (до 2014 г)'",
		КодОсновногоЯзыка);
	ЭлементПФРНакопительнаяСотрудники.Код = "00-000009";
	ЗаполнитьСчетаУчета(ЭлементПФРНакопительнаяСотрудники);
	ЭлементПФРНакопительнаяСотрудники.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_НакопительнаяЧасть;
	ЗаполнитьИзКлассификатора(ЭлементПФРНакопительнаяСотрудники);

	ЭлементПФРСтраховаяСотрудники = Элементы.Добавить();
	ЭлементПФРСтраховаяСотрудники.ИмяПредопределенныхДанных = "ПФРСтраховаяСотрудники";
	ЭлементПФРСтраховаяСотрудники.Наименование = НСтр("ru = 'Страховые взносы в ПФР (до 2023 года)'",
		КодОсновногоЯзыка);
	ЭлементПФРСтраховаяСотрудники.Код = "00-000010"; 
	ЭлементПФРСтраховаяСотрудники.КодБК = "18210214010061001160";
	ЗаполнитьСчетаУчета(ЭлементПФРСтраховаяСотрудники);
	ЭлементПФРСтраховаяСотрудники.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть;
	ЗаполнитьИзКлассификатора(ЭлементПФРСтраховаяСотрудники);

	ЭлементФСССотрудники = Элементы.Добавить();
	ЭлементФСССотрудники.ИмяПредопределенныхДанных = "ФСССотрудники";
	ЭлементФСССотрудники.Наименование = НСтр("ru = 'Страховые взносы на обязательное социальное страхование'", КодОсновногоЯзыка);
	ЭлементФСССотрудники.Код = "00-000011";
	ЗаполнитьСчетаУчета(ЭлементФСССотрудники); 
	ЭлементФСССотрудники.КодБК = "18210214020061001160";
	ЭлементФСССотрудники.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС;
	ЗаполнитьИзКлассификатора(ЭлементФСССотрудники);

	ЭлементФССТравматизмСотрудники = Элементы.Добавить();
	ЭлементФССТравматизмСотрудники.ИмяПредопределенныхДанных = "ФССТравматизмСотрудники";
	ЭлементФССТравматизмСотрудники.Наименование = НСтр("ru = 'Страховые взносы в ФСС от несчастных случаев'",
		КодОсновногоЯзыка);
	ЭлементФССТравматизмСотрудники.Код = "00-000012";
	ЗаполнитьСчетаУчета(ЭлементФССТравматизмСотрудники);
	ЭлементФССТравматизмСотрудники.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ; 
	ЭлементФССТравматизмСотрудники.КодБК = "79710212000061000160";
	ЗаполнитьИзКлассификатора(ЭлементФССТравматизмСотрудники);

	ЭлементФОМССотрудники = Элементы.Добавить();
	ЭлементФОМССотрудники.ИмяПредопределенныхДанных = "ФОМССотрудники";
	ЭлементФОМССотрудники.Наименование = НСтр("ru = 'Страховые взносы в ФФОМС (до 2023 года)'", КодОсновногоЯзыка);
	ЭлементФОМССотрудники.Код = "00-000013";
	ЗаполнитьСчетаУчета(ЭлементФОМССотрудники);
	ЭлементФОМССотрудники.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС;
	ЭлементФОМССотрудники.КодБК = "18210214030081001160";
	ЗаполнитьИзКлассификатора(ЭлементФОМССотрудники);

	ЭлементПФРСвыше300ТР = Элементы.Добавить();
	ЭлементПФРСвыше300ТР.ИмяПредопределенныхДанных = "ПФРСвыше300ТР";
	ЭлементПФРСвыше300ТР.Наименование = НСтр("ru = 'Страховые взносы ИП с дохода свыше предела'",
		КодОсновногоЯзыка);
	ЭлементПФРСвыше300ТР.Код = "00-000014"; 
	ЭлементПФРСвыше300ТР.КодБК = "18210203000011000160";
	ЗаполнитьСчетаУчета(ЭлементПФРСвыше300ТР);
	ЭлементПФРСвыше300ТР.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела; 
	ЗаполнитьИзКлассификатора(ЭлементПФРСвыше300ТР);

	ЭлементПатент = Элементы.Добавить();
	ЭлементПатент.ИмяПредопределенныхДанных = "Патент";
	ЭлементПатент.Наименование = НСтр("ru = 'Налог с патента'", КодОсновногоЯзыка);
	ЭлементПатент.Код = "01-000015"; 
	ЭлементПатент.КодБК = "18210504010021000110";
	ЭлементПатент.ВидНалога = Перечисления.ВидыНалогов.ПСН_ГородскойОкруг;
	ЗаполнитьСчетаУчета(ЭлементПатент);

	ЭлементТорговыйСбор = Элементы.Добавить();
	ЭлементТорговыйСбор.ИмяПредопределенныхДанных = "ТорговыйСбор";
	ЭлементТорговыйСбор.Наименование = НСтр("ru = 'Торговый сбор'", КодОсновногоЯзыка);
	ЭлементТорговыйСбор.Код = "01-000016";
	ЗаполнитьСчетаУчета(ЭлементТорговыйСбор);
	ЭлементТорговыйСбор.ВидНалога = Перечисления.ВидыНалогов.ТорговыйСбор;
	ЗаполнитьИзКлассификатора(ЭлементТорговыйСбор);     
	
	ЭлементНалогАУСН = Элементы.Добавить();
	ЭлементНалогАУСН.ИмяПредопределенныхДанных = "АУСН";
	ЭлементНалогАУСН.Наименование = НСтр("ru = 'Налог при АУСН'", КодОсновногоЯзыка);
	ЭлементНалогАУСН.Код = "01-000018";
	ЗаполнитьСчетаУчета(ЭлементНалогАУСН);
	ЭлементНалогАУСН.ВидНалога = Перечисления.ВидыНалогов.АУСН; 
	ЭлементНалогАУСН.КодБК = "18210507000011000110";
	ЗаполнитьИзКлассификатора(ЭлементНалогАУСН); 
	
	ЭлементНалогЕНП = Элементы.Добавить();
	ЭлементНалогЕНП.ИмяПредопределенныхДанных = "ЕдиныйНалоговыйПлатеж";
	ЭлементНалогЕНП.Наименование = НСтр("ru = 'Единый налоговый платеж'", КодОсновногоЯзыка);
	ЭлементНалогЕНП.Код = "00-000019";
	ЭлементНалогЕНП.СчетУчета = ПланыСчетов.Управленческий.ЕдиныйНалоговыйСчет;
	ЭлементНалогЕНП.СчетУчетаКВозмещению = ПланыСчетов.Управленческий.НалогиКВозмещению;
	ЭлементНалогЕНП.ВидНалога = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж; 
	ЭлементНалогЕНП.КодБК = "18201061201010000510";
	ЗаполнитьИзКлассификатора(ЭлементНалогЕНП); 
	
	ЭлементСтраховыеВзносыЕдиныйТариф = Элементы.Добавить();
	ЭлементСтраховыеВзносыЕдиныйТариф.ИмяПредопределенныхДанных = "СтраховыеВзносыЕдиныйТариф";
	ЭлементСтраховыеВзносыЕдиныйТариф.Наименование = НСтр("ru = 'Страховые взносы по единому тарифу'", КодОсновногоЯзыка);
	ЭлементСтраховыеВзносыЕдиныйТариф.Код = "00-000020";
	ЗаполнитьСчетаУчета(ЭлементСтраховыеВзносыЕдиныйТариф);
	ЭлементСтраховыеВзносыЕдиныйТариф.ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносыЕдиныйТариф;
	ЭлементСтраховыеВзносыЕдиныйТариф.КодБК = "18210201000011000160";
	ЗаполнитьИзКлассификатора(ЭлементСтраховыеВзносыЕдиныйТариф);
	
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф = Элементы.Добавить();
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф.ИмяПредопределенныхДанных = "ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф";
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф.Наименование = НСтр("ru = 'Страховые взносы единый тариф ИП'", КодОсновногоЯзыка);
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф.Код = "00-000021"; 
	ЗаполнитьСчетаУчета(ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф);
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф.ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф;
	ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф.КодБК = "18210202000011000160";
	ЗаполнитьИзКлассификатора(ЭлементФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	Возврат;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Возвращает дату вступления в силу актуального классификатора кодов бюджетной классификации (КБК).
// 
// Возвращаемое значение:
// 	Дата - дата вступления в силу актуального классификатора КБК.
Функция ДатаАктуальностиКлассификатора() Экспорт
	
	Возврат '20230101';
	
КонецФункции


// Возвращает содержимое см. Перечисления.ВидыНалогов в виде структуры.
// 
// Возвращаемое значение:
// 	Структура - содержимое значений см. Перечисления.ВидыНалогов
Функция ВидыНалогов() Экспорт

	Результат = Новый Структура;

	КоллекцияМетаданных = Перечисления.ВидыНалогов.Получить(0).Метаданные().ЗначенияПеречисления;

	Для Индекс = 0 По КоллекцияМетаданных.Количество() - 1 Цикл
		Результат.Вставить(КоллекцияМетаданных[Индекс].Имя, Перечисления.ВидыНалогов.Получить(Индекс));
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Возвращает КБК с учетом вида налогового обязательства
// 
// Параметры:
// 	КБК - Строка - Описание
// 	ВидНалога - ПеречислениеСсылка.ВидыНалогов - Описание
// 	ВидНалоговогоОбязательства - Строка, Неопределено - Описание
// 	Период - Дата, Неопределено - Описание
// 	ПериодПлатежа - Неопределено - Описание
// Возвращаемое значение:
// 	Строка - КБК с учетом вида налогового обязательства
Функция КБКПоВидуНалоговогоОбязательства(КБК, ВидНалога, Знач ВидНалоговогоОбязательства = Неопределено,
	Знач Период = Неопределено, Знач ПериодПлатежа = Неопределено) Экспорт

	Если ПустаяСтрока(КБК) Тогда
		Возврат КБК;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК) <> ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
			Возврат КБК; // Сохраняем заданный код подвида доходов
		Иначе
			ВидНалоговогоОбязательства = "Налог";
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;

	КодГлавногоАдминистратора    = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК);
	КодВидаДоходов               = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК);
	КодПодвидаДоходов            = КодПодвидаДоходов(КБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	КодОперацииСектораУправления = ПлатежиВБюджетКлиентСервер.КодОперацииСектораУправления(КБК);

	Возврат КодГлавногоАдминистратора + КодВидаДоходов + КодПодвидаДоходов + КодОперацииСектораУправления;

КонецФункции

// Ищет КБК в макете (указанной версии) по переданному Коду
//
// Параметры:
//
//	- Код - Строка
//	- ВерсияКлассификатора - Число - Значение по умолчанию: 0
//	- КБКИсходный - Строка
//
Функция НайтиКодДоходаБюджета(Код, ВерсияКлассификатора = 0, КБКИсходный = "", ОпределитьКОСГУ = Ложь) Экспорт
	
	// Функция оставлена для совместимости.
	// Следует использовать ОписаниеКБК()
	// В качестве Код и КБКИсходный передается шаблон кода (17 знаков)

	ВидДохода = Лев(Код, 10);
	КОСГУ    = Сред(Код, 15);

	КодДоходаБюджета = Новый Структура;
	КодДоходаБюджета.Вставить("ВидДохода", ВидДохода);
	КодДоходаБюджета.Вставить("КОСГУ", КОСГУ);
	КодДоходаБюджета.Вставить("Наименование", "");
	КодДоходаБюджета.Вставить("КраткоеНаименование", "");
	КодДоходаБюджета.Вставить("ПодвидДохода", "");

	Период = ДатаАктуальностиКлассификатора();
	Если ЗначениеЗаполнено(ВерсияКлассификатора) Тогда
		Период = Дата(ВерсияКлассификатора, 1, 1);
	КонецЕсли;

	Если Период < ДатаАктуальностиКлассификатора() Тогда
		Период = НачалоГода(ДатаАктуальностиКлассификатора() - 1);
	КонецЕсли;

	ОписаниеКБК = Неопределено;
	Если ЗначениеЗаполнено(КБКИсходный) Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(КБКИсходный), Период, ОпределитьКОСГУ);
	КонецЕсли;

	Если ОписаниеКБК = Неопределено Тогда
		ОписаниеКБК = ОписаниеКБК("000" + СокрЛП(Код), Период, ОпределитьКОСГУ);
	КонецЕсли;

	Если ОписаниеКБК = Неопределено Тогда
		Возврат КодДоходаБюджета;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(КодДоходаБюджета, ОписаниеКБК);

	НайденныйШаблон = ОписаниеКБК.НайденныйШаблон;

	КодДоходаБюджета.ВидДохода = Лев(НайденныйШаблон, 10);
	КодДоходаБюджета.КОСГУ     = Сред(НайденныйШаблон, 15);

	ВыбранныйПодвидДохода = Сред(НайденныйШаблон, 11, 4);
	Если Не ПустаяСтрока(ВыбранныйПодвидДохода) И ВыбранныйПодвидДохода <> "0000" И СтрДлина(ВыбранныйПодвидДохода) = 4
		И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ВыбранныйПодвидДохода) Тогда
		КодДоходаБюджета.ПодвидДохода = ВыбранныйПодвидДохода;
	КонецЕсли;

	Возврат КодДоходаБюджета;

КонецФункции

// Находит данные в классификаторе по указанным параметрам
// 
// Параметры:
// 	ВидНалога - ПеречислениеСсылка.ВидыНалогов
// 	КБК - Строка
// Возвращаемое значение:
// 	Структура - описание
//   * НазначениеПлатежаИзменено - Булево
//   * ДатаАктуальности - Дата
//   * СчетУчета - ПланСчетовСсылка.Управленческий
Функция НайтиПоставляемыеДанные(ВидНалога = Неопределено, КБК = Неопределено) Экспорт

	Период = '00010101';
	
	Если ЗначениеЗаполнено(ВидНалога) Тогда

		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога",
			ВидНалога));

	ИначеЕсли Не ПустаяСтрока(КБК) Тогда

		КодБК = Лев(СокрЛ(КБК), 13) + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() + Сред(СокрЛП(КБК), 18);
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("КодБК", КодБК));

	Иначе

		Возврат Неопределено;

	КонецЕсли;

	Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда

		СтруктураДанных = НоваяСтруктураПоставляемыхДанных();
		ЗаполнитьЗначенияСвойств(СтруктураДанных, ТаблицаПоставляемыхДанных[0]);

		Если ЗначениеЗаполнено(СтруктураДанных.ВидНалога) Тогда

			Если ЗначениеЗаполнено(Период) И Период < ДатаАктуальностиКлассификатора() Тогда
				СтруктураДанных.ДатаАктуальности = Период;
			Иначе
				СтруктураДанных.ДатаАктуальности = ДатаАктуальностиКлассификатора();
			КонецЕсли;

			СтруктураДанных.НазначениеПлатежаИзменено = Ложь;
			ПодменитьРеквизитыПриСовпаденииКБК(СтруктураДанных);

		КонецЕсли;

		Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(СтруктураДанных.КодБК)
			= ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
			СтруктураДанных.КодБК = КБКПоВидуНалоговогоОбязательства(СтруктураДанных.КодБК, СтруктураДанных.ВидНалога,
				, СтруктураДанных.ДатаАктуальности);
		КонецЕсли;

	Иначе

		СтруктураДанных = Неопределено;

	КонецЕсли;

	Возврат СтруктураДанных;

КонецФункции

// Описание кода бюджетной классификации
// 
// Параметры:
// 	КБК - Строка - код бюджетной классификации
// 	Период - Дата - период
// 	ОпределитьКОСГУ - Булево - определить классификацию операций сектора государственного управления
// Возвращаемое значение:
// 	Структура, Неопределено - Описание:
//   * Наименование - Строка - описание кода бюджетной классификации
Функция ОписаниеКБК(КБК, Период, ОпределитьКОСГУ = Ложь) Экспорт

	Если Не ПлатежиВБюджетКлиентСервер.КБКЗадан(КБК) Тогда
		ДлинаКБК = СтрДлина(КБК);
		Если Не (ОпределитьКОСГУ И (ДлинаКБК = 13 Или ДлинаКБК = 17)) Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;

	ИмяМакета = ИмяМакетаКБК(Период);

	ИсточникДанных = Справочники.ВидыНалогов.ПолучитьМакет(ИмяМакета);
	ОбластьПоиска  = ИсточникДанных.Область(2, 1, ИсточникДанных.ВысотаТаблицы, 1);

	ШаблонКБК      = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Ложь);

	ОбластьДанных = Неопределено;
	Если ШаблонКБК <> "" Тогда
		ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК, , ОбластьПоиска);
		Если ОбластьДанных = Неопределено Тогда
			ШаблонКБК     = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК, Истина);
			ОбластьДанных = ИсточникДанных.НайтиТекст(ШаблонКБК, , ОбластьПоиска);
		КонецЕсли;
	КонецЕсли;

	Если ОбластьДанных = Неопределено И ОпределитьКОСГУ Тогда
		ШаблонКБКБезКОСГУ = ПлатежиВБюджетКлиентСервер.ЭлементКБК(КБК, "КодВидаДоходов");
		ОбластьДанных     = ИсточникДанных.НайтиТекст(ШаблонКБКБезКОСГУ, , ОбластьПоиска);

		Если ОбластьДанных <> Неопределено Тогда
			ШаблонКБК = ШаблонКБКБезКОСГУ + ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов();
		КонецЕсли;
	КонецЕсли;

	Если ОбластьДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	НомерСтроки                     = ОбластьДанных.Верх;
	НомерКолонкиКод                 = 1;
	НомерКолонкиНаименование        = 2;
	НомерКолонкиКраткоеНаименование = 3;

	Описание = Новый Структура;

	Если ОпределитьКОСГУ Тогда
		НайденныйКБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКод);
		Описание.Вставить("КОСГУ", Прав(НайденныйКБК, 3));
		Описание.Вставить("НайденныйШаблон", НайденныйКБК); // ШаблонКБК + Описание.КОСГУ);
	Иначе
		Описание.Вставить("НайденныйШаблон", ШаблонКБК);
	КонецЕсли;

	Описание.Вставить("Наименование", ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование));
	Описание.Вставить("КраткоеНаименование", ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки,
		НомерКолонкиКраткоеНаименование));

	Возврат Описание;

КонецФункции

// Проверяет актуальность реквизитов при подготовке платежных документов
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалоговИПлатежейВБюджет
//   Период - Дата - дата платежного документа
//
// Возвращаемое значение:
//  Булево
//
Функция РеквизитыАктуальны(Налог, Знач Период = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Налог) Или Налог = Справочники.ВидыНалогов.ПустаяСсылка() Тогда
		Возврат Истина;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;

	Если НачалоДня(Период) < ДатаАктуальностиКлассификатора() Тогда
		// Платеж относится к периоду действия старого классификатора
		Возврат Истина;
	КонецЕсли;

	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, ДатаАктуальности");

	Если ЗначениеЗаполнено(ЗначениеРеквизитов.ВидНалога) И ЗначениеЗаполнено(ЗначениеРеквизитов.ДатаАктуальности) Тогда
		// Проверка актуальности выполняется только для поставляемых элементов
		РеквизитыАктуальны = (НачалоДня(ЗначениеРеквизитов.ДатаАктуальности) >= ДатаАктуальностиКлассификатора());
	Иначе
		РеквизитыАктуальны = Истина;
	КонецЕсли;

	Возврат РеквизитыАктуальны;

КонецФункции

// Возвращает администратора налога по КБК
//
// Параметры:
//   Налог - СправочникСсылка.ВидыНалогов
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыГосударственныхОрганов
//
Функция ВидГосударственногоОргана(Налог, Период = Неопределено) Экспорт

	Если ЗначениеЗаполнено(Налог) Тогда
		КБК = КБК(Налог,, Период);
	Иначе
		КБК = "";
	КонецЕсли;
	
	Возврат ПлатежиВБюджетКлиентСервер.ВидГосударственногоОргана(КБК);

КонецФункции

// Данные выбора налога по коду бюджетной классификации
// 
// Параметры:
// 	КодБК - Строка - код бюджетной классификации
// Возвращаемое значение:
// 	СписокЗначений - список для выбора вида налога по указанному коду бюджетной классификации 
Функция ДанныеВыбораПоКБК(КодБК) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонКБК", Лев(КодБК, 13) + "%");

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
	|ИЗ
	|	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК";
	Если ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяПенсионнымФондом(КодБК)
		Или ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяФСС(КодБК) Тогда
		Запрос.УстановитьПараметр("ШаблонКБК182", ПлатежиВБюджетКлиентСервер.КодАдминистрированияНалоговымиОрганами()
			+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");

		ТекстЗапроса = ТекстЗапроса + "
									  |
									  |ОБЪЕДИНИТЬ ВСЕ
									  |
									  |" + "ВЫБРАТЬ
										   |	ВидыНалоговИПлатежейВБюджет.Ссылка,
										   |	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
										   |ИЗ
										   |	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
										   |ГДЕ
										   |	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК182
										   |	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления";
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(КодБК)
		Или ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяФСС(КодБК) Тогда
		Запрос.УстановитьПараметр("ШаблонКБК392", ПлатежиВБюджетКлиентСервер.КодАдминистрированияПенсионнымФондом()
			+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");
		Запрос.УстановитьПараметр("ШаблонКБК393", ПлатежиВБюджетКлиентСервер.КодАдминистрированияФСС()
			+ ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КодБК) + "%");

		ТекстЗапроса = ТекстЗапроса + "
									  |
									  |ОБЪЕДИНИТЬ ВСЕ
									  |
									  |" + "ВЫБРАТЬ
										   |	ВидыНалоговИПлатежейВБюджет.Ссылка,
										   |	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
										   |ИЗ
										   |	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
										   |ГДЕ
										   |	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК392
										   |	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
										   |
										   |ОБЪЕДИНИТЬ ВСЕ
										   |
										   |ВЫБРАТЬ
										   |	ВидыНалоговИПлатежейВБюджет.Ссылка,
										   |	ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
										   |ИЗ
										   |	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
										   |ГДЕ
										   |	ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК393
										   |	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления";
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;

	ДанныеВыбора = Новый СписокЗначений;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(
			Новый Структура("Значение, ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления));
	КонецЦикла;

	Возврат ДанныеВыбора;

КонецФункции

// Формирует назначение платежа для уплаты налогов
//
// Параметры:
//   Налог                          - СправочникСсылка.ВидыНалогов
//   ВидНалоговогоОбязательства     - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Организация                    - СправочникСсылка.Организации
//   Период                         - Дата, Неопределено
//   ПредставлениеНалоговогоПериода - Строка
//   ШаблонНазначения               - Строка
//
// Возвращаемое значение:
//  Строка - назначение платежа
//
Функция НазначениеПлатежа(Налог, ВидНалоговогоОбязательства = Неопределено, Организация = Неопределено,
	Знач Период = Неопределено, ПредставлениеНалоговогоПериода = "", ШаблонНазначения = "") Экспорт

	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;

	НазначениеПлатежа = "";
	Если ЗначениеЗаполнено(Налог) Тогда
		ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог,
			"ВидНалога, НазначениеПлатежа, ДатаАктуальности");
		ВидНалога         = ЗначениеРеквизитов.ВидНалога;
		НазначениеПлатежа = ЗначениеРеквизитов.НазначениеПлатежа;
		ДатаАктуальности  = ЗначениеРеквизитов.ДатаАктуальности;
	КонецЕсли;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда

		Если (НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности
			< ДатаАктуальностиКлассификатора()) Или (НачалоДня(Период) < ДатаАктуальностиКлассификатора()
			И ДатаАктуальности >= ДатаАктуальностиКлассификатора()) Тогда

			ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора( , Новый Структура("ВидНалога",
				ВидНалога));
			Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
				НазначениеПлатежа = ТаблицаПоставляемыхДанных[0].НазначениеПлатежа;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	Если ПустаяСтрока(ШаблонНазначения) Тогда
		ШаблонНазначения = НазначениеПлатежа;
	Иначе
		Параметр = "";
		ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<Период");
		Если ПозицияПараметра = 0 Тогда
			ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<ПФР");
		КонецЕсли;
		Если ПозицияПараметра = 0 Тогда
			ПозицияПараметра = СтрНайти(НазначениеПлатежа, "<ФСС");
		КонецЕсли;

		Если ПозицияПараметра > 0 Тогда
			Параметр = Сред(НазначениеПлатежа, ПозицияПараметра);
		КонецЕсли;

		НазначениеПлатежа = СокрЛП(ШаблонНазначения + " " + ?(СтрНайти(НазначениеПлатежа, Параметр) = 0, Параметр, ""));
	КонецЕсли;

	Если Не ПустаяСтрока(ПредставлениеНалоговогоПериода) Тогда

		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", ПредставлениеНалоговогоПериода);

	Иначе
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", "");
	КонецЕсли;
	
	// При уплате страховых взносов, как правило, требуется указывать регистрационный номер
	ВидГосударственногоОргана = Справочники.ВидыНалогов.ВидГосударственногоОргана(Налог);
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Или ВидГосударственногоОргана
		= Перечисления.ВидыГосударственныхОрганов.ОрганФСС
		Или ПлатежиВБюджетКлиентСервер.ЭтоВзносыНаПенсионноеСтрахование(ВидНалога)
		Или ПлатежиВБюджетКлиентСервер.ЭтоВзносыНаСоциальноеСтрахование(ВидНалога) Тогда

		КлючиРеквизитов = "РегистрационныйНомерПФР, РегистрационныйНомерПФР_ИП, РегистрационныйНомерФСС";
		РеквизитыОрганизации = ?(ЗначениеЗаполнено(Организация), ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Организация, КлючиРеквизитов), Новый Структура(КлючиРеквизитов));

		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР>", РеквизитыОрганизации.РегистрационныйНомерПФР);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР_ИП>", РеквизитыОрганизации.РегистрационныйНомерПФР_ИП);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС>", РеквизитыОрганизации.РегистрационныйНомерФСС);
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС_ИП>", РеквизитыОрганизации.РегистрационныйНомерФСС);

	КонецЕсли;

	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "  ", " ");

	Возврат НазначениеПлатежа;

КонецФункции

// Возвращает КБК налога
//
// Параметры:
//   Налог                      - СправочникСсылка.ВидыНалогов
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Период                     - Дата - период действия классификатора КБК
//   ПериодПлатежа              - Дата
//
// Возвращаемое значение:
//   Строка
//
Функция КБК(Налог, Знач ВидНалоговогоОбязательства = Неопределено, Знач Период = Неопределено,
	ПериодПлатежа = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат "";
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		ВидНалоговогоОбязательства = "Налог";
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;

	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Налог, "ВидНалога, КодБК, ДатаАктуальности");
	ВидНалога        = ЗначениеРеквизитов.ВидНалога;
	КодБК            = ЗначениеРеквизитов.КодБК;
	ДатаАктуальности = ЗначениеРеквизитов.ДатаАктуальности;
	
	// Если требуется устаревшее значение или новое значение до вступления классификатора в силу, читаем данные из макета
	Если ЗначениеЗаполнено(ВидНалога) И ЗначениеЗаполнено(ДатаАктуальности) Тогда

		Если (НачалоДня(Период) >= ДатаАктуальностиКлассификатора() И ДатаАктуальности
			< ДатаАктуальностиКлассификатора()) Или (НачалоДня(Период) < ДатаАктуальностиКлассификатора()
			И ДатаАктуальности >= ДатаАктуальностиКлассификатора()) Тогда

			ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(Период, Новый Структура("ВидНалога",
				ВидНалога));
			Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
				КодБК = ТаблицаПоставляемыхДанных[0].КодБК;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КодБК) = ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
		КодБК = КБКПоВидуНалоговогоОбязательства(КодБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоКБКНалогиВзносы(КодБК) И ВидНалоговогоОбязательства <> "Налог" Тогда
		ШаблонКБК = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КодБК) + ПлатежиВБюджетКлиентСервер.ШаблонКБК(
			КодБК, Истина);
		КодБК = КБКПоВидуНалоговогоОбязательства(ШаблонКБК, ВидНалога, ВидНалоговогоОбязательства, Период,
			ПериодПлатежа);
	ИначеЕсли ЗначениеЗаполнено(ПериодПлатежа) И Год(ПериодПлатежа) < Год(Период) Тогда
		КодБК = КБКПоВидуНалоговогоОбязательства(КодБК, ВидНалога, ВидНалоговогоОбязательства, Период, ПериодПлатежа);
	КонецЕсли;

	Возврат КодБК;

КонецФункции

// Ищет налог по виду и создает поставляемый элемент при необходимости
//
Функция НалогПоВиду(ВидНалога, СоздаватьНовый = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидНалога) Тогда
		Возврат Справочники.ВидыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	Ссылка = Справочники.ВидыНалогов.НайтиПоРеквизиту("ВидНалога", ВидНалога);
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("ВидНалога", ВидНалога));
		Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
			Ссылка = СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных[0]);
		КонецЕсли;
	КонецЕсли;
		
	Возврат Ссылка;
	
КонецФункции   

// Ищет налог по КБК и создает поставляемый элемент при необходимости
//
Функция НалогПоКБК(Знач КодБК, СоздаватьНовый = Истина, СтатусПлательщика = Неопределено) Экспорт
	
	// Ищем по всем значениям КодПодвидаДоходов
	ГраницыАдминистратораКБК  = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодГлавногоАдминистратора");
	ГраницыПеременнойЧастиКБК = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	ШаблонКБК = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыАдминистратораКБК.Длина)
		+ Сред(КодБК, ГраницыАдминистратораКБК.Начало + ГраницыАдминистратораКБК.Длина);
	ШаблонКБК = Лев(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало - 1)
		+ СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыПеременнойЧастиКБК.Длина)
		+ Сред(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало + ГраницыПеременнойЧастиКБК.Длина);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ШаблонКБК", ШаблонКБК);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
	|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК,
	|	ВидыНалоговИПлатежейВБюджет.ВидНалога КАК ВидНалога
	|ИЗ
	|	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
	|ГДЕ
	|	НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления
	|	И ВидыНалоговИПлатежейВБюджет.КодБК ПОДОБНО &ШаблонКБК
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодБК,
	|	ВидыНалоговИПлатежейВБюджет.Предопределенный,
	|	ВидыНалоговИПлатежейВБюджет.СчетУчета.Код,
	|	Ссылка";
	
	Ссылка = Справочники.ВидыНалогов.ПустаяСсылка();
	
	// Выберем лучший
	МаксимальныйПриоритет = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Приоритет = 1; // За соответствие шаблону
		
		Если Выборка.КодБК = КодБК Тогда
			Приоритет = Приоритет + 2;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВидНалога) Тогда
			// Предопределенный налог должен иметь больший приоритет.
			Приоритет = Приоритет + 4;
		КонецЕсли;
		
		МаксимальныйПриоритет = Приоритет;
		Ссылка = Выборка.Ссылка;
		
	КонецЦикла;
	
	Если Ссылка.Пустая() И СоздаватьНовый Тогда
		ТаблицаПоставляемыхДанных = ПрочитатьПоставляемыеДанныеКлассификатора(, Новый Структура("КодБК", КодБК));
		Если ТаблицаПоставляемыхДанных.Количество() > 0 Тогда
			Ссылка = СоздатьПоставляемыйЭлемент(ТаблицаПоставляемыхДанных[0]);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

// Возвращает вид налогового обязательства по КБК
//
// Параметры:
//   КБК - Строка
//   Период - Дата - период уплаты
//   ВидНалога - ПеречислениеСсылка.ВидыНалогов - вид налога, если известен
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//
Функция ВидНалоговогоОбязательстваПоКБК(КБК, Знач Период = Неопределено, ВидНалога = Неопределено) Экспорт
	
	Если ПустаяСтрока(КБК)
		Или ВидНалога = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж Тогда
		Возврат Перечисления.ВидыПлатежейВГосБюджет.Налог;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	КодПодвидаДоходов = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	
	Если Лев(КодПодвидаДоходов, 1) = "3" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Штраф;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 2) = "22"
		И ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Проценты;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "2" Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ПениСам;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "1" Тогда
		
		Если ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть
			И ПлатежиВБюджетКлиентСервер.СтраховыеВзносыРаздельно(Период) Тогда
			
			Если Лев(КодПодвидаДоходов, 2) = "12" Тогда
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
			Иначе
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			КонецЕсли;
			
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия
			Или ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия Тогда
			
			Если СтрЗаканчиваетсяНа(КодПодвидаДоходов, "10") Тогда
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки;
			Иначе
				ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			КонецЕсли;
			
		Иначе
			ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
		
	ИначеЕсли Лев(КодПодвидаДоходов, 1) = "8"
		И ВидНалога = Перечисления.ВидыНалогов.Госпошлина_ГосрегистрацияОрганизаций Тогда
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ГосрегистрацияОрганизацийЧерезМФЦ;
		
	Иначе
		
		ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		
	КонецЕсли;
	
	Возврат ВидНалоговогоОбязательства;
	
КонецФункции

Функция СоздатьПоставляемыйЭлемент(СтрокаДанныхКлассификатора)
	
	Если СтрокаДанныхКлассификатора = Неопределено Или Не ЗначениеЗаполнено(СтрокаДанныхКлассификатора.ВидНалога) Тогда
		Возврат Справочники.ВидыНалогов.ПустаяСсылка();
	КонецЕсли;
	
	// Для исключения дублей проверяем наличие элемента в справочнике
	ПоставляемыйЭлемент = Справочники.ВидыНалогов.НайтиПоРеквизиту("ВидНалога", СтрокаДанныхКлассификатора.ВидНалога);
	
	Если ПоставляемыйЭлемент.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Объект = Справочники.ВидыНалогов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Объект, СтрокаДанныхКлассификатора);
		Объект.СчетУчета        = ПланыСчетов.Управленческий.Налоги;
		Объект.ДатаАктуальности = ДатаАктуальностиКлассификатора();
		Объект.КодБК = Справочники.ВидыНалогов.КБКПоВидуНалоговогоОбязательства(
			СтрокаДанныхКлассификатора.КодБК, СтрокаДанныхКлассификатора.ВидНалога, , ДатаАктуальностиКлассификатора());
		Объект.Записать();
		
		ПоставляемыйЭлемент = Объект.Ссылка;
		
	КонецЕсли;
	
	Возврат ПоставляемыйЭлемент;
	
КонецФункции

// Функция возвращает счет учета НДС к уплате в бюджет
// 
// Возвращаемое значение:
//  ПланСчетовСсылка.Управленческий - Счет учета НДС к уплате в бюджет
//
Функция СчетУчетаНДСКУплате() Экспорт
	
	СчетУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыНалогов.НДС, "СчетУчета");
	
	Если НЕ ЗначениеЗаполнено(СчетУчета)  Тогда
		СчетУчета = ПланыСчетов.Управленческий.Налоги;
	КонецЕсли;
	
	Возврат СчетУчета;
	
КонецФункции

// Функция возвращает счет учета НДС к возмещению
// 
// Возвращаемое значение:
//  ПланСчетовСсылка.Управленческий - счет учета НДС к возмещению
//
Функция СчетУчетаНДСКВозмещению() Экспорт
	
	СчетУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыНалогов.НДС, "СчетУчетаКВозмещению");
	
	Если НЕ ЗначениеЗаполнено(СчетУчета)  Тогда
		СчетУчета = ПланыСчетов.Управленческий.НалогиКВозмещению;
	КонецЕсли;
	
	Возврат СчетУчета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаАктуальностиКлассификатораПользователя(Знач Период = Неопределено)
	
	ДатаАктуальностиКлассификатора = ДатаАктуальностиКлассификатора();
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаПользователя;
	КонецЕсли;
	
	ДатаАктуальностиВПрошломГоду = НачалоГода(Период) < ДатаАктуальностиКлассификатора
		Или Год(ТекущаяДатаПользователя) < Год(ДатаАктуальностиКлассификатора);
	Если ДатаАктуальностиВПрошломГоду Тогда
		ДатаАктуальностиКлассификатора = НачалоГода(НачалоГода(ДатаАктуальностиКлассификатора) - 1);
	КонецЕсли;
	
	Возврат ДатаАктуальностиКлассификатора;
	
КонецФункции

Функция КодПодвидаДоходов(КБК, ВидНалога, ВидНалоговогоОбязательства, Знач Период, Знач ПериодПлатежа = Неопределено)

Если Не ЗначениеЗаполнено(ПериодПлатежа) Тогда
		ПериодПлатежа = Период;
	КонецЕсли;
	
	ЭтоНалог    = ВидНалоговогоОбязательства = "Налог";
	ЭтоПени     = ЭтоПени(ВидНалоговогоОбязательства);
	ЭтоПроценты = ЭтоПроценты(ВидНалоговогоОбязательства);
	ЭтоШтраф    = ЭтоШтраф(ВидНалоговогоОбязательства);
	
	ЭтоВзносыСвышеПредела = ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
	
	ЭтоТаможенныйПлатеж = ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяТаможеннымиОрганами(КБК);
	КодПодвидаДоходаПоУмолчанию = "0000";
	ТекущийКодПодвидаДохода = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	ЭтоОсобыйКодПодвидаДохода = СтрНачинаетсяС(ТекущийКодПодвидаДохода, "0")
		И Не СтрЗаканчиваетсяНа(ТекущийКодПодвидаДохода, "00");
	
	СоответствиеСтраховыхДо2023 =
		ПлатежиВБюджетКлиентСерверПереопределяемый.СоответствиеКБКСтраховыхВзносов2022ГодаВ2023Году();
	КБКПриведенный = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК);
	ВидСтраховогоВзносаДо2023 = СоответствиеСтраховыхДо2023[КБКПриведенный];
	ЭтоСтарыйСтраховойВзносДо2023 = ЗначениеЗаполнено(ВидСтраховогоВзносаДо2023);
	
	Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС
		Или ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(Период) // дата платежа больше 2023 года
			И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
			Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "1001";
			ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "1002";
			КонецЕсли;
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа) Тогда
			КодПодвидаДоходов = "1013";
			Если ЭтоПени ИЛИ ЭтоПроценты И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "2013";
			ИначеЕсли ЭтоПроценты И ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС Тогда
				КодПодвидаДоходов = "2213";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3013";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "1011";
			Если ЭтоПени Или ЭтоПроценты Тогда
				КодПодвидаДоходов = "2011";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3011";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_Добровольные Тогда
		
		КодПодвидаДоходов = "12";
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
			КодПодвидаДоходов = "1";
		Иначе
			КодПодвидаДоходов = "6";
		КонецЕсли;
		
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносыЕдиныйТариф Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф
		И ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
	//ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела Тогда
	//	// Пени и проценты по этим фиксированным страховым не предусмотрены
	//	Если ЭтоШтраф Тогда
	//		КодПодвидаДоходов = "3";
	//	Иначе
	//		КодПодвидаДоходов = "1";
	//	КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия
		ИЛИ ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(Период) Тогда
			КодПодвидаДоходаДоИзменения = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
			НеЗависитОтСпецОценки = СтрЗаканчиваетсяНа(КодПодвидаДоходаДоИзменения, "10");
			Если ЭтоНалог Тогда
				КодПодвидаДоходов = "102";
			ИначеЕсли ЭтоПени Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "211";
				Иначе
					КодПодвидаДоходов = "210";
				КонецЕсли;
			ИначеЕсли ЭтоПроценты Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "221";
				Иначе
					КодПодвидаДоходов = "220";
				КонецЕсли;
			ИначеЕсли ЭтоШтраф Тогда
				Если НеЗависитОтСпецОценки Тогда
					КодПодвидаДоходов = "301";
				Иначе
					КодПодвидаДоходов = "300";
				КонецЕсли
			Иначе
				КодПодвидаДоходов = "101";
			КонецЕсли;
		Иначе
			КодПодвидаДоходов = "101";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ЛетныеЭкипажи
		Или ВидНалога = Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_Шахтеры Тогда
		Если ЭтоНалог Тогда
			КодПодвидаДоходов = "1";
		ИначеЕсли ЭтоПени Или ЭтоПроценты Тогда
			КодПодвидаДоходов = "21";
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		КонецЕсли;
	ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.Госпошлина_ГосрегистрацияОрганизаций Тогда
		
		Если ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ГосрегистрацияОрганизацийЧерезМФЦ Тогда
			КодПодвидаДоходов = "8";
		ИначеЕсли ЭтоПени ИЛИ ЭтоПроценты Тогда
			КодПодвидаДоходов = "2";
			Если ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
				КодПодвидаДоходов = ?(ЭтоПени, "21", "22");
			КонецЕсли;
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
		
	ИначеЕсли ЭтоТаможенныйПлатеж И ЭтоАвансовыйПлатежТаможня(КБК) Тогда
		
		Если АвансовыйПлатежТаможня2022(Период) Тогда
			КодПодвидаДоходов = "1000";
		Иначе
			КодПодвидаДоходов = КодПодвидаДоходаПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоЕдиныйНалоговыйПлатеж(КБК) Тогда
		
		КодПодвидаДоходов = КодПодвидаДоходаПоУмолчанию;
		
	ИначеЕсли (ЭтоПениПоВременнойНетрудоспособности(КБК) Или ЭтоШтрафыПоПлатежу(КБК))
		И ТекущийКодПодвидаДохода = КодПодвидаДоходаПоУмолчанию Тогда
		
		КодПодвидаДоходов = ТекущийКодПодвидаДохода;
		
	ИначеЕсли ЭтоПроцентыПоПлатежу(КБК) Тогда
		
		КодПодвидаДоходов = "2";
		
	Иначе
		
		КодПодвидаДоходов = "1";
		Если ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа)
			И (ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть
			ИЛИ ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть) Тогда
			
			Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(Период) // дата платежа больше 2023 года
				И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
				Если ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть Тогда
					КодПодвидаДоходов = "1001";
				ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
					КодПодвидаДоходов = "1005";
				КонецЕсли;
			Иначе
				Если ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть
					И (ЭтоНалог ИЛИ ЭтоВзносыСвышеПредела) Тогда
					КодПодвидаДоходов = "111";
				ИначеЕсли ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ВидНалога = Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС Тогда
			Если ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ПериодПлатежа) Тогда
				Если ТекущийКодПодвидаДохода = "1101" Тогда
					КодПодвидаДоходов = ТекущийКодПодвидаДохода;
				Иначе
					КодПодвидаДоходов = "1001";
				КонецЕсли;
			ИначеЕсли ПлатежиВБюджетКлиентСервер.ДействуетНовыйАдмиинстраторСтраховыхВзносов(ПериодПлатежа) Тогда
				Если ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ЭтоОсобыйКодПодвидаДохода Тогда
			
			КодПодвидаДоходов = ТекущийКодПодвидаДохода;
			
		Иначе
			
			Если ЭтоНалог И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
				КодПодвидаДоходов = "11";
			ИначеЕсли ЭтоВзносыСвышеПредела И ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть Тогда
				КодПодвидаДоходов = "12";
			ИначеЕсли ЭтоПени ИЛИ ЭтоПроценты Тогда
				КодПодвидаДоходов = "2";
				Если ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
					КодПодвидаДоходов = ?(ЭтоПени, "21", "22");
				КонецЕсли;
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодПодвидаДоходов) Тогда
		КодПодвидаДоходов = "1"; // по умолчанию
	КонецЕсли;
	
	РасположениеЭлемента = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	Для Счетчик = СтрДлина(КодПодвидаДоходов) + 1 По РасположениеЭлемента.Длина Цикл
		КодПодвидаДоходов = КодПодвидаДоходов + "0";
	КонецЦикла;
	
	Возврат КодПодвидаДоходов;

КонецФункции

Функция АвансовыйПлатежТаможня2022(Период) Экспорт
	
	Возврат Период <> Неопределено
		И Период >= '20220101';
	
КонецФункции

Функция ЭтоАвансовыйПлатежТаможня(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1100900001";
	
КонецФункции

Функция ЭтоПениПоВременнойНетрудоспособности(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1161902001";
	
КонецФункции

Функция ЭтоПроцентыПоПлатежу(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1162000001";
	
КонецФункции

Функция ЭтоШтрафыПоПлатежу(КБК) Экспорт
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1162102006";
	
КонецФункции

Функция ИмяМакетаКБК(Знач Период = Неопределено)
	
	ДатаАктуальностиКлассификатора = ДатаАктуальностиКлассификатора();
	ДатаАктуальностиПользователя   = ДатаАктуальностиКлассификатораПользователя(Период);
	
	Если ДатаАктуальностиПользователя < ДатаАктуальностиКлассификатора Тогда
		Возврат "КлассификацияДоходовБюджетов";
	Иначе
		Возврат "КлассификацияДоходовБюджетов2023";
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьСчетаУчета(ЭлементАвансовыйПлатежПриУСН)
	ЭлементАвансовыйПлатежПриУСН.СчетУчета = ПланыСчетов.Управленческий.Налоги;
	ЭлементАвансовыйПлатежПриУСН.СчетУчетаКВозмещению = ПланыСчетов.Управленческий.НалогиКВозмещению;
КонецПроцедуры

Процедура ЗаполнитьИзКлассификатора(ПредопределенныйЭлемент)
	ДанныеКлассификатора = НайтиПоставляемыеДанные(ПредопределенныйЭлемент.ВидНалога);
	Если ДанныеКлассификатора <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПредопределенныйЭлемент, ДанныеКлассификатора, , "Наименование, СчетУчета");
	КонецЕсли;
КонецПроцедуры

Процедура ПодменитьРеквизитыПриСовпаденииКБК(Объект)

	Если ДатаАктуальностиКлассификатора() < ПлатежиВБюджетКлиентСервер.НачалоДействияПриказа90н() Тогда

		Если ЗначениеЗаполнено(Объект.ВидНалога) И Объект.ВидНалога
			= Перечисления.ВидыНалогов.ФиксированныеВзносы_ФФОМС Тогда
			// До 2016 года фиксированные взносы в ФФОМС уплачиваются по тому же КБК, что и взносы за работников.
			// По умолчанию реквизиты содержат наименование и назначение для взносов с ФОТ.
			Объект.Наименование      = НСтр("ru = 'Фиксированные взносы в ФФОМС'");
			Если Не Объект.НазначениеПлатежаИзменено Тогда
				Объект.НазначениеПлатежа = НСтр(
					"ru = 'Страховые взносы на ОМС, зачисляемые в бюджет ФФОМС. Регистрационный номер в ПФР <ПФР_ИП>'");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ПрочитатьПоставляемыеДанныеКлассификатора(Период = Неопределено, Отбор = Неопределено,
	ТолькоСИдентификаторами = Ложь)
	Перем ВидНалога;
	Перем ОтборПоВидуНалога, ОтборПоКБК, ОтборПоШаблонуКБК;

	УстановленОтбор = (Отбор <> Неопределено);

	Если УстановленОтбор И ТипЗнч(Отбор) = Тип("Структура") Тогда
		Отбор.Свойство("ВидНалога", ОтборПоВидуНалога);
		Отбор.Свойство("КодБК", ОтборПоКБК);
		Если ОтборПоКБК <> Неопределено Тогда
			ОтборПоШаблонуКБК = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора()
				+ ПлатежиВБюджетКлиентСервер.ШаблонКБК(ОтборПоКБК, Истина);
		КонецЕсли;
	КонецЕсли;

	ТипОтбораПоВидуНалога = ТипЗнч(ОтборПоВидуНалога);

	УстановленМножественныйОтбор = (ТипОтбораПоВидуНалога = Тип("Массив"));
	Если УстановленМножественныйОтбор Тогда
		ОтборПоВидуНалога = ОбщегоНазначенияПереопределяемый.УдалитьПовторяющиесяЭлементыМассива(ОтборПоВидуНалога);
	КонецЕсли;

	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();

	НомерКолонкиКБК                = 1; // Код дохода бюджета
	НомерКолонкиНаименованиеПолное = 2; // Полное наименование
	НомерКолонкиНаименование       = 3; // Краткое наименование
	НомерКолонкиКодАдминистратора  = 4; // Код главного администратора доходов бюджета
	НомерКолонкиНазначениеПлатежа  = 5; // Шаблон назначения платежа
	НомерКолонкиИдентификатор      = 6; // Идентификатор

	ИсточникДанных = Справочники.ВидыНалогов.ПолучитьМакет(ИмяМакетаКБК(Период));

	ПредопределенныеВидовНалогов = ВидыНалогов();

	ПустойКодГлавногоАдминистратора = ПлатежиВБюджетКлиентСервер.ПустойКодГлавногоАдминистратора();

	Для НомерСтроки = 2 По ИсточникДанных.ВысотаТаблицы Цикл

		КБК = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКБК);
		Если ПустаяСтрока(КБК) Тогда
			// Конец таблицы
			Прервать;
		КонецЕсли;

		КодАдминистратора = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиКодАдминистратора);

		КодБК = ?(Не ПустаяСтрока(КодАдминистратора), КодАдминистратора + КБК, ПустойКодГлавногоАдминистратора + КБК);

		Если УстановленОтбор И ОтборПоКБК <> Неопределено Тогда
			Если Не ПустаяСтрока(КодАдминистратора) Тогда
				Если ОтборПоКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Если ОтборПоШаблонуКБК <> КодБК Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		СтрокаИдентификаторов = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиИдентификатор);
		Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено И ПустаяСтрока(СтрокаИдентификаторов) Тогда
			// Непоставляемые данные
			Продолжить;
		КонецЕсли;

		Если Не ПустаяСтрока(СтрокаИдентификаторов) Тогда
			МассивИдентификаторов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаИдентификаторов);
		Иначе
			МассивИдентификаторов = Новый Массив;
			МассивИдентификаторов.Добавить("");
		КонецЕсли;

		НаименованиеПолное    = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименованиеПолное);
		Наименование          = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНаименование);
		НазначениеПлатежа     = ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонкиНазначениеПлатежа);

		Для Индекс = 0 По МассивИдентификаторов.ВГраница() Цикл

			Идентификатор = СокрЛП(МассивИдентификаторов[Индекс]);

			Если ПустаяСтрока(Идентификатор) Или Не ПредопределенныеВидовНалогов.Свойство(Идентификатор, ВидНалога) Тогда
				Если ТолькоСИдентификаторами Тогда
					Если Лев(КБК, 3) = "118" Тогда // не будем читать весь макет, начиная с 118 точно нет КБК, с поддерживаемыми налогами.
						Прервать;
					Иначе
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка();
			КонецЕсли;

			Если УстановленОтбор И ОтборПоВидуНалога <> Неопределено Тогда
				Если ТипОтбораПоВидуНалога = Тип("Массив") Тогда
					Если ОтборПоВидуНалога.Найти(ВидНалога) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ОтборПоВидуНалога <> ВидНалога Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			НоваяСтрока = ТаблицаПоставляемыхДанных.Добавить();
			НоваяСтрока.ВидНалога          = ВидНалога;
			НоваяСтрока.КодБК              = КодБК;
			НоваяСтрока.НаименованиеПолное = НаименованиеПолное;
			НоваяСтрока.Наименование       = Наименование;
			НоваяСтрока.НазначениеПлатежа  = НазначениеПлатежа;

			Если УстановленОтбор Тогда
				Если УстановленМножественныйОтбор Тогда
					Если ОтборПоВидуНалога <> Неопределено И ТаблицаПоставляемыхДанных.Количество()
						= ОтборПоВидуНалога.Количество() Тогда
						// Все значения отбора уже найдены
						Возврат ТаблицаПоставляемыхДанных;
					КонецЕсли;
				Иначе
					// Возвращаем первую найденную запись
					Возврат ТаблицаПоставляемыхДанных;
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	Возврат ТаблицаПоставляемыхДанных;

КонецФункции

Функция НоваяСтруктураПоставляемыхДанных()

	ТаблицаПоставляемыхДанных = НоваяТаблицаПоставляемыхДанных();

	СтруктураПоставляемыхДанных = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаПоставляемыхДанных.Добавить());
	СтруктураПоставляемыхДанных.Вставить("СчетУчета", ПланыСчетов.Управленческий.ПустаяСсылка());
	СтруктураПоставляемыхДанных.Вставить("ДатаАктуальности", Дата(1, 1, 1));
	СтруктураПоставляемыхДанных.Вставить("НазначениеПлатежаИзменено", Ложь);

	Возврат СтруктураПоставляемыхДанных;

КонецФункции

Функция НоваяТаблицаПоставляемыхДанных()

	ТаблицаПоставляемыхДанных = Новый ТаблицаЗначений;
	ТаблицаПоставляемыхДанных.Колонки.Добавить("ВидНалога", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНалогов"));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("КодБК", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НаименованиеПолное", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаПоставляемыхДанных.Колонки.Добавить("НазначениеПлатежа", ОбщегоНазначения.ОписаниеТипаСтрока(210));

	Возврат ТаблицаПоставляемыхДанных;

КонецФункции

Функция ЗначениеИзИсточникаДанных(ИсточникДанных, НомерСтроки, НомерКолонки)

	ОбластьДанных = ИсточникДанных.Область(НомерСтроки, НомерКолонки);
	Если ОбластьДанных = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат ОбластьДанных.Текст;
	КонецЕсли;

КонецФункции

Функция ЭтоПени(ВидНалоговогоОбязательства)
	Возврат ВидНалоговогоОбязательства = "ПениАкт" Или ВидНалоговогоОбязательства = "ПениСам";
КонецФункции

Функция ЭтоПроценты(ВидНалоговогоОбязательства)
	Возврат ВидНалоговогоОбязательства = "Проценты";
КонецФункции

Функция ЭтоШтраф(ВидНалоговогоОбязательства)
	Возврат ВидНалоговогоОбязательства = "Штраф";
КонецФункции

#КонецОбласти

#КонецЕсли