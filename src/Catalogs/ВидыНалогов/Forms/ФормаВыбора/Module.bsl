#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрименяетсяОсобыйПорядокУплатыНалога = Неопределено;
	Параметры.Отбор.Свойство("ПрименяетсяОсобыйПорядокУплатыНалога", ПрименяетсяОсобыйПорядокУплатыНалога);
	Если ПрименяетсяОсобыйПорядокУплатыНалога = Истина Тогда
		Параметры.Отбор.Удалить("ПрименяетсяОсобыйПорядокУплатыНалога");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			ЕдиныйНалоговыйСчетПовтИсп.МассивДоступныхВидовНалогов(),
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ИначеЕсли ПрименяетсяОсобыйПорядокУплатыНалога = Ложь Тогда
		Параметры.Отбор.Удалить("ПрименяетсяОсобыйПорядокУплатыНалога");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ВидНалога",
			Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж,
			ВидСравненияКомпоновкиДанных.НеРавно,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИзПолногоСписка(Команда)
	
	ВыбратьИзПолногоСпискаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыбратьИзПолногоСпискаНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыбратьИзПолногоСписка",
		"Видимость",
		Ложь);
		
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы);
	ОтключитьЭлементыНастроек(Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьЭлементыНастроек(КоллекцияЭлементов)
	
	Для Каждого ЭлементНастроек Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных")
			Или ТипЗнч(ЭлементНастроек) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			ЭлементНастроек.Использование = Ложь;
		ИначеЕсли ТипЗнч(ЭлементНастроек) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
			ОтключитьЭлементыНастроек(ЭлементНастроек.Элементы)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти