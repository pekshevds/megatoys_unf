
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = "Список";
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Доступ") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Доступ",
		Параметры.Отбор.Доступ);
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВыбранныеАдресаты") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Ссылка",
		ГруппыВыбранныхАдресатов(Параметры.ВыбранныеАдресаты),
		ВидСравненияКомпоновкиДанных.ВСписке);
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Автор", Пользователи.ТекущийПользователь());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ГруппыВыбранныхАдресатов(ВыбранныеАдресаты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставыГруппАдреснойКниги.ГруппаАдреснойКниги КАК ГруппаАдреснойКниги
	|ИЗ
	|	РегистрСведений.СоставыГруппАдреснойКниги КАК СоставыГруппАдреснойКниги
	|ГДЕ СоставыГруппАдреснойКниги.КонтактCRM В (&ВыбранныеАдресаты)";
	
	Запрос.УстановитьПараметр("ВыбранныеАдресаты", ВыбранныеАдресаты);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ГруппаАдреснойКниги");
	
КонецФункции

#КонецОбласти

