
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем АктивнаяЗакладка;
	
	ПрайсЛист.ПредставлениеНоменклатуры.Загрузить(Параметры.ПредставлениеНоменклатуры.Выгрузить());
	Для Каждого Структура Из Параметры.ВидыЦен Цикл
		ЗаполнитьЗначенияСвойств(ПрайсЛист.ВидыЦен.Добавить(), Структура);
	КонецЦикла;
	ПрайсЛист.ПредставлениеОстатков = Параметры.ПредставлениеОстатков;
	ПрайсЛист.ПечатьПрайсЛиста = Параметры.ПечатьПрайсЛиста;
	
	ПрайсЛист.КоличествоКолонок = Параметры.КоличествоКолонок;
	ПрайсЛист.КартинкаШирина = Параметры.КартинкаШирина;
	ПрайсЛист.КартинкаВысота = Параметры.КартинкаВысота;
	ПрайсЛист.ИзменятьРазмерПропорционально = Параметры.ИзменятьРазмерПропорционально;
	
	Параметры.Свойство("НазваниеЦеныПредставлением",	ПрайсЛист.НазваниеЦеныПредставлением);
	Параметры.Свойство("ВыводитьЦенуСЕдиницейИзмерения", ПрайсЛист.ВыводитьЦенуСЕдиницейИзмерения);
	
	ЗаголовкиКолонокСЦенами = (ПрайсЛист.НазваниеЦеныПредставлением = 1);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КартинкаВысота", "Доступность",
		Не ПрайсЛист.ИзменятьРазмерПропорционально);
	
	ЭтоДиафильм = (ПрайсЛист.ПечатьПрайсЛиста = Перечисления.ВариантыПечатиПрайсЛиста.Диафильм);
	
	Если ПрайсЛист.ВидыЦен.Количество() = 0 Тогда
		
		ПрайсЛист.ВидыЦен.Добавить();
		
	КонецЕсли;
	
	Параметры.Свойство("АктивнаяЗакладка", АктивнаяЗакладка);
	Если АктивнаяЗакладка <> Неопределено 
		И АктивнаяЗакладка = "ГруппаВидыЦен" Тогда
		ТекущийЭлемент = Элементы.ВыбранныйВидЦен;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПредставлений(ЭтоДиафильм);
	
	Если НЕ ЭтоДиафильм Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницаКартинка", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНастройкаКолонок", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Страницы", "ОтображениеСтраниц", ОтображениеСтраницФормы.Нет);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаХарактеристики", "Видимость", ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") И ЭтоДиафильм);
	
	РедактированиеДоступно = ПравоДоступа("Редактирование", Метаданные.Справочники.ПрайсЛисты);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Страницы", "ТолькоПросмотр", НЕ РедактированиеДоступно);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодАртикулПриИзменении(Элемент)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "Код"));
	МассивСтрок[0].Использование = (КодАртикул = "Код");
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "Артикул"));
	МассивСтрок[0].Использование = (КодАртикул = "Артикул");
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры", "Штрихкод"));
	МассивСтрок[0].Использование = (КодАртикул = "Штрихкод");
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПриИзменении(Элемент)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры",
		"Наименование"));
	МассивСтрок[0].Использование = (Представление = "Наименование");
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры",
		"НаименованиеПолное"));
	МассивСтрок[0].Использование = (Представление = "НаименованиеПолное");
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры",
		"Комментарий"));
	МассивСтрок[0].Использование = (Представление = "Описание");
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаШиринаПриИзменении(Элемент)
	
	Если ПрайсЛист.ИзменятьРазмерПропорционально Тогда
		
		ПрайсЛист.КартинкаВысота = ПрайсЛист.КартинкаШирина * 5;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрайсЛистИзменятьРазмерПропорциональноПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КартинкаВысота", "Доступность",
		Не ПрайсЛист.ИзменятьРазмерПропорционально);
	
	Если ПрайсЛист.ИзменятьРазмерПропорционально Тогда
		
		ПрайсЛист.КартинкаВысота = ПрайсЛист.КартинкаШирина * 5;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьХарактеристикиПриИзменении(Элемент)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры",
		"Характеристика"));
	МассивСтрок[0].Использование = ВыводитьХарактеристики;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПризнакНовинкаПриИзменении(Элемент)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("РеквизитНоменклатуры",
		"ПризнакНовинка"));
	МассивСтрок[0].Использование = ВыводитьПризнакНовинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйВидЦенПриИзменении(Элемент)
	Если ПрайсЛист.ВидыЦен.Количество() > 0 Тогда
		ПрайсЛист.ВидыЦен[0].Использование = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	МассивСтрок = ПрайсЛист.ПредставлениеНоменклатуры.НайтиСтроки(Новый Структура("Использование", Истина));
	Отказ = (МассивСтрок.Количество() = 0);
	Если Отказ Тогда
		
		ТекстОшибки = НСтр("ru ='Необходимо выбрать минимум одно поле...'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		
	Иначе
		
		ПараметрыЗакрытия = Новый Структура;
		ПараметрыЗакрытия.Вставить("РезультатЗакрытия", КодВозвратаДиалога.ОК);
		ПараметрыЗакрытия.Вставить("ПредставлениеНоменклатуры", ПрайсЛист.ПредставлениеНоменклатуры);
		ПараметрыЗакрытия.Вставить("ПредставлениеОстатков", ПрайсЛист.ПредставлениеОстатков);
		ПараметрыЗакрытия.Вставить("КоличествоКолонок", ПрайсЛист.КоличествоКолонок);
		ПараметрыЗакрытия.Вставить("КартинкаШирина", ПрайсЛист.КартинкаШирина);
		ПараметрыЗакрытия.Вставить("КартинкаВысота", ПрайсЛист.КартинкаВысота);
		ПараметрыЗакрытия.Вставить("ИзменятьРазмерПропорционально", ПрайсЛист.ИзменятьРазмерПропорционально);
		
		ПараметрыЗакрытия.Вставить("НазваниеЦеныПредставлением",	ПрайсЛист.НазваниеЦеныПредставлением);
		ПараметрыЗакрытия.Вставить("ВыводитьЦенуСЕдиницейИзмерения", ПрайсЛист.ВыводитьЦенуСЕдиницейИзмерения);
		
		МассивВидовЦен = Новый Массив;
		Для Каждого СтрокаТаблицы Из ПрайсЛист.ВидыЦен Цикл

			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ВидЦен) Тогда
				Продолжить;
			КонецЕсли;

			СтруктураВидаЦен = Новый Структура("Использование, ВидЦен", СтрокаТаблицы.Использование,
				СтрокаТаблицы.ВидЦен);
			МассивВидовЦен.Добавить(СтруктураВидаЦен);

		КонецЦикла;
		ПараметрыЗакрытия.Вставить("МассивВидовЦен", МассивВидовЦен);
		
		Закрыть(ПараметрыЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыПредставлений(Знач ЭтоДиафильм)
	Перем Строка;
	Для Каждого Строка Из ПрайсЛист.ПредставлениеНоменклатуры Цикл
			
			Если Строка.Использовать Тогда
				
				Если Строка.РеквизитНоменклатуры = "Артикул" Тогда 
					КодАртикул = "Артикул";
				КонецЕсли;
				
				Если Строка.РеквизитНоменклатуры = "Код" Тогда 
					КодАртикул = "Код";
				КонецЕсли;
				
				Если Строка.РеквизитНоменклатуры = "Штрихкод" Тогда 
					КодАртикул = "Штрихкод";
				КонецЕсли;
				
				Если Строка.РеквизитНоменклатуры = "Наименование"
					ИЛИ Строка.РеквизитНоменклатуры = "НаименованиеПолное" Тогда
					
					Представление = Строка.РеквизитНоменклатуры;
					
				ИначеЕсли Строка.РеквизитНоменклатуры = "Комментарий" Тогда
					
					Представление = "Описание";
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Строка.РеквизитНоменклатуры = "Характеристика"
				И ЭтоДиафильм Тогда
				
				ВыводитьХарактеристики = Строка.Использовать;
				
			КонецЕсли;
			
			Если Строка.РеквизитНоменклатуры = "ПризнакНовинка" Тогда
				
				ВыводитьПризнакНовинка = Строка.Использовать;
				
			КонецЕсли;
			
		КонецЦикла;
КонецПроцедуры

#КонецОбласти