#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьЗаписьКалендаря(Организация, СобытиеКалендаря, СсылкаЗаписи = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаписиКалендаряПодготовкиОтчетности.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗаписиКалендаряПодготовкиОтчетности КАК ЗаписиКалендаряПодготовкиОтчетности
	|ГДЕ
	|	ЗаписиКалендаряПодготовкиОтчетности.Организация = &Организация
	|	И ЗаписиКалендаряПодготовкиОтчетности.СобытиеКалендаря = &СобытиеКалендаря
	|	И НЕ ЗаписиКалендаряПодготовкиОтчетности.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СобытиеКалендаря", СобытиеКалендаря);
	
	Если СсылкаЗаписи <> Неопределено И ЗначениеЗаполнено(СсылкаЗаписи) Тогда 
		Запрос.Текст = Запрос.Текст + " И ЗаписиКалендаряПодготовкиОтчетности.Ссылка = &СсылкаЗаписи";
		Запрос.УстановитьПараметр("СсылкаЗаписи", СсылкаЗаписи);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИмяФормыПоЗадачеИСостоянию(Задача, СтруктураСобытия) Экспорт        
	
	Если СтруктураСобытия.Свойство("ПараметрыФормы") Тогда
		СостояниеСобытия = СтруктураСобытия.ПараметрыФормы.Состояние;
		Организация = СтруктураСобытия.ПараметрыФормы.Организация;
		СобытиеКалендаря = СтруктураСобытия.ПараметрыФормы.СобытиеКалендаря;
	Иначе
		СостояниеСобытия = СтруктураСобытия.СостояниеСобытия;
		Организация = СтруктураСобытия.Организация;
		СобытиеКалендаря = СтруктураСобытия.СобытиеКалендаря;
	КонецЕсли;
	
	СписокРегламентировнныхОтчетов = РегламентированнаяОтчетностьУСНКлиентСервер.ПолучитьСоответствиеЗадачОтчетностиИменамОтчетов(СобытиеКалендаря.ДатаОкончанияДокументов); 
	
	ЭтоРегламентированныйОтчет = СписокРегламентировнныхОтчетов.Получить(Задача);
	
	Если ЗначениеЗаполнено(ЭтоРегламентированныйОтчет) Тогда 
		ДокументОтчетности = РегламентированнаяОтчетностьУСН.ПолучитьДокументРегламентированнойОтчетностиПоСобытиюКалендаря(Организация, СобытиеКалендаря);	
		
		// Обработка ситуации, если документа нет, возврат к заполнению
		Если НЕ ЗначениеЗаполнено(ДокументОтчетности) И
			НЕ (СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.НеНачато
			ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Заполнить
			ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Проверить
			ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Рассчитать
			ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Сформировать) Тогда
			СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Заполнить;
			КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(Организация, СобытиеКалендаря, СостояниеСобытия, "");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.НалогАУСН Тогда
		
		Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.НалогАУСН";
		
	ИначеЕсли СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.НеНачато
		ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Заполнить
		ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Проверить
		ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Рассчитать
		ИЛИ СостояниеСобытия = Перечисления.СостоянияСобытийКалендаря.Сформировать Тогда  
		
		Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ОписанияЗадачИСтартВыполнения"; 
		
	Иначе
		
		Если Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.АвансовыйПлатежПоУСН Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.АвансовыйПлатежПоУСН";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ЕдиныйНалогЕНВД Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ЕдиныйНалогЕНВД";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.НалогиСотрудников Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.НалогиСотрудников";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ЕдиныйНалог Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ЕдиныйНалог";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыИП Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.СтраховыеВзносыИП";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыПриДоходахСвыше300ТР Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.СтраховыеВзносыПриДоходахСвыше300ТР";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ДекларацияПоУСН Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ДекларацияПоУСН";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ДекларацияПоЕНВД Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ДекларацияПоЕНВД";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ОтчетностьВПФР Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ОтчетностьВПФР";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Справки2НДФЛ Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Справки2НДФЛ";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СреднесписочнаяЧисленность Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.СведенияОСреднесписочнойЧисленности";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Форма4ФСС Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Форма4ФСС";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.НалогПатент Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.НалогПатент";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ТорговыйСбор Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ТорговыйСбор";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Форма1Предприниматель Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Форма1Предприниматель";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Форма6НДФЛ Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Форма6НДФЛ";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СведенияОЗастрахованныхЛицах Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.СведенияОЗастрахованныхЛицах";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Декларация12 Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Декларация12";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.Декларация8 Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.Декларация8";			
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.РасчетПоСтраховымВзносам Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.РасчетПоСтраховымВзносам";
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СЗВСтаж Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.СЗВСтаж";   
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыЗаСотрудников Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.НалогиСотрудниковСтраховыеВзносы";		  
		ИначеЕсли Задача = Справочники.ЗадачиКалендаряПодготовкиОтчетности.ПерсСведенияОФизлицах Тогда
			Возврат "Обработка.ОбработкиНалоговИОтчетности.Форма.ПерсСведенияОФизлицах";		
		Иначе
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ИнтерфейсКалендаряСотрудника

// Функция определяет пиктограмму для элемента записи календаря
//
// Параметры:
//  ЗаписьКалендаряПодготовкиОтчетности	 - СправочникСсылка.ЗаписьКалендаряПодготовкиОтчетности
// 
// Возвращаемое значение:
//  Картинка - пиктограмма записи календаря
//
Функция КартинкаЗаписиКалендаря(ЗаписьКалендаряПодготовкиОтчетности) Экспорт
	
	Возврат Новый Картинка;
	
КонецФункции

// Функция определяет цвет текста для элемента записи календаря
//
// Параметры:
//  ЗаписьКалендаряПодготовкиОтчетности	 - СправочникСсылка.ЗаписьКалендаряПодготовкиОтчетности
// 
// Возвращаемое значение:
//  Цвет - цвет текста записи календаря
//
Функция ЦветТекстаЗаписиКалендаря(ЗаписьКалендаряПодготовкиОтчетности) Экспорт
	
	Возврат Новый Цвет;
	
КонецФункции

// Процедура заполняет таблицу описаний расширенного ввода записи календаря
//
// Параметры:
//  ТаблицаОписаний	 - ТаблицаЗначений	 - описание колонок см. Справочник.ЗаписиКалендаряСотрудника.ПриЗаполненииРасширенногоВводаЗаписиКалендаря()
//
Процедура ПриЗаполненииРасширенногоВводаЗаписиКалендаря(ТаблицаОписаний) Экспорт
	
	// Запись календаря подготовки отчетности не редактируется пользователем
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли