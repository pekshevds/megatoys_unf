////////////////////////////////////////////////////////////////////////////////
// Подсистема "Электронный документооборот с контролирующими органами".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ПолучитьОбработкуЭДО(ТекстСообщения = "") Экспорт
	
	ИнформацияОбОбработке = ДокументооборотСКОВызовСервера.ПодключатьВнешнююОбработкуЭДО();
	
	Если ИнформацияОбОбработке.Подключать И НЕ ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы() Тогда
		Если ДокументооборотСКОВызовСервера.ЕстьПравоНаДОсКО(Истина) Тогда
			ИндексОбработкиЭДОСтрокой = Формат(ИнформацияОбОбработке.ИндексОбработкиЭДО, "ЧДЦ=; ЧГ=");
			
			ОбъектОбработка = ВнешниеОбработки.Создать("Обработка_ДокументооборотСКО" + ИндексОбработкиЭДОСтрокой);
			ОбъектОбработка.ПутьКОбъекту = "ВнешняяОбработка.Обработка_ДокументооборотСКО" + ИндексОбработкиЭДОСтрокой;
			Возврат ОбъектОбработка;
			
		Иначе
			ТекстСообщения = НСтр("ru = 'Недостаточно прав для использования методов электронного документооборота с контролирующими органами.'");
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		Если ДокументооборотСКОВызовСервера.ЕстьПравоНаДОсКО(Ложь) Тогда 
			ОбъектОбработка = Обработки.ДокументооборотСКонтролирующимиОрганами.Создать();
			ОбъектОбработка.ПутьКОбъекту = "Обработка.ДокументооборотСКонтролирующимиОрганами";
			Возврат ОбъектОбработка;
			
		Иначе
			ТекстСообщения = НСтр("ru = 'Недостаточно прав для использования методов электронного документооборота с контролирующими органами.'");
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Для задания обработчиков параметров сеанса следует использовать шаблон:
// Обработчики.Вставить("<ИмяПараметраСеанса>|<НачалоИмениПараметраСеанса*>", "Обработчик");
//
// Примечание. Символ '*'используется в конце имени параметра сеанса и обозначает,
//             что один обработчик будет вызван для инициализации всех параметров сеанса
//             с именем, начинающимся на слово НачалоИмениПараметраСеанса
//
Процедура ОбработчикиИнициализацииПараметровСеанса(Обработчики) Экспорт
	
	// Электронный документооборот с контролирующими органами
	Обработчики.Вставить("ТекущиеУчетныеЗаписиНалогоплательщика",
		"ДокументооборотСКОВызовСервера.УстановитьПараметрСеансаТекущиеУчетныеЗаписиНалогоплательщика");
	Обработчики.Вставить("ТекущийСеансДокументооборотаСКО",
		"ДокументооборотСКО.УстановитьПараметрСеансаТекущийСеансДокументооборотаСКО");
	Обработчики.Вставить(ДлительнаяОтправкаКлиентСервер.СерверныйКлюч(),
		"ДлительнаяОтправка.ПриУстановкеПараметровСеанса");
	Обработчики.Вставить("СертификатыОбменаСКО",
		"ДокументооборотСКООблачнаяПодписьВызовСервера.УстановкаПараметровСеанса");
	Обработчики.Вставить("РезультатАвторизацииНаСервереМЧДРРСКО",
		"ДокументооборотСКОВызовСервера.УстановитьПараметрСеансаРезультатАвторизацииНаСервереМЧДРРСКО");
	Обработчики.Вставить("РезультатыОбработкиМЧДСКО",
		"ДокументооборотСКОВызовСервера.УстановитьПараметрСеансаРезультатыОбработкиМЧДСКО");
	// Конец Электронный документооборот с контролирующими органами
	
КонецПроцедуры

// Возвращает структуру параметров, необходимых для создания информационной панели в объектах ЕНП/ЕНС
// 
// Возвращаемое значение:
//  Структура - Параметры, необходимые для создания информационной панели в объектах ЕНП/ЕНС
//
Функция ПараметрыИнформационнойПанелиБРО() Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	// Если форма вызывается из формы списка без отбора по организации,
	// или в форме нет организации, то оставьте равным Неопределено
	ДополнительныеПараметры.Вставить("Организация", Неопределено);
	// Группа, в которой надо разместить элементы информационной панели
	ДополнительныеПараметры.Вставить("Группа", Неопределено);
	ДополнительныеПараметры.Вставить("Форма", Неопределено);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

#Область СеансСвязиСКонтролирующимиОрганами

Процедура УстановитьПараметрСеансаТекущийСеансДокументооборотаСКО(ИмяПараметра = Неопределено, УстановленныеПараметры = Неопределено) Экспорт
	
	ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = Справочники.СеансыСвязиСКонтролирующимиОрганами.ПустаяСсылка();
	
КонецПроцедуры

Процедура НачатьСеансДокументооборотаСКО(ИнициаторСеанса, УчетнаяЗапись = Неопределено) Экспорт
	
	НовыйСеанс = Справочники.СеансыСвязиСКонтролирующимиОрганами.СоздатьЭлемент();
	НовыйСеанс.Начало = ТекущаяУниверсальнаяДата();
	НовыйСеанс.ИнициаторСеанса = ИнициаторСеанса;
	Если ТипЗнч(УчетнаяЗапись) = Тип("СправочникСсылка.УчетныеЗаписиДокументооборота") Тогда
		НовыйСеанс.УчетнаяЗапись = УчетнаяЗапись;
	КонецЕсли;
	
	НовыйСеанс.Записать();
	
	ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = НовыйСеанс.Ссылка;
	
КонецПроцедуры

Процедура ЗакончитьСеансДокументооборотаСКО(УчетнаяЗапись = Неопределено, Успешно = Истина) Экспорт
	
	ТекущийСеанс = ТекущийСеансДокументооборотаСКО();
	Если ЗначениеЗаполнено(ТекущийСеанс) Тогда
		СеансОбъект = ТекущийСеанс.ПолучитьОбъект();
		
		СеансОбъект.Окончание = ТекущаяУниверсальнаяДата();
		СеансОбъект.Успешно = Успешно;
		СеансОбъект.Записать();
		
		ПараметрыСеанса.ТекущийСеансДокументооборотаСКО = Справочники.СеансыСвязиСКонтролирующимиОрганами.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущийСеансДокументооборотаСКО() Экспорт
	
	Возврат ПараметрыСеанса.ТекущийСеансДокументооборотаСКО;
	
КонецФункции

#КонецОбласти

Функция УстановитьСоединениеССерверомИнтернета(URLСервера, ОписаниеОшибки = "", Таймаут = 60) Экспорт

	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLСервера);
	Схема = ?(ЗначениеЗаполнено(СтруктураURI.Схема), СтруктураURI.Схема, "http");
	Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(Схема);
	
	Попытка
		Соединение = Новый HTTPСоединение(
			СтруктураURI.Хост,
			СтруктураURI.Порт,
			СтруктураURI.Логин,
			СтруктураURI.Пароль, 
			Прокси,
			Таймаут,
			?(НРег(Схема) = "http", Неопределено, ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение()));
	Исключение
			
		ИнформацияОбОшибке = ОбщегоНазначенияЭДКОКлиентСервер.ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Электронный документооборот с контролирующими органами. Установление соединения с сервером интернета'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		ОписаниеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

#Область ОбменСКонтролирующимиОрганами

Процедура ВыполнитьОбмен(УчетнаяЗапись) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбменСКонтролирующимиОрганами);
	
	Если ОтключитьПриНеобходимостиАвтоматическийОбмен(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаЭДО = ПолучитьОбработкуЭДО();	
	ОбработкаЭДО.ВыполнитьОбмен(УчетнаяЗапись);
	
КонецПроцедуры

Функция ПолучитьСообщения(УчетнаяЗапись) Экспорт
	
	ОбработкаЭДО = ПолучитьОбработкуЭДО();	
	Возврат ОбработкаЭДО.ПолучитьСообщения(УчетнаяЗапись);
	
КонецФункции

Процедура ОтправитьСообщения(УчетнаяЗапись) Экспорт
	
	ОбработкаЭДО = ПолучитьОбработкуЭДО();	
	ОбработкаЭДО.ОтправитьСообщения(УчетнаяЗапись);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеСтатусомАвтообмена

Процедура НастроитьОбменПоУчетнойЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если УчетнаяЗапись.ОтключитьАвтообмен Тогда
		ОтключитьОбменПоУчетнойЗаписи(УчетнаяЗапись);
	Иначе
		ВключитьОбменПоУчетнойЗаписи(УчетнаяЗапись);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВключитьОбменПоУчетнойЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтключитьОбменПоУчетнойЗаписи(УчетнаяЗапись);
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ПериодПовтораВТечениеДня = ПолучитьИнтервалВыполненияОбмена(УчетнаяЗапись);
	Расписание.ПериодПовтораДней 		= 1;

	Параметры = Новый Массив;
	Параметры.Добавить(УчетнаяЗапись);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбменСКонтролирующимиОрганами);
	ПараметрыЗадания.Вставить("Ключ", Строка(УчетнаяЗапись.УникальныйИдентификатор()));
	ПараметрыЗадания.Вставить("Параметры", Параметры);
	ПараметрыЗадания.Вставить("Расписание", Расписание);
	ПараметрыЗадания.Вставить("Использование", Истина);
	ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 10);
	ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 3);
	ПараметрыЗадания.Вставить("Наименование", СтрШаблон(НСтр("ru = 'Обмен с контролирующими органами (%1)'"), УчетнаяЗапись));
	
	РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	
КонецПроцедуры

Процедура ОтключитьОбменПоУчетнойЗаписи(УчетнаяЗапись) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", Строка(УчетнаяЗапись.УникальныйИдентификатор()));
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОбменСКонтролирующимиОрганами);
	НайденныеЗадания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Для Каждого Задание Из НайденныеЗадания Цикл
		РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИнтернетПочтой

Функция ПолучитьПочтовоеСоединение(ПочтовыйПрофиль) Экспорт
	
	ПочтовоеСоединение = Неопределено;
	Попытка
		ПочтовоеСоединение = Новый ИнтернетПочта;
		ПочтовоеСоединение.Подключиться(ПочтовыйПрофиль, ПротоколИнтернетПочты.POP3);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Электронный документооборот с контролирующими органами. Почтовое соединение'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ПочтовоеСоединение;

КонецФункции

Функция ОткрытьСоединениеДляПолученияПочты(ПараметрыПодключения) Экспорт
	
	ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
	
	ПочтовыйПрофиль.АдресСервераPOP3   = ПараметрыПодключения.POP3.Сервер;
	ПочтовыйПрофиль.ПортPOP3           = ПараметрыПодключения.POP3.Порт;
	ПочтовыйПрофиль.АутентификацияPOP3 = ПараметрыПодключения.POP3.СпособАутентификации;
	ПочтовыйПрофиль.Пользователь       = ПараметрыПодключения.POP3.Пользователь;	
	ПочтовыйПрофиль.Пароль             = ПараметрыПодключения.POP3.Пароль;
	
	ПочтовыйПрофиль.Таймаут = ПараметрыПодключения.Таймаут;
	
	Соединение = ПолучитьПочтовоеСоединение(ПочтовыйПрофиль);
	
	Возврат Соединение;
	
КонецФункции

Функция ОткрытьСоединениеДляОтправкиПочты(ПараметрыПодключения) Экспорт
	
	ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;

	ПочтовыйПрофиль.АдресСервераSMTP   = ПараметрыПодключения.SMTP.Сервер;
	ПочтовыйПрофиль.ПортSMTP           = ПараметрыПодключения.SMTP.Порт;
	ПочтовыйПрофиль.АутентификацияSMTP = ПараметрыПодключения.SMTP.СпособАутентификации;
	ПочтовыйПрофиль.ПользовательSMTP   = ПараметрыПодключения.SMTP.Пользователь;	
	ПочтовыйПрофиль.ПарольSMTP         = ПараметрыПодключения.SMTP.Пароль;
	
	ПочтовыйПрофиль.Таймаут = ПараметрыПодключения.Таймаут;
	
	Соединение = ПолучитьПочтовоеСоединение(ПочтовыйПрофиль);
	
	Возврат Соединение;
	
КонецФункции

Процедура ЗакрытьПочтовоеСоединение(ПочтовоеСоединение) Экспорт
	
	ПочтовоеСоединение.Отключиться();
	
КонецПроцедуры

#КонецОбласти

#Область ПереносТранспортныхСообщенийВПрисоединенныеФайлы

Процедура ПеренестиТранспортныеСообщенияВПрисоединенныеФайлы() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПереносСообщений1СОтчетностиВПрисоединенныеФайлы);
	
	ОбработкаЭДО = ПолучитьОбработкуЭДО();
	Если ОбработкаЭДО.ПеренестиТранспортныеСообщенияВПрисоединенныеФайлы() Тогда
		УстановитьПривилегированныйРежим(Истина);
		РегламентныеЗаданияСервер.УдалитьЗадание(Метаданные.РегламентныеЗадания.ПереносСообщений1СОтчетностиВПрисоединенныеФайлы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	ТекущемуПользователюЭДОДоступен = ДокументооборотСКОВызовСервера.ТекущемуПользователюЭДОДоступен();
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ТекущемуПользователюЭДОДоступен", ТекущемуПользователюЭДОДоступен);
	
	ТекущемуПользователюАОДоступен = ДокументооборотСКОВызовСервера.ТекущемуПользователюАОДоступен();
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ТекущемуПользователюАОДоступен", ТекущемуПользователюАОДоступен);
	
	ВыбранныйCSPИзВременныхНастроек = ДокументооборотСКОВызовСервера.ПолучитьВыбранныйCSPИзВременныхНастроек();
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ВыбранныйCSPИзВременныхНастроек", ВыбранныйCSPИзВременныхНастроек);
	
	Параметры.Вставить("ДоступныеЛокальныеСертификатыПользователя", ОбщегоНазначенияЭДКОСлужебныйВызовСервера.ДоступныеЛокальныеСертификатыПользователя());
	Параметры.Вставить(
		"НеПоказыватьПредупреждениеОКонфликтеКриптопровайдеров", 
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПредупреждениеОКонфликтеКриптопровайдеров", "БольшеНеПоказывать", Ложь));
		
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ЕстьОтправленныеЗаявленияАбонентов", 
		ОбработкаЗаявленийАбонентаВызовСервера.ОтправленныеЗаявленияАбонентов(,,,,Истина).Количество() > 0);
		
	ИспользуетсяРежимТестирования = ДокументооборотСКОВызовСервера.ИспользуетсяРежимТестирования();
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ИспользуетсяРежимТестирования", ИспользуетсяРежимТестирования);
	
	ПутьВК = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПутьВК();
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ПутьВК", ПутьВК);
	
	ЕстьПравоНаДОсКО = ДокументооборотСКОВызовСервера.ЕстьПравоНаДОсКО(Ложь);
	Параметры.Вставить("ДокументооборотСКонтролирующимиОрганами_ЕстьПравоНаДОсКОПриЗапуске", ЕстьПравоНаДОсКО);
	
КонецПроцедуры

Функция ТекущиеУчетныеЗаписиНалогоплательщика() Экспорт
	
	Если ПараметрыСеанса.ТекущиеУчетныеЗаписиНалогоплательщика = Неопределено Тогда
		ТекущиеУчетныеЗаписи = Новый Массив;
	Иначе
		ТекущиеУчетныеЗаписи = ПараметрыСеанса.ТекущиеУчетныеЗаписиНалогоплательщика;
	КонецЕсли;
	
	Возврат ТекущиеУчетныеЗаписи;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиПосимвольно(
		ТабличныйДокумент, 
		Значение, 
		ПрефиксИмениПараметра, 
		КоличествоКлеток, 
		СимволНаполнения = "-") Экспорт
	
	ДлинаЗначения = СтрДлина(Значение);
	Для НомерСимвола = 0 По КоличествоКлеток - 1 Цикл
		
		ИмяПраметра = ПрефиксИмениПараметра + Строка(НомерСимвола + 1);
		
		Если НомерСимвола > ДлинаЗначения - 1 Тогда
			ЗначениеПараметра = СимволНаполнения;
		Иначе
			ЗначениеПараметра = Сред(Значение, НомерСимвола + 1, 1);
		КонецЕсли;
		
		ТабличныйДокумент.Параметры[ИмяПраметра] = ЗначениеПараметра;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьИсключение(ИнформацияОбОшибке, ВыполняемоеДействие = "") Экспорт

	КодОсновногоЯзыка  = ОбщегоНазначения.КодОсновногоЯзыка();
	Подсистема = НСтр("ru = 'Электронный документооборот с контролирующими органами. '", КодОсновногоЯзыка);
	ПодробноеПредставление = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЗаписьЖурналаРегистрации( // Универсальная
		Подсистема + ВыполняемоеДействие, 
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ПодробноеПредставление);

КонецПроцедуры

Функция ДваждыРазобратьОтветНаPostЗапрос(Ответ) Экспорт
	
	ОтветСтрокой = Ответ.ПолучитьТелоКакСтроку();
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ОтветСтрокой);
	
	ПостроительДОМ 	= Новый ПостроительDOM();
	ДОМ = ПостроительДОМ.Прочитать(ЧтениеXML);
	
	Содержимое = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьЗначениеУзлаXML(ДОМ, "string");
	Содержимое = "<?xml version=""1.0"" encoding=""windows-1251""?>" + Символы.ПС + Содержимое;
	
	ЧтениеXML2 = Новый ЧтениеXML;
	ЧтениеXML2.УстановитьСтроку(Содержимое);
	
	ПостроительДОМ2 	= Новый ПостроительDOM();
	ДОМ2 = ПостроительДОМ2.Прочитать(ЧтениеXML2);
	
	ЧтениеXML.Закрыть();
	ЧтениеXML2.Закрыть();
	
	Возврат ДОМ2;
		
КонецФункции

Процедура ОбъединитьВTIFСканы(ОписаниеФайлов, АдресРезультата) Экспорт
	
	ЭтоТестирование = ДокументооборотСКОКлиентСервер.ИспользуетсяРежимТестирования();
	
	Результат = Новый Структура();
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("ОписаниеФайлов", Неопределено);
	
	Для каждого СтрокаОписанияФайлов Из ОписаниеФайлов Цикл
		
		Картинки = Новый Массив;
		Для Каждого ДвоичныеДанные из СтрокаОписанияФайлов.МассивДвоичныхДанных Цикл
			Картинки.Добавить(Новый Картинка(ДвоичныеДанные));
		КонецЦикла;
		
		Если ЭтоТестирование Тогда
			ОбрабатываемаяКартинка = Новый ОбрабатываемаяКартинка(Картинки[0]);
		Иначе
			ОбрабатываемаяКартинка = Новый ОбрабатываемаяКартинка(Картинки, ФорматКартинки.TIFF);
		КонецЕсли;
		ОбрабатываемаяКартинка.УстановитьГлубинуЦвета(ГлубинаЦвета.БитНаПиксел1);
		ОбрабатываемаяКартинка.УстановитьПлотность(300, 300);
		Картинка = ОбрабатываемаяКартинка.ПолучитьКартинку();
		Данные = Картинка.ПолучитьДвоичныеДанные();
		Если Данные = Неопределено Тогда
			
			Результат.ТекстОшибки = НСтр("ru = 'Не удалось получить данные скана'");
			ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
			Возврат;
			
		КонецЕсли;
		СтрокаОписанияФайлов.Вставить("ДвДанныеИтоговогоФайла", Данные);
	
	КонецЦикла;
	
	Результат.Вставить("Выполнено", Истина);
	Результат.Вставить("ОписаниеФайлов", ОписаниеФайлов);
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

#Область СлужебныеОбработчики

// Процедура, необходимая срабатывания рег задания по отслеживанию заявлений
// в модели сервиса как элемента очереди заданий.
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОбработкаЗаявленийАбонента.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОбменСКонтролирующимиОрганами.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПереносСообщений1СОтчетностиВПрисоединенныеФайлы.ИмяМетода);
	
КонецПроцедуры

// Определяет следующие свойств регламентных заданий:
//  - зависимость от функциональных опций.
//  - возможность выполнения в различных режимах работы программы.
//  - прочие параметры.
//
// Параметры:
//  Настройки - ТаблицаЗначений - таблица значений с колонками:
//    * РегламентноеЗадание - ОбъектМетаданных:РегламентноеЗадание - регламентное задание.
//    * ФункциональнаяОпция - ОбъектМетаданных:ФункциональнаяОпция - функциональная опция,
//        от которой зависит регламентное задание.
//    * ЗависимостьПоИ      - Булево - если регламентное задание зависит более, чем
//        от одной функциональной опции и его необходимо включать только тогда,
//        когда все функциональные опции включены, то следует указывать Истина
//        для каждой зависимости.
//        По умолчанию Ложь - если хотя бы одна функциональная опция включена,
//        то регламентное задание тоже включено.
//    * ВключатьПриВключенииФункциональнойОпции - Булево, Неопределено - если Ложь, то при
//        включении функциональной опции регламентное задание не будет включаться. Значение
//        Неопределено соответствует значению Истина.
//        По умолчанию - неопределено.
//    * ДоступноВПодчиненномУзлеРИБ - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в РИБ.
//        По умолчанию - неопределено.
//    * ДоступноВАвтономномРабочемМесте - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в автономном рабочем месте.
//        По умолчанию - неопределено.
//    * ДоступноВМоделиСервиса      - Булево, Неопределено - Истина или Неопределено, если регламентное
//        задание доступно в модели сервиса.
//        По умолчанию - неопределено.
//    * РаботаетСВнешнимиРесурсами  - Булево - Истина, если регламентное задание модифицирует данные
//        во внешних источниках (получение почты, синхронизация данных и т.п.).
//        По умолчанию - Ложь.
//    * Параметризуется             - Булево - Истина, если регламентное задание параметризованное.
//        По умолчанию - Ложь.
//
// Например:
//	Настройка = Настройки.Добавить();
//	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбновлениеСтатусовДоставкиSMS;
//	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользоватьПочтовыйКлиент;
//	Настройка.ДоступноВМоделиСервиса = Ложь;
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбменСКонтролирующимиОрганами;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.Параметризуется = Истина;
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбработкаЗаявленийАбонента;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.Параметризуется = Истина;
	
КонецПроцедуры

#КонецОбласти

Функция ОтключитьПриНеобходимостиАвтоматическийОбмен(УчетнаяЗапись)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.УчетнаяЗаписьОбмена = &УчетнаяЗапись
	|	И НЕ Организации.ПометкаУдаления
	|	И Организации.ВидОбменаСКонтролирующимиОрганами = ЗНАЧЕНИЕ(Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате)";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	Если Запрос.Выполнить().Пустой() ИЛИ УчетнаяЗапись.ПометкаУдаления Тогда
		ОтключитьОбменПоУчетнойЗаписи(УчетнаяЗапись);
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьИнтервалВыполненияОбмена(УчетнаяЗапись)
	
	Если ДокументооборотСКОВызовСервера.ИспользуетсяРежимТестирования() Тогда
		Интервал = 90;
	Иначе
		Интервал = 3600;
	КонецЕсли;
	
	Возврат Интервал;
	
КонецФункции

Функция ПолучитьЗаменяемоеЗначениеТестовогоСервера1С(ИсходноеЗначение, ЗначениеДляОтправки = Неопределено, СоединениеHTTP = Неопределено) Экспорт
		
	Если ДокументооборотСКОВызовСервера.ИспользуетсяРежимТестирования() = Ложь Тогда
		Возврат ИсходноеЗначение;
	КонецЕсли;
	
	ПодключенТестовыйКонтур1С = Ложь;
	
	Если СоединениеHTTP <> Неопределено И СоединениеHTTP.Сервер = "ego712.tc7app.com" Тогда
		ПодключенТестовыйКонтур1С = Истина;
	ИначеЕсли СоединениеHTTP <> Неопределено И СоединениеHTTP.Сервер <> "ego712.tc7app.com" Тогда
		ПодключенТестовыйКонтур1С = Ложь;
	Иначе
		URLСервера = "http://ego712.tc7app.com:80";
		СоединениеHTTPТестовогоСервера = ДокументооборотСКО.УстановитьСоединениеССерверомИнтернета(URLСервера);
		Если СоединениеHTTPТестовогоСервера = Неопределено Тогда
			Возврат ИсходноеЗначение;	
		КонецЕсли;
	
		Если СоединениеHTTPТестовогоСервера <> Неопределено Тогда
			ЗапросHTTP = Новый HTTPЗапрос("/ego/hs/Agents/AutoSettings/Connect/Status");
			ИдентификаторИнформационнойБазы = СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы();
			НомерОбласти = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
			ЗапросHTTP.Заголовки.Вставить("BaseID", ИдентификаторИнформационнойБазы + ?(НомерОбласти = 0, "", "_" + Строка(НомерОбласти))); 
			Попытка
				ОтветHTTP = СоединениеHTTPТестовогоСервера.ОтправитьДляОбработки(ЗапросHTTP);
			Исключение
				Возврат ИсходноеЗначение;	
			КонецПопытки;
			
			Если ОтветHTTP.КодСостояния = 200 Тогда
				ПодключенТестовыйКонтур1С = Истина;
				СоединениеHTTP = СоединениеHTTPТестовогоСервера;
			Иначе
				Возврат ИсходноеЗначение;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ПодключенТестовыйКонтур1С = Ложь Тогда
		Возврат ИсходноеЗначение;
	КонецЕсли;
	
	Если ЗначениеДляОтправки = Неопределено Тогда
		ЗначениеДляОтправки = ИсходноеЗначение;
	КонецЕсли;
	
	ЗапросHTTP = Новый HTTPЗапрос("/ego/hs/Agents/TestingMode/Values/Change?Value=" + ЗначениеДляОтправки);
	
	Попытка
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP);
	Исключение
		Возврат ИсходноеЗначение;	
	КонецПопытки;
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		Значение = ОтветHTTP.ПолучитьТелоКакСтроку();
		Возврат Значение;
	КонецЕсли;
	
	Возврат ИсходноеЗначение;
	
КонецФункции

Процедура ДобавитьЗаголовкиДляТестовогоСервера1С(ЗаголовкиHTTP, СоединениеHTTP) Экспорт
	
	Если СоединениеHTTP = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СоединениеHTTP.Сервер = "ego712.tc7app.com" Тогда
		ИдентификаторИнформационнойБазы = СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы();
		НомерОбласти = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
		ЗаголовкиHTTP.Вставить("BaseID", ИдентификаторИнформационнойБазы + ?(НомерОбласти = 0, "", "_" + Строка(НомерОбласти)));	
	КонецЕсли;
	
КонецПроцедуры

#Область АудиторскоеЗаключение

Функция КлючОбъектаАудиторскогоЗаключения()
	
	Возврат "ДокументооборотСКонтролирующимиОрганами_БухОтчет_УбратьИнформационнуюПанель";
	
КонецФункции

Функция ПриложенФайлАудиторскогоЗаключения(Форма)
	
	ЗаписьРегистраСведений = РегистрыСведений.ДополнительныеФайлыРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
	ЗаписьРегистраСведений.РегламентированныйОтчет = Форма.СтруктураРеквизитовФормы.мСохраненныйДок;
	ЗаписьРегистраСведений.ВидДополнительногоФайла = "Аудиторское заключение";
	ЗаписьРегистраСведений.Прочитать();
	Возврат НЕ ПустаяСтрока(ЗаписьРегистраСведений.ВидДополнительногоФайла);
	
КонецФункции

Процедура БольшеНеПоказыватьИнформационнуюПанель(Форма) Экспорт
	
	ХранилищеОбщихНастроек.Сохранить(
		КлючОбъектаАудиторскогоЗаключения(),
		,
		Истина);
	
	УстановитьВидимостьПанелиАудиторскогоЗаключения(Форма);
	
КонецПроцедуры

Процедура УправлениеПанельюАудиторскогоЗаключения(Форма, ВидимостьПанели, ВидимостьПодсказки)
	
	Элементы = Форма.Элементы;

	Элементы.ПанельАудиторскогоЗаключения.Видимость = ВидимостьПанели;
	
	Если ВидимостьПодсказки Тогда
		Элементы.ИмяФайлаАудиторскогоЗаключения.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.ИмяФайлаАудиторскогоЗаключения.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьПанелиАудиторскогоЗаключения(Форма) Экспорт

	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		УправлениеПанельюАудиторскогоЗаключения(Форма, Ложь, Ложь);
		Возврат;
	КонецЕсли;
	
	ДолжноБытьЗаключение = РегламентированнаяОтчетность.ОтображаетсяРазделДопФайловБухотчетности(Форма);
	
	ОтчетОтправлен = ОтчетОтправлен(Форма);
	ПанельУбранаПользователем = ПанельАудиторскогоЗаключенияУбрана();
	
	Если ПанельУбранаПользователем ИЛИ НЕ ДолжноБытьЗаключение Тогда
		УправлениеПанельюАудиторскогоЗаключения(Форма, Ложь, Ложь);
	ИначеЕсли ОтчетОтправлен И НЕ ПриложенФайлАудиторскогоЗаключения(Форма) Тогда
		УправлениеПанельюАудиторскогоЗаключения(Форма, Истина, Истина);
	Иначе
		УправлениеПанельюАудиторскогоЗаключения(Форма, Ложь, Истина);
	КонецЕсли;

КонецПроцедуры

Функция ОтчетОтправлен(Форма)
	
	Если Форма.СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	Ссылка = Форма.СтруктураРеквизитовФормы.мСохраненныйДок.Ссылка;
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	ДополнительныеПараметры = Новый Структура("ПолучатьДаты, ПолучатьОшибкиОтправки", Ложь, Ложь);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(
		Ссылка,
		,
		ДополнительныеПараметры);
		
	Если ТекущееСостояние = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекущийЭтапОтправки = ТекущееСостояние.ТекущийЭтапОтправки;
	
	Если ТекущийЭтапОтправки = Неопределено Тогда
		Возврат  Ложь;
	КонецЕсли;
	
	СостояниеСдачиОтчетности = ТекущийЭтапОтправки.СостояниеСдачиОтчетности;
	
	Отправлен = 
		СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНачат
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота;
		
	Возврат Отправлен;

КонецФункции

Функция ПанельАудиторскогоЗаключенияУбрана()

	ПанельУбранаПользователем = ХранилищеОбщихНастроек.Загрузить(КлючОбъектаАудиторскогоЗаключения()) = Истина;
	Возврат ПанельУбранаПользователем;
	
КонецФункции

#КонецОбласти

#Область ПанельТарифа

// Перерисовывает и при необходимости создает элементы информационной панели БРО в объектах ЗУП
//
// Параметры:
//  ПараметрыОтображения - Структура - Параметры, необходимые для создания информационной панели в объектах ЗУП
//                         Шаблон для описания параметров возвращает метод ПараметрыИнформационнойПанелиБРО()
//	Тариф - Перечисления.ТарифыОператораЭДО
// 
Процедура ОбновитьИнформационнуюПанельБРО(ПараметрыОтображения, Тариф) Экспорт
	
	Попытка
	
		Организация = ПараметрыОтображения.Организация;
		Если Тариф = Перечисления.ТарифыОператораЭДО.КадровыеРешения Тогда
			Видимость = НужноПоказатьРекламуТарифаКадровыеРешения(Организация);
		Иначе
			Видимость = НужноПоказатьРекламуТарифаПромоЕНС(Организация);
		КонецЕсли;
		
		Группа = ПараметрыОтображения.Группа;
		Группа.Видимость = Видимость;
		
		Форма = ПараметрыОтображения.Форма;
		
		ИнформационнаяПанельБРОКэш = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыОтображения);
		ИнформационнаяПанельБРОКэш.Удалить("Форма");
		ИнформационнаяПанельБРОКэш.Удалить("Группа");
		ИнформационнаяПанельБРОКэш.Вставить("ИмяГруппы", Группа.Имя);
		
		Форма.ИнформационнаяПанельБРОКэш = ИнформационнаяПанельБРОКэш;
		
		Если Видимость Тогда
			
			СоздатьТекстИнформацииБРО(ПараметрыОтображения, Тариф);
			СоздатьЗакрытьИнформациюБРО(ПараметрыОтображения);

			ИзменитьОформлениеИнформационнойПанельБРО(ПараметрыОтображения);
			
		КонецЕсли;
		
	Исключение
		
		Действие = НСтр("ru = 'Обновить информационную панель БРО в объектах потребителей (%1)'");
		Действие = СтрШаблон(Действие, Строка(Тариф));
		
		ДокументооборотСКО.ОбработатьИсключение(
			ИнформацияОбОшибке(), 
			Действие);
	
	КонецПопытки;

КонецПроцедуры

Функция НужноПоказатьРекламуТарифаКадровыеРешения(Организация = Неопределено) Экспорт
	
	Скрыть = ДокументооборотСКОВызовСервера.ЗагрузитьНастройку("КадровыеРешенияНапоминать") = Ложь;
	
	Если Скрыть Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФСС = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС");
	
	Если ЗначениеЗаполнено(Организация) Тогда
	
		ФССПодключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(
			Организация,
			ФСС);
	
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Направления       = КонтекстЭДОСервер.ПоддерживаемыеНаправленияОрганизации(Организация);
		УчетнаяЗапись     = ДокументооборотСКОВызовСервера.УчетнаяЗаписьОрганизации(Организация);
			
		НужноПоказать = 
			НЕ ФССПодключен 
			И НЕ ЗначениеЗаполнено(УчетнаяЗапись)
			И Направления.ПризнакПоддержкиФСС 
			И Направления.ПризнакПоддержкиПФР;
			
		Возврат НужноПоказать;
			
	Иначе
			
		Подключен = ОрганПодключенХотяБыУОднойОрганизации(ФСС);
		Возврат НЕ Подключен;
		
	КонецЕсли;
	
КонецФункции

Функция НужноПоказатьРекламуТарифаПромоЕНС(Организация = Неопределено) Экспорт
	
	СобытиеНаступило = ДокументооборотСКОВызовСервера.СобытиеПромоЕНСНаступило();
	Если НЕ СобытиеНаступило Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Скрыть = ДокументооборотСКОВызовСервера.ЗагрузитьНастройку("ПромоЕНСНапоминать") = Ложь;
	
	Если Скрыть Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФНС = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС");
	
	Если ЗначениеЗаполнено(Организация) Тогда
	
		Подключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(
			Организация,
			ФНС);
	
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		Направления       = КонтекстЭДОСервер.ПоддерживаемыеНаправленияОрганизации(Организация);
		УчетнаяЗапись     = ДокументооборотСКОВызовСервера.УчетнаяЗаписьОрганизации(Организация);
			
		НужноПоказать = 
			НЕ Подключен 
			И НЕ ЗначениеЗаполнено(УчетнаяЗапись)
			И Направления.ПризнакПоддержкиФНС;
			
		Возврат НужноПоказать;
			
	Иначе
			
		Подключен = ОрганПодключенХотяБыУОднойОрганизации(ФНС);
		Возврат НЕ Подключен;
		
	КонецЕсли;
	
КонецФункции

Функция ОрганПодключенХотяБыУОднойОрганизации(Орган, ПроверятьДляТекущегоПользователя = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Подключен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(
			Выборка.Организация,
			Орган,,
			ПроверятьДляТекущегоПользователя);
			
		Если Подключен Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

Функция ИзменитьОформлениеИнформационнойПанельБРО(ПараметрыОтображения) Экспорт
	
	Группа = ПараметрыОтображения.Группа;
	
	Группа.ЦветФона = ЦветаСтиля.ЦветФонаТекущейОтправки;// желтый
	Группа.РастягиватьПоГоризонтали = Истина;
	
КонецФункции

Функция СоздатьЗакрытьИнформациюБРО(ПараметрыОтображения) Экспорт
	
	Группа   = ПараметрыОтображения.Группа;
	Форма    = ПараметрыОтображения.Форма;
	Элементы = Форма.Элементы;
	
	Элемент = Группа.ПодчиненныеЭлементы.Найти("ЗакрытьИнформациюБРО");
	Создать = Элемент = Неопределено;
	
	Если Создать Тогда
		Элемент = Элементы.Добавить("ЗакрытьИнформациюБРО", Тип("ДекорацияФормы"), Группа);
		Элемент.Заголовок   = Новый ФорматированнаяСтрока(НСтр("ru = 'Закрыть'"),,,, НСтр("ru = 'Закрыть'"));
		Элемент.УстановитьДействие(
			"ОбработкаНавигационнойСсылки", 
			"Подключаемый_ИнформационнаяПанельБРООбработкаНавигационнойСсылки");
	КонецЕсли;
	
КонецФункции

Функция СоздатьТекстИнформацииБРО(ПараметрыОтображения, Тариф) Экспорт
	
	Группа   = ПараметрыОтображения.Группа;
	Форма    = ПараметрыОтображения.Форма;
	ЭтоСЭДО  = ПараметрыОтображения.Свойство("ЭтоСЭДО") И ПараметрыОтображения.ЭтоСЭДО;
	Элементы = Форма.Элементы;
	
	Элемент = Группа.ПодчиненныеЭлементы.Найти("ТекстИнформацииБРО");
	Создать = Элемент = Неопределено;
	
	Если Создать Тогда
		
		Элемент = Элементы.Добавить("ТекстИнформацииБРО", Тип("ДекорацияФормы"), Группа);
		Элемент.УстановитьДействие(
			"ОбработкаНавигационнойСсылки", 
			"Подключаемый_ИнформационнаяПанельБРООбработкаНавигационнойСсылки");
		
		Элемент.РастягиватьПоГоризонтали = Истина;
		Элемент.АвтоМаксимальнаяШирина = Ложь;
		
		Если Тариф = Перечисления.ТарифыОператораЭДО.КадровыеРешения Тогда
			Если ЭтоСЭДО Тогда
				Текст = НСтр("ru = 'Подключите обмен с СФР (бывш. ФСС и ПФР) бесплатно на 3 месяца! Попробуйте Социальный документооборот и другие возможности.'");
			Иначе
				Текст = НСтр("ru = 'Подключите обмен с СФР (бывш. ФСС и ПФР) бесплатно на 3 месяца!'");
			КонецЕсли;
		ИначеЕсли Тариф = Перечисления.ТарифыОператораЭДО.ПромоЕНС Тогда
			Текст = НСтр("ru = 'Подключите тариф ""Промо ЕНС"" и отправляйте уведомления по ЕНП в ФНС бесплатно в течение 6 месяцев!'");
		КонецЕсли;
		
		Подробнее = Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее...'"),,,, НСтр("ru = 'Подробнее...'"));
		
		Элемент.Заголовок = Новый ФорматированнаяСтрока(
			Текст,
			" ",
			Подробнее);
			
	КонецЕсли;
		
КонецФункции

Функция ЭтоКрупнейший(Организация) Экспорт

	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("ДатаЗначения", ТекущаяДатаСеанса());

	ЭтоКрупнейший = РегламентированнаяОтчетность.ОрганизацияЯвляетсяКрупнейшимНалогоплательщиком(СтруктураПараметров);
		
	Возврат ЭтоКрупнейший;

КонецФункции

Функция КодНОКрупнейшего(Организация) Экспорт
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("ДатаЗначения", ТекущаяДатаСеанса());

	КодНО = РегламентированнаяОтчетность.КодНалоговогоОрганаПолучателяОтчетности(СтруктураПараметров);
		
	Возврат КодНО;

КонецФункции

Функция ШаблонЗаписиРегистра(Регистр) Экспорт
	
	Шаблон = Новый Структура;
	
	ДобавитьМетаданныеВШаблонРегистра(Регистр, Шаблон, "Измерения");
	ДобавитьМетаданныеВШаблонРегистра(Регистр, Шаблон, "Ресурсы");
	ДобавитьМетаданныеВШаблонРегистра(Регистр, Шаблон, "Реквизиты");
	
	Возврат Шаблон;
	
КонецФункции

Функция ДобавитьМетаданныеВШаблонРегистра(Регистр, Шаблон, Вид) Экспорт
	
	Для каждого Реквизит Из Регистр[Вид] Цикл

		Значение = Реквизит.Тип.ПривестиЗначение(Неопределено);
		Шаблон.Вставить(Реквизит.Имя, Значение);
	
	КонецЦикла;
	
КонецФункции

Функция ИдентификаторАбонентаДляЗапросаРегФайла(ИдентификаторАбонента) Экспорт

	Если СтрДлина(ИдентификаторАбонента) = 36 Тогда
		// AF487A1A-B116-416A-9573-A8CA8A8A56FB
		Возврат ИдентификаторАбонента;
	Иначе
		// Из 1AEAF487A1A-B116-416A-9573-A8CA8A8A56FB делаем AF487A1A-B116-416A-9573-A8CA8A8A56FB
		Возврат Сред(ИдентификаторАбонента, 4);
	КонецЕсли;

КонецФункции

Функция РеквизитыСовпадают(Объект1, Объект2, РеквизитыСтрокой) Экспорт
	
	Реквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РеквизитыСтрокой, , Истина, Истина);
	
	Для каждого Реквизит Из Реквизиты Цикл
		Если Объект1[Реквизит] <> Объект2[Реквизит] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти