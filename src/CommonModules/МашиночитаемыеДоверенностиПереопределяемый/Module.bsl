

#Область ПрограммныйИнтерфейс

// Заполняет при изменении данные физического лица по ссылке.
//
// Параметры:
//  ФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. МашиночитаемыеДоверенности.НовыеДанныеФизЛица
//
Процедура ПриИзмененииДанныеФизЛица(ФизЛицо, Сведения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
	|	ФизическиеЛица.ИНН КАК ИНН,
	|	ФизическиеЛица.Пол КАК Пол,
	|	ФизическиеЛица.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	ФизическиеЛица.Наименование КАК ФИО,
	|	ФизическиеЛица.Гражданство КАК Гражданство,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &Физлицо) КАК ФИОФизЛицСрезПоследних
	|		ПО ФизическиеЛица.Ссылка = ФИОФизЛицСрезПоследних.ФизическоеЛицо
	|ГДЕ
	|	ФизическиеЛица.Ссылка = &Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументыФизическихЛиц.Серия КАК Серия,
	|	ДокументыФизическихЛиц.Номер КАК Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛиц.КемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения КАК КодПодразделения,
	|	ВидыДокументовФизическихЛиц.КодМВД КАК КодФНС
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
	|		ПО ДокументыФизическихЛиц.ВидДокумента = ВидыДокументовФизическихЛиц.Ссылка
	|ГДЕ
	|	ДокументыФизическихЛиц.Физлицо = &Физлицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыФизическихЛиц.Период УБЫВ";
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ПервыйПакет = РезультатЗапроса[0].Выбрать();
	ВторойПакет = РезультатЗапроса[1].Выбрать();
	
	ПервыйПакет.Следующий();
	ВторойПакет.Следующий();
	
	ЗаполнитьЗначенияСвойств(Сведения, ПервыйПакет);
	ЗаполнитьЗначенияСвойств(Сведения, ВторойПакет);
	
КонецПроцедуры

#КонецОбласти
