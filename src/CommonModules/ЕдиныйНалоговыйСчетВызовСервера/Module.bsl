
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьНастройкиПечатиУведомлений(МассивДокументов) Экспорт
	
	МассивУведомлений = Новый Массив;
	
	СвойстваДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивДокументов, "Уведомление");
	Для Каждого ЗаписьМассива Из МассивДокументов Цикл
		МассивУведомлений.Добавить(СвойстваДокументов[ЗаписьМассива]);
	КонецЦикла;
	
	Возврат МассивУведомлений;
	
КонецФункции

Функция ЗаголовкиФормыПечати(МассивДокументов) Экспорт
	
	Заголовки = Новый Соответствие();
	
	Для Каждого ДокументСсылка Из МассивДокументов Цикл
		Заголовки.Вставить(ДокументСсылка, ЗаголовокФормыПечати(ДокументСсылка));
	КонецЦикла;
	
	Возврат Заголовки;
	
КонецФункции

Функция ЗаголовокФормыПечати(Ссылка) Экспорт
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ВидУведомления, Организация");
	Возврат Новый Структура("ЗаголовокФормы", "" + Реквизиты.ВидУведомления + " (" + Реквизиты.Организация + ")");
	
КонецФункции

Функция НалогУплачиваетсяОтдельно(Организация, Период, Налог, Контрагент) Экспорт
	
	НалогУплачиваетсяОтдельно = Ложь;
	
	Если Не УчетнаяПолитика.ПлательщикЕНП(Организация, Период) Тогда
		НалогУплачиваетсяОтдельно = Истина;
	Иначе
		Если ЗначениеЗаполнено(Налог) Тогда
			НалогиУплачиваемыеОтдельно = ЕдиныйНалоговыйСчетПовтИсп.НалогиУплачиваемыеОтдельно();
			НалогУплачиваетсяОтдельно = НалогиУплачиваемыеОтдельно.Найти(Налог) <> Неопределено;
		КонецЕсли;
		Если Не НалогУплачиваетсяОтдельно
			И ЗначениеЗаполнено(Контрагент)
			И ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			СвойстваКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ВидКонтрагента, ВидГосударственногоОргана");
			НалогУплачиваетсяОтдельно = Не (СвойстваКонтрагента.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ГосударственныйОрган
				И СвойстваКонтрагента.ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НалогУплачиваетсяОтдельно;
	
КонецФункции

#КонецОбласти
