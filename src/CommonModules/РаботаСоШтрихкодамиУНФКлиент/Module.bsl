#Область ПрограммныйИнтерфейс

// Обработка полученных данных.
//
// Параметры:
//  Форма - УправляемаяФорма - форма родитель.
//  СтруктураПараметровКлиента - Структура - параметры клиента.
//  ОткрытаБлокирующаяФорма - Булево - признак открытия блокирующей формы.
//
Процедура ОбработатьДанныеПоКоду(Форма, СтруктураПараметровКлиента, ОткрытаБлокирующаяФорма) Экспорт
	
	Если СтруктураПараметровКлиента.Свойство("ИспользоватьНовыйАлгоритмПоискаПоШтрихкоду") Тогда
		РаботаСоШтрихкодамиКлиент.ОбработатьДанныеПоКоду(Форма, СтруктураПараметровКлиента, ОткрытаБлокирующаяФорма);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Обработка внешнего события формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма родитель.
//  Источник - ОбъектМетаданных - источник события.
//  Событие - Строка - имя внешнего события.
//  Данные - Структура - структура данных события.
//
Процедура ВнешнееСобытиеОборудования(Форма, Источник, Событие, Данные) Экспорт
	
	// Заменяем спец.символы, которые потом не сможем передать на сервер.
	РазделительGS1 = МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1();
	ЭкранированныйСимволGS1 = МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
	Данные = СтрЗаменить(Данные, РазделительGS1, ЭкранированныйСимволGS1);
	
	ОписаниеСобытия = Новый Структура();
	ОписаниеОшибки  = "";
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	
	Результат = МенеджерОборудованияКлиент.ПолучитьСобытиеОтУстройства(ОписаниеСобытия, ОписаниеОшибки);
	
	Если Результат = Неопределено Тогда 
		ТекстСообщения = НСтр("ru = 'При обработке внешнего события от устройства произошла ошибка:'")
							+ Символы.ПС + ОписаниеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		Если Результат.Источник = "ПодключаемоеОборудование" Тогда
			Если Результат.ИмяСобытия = "ScanData"
				И Найти(Форма.ПоддерживаемыеТипыПодключаемогоОборудования, "СканерШтрихкода") > 0 Тогда
				
				Если Результат.Параметр[1] = Неопределено Тогда
					ДанныеОповещения = Результат.Параметр[0];
				Иначе
					ДанныеОповещения = Результат.Параметр[1][1];
				КонецЕсли;
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПоискаПоШтрихкоду", Форма, Результат);
				ВыполнитьОбработкуОповещения(ОписаниеОповещения, ДанныеОповещения);
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработка ручного ввода штрихкода.
//
// Параметры:
//  Форма - УправляемаяФорма - форма родитель на которой вызвали команду ввода штрихкода.
//
Процедура ВвестиШтрихкод(Форма) Экспорт
	
	Штрихкод = "";
	ТекстЗаголовка = НСтр("ru = 'Введите штрихкод'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеПоискаПоШтрихкоду", Форма);
	
	ПоказатьВводСтроки(ОписаниеОповещения, Штрихкод, ТекстЗаголовка);
	
КонецПроцедуры

#КонецОбласти