
#Область ИнтеграцияИСМППереопределяемый

// Определяет использование актов о расхождении после приемки для документа
//
// Параметры:
//  Документ     - ДокументСсылка - документ, для которого необходимо определить возможность использования актов о расхождении.
//  Используется - Булево - в данный параметр необходимо установить признак использования актов, по умолчанию установлен
//                          в Ложь.
//
Процедура ОпределитьИспользованиеАктовОРасхожденииПослеПриемки(Документ, Используется) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		Используется = ПолучитьФункциональнуюОпцию("ИспользоватьКорректировкиПоступлений");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет переданную таблицу данные из ТЧ документа.
//
// Параметры:
//   Документ - ДокументСсылка - Документ из ТЧ которого будет происходить заполнение.
//   ТаблицаПродукции - ТаблицаЗначений - Таблица для заполнения данными из документа.
//   ВидыМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Массив из ПеречислениеСсылка.ВидыПродукцииИС - 
//     вид(ы) маркируемой продукции, которым(и) необходимо заполнить таблицу.
//
Процедура СформироватьТаблицуМаркируемойПродукцииДокумента(Документ, ТаблицаПродукции, ВидыМаркируемойПродукции) Экспорт
	
	ТекстЗапроса = ТекстЗапросаМаркируемойПродукции(Документ);
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидыМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаПродукции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Получение ссылки ТН ВЭД по коду.
//
// Параметры:
//  КодТНВЭД - Строка - Код по классификатору товарной номенклатуры внешнеэкономической деятельности.
//  ТНВЭД - Произвольный - искомый элемент.
//
Процедура КлассификаторТНВЭД(КодТНВЭД, ТНВЭД) Экспорт
	
	ТНВЭД = Справочники.КлассификаторТНВЭД.ПустаяСсылка();
	Если ЗначениеЗаполнено(КодТНВЭД) Тогда
		ТНВЭД = Справочники.КлассификаторТНВЭД.НайтиПоКоду(КодТНВЭД);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет свойства номенклатуры, используемые для передачи в каталог GS46. Могут быть заполнены колонки:
//   * Торговая марка,
//   * Страна производства,
//   * Вид обуви,
//   * Материал верха,
//   * Материал подкладки,
//   * Материал низа,
//   * Цвет,
//   * Размер.
// 
// Параметры:
//   Товары - ДанныеФормыКоллекция - таблица для заполнения.
//
Процедура ЗаполнитьСвойстваНоменклатурыДляКаталогаGS46(Товары) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.GTIN КАК GTIN
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.GTIN КАК GTIN,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(СправочникНоменклатура.КатегорияНоменклатуры) КАК ВидОбуви,
	|	ПРЕДСТАВЛЕНИЕ(ХарактеристикиНоменклатуры.Ссылка) КАК Размер,
	|	ПРЕДСТАВЛЕНИЕ(СправочникНоменклатура.Ссылка) КАК Наименование,
	|	СправочникНоменклатура.Производитель КАК ТорговаяМарка
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Товары.Характеристика = ХарактеристикиНоменклатуры.Ссылка";
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для Каждого СтрокаТовар Из Товары Цикл
		СтрокаТовар.Наименование = Справочники.Номенклатура.Представление(СтрокаТовар.Наименование, СтрокаТовар.Размер);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьПолученныеGTINКаталогаGS46(Товары) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.GTIN КАК GTIN
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.GTIN КАК Штрихкод,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО Товары.GTIN = Штрихкоды.Штрихкод
	|ГДЕ
	|	Штрихкоды.Штрихкод ЕСТЬ NULL";
	
	Набор = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
	СтрокаНабора = Набор.Добавить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Выборка);
		Набор.Отбор.Штрихкод.Установить(Выборка.Штрихкод, Истина);
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Предназначена для модификации текста запроса по расчету неоформленных документов ЭДО.
//   Сценарий использования: заменить текст запроса на требуемый (требующие оформления
//   с помощью ЭДО документы продажи с маркируемой продукцией).
//
// Параметры:
//  ТекстЗапроса - Строка - Текст запроса
//
Процедура ПриПолученииТекстаЗапросаНеоформленныхДокументовЭДО(ТекстЗапроса) Экспорт

	//Если ЭлектронноеВзаимодействиеИСМП.ВерсияАПИ() = 1 Тогда
	//	ТекстЗапроса = 
	//	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	//	| СостоянияЭД.СсылкаНаОбъект КАК Ссылка
	//	|ИЗ
	//	| РегистрСведений.СостоянияЭД КАК СостоянияЭД
	//	| ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.ШтрихкодыУпаковок КАК ШтрихкодыРеализации
	//	|  ПО СостоянияЭД.СсылкаНаОбъект = ШтрихкодыРеализации.Ссылка
	//	|ГДЕ
	//	| СостоянияЭД.СостояниеВерсииЭД = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсийЭД.НеСформирован)
	//	| И НЕ ШтрихкодыРеализации.Ссылка ЕСТЬ NULL
	//	| И (&БезОтбораПоОрганизации
	//	| ИЛИ СостоянияЭД.Организация В (&Организации))";
	//Иначе
	//	ТекстЗапроса = ТекстЗапроса +"
	//	| ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.ШтрихкодыУпаковок КАК ШтрихкодыРеализации
	//	|  ПО ДокументыТребующиеОформления.Ссылка = ШтрихкодыРеализации.Ссылка
	//	|ГДЕ
	//	| НЕ ШтрихкодыРеализации.Ссылка ЕСТЬ NULL";
	//КонецЕсли;
	//
	//Возврат;
	
	////////////////////////////////////////////////////////////////////////////////
	
	//ТекстЗапроса = 
	//"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	//|	СостоянияЭД.СсылкаНаОбъект КАК Ссылка
	//|ИЗ
	//|	РегистрСведений.СостоянияПоОбъектамУчетаЭДО КАК СостоянияЭД
	//|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная.ШтрихкодыУпаковок КАК ШтрихкодыРеализации
	//|		ПО СостоянияЭД.СсылкаНаОбъект = ШтрихкодыРеализации.Ссылка
	//|ГДЕ
	//|	СостоянияЭД.СостояниеЭДО = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсийЭД.НеСформирован)
	//|	И НЕ ШтрихкодыРеализации.Ссылка ЕСТЬ NULL
	//|	И (&БезОтбораПоОрганизации
	//|			ИЛИ СостоянияЭД.Организация В (&Организации))";
	
КонецПроцедуры

// Вызывается при вводе документа на основании, при выполнении метода Заполнить или при интерактивном вводе нового.
//
// Параметры:
//   ДокументОбъект - ДокументОбъект - заполняемый документ,
//   ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения,
//   ТекстЗаполнения - Строка, Неопределено - текст, используемый для заполнения документа,
//   СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ФильтрВидПродукции = Неопределено;
	Основание = Неопределено;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ДанныеЗаполнения.Свойство("Основание", Основание);
		ДанныеЗаполнения.Свойство("Организация", ДокументОбъект.Организация);
		Если ДанныеЗаполнения.Свойство("ИмяФильтра") И ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеСВидомПродукции" Тогда
			ФильтрВидПродукции = ДанныеЗаполнения.ЗначениеФильтра;
		КонецЕсли;
	Иначе
		Основание = ДанныеЗаполнения;
		Если ЗначениеЗаполнено(ДокументОбъект.ВидПродукции) Тогда
			ФильтрВидПродукции = ДокументОбъект.ВидПродукции;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокументаОснования = Основание.Метаданные();
	МетаданныеДокументаИСМП = ДокументОбъект.Ссылка.Метаданные();
	
	ПараметрыФормированияЗапроса = ИнтеграцияИСУНФ.ПараметрыФормированияЗапросаЗаполнения(МетаданныеДокументаОснования, МетаданныеДокументаИСМП);
	ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля = Истина;
	Если ЗначениеЗаполнено(ФильтрВидПродукции) Тогда
		ПараметрыФормированияЗапроса.ОтборНоменклатуры = ИнтеграцияИСУНФ.ОпределениеВидаПродукцииТекстаЗапроса("СправочникНоменклатура", ФильтрВидПродукции);
	КонецЕсли;
	
	ЗаполнитьПараметрыФормированияЗапроса(ПараметрыФормированияЗапроса);
	
	ТипОснования = ТипЗнч(Основание);
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратВОборотИСМП") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ЗаполнитьВозвратВОборотИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ЗаполнитьВозвратВОборотИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЧекККМВозврат") Тогда
			ЗаполнитьВозвратВОборотИСМПНаОснованииЧекаККМВозврат(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВыводИзОборотаИСМП") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииЗаказаПокупателя(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.КомплектацияЗапасов") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииКомплектацииЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииРасходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.СборкаЗапасов") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииСборкиЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.СписаниеЗапасов") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииСписанияЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЧекККМ") Тогда
			ЗаполнитьВыводИзОборотаИСМПНаОснованииЧекаККМ(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаЭмиссиюКодовМаркировкиСУЗ") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.ЗаказНаПроизводство")Тогда
			ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаНаПроизводство(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказПоставщику")Тогда
			ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
		// Документ не содержит упаковок.
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			СтрокаТовары.КоличествоУпаковок = СтрокаТовары.Количество;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.МаркировкаТоваровИСМП") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.ИнвентаризацияЗапасов")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииИнвентаризацииЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.КомплектацияЗапасов")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииКомплектацияЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетОПереработке")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииОтчетОПереработке(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетПереработчика")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииОтчетПереработчика(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПриходнаяНакладная")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.СборкаЗапасов")Тогда
			ЗаполнитьМаркировкаТоваровИСМПНаОснованииСборкиЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтгрузкаТоваровИСМП") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.РасходнаяНакладная")Тогда
			ЗаполнитьОтгрузкаТоваровИСМПНаОснованииРасходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПеремаркировкаТоваровИСМП") Тогда
		
		Если ТипОснования = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ЗаполнитьПеремаркировкаТоваровИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЧекККМВозврат")Тогда
			ЗаполнитьПеремаркировкаТоваровИСМПНаОснованииЧекаККМВозврат(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыФормированияЗапроса);
	
	Если ФильтрВидПродукции = Неопределено Тогда
		ИнтеграцияИС.ЗаполнитьВидПродукцииПоТабличнойЧасти(ДокументОбъект);
	Иначе
		ДокументОбъект.ВидПродукции = ФильтрВидПродукции;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается расширением формы при необходимости проверки заполнения реквизитов при записи или при проведении документа
// в форме, а также при выполнении метода ПроверитьЗаполнение.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проверяемый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  ПроверяемыеРеквизиты - Массив - массив путей к реквизитам, для которых будет выполнена проверка заполнения,
//  МассивНепроверяемыхРеквизитов - Массив - массив путей к реквизитам, для которых не будет выполнена проверка заполнения.
Процедура ПриОпределенииОбработкиПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаЭмиссиюКодовМаркировкиСУЗ")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.МаркировкаТоваровИСМП")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВыводИзОборотаИСМП")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеКодовМаркировкиИСМП")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтгрузкаТоваровИСМП")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.УточнениеСведенийОКодахМаркировкиИСМП") Тогда
		
		ИнтеграцияВЕТИС.ПроверитьЗаполнениеКоличества(ДокументОбъект, Отказ, МассивНепроверяемыхРеквизитов);
		ИнтеграцияИСУНФ.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, "Товары", Отказ);
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПеремаркировкаТоваровИСМП") Тогда
		
		ИнтеграцияИСУНФ.ПроверитьЗаполнениеХарактеристик(ДокументОбъект, МассивНепроверяемыхРеквизитов, "Товары", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииСкладаДокументаОснования(Склад, ДокументОснование) Экспорт
	
	СкладОснования      = Справочники.СтруктурныеЕдиницы;
	МетаданныеОснования = ДокументОснование.Метаданные();
	
	Если МетаданныеОснования.Реквизиты.Найти("СтруктурнаяЕдиница") <> Неопределено Тогда
		СкладОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "СтруктурнаяЕдиница");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СкладОснования) Тогда
		
		ИменаТабличныхЧастей = Новый Массив();
		ИменаТабличныхЧастей.Добавить("Запасы");
		ИменаТабличныхЧастей.Добавить("Продукция");
		
		Для Каждого ИмяТабличнойЧасти Из ИменаТабличныхЧастей Цикл
			МетаданныеТабличнойЧасти = МетаданныеОснования.ТабличныеЧасти.Найти(ИмяТабличнойЧасти);
			Если МетаданныеТабличнойЧасти <> Неопределено Тогда
				Если МетаданныеТабличнойЧасти.Реквизиты.Найти("СтруктурнаяЕдиница") <> Неопределено Тогда
					ТекстЗапроса =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	СтруктурнаяЕдиница
					|ИЗ
					|	Документ." + МетаданныеОснования.Имя + "." + ИмяТабличнойЧасти + "
					|ГДЕ
					|	СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)";
					Запрос = Новый Запрос(ТекстЗапроса);
					Выборка = Запрос.Выполнить().Выбрать();
					
					Если Выборка.Следующий() Тогда
						СкладОснования = Выборка.СтруктурнаяЕдиница;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СкладОснования) Тогда
		Склад = СкладОснования;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКодыТНВЭДПоНоменклатуреВТабличнойЧасти(ТабличнаяЧасть) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура
	|ПОМЕСТИТЬ вт_Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Товары.НомерСтроки КАК НомерСтроки,
	|	вт_Товары.Номенклатура,
	|	вт_Товары.Номенклатура.ТоварнаяНоменклатураВЭД.Код КАК КодТНВЭД
	|ИЗ
	|	вт_Товары КАК вт_Товары
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Товары", ТабличнаяЧасть.Выгрузить(, "НомерСтроки, Номенклатура"));
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТабличнаяЧасть[Выборка.НомерСтроки - 1].КодТНВЭД = Выборка.КодТНВЭД;
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет признак использования производства на стороне.
//
// Параметры:
//  Используется - Булево - Признак использования производства на стороне.
Процедура ИспользуетсяПереработкаНаСтороне(Используется) Экспорт

	Используется = ПолучитьФункциональнуюОпцию("ПередачаСырьяВПереработку");

КонецПроцедуры

// Заполняет таблицу маркированный товаров по выбранным документам.
//
// Параметры:
//   Запрос - Запрос - запрос, в котором требуется сформировать временную таблицу.
//   ИсточникОснований - Строка - Имя временной таблицы с колонкой "ДокументОснование".
//   СтандартнаяОбработка - Булево - Необходимость обработки события "по-умолчанию" (установить Ложь при переопределении).
//
Процедура СформироватьТаблицуМаркированныхТоваровОснований(Запрос, ИсточникОснований, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ТекстЗапроса =
	"
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Продукция КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.СборкаЗапасов.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.СписаниеЗапасов.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.РасходнаяНакладная.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ЧекККМ.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ЧекККМВозврат.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка,
	|	Товары.Ссылка.Организация,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ОбувнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|		КОГДА СправочникНоменклатура.ТабачнаяПродукция
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА СправочникНоменклатура.ОбувнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.Обувь)
	|	КОГДА СправочникНоменклатура.ТабачнаяПродукция ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ТабачнаяПачка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт),
	|	Товары.Количество
	|ИЗ
	|	Документ.ОприходованиеЗапасов.Запасы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %1 КАК Основания
	|		ПО Основания.ДокументОснование = Товары.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ОбувнаяПродукция
	|	ИЛИ СправочникНоменклатура.ТабачнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПредварительно.Ссылка             КАК ДокументОснование,
	|	ТоварыПредварительно.Организация        КАК Организация,
	|	ТоварыПредварительно.Номенклатура       КАК Номенклатура,
	|	ТоварыПредварительно.Характеристика     КАК Характеристика,
	|	ТоварыПредварительно.ВидПродукции       КАК ВидПродукции,
	|	ТоварыПредварительно.Шаблон             КАК Шаблон,
	|	ТоварыПредварительно.СпособВводаВОборот КАК СпособВводаВОборот,
	|	СУММА(ТоварыПредварительно.Количество)  КАК Количество
	|ПОМЕСТИТЬ МаркированныеТоварыОснований
	|ИЗ
	|	ТоварыПредварительно КАК ТоварыПредварительно
	|СГРУППИРОВАТЬ ПО
	|	ТоварыПредварительно.Ссылка,
	|	ТоварыПредварительно.Организация,
	|	ТоварыПредварительно.Номенклатура,
	|	ТоварыПредварительно.Характеристика,
	|	ТоварыПредварительно.ВидПродукции,
	|	ТоварыПредварительно.Шаблон,
	|	ТоварыПредварительно.СпособВводаВОборот
	|";
	
	Запрос.Текст = Запрос.Текст + СтрШаблон(ТекстЗапроса, ИсточникОснований);
	
КонецПроцедуры

Процедура ЯвляетсяОснованиемДляМаркировкиОстатков(СсылкаНаДокумент, ЯвляетсяОснованием) Экспорт
	
	Если ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(СсылкаНаДокумент, "ИнвентаризацияЗапасов") Тогда
		
		ЯвляетсяОснованием = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Ищет по коду элементы в справочнике Классификатор ТН ВЭД.
// Если их нет, то создает элементы справочника в соответствии с классификатором ТН ВЭД ЕАЭС.
// Если кода нет в классификаторе, то создает элемент справочника по данным ГИС МТ.
//
// Параметры:
//  Код - Строка - Строка с кодом классификатора ТН ВЭД.
//  Наименование - Строка - Наименование элемента классификатора ТН ВЭД по данным ГИС МТ.
//
// Возвращаемое значение:
//  СправочникСсылка.КлассификаторТНВЭД - ссылка на элемент классификатора или Неопределено,
//                                        если не удалось определить единицу измерения для нового элемента.
Функция ПриОпределенииСопоставленногоКлассификатораТНВЭД(КодТНВЭД, Наименование) Экспорт
	
	ЭлементСправочника = Справочники.КлассификаторТНВЭД.НайтиСоздатьЭлементКлассификатораТНВЭД(КодТНВЭД);
	
	Если ЭлементСправочника <> Неопределено Тогда
		Возврат ЭлементСправочника;
	КонецЕсли;
	
	ДлинаКода = СтрДлина(КодТНВЭД);
	ЕдиницаИзмеренияКод = Неопределено;
	
	МассивКодовДляОпределенияЕдиницыИзмерения = Новый Массив;
	
	Для Счетчик = 1 По ДлинаКода-1 Цикл
		МассивКодовДляОпределенияЕдиницыИзмерения.Добавить(Лев(КодТНВЭД, Счетчик));
	КонецЦикла;
	
	ТаблицаКлассификатора = Справочники.КлассификаторТНВЭД.ТаблицаКлассификатораТНВЭД();
	ТаблицаКлассификатора.Сортировать("Код");
	
	// Определить единицу измерения по данным подобных кодов.
	НачальныйИндекс = 0;
	Для Каждого СтрокаКлассификатора Из ТаблицаКлассификатора Цикл
		
		НайденыСовпаденияВСтроке = Ложь;
		Для Индекс = НачальныйИндекс По МассивКодовДляОпределенияЕдиницыИзмерения.ВГраница() Цикл
			
			Если Лев(СтрокаКлассификатора.Код, Индекс + 1) = МассивКодовДляОпределенияЕдиницыИзмерения[Индекс] Тогда
				ЕдиницаИзмеренияКод = СтрокаКлассификатора.ЕдиницаИзмерения;
				НайденыСовпаденияВСтроке = Истина;
				Если Индекс > НачальныйИндекс Тогда
					НачальныйИндекс = Индекс;
				КонецЕсли;
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НачальныйИндекс > 0 И Не НайденыСовпаденияВСтроке Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕдиницаИзмеренияКод = Неопределено Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		СправочникОбъект = Справочники.КлассификаторТНВЭД.СоздатьЭлемент();
		
		СправочникОбъект.Наименование       = Наименование;
		СправочникОбъект.НаименованиеПолное = Наименование;
		СправочникОбъект.Код                = КодТНВЭД;
		
		ЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ЕдиницаИзмеренияПоКоду(ЕдиницаИзмеренияКод);
		Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			СправочникОбъект.ЕдиницаИзмерения = ЕдиницаИзмерения;
		КонецЕсли;
		
		СправочникОбъект.Записать();
		
		Возврат СправочникОбъект.Ссылка;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СобытияФормИСМППереопределяемый

// Серверная переопределяемая процедура, вызываемая при заполнении реквизитов формы созданных ИСМП (при открытии)
//
// Параметры:
//   Форма - УправляемаяФорма - форма, из которой происходит вызов процедуры.
//
Процедура ЗаполнениеРеквизитовФормы(Форма) Экспорт
	
	Если СтрНачинаетсяС(Форма.ИмяФормы,"Документ.ЧекККМ.")
		Или СтрНачинаетсяС(Форма.ИмяФормы,"Документ.ЧекККМВозврат.")
		Или СтрНачинаетсяС(Форма.ИмяФормы,"Справочник.ШаблоныЭтикетокИЦенников.") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыИнтеграции = Неопределено;
	
	Для Каждого ВидПродукции Из ИнтеграцияИСКлиентСервер.ВидыПродукцииИСМП(Истина) Цикл
		ПараметрыИнтеграции = Форма.ПараметрыИнтеграцииГосИС.Получить(ВидПродукции);
		Если ПараметрыИнтеграции <> Неопределено Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыИнтеграции = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыИнтеграции.ИспользоватьКолонкуСтатусаПроверкиПодбора Тогда
		
		Объект = ПараметрыИнтеграции.ИмяРеквизитаФормыОбъект;
		Товары = ПараметрыИнтеграции.ИмяТабличнойЧастиТовары;
		
		Если Форма.ИмяФормы = "Документ.РасходнаяНакладная.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.ПриходнаяНакладная.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.КорректировкаРеализации.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.ПередачаТоваровМеждуОрганизациями.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.ОтчетОПереработке.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.ОтчетПереработчика.Форма.ФормаДокумента"
			ИЛИ Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаЗаказНаряда" Тогда
			ИменаРеквизитов = "МаркируемаяПродукция, ВидПродукцииИС, КоэффициентЕдиницыИзмеренияГосИС, КоличествоВБазовыхЕдиницахГосИС";
		Иначе
			ИменаРеквизитов = "";
		КонецЕсли;
		Если ПараметрыИнтеграции.ДоступныОбъемноСортовыеКоды Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", АвтоматическийОСУИС";
		КонецЕсли;
		СлужебныеРеквизиты = Новый Структура(ИменаРеквизитов);
		
		ИнтеграцияИСУНФ.ЗаполнитьСлужебныеРеквизитыВКоллекции(Форма, Форма[Объект][Товары], СлужебныеРеквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

// Переопределение параметров интеграции ИСМП (расположения команды проверки и подбора)
//
// Параметры:
//   Форма                - УправляемаяФорма - прикладная форма для встраивания форматированной строки
//   СтандартнаяОбработка - Булево - стандартная работа с элементами проверки подбора
//
Процедура ПриОпределенииПараметровИнтеграции(Форма, СтандартнаяОбработка) Экспорт
	
	Если Форма.ИмяФормы = "Справочник.ШаблоныЭтикетокИЦенников.Форма.ФормаРедактированияШаблонаЭтикетокИЦенников" Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента_РМК"
		Или Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ЧекККМВозврат.Форма.ФормаДокумента" Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Переопределение параметров интеграции ИСМП (расположения форматированной строки перехода к связанному объекту)
//
// Параметры:
//   Форма            - УправляемаяФорма - прикладная форма для встраивания форматированной строки
//   ПараметрыНадписи - Структура        - (см. СобытияФормИСМП.ПараметрыИнтеграцииГиперссылкиИСМП)
//
Процедура ПриОпределенииПараметровИнтеграцииГиперссылкиИСМП(Форма, ПараметрыНадписи) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект")
		И ТипЗнч(Форма.Объект) = Тип("ДанныеФормыСтруктура")
		И ИнтеграцияИСМП.ИспользуетсяИнтеграцияВФормеДокументаОснования(Форма, Форма.Объект) Тогда
		
		ПараметрыНадписи.Вставить("ИмяЭлементаФормы",  "ТекстДокументаИСМП");
		ПараметрыНадписи.Вставить("ИмяРеквизитаФормы", "ТекстДокументаИСМП");
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаСостояние") Тогда
		ПараметрыНадписи.РазмещениеВ = "ГруппаСостояние";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииЧтенииНаСервере(Форма) Экспорт
	
	ИнтеграцияИСУНФ.ПриСозданииЧтенииНаСервере(Форма);
	
КонецПроцедуры

// Серверная переопределяемая процедура, вызываемая из обработчика события элемента.
//
// Параметры:
//   Форма                   - УправляемаяФорма - форма, из которой происходит вызов процедуры.
//   Элемент                 - Строка           - имя элемента-источника события "При изменении"
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
Процедура ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	Если Форма.ИмяФормы = "Документ.РасходнаяНакладная.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ПриходнаяНакладная.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.КорректировкаРеализации.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ПередачаТоваровМеждуОрганизациями.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ОтчетОПереработке.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ОтчетПереработчика.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаЗаказНаряда" Тогда
		
		ИнтеграцияИСУНФ.ПрименитьКешШтрихкодовУпаковок(Форма, Истина);
		
	ИначеЕсли Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента_РМК"
		Или Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ЧекККМВозврат.Форма.ФормаДокумента" Тогда
			
		Если Элемент = "Событие" И СтрНачинаетсяС(ДополнительныеПараметры.ИмяСобытия,"ЗакрытиеФормыПроверкиИПодбораГосИС") Тогда
			
			ИндексВидаПродукции     = Число(СтрЗаменить(ДополнительныеПараметры.ИмяСобытия, "ЗакрытиеФормыПроверкиИПодбораГосИС", ""));
			ВидМаркируемойПродукции = ИнтеграцияИСКлиентСервер.ИндексВидаПродукцииИС(ИндексВидаПродукции);
			
			ПриЗакрытииФормыПроверкиИПодбораВФормеРМК(
				Форма,
				ДополнительныеПараметры.Параметр,
				ВидМаркируемойПродукции);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении номенклатуры в строке табличной части.
//
// Параметры:
//  Форма                  - УправляемаяФорма - форма, в которой произошло событие,
//  ТекущаяСтрока          - ДанныеФормыЭлементКоллекции - редактируемая строка таблицы,
//  КэшированныеЗначения   - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - Структура - (См. ПроверкаИПодборПродукцииМОТП.ПараметрыУказанияСерий).
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	Если Форма.ИмяФормы = "Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ПроверкаИПодбор"
		ИЛИ Форма.ИмяФормы = "Обработка.ПодготовкаСведенийВКаталогGS46.Форма.Форма" Тогда
		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ДанныеНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрока.Номенклатура, "ТипНоменклатуры, ИспользоватьХарактеристики");
			ДанныеНоменклатуры.Вставить("ХарактеристикиИспользуются", ДанныеНоменклатуры.ИспользоватьХарактеристики);
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ДанныеНоменклатуры);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	// Завершаем процедуру, если вызов обработчика выполнен после завершения обмена данными в документе ВыводИзОборотаИСМП
	// В этом случае элемент формы СтраницаТовары находится в режиме ТолькоПросмотр
	Если ЭтоЗавершениеОбменаДаннымиВыводИзОборотаИСМП(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Неопределено;
	ИмяТабличнойЧасти = "";
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Запасы") Тогда
			ИмяТабличнойЧасти = "Запасы";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Товары") Тогда
			ИмяТабличнойЧасти = "Товары";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Продукция") Тогда
			ИмяТабличнойЧасти = "Продукция";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяТабличнойЧасти) Тогда
		НоменклатураВДокументахСервер.ЗаполнитьДанныеВСтрокеТабличнойЧасти(Форма.Объект, ИмяТабличнойЧасти, ТекущаяСтрока);
	КонецЕсли;
	
	СлужебныеРеквизиты = Новый Структура;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ТребуетВзвешивания") Тогда
		СлужебныеРеквизиты.Вставить("ТребуетВзвешивания");
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ПроизвольнаяЕдиницаУчета") Тогда
		СлужебныеРеквизиты.Вставить("ПроизвольнаяЕдиницаУчета");
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "СкоропортящаясяПродукция") Тогда
		СлужебныеРеквизиты.Вставить("СкоропортящаясяПродукция", ТекущаяСтрока.СкоропортящаясяПродукция);
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "АвтоматическийОСУИС") Тогда
		СлужебныеРеквизиты.Вставить("АвтоматическийОСУИС", ТекущаяСтрока.АвтоматическийОСУИС);
	КонецЕсли;
	
	Если СлужебныеРеквизиты.Количество() Тогда
		ИнтеграцияИСУНФКлиентСервер.ЗаполнитьСлужебныеРеквизитыСтроки(Форма, ТекущаяСтрока, СлужебныеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении подобранного количества в строке таблицы формы.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
Процедура ПриИзмененииКоличества(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено) Экспорт

	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "Упаковка") Тогда
		Возврат;
	КонецЕсли;

	Если ТекущаяСтрока.Количество = 0 Тогда
		ТекущаяСтрока.КоличествоУпаковок = 0;
	Иначе
		Коэффициент = ?(ТипЗнч(ТекущаяСтрока.Упаковка) = Тип("СправочникСсылка.ЕдиницыИзмерения"),
			УправлениеНебольшойФирмойВызовСервера.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Упаковка, "Коэффициент"), 1);
		Если Коэффициент <> 0 Тогда
			ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество / Коэффициент;
		Иначе
			ТекстИсключения = НСтр(
				"ru = 'При попытке пересчета количества в %ЕдИзмерения% превышена допустимая разрядность.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ЕдИзмерения%", ТекущаяСтрока.Упаковка);

			ТекущаяСтрока.Количество = 0;
			ТекущаяСтрока.КоличествоУпаковок = 0;
			ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка");

			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет табличную часть Товары подобранными товарами.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой производится подбор,
//  ВыбранноеЗначение - Произвольный - данные, содержащие подобранную пользователем номенклатуру,
Процедура ОбработкаРезультатаПодбораНоменклатуры(Форма, ВыбранноеЗначение, ПараметрыЗаполнения, КэшированныеЗначения, ДобавленныеСтроки) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеЗначение.Свойство("АдресТоваровВХранилище") Тогда
		АдресТоваровВХранилище = ВыбранноеЗначение.АдресТоваровВХранилище
	Иначе
		АдресТоваровВХранилище = ВыбранноеЗначение.АдресКорзиныВХранилище
	КонецЕсли;
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	ТекущаяСтрока = Неопределено;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Форма.Объект.Товары.Добавить();
		
		СписокСвойств = "Номенклатура, Характеристика";
		Если ТекущаяСтрока.Свойство("Количество") Тогда
			СписокСвойств = СписокСвойств + ", Количество";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, СписокСвойств);
		
		Если ТекущаяСтрока.Свойство("КоличествоУпаковок") Тогда
			ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.Количество;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Форма.Элементы.Товары.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает параметры выбора шаблона этикетки.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой нужно установить параметры выбора,
//  ИмяПоляВвода - Строка - имя поля ввода шаблона этикетки.
Процедура УстановитьПараметрыВыбораШаблонаЭтикетки(Форма,  ИмяПоляВвода) Экспорт
	
	ПараметрыВыбора = ОбщегоНазначения.СкопироватьРекурсивно(Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора, Ложь);
	
	ТипШаблона = ПредопределенноеЗначение("Перечисление.ТипыШаблонов.ЭтикеткаКодМаркировкиИСМП");
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТипШаблона", ТипШаблона));
	
	Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Форма.Элементы[ИмяПоляВвода].КнопкаСоздания = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаИПодборПродукцииИСМППереопределяемый

// Возвращает через третий параметр признак наличия маркируемой продукции.
//
// Параметры:
//  Коллекция                - ДанныеФормыКоллекция - ТЧ с товарами.
//  ВидМаркируемойПродукции  - ПеречислениеСсылка.ВидыПродукцииИС - вид продукции, наличие которой необходимо определить.
//  ЕстьМаркируемаяПродукция - Булево - Исходящий, признак наличия маркируемой продукции.
Процедура ЕстьМаркируемаяПродукцияВКоллекции(Коллекция, ВидМаркируемойПродукции, ЕстьМаркируемаяПродукция) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ
	|	ВремТаблТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьМаркируемаяПродукция
	|ИЗ
	|	ВремТаблТовары КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	&УсловиеМаркируемаяПродукция
	|";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаТовары", Коллекция.Выгрузить(, "Номенклатура"));
	Результат = Запрос.Выполнить();
	
	ЕстьМаркируемаяПродукция = НЕ Результат.Пустой();
	
КонецПроцедуры

// Возвращает через третий параметр таблицу товаров документа, являющихся маркируемой продукцией требуемого вида.
//
// Параметры:
//  * Контекст                 - УправляемаяФорма, ДокументСсылка - документ, маркируемую продукцию которого необходимо получить.
//  * ВидМаркируемойПродукции  - ПеречислениеСсылка.ВидыПродукцииИС - вид маркируемой продукции, которую необходимо получить.
//  * ТаблицаМаркируемойПродукции - ТаблицаЗначений - Исходящий, таблица с маркируемой продукцией документа. Должна
//                                                    содержать колонки:
//  ** GTIN           - Строка - GTIN продукции
//  ** Номенклатура   - ОпределяемыйТип.Номенклатура - номенклатура маркируемой продукции
//  ** Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатура маркируемой продукции
//  ** Серия          - ОпределяемыйТип.СерияНоменклатуры - серия номенклатура маркируемой продукции
//  ** Количество     - Число - количество маркируемой продукции
Процедура ПриОпределенииМаркируемойПродукцииДокумента(Контекст, ВидМаркируемойПродукции, ТаблицаМаркируемойПродукции) Экспорт
	
	Если ТаблицаМаркируемойПродукции.Количество() > 0 Тогда
		РезультатЗапроса = ЗапросGTINпоТаблицеМаркируемойПродукции(ТаблицаМаркируемойПродукции);
		ТаблицаМаркируемойПродукции.Очистить();
	ИначеЕсли ТипЗнч(Контекст) = Тип("ДанныеФормыСтруктура") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииДанныеФормыСтруктура(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ПриходнаяНакладная") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииПриходнаяНакладная(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ЧекККМ")
		Или ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ЧекККМВозврат") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииЧекККМ(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "РасходнаяНакладная") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииРасходнаяНакладная(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "КорректировкаРеализации") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииКорректировкаРеализации(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ПередачаТоваровМеждуОрганизациями") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииПередачаТоваровМеждуОрганизациями(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "СборкаЗапасов") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииСборкаЗапасов(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ОтчетОПереработке") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииОтчетОПереработке(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ОтчетПереработчика") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииОтчетПереработчика(Контекст, ВидМаркируемойПродукции);
	ИначеЕсли ИнтеграцияИСМПУНФКлиентСервер.ЭтоДокументПоНаименованию(Контекст, "ЗаказПокупателя") Тогда
		РезультатЗапроса = ЗапросМаркируемойПродукцииЗаказПокупателя(Контекст, ВидМаркируемойПродукции);
	Иначе
		Возврат;
	КонецЕсли;
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаХарактеристика.Следующий() Цикл
			ПродукцияПоGTIN = ТаблицаМаркируемойПродукции.СкопироватьКолонки();
			СписокКодовGTIN = Новый Массив;
			
			ВыборкаGTIN = ВыборкаХарактеристика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаGTIN.Следующий() Цикл
				
				Если Не ЗначениеЗаполнено(ВыборкаНоменклатура.Номенклатура) Тогда
					// Соответственно тут должен быть остаточный GTIN
					Выборка = ВыборкаGTIN.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ТаблицаМаркируемойПродукции.Добавить(), Выборка);
					КонецЦикла;
					Продолжить;
				КонецЕсли;
				
				Если ПродукцияПоGTIN.Количество() = 0 Тогда
					Выборка = ВыборкаGTIN.Выбрать();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ПродукцияПоGTIN.Добавить(), Выборка,, "GTIN");
					КонецЦикла;
				КонецЕсли;
				
				Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(ВыборкаGTIN.GTIN) Тогда
					GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(ВыборкаGTIN.GTIN);
					СписокКодовGTIN.Добавить(GTIN);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого СтрокаПродукцииПоGTIN Из ПродукцияПоGTIN Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаМаркируемойПродукции.Добавить(), СтрокаПродукцииПоGTIN);
				СтрокаПродукцииПоGTIN.КодыGTIN.ЗагрузитьЗначения(СписокКодовGTIN);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает через второй параметр признак что переданный контрагент является нерезидентом.
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты - Проверяемы контрагент
//  НеРезидент - Булево - Признак что контрагент не резидент (Истина) или резидент (Ложь).
//
Процедура ПриОпределенииКонтрагентНеРезидент(Контрагент, НеРезидент) Экспорт
	
	СтранаРегистрации = Контрагент.СтранаРегистрации;
	
	НеРезидент = (СтранаРегистрации <> Справочники.СтраныМира.Россия);
	
КонецПроцедуры

// Устанавливает режим просмотра (доступности, для команд) элементам формы.
//   Переопределение необходимо использовать для совместной работы с аналогичными механизмами.
//   Обработанные здесь реквизиты следует удалить из массива "Блокируемые элементы".
// 
// Параметры:
//  Форма               - УправляемаяФорма - форма в которой производится изменение доступности
//  БлокируемыеЭлементы - Массив - наименования реквизитов
//  Заблокировать       - Булево - заблокировать или разблокировать реквизиты
Процедура УстановитьТолькоПросмотрЭлементов(
		Форма,
		БлокируемыеЭлементы,
		Заблокировать) Экспорт
	
	
	Если ТипЗнч(БлокируемыеЭлементы) = Тип("Массив") Тогда
		
		Для Каждого ИмяЭлемента Из БлокируемыеЭлементы Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "Доступность", Не Заблокировать);
		КонецЦикла
		
	Иначе
		
		Если ТипЗнч(БлокируемыеЭлементы) = Тип("Строка") Тогда
			СтруктураИменЭлементов = Новый Структура(БлокируемыеЭлементы);
		Иначе
			СтруктураИменЭлементов = БлокируемыеЭлементы;
		КонецЕсли;
		
		Для Каждого ИмяЭлемента Из СтруктураИменЭлементов Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента.Ключ, "Доступность", Не Заблокировать);
		КонецЦикла
		
	КонецЕсли;
	
	БлокируемыеЭлементы = Новый Массив;
	
КонецПроцедуры

// Обрабатывает таблицу штриховых кодов и получает для каждого из них данные в информационной базе.
//   На входе имеется таблица, в которой заполнены только колонки "Номер строки", "Штриховой код" и "Количество",
//   опционально заполнена колонка "Родитель".
//   В процедуре заполняется допустимый штрихкод упаковки из справочника или признак новой (неизвестной) упаковки.
//
// Параметры:
//  ТаблицаНеАкцизныеМарки - ТаблицаЗначение - имеет следующие колонки:
//   * ШтриховойКод        - Строка                              - штриховой код полученный с ТСД.
//   * Количество          - Число                               - сколько раз был считан данный штрихкод.
//   * ШтрихКодУпаковки    - Справочник.ШтрихкодыУпаковокТоваров - ссылка на имеющуюся в базе упаковку.
//   * Родитель            - Строка                              - штрихкод внешней упаковки.
//   * НомерСтроки         - Число                               - номер строки таблицы
//   * ЭтоУпаковка         - Булево                              - признак новой упаковки
//
Процедура РаспознатьШтрихкоды(ТаблицаНеАкцизныеМарки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтриховыеКоды.ШтриховойКод КАК ШтриховойКод,
	|	ШтриховыеКоды.Количество КАК Количество,
	|	ШтриховыеКоды.НомерСтроки КАК НомерСтроки,
	|	ШтриховыеКоды.Родитель КАК Родитель
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	&ШтриховыеКоды КАК ШтриховыеКоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Штрихкоды.ШтриховойКод КАК ШтриховойКод,
	|	Штрихкоды.Количество КАК Количество,
	|	Штрихкоды.Родитель КАК Родитель,
	|	Штрихкоды.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ШтрихкодыУпаковокТоваров.Ссылка, НЕОПРЕДЕЛЕНО) КАК ШтрихКодУпаковки,
	|	ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL
	|		И ШтрихкодыУпаковокТоваров.Ссылка ЕСТЬ NULL КАК ЭтоУпаковка
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО Штрихкоды.ШтриховойКод = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО Штрихкоды.ШтриховойКод = ШтрихкодыНоменклатуры.Штрихкод
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ШтриховыеКоды", ТаблицаНеАкцизныеМарки);
	
	ТаблицаНеАкцизныеМарки = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Вызывается при завершении проверки и подбора для отражения данных в документе.
// Предназначена для сохранения связей номенклатуры с GTIN, с учетом коэффициентов групповых упаковок. 
// 
// Параметры:
//  ТаблицаОписанияGTIN - см. ПроверкаИПодборПродукцииИСМП.ПустаяТаблицаОписанияGTIN.
Процедура ЗафиксироватьОписаниеGTIN(ТаблицаОписанияGTIN) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаДляЗапроса = ТаблицаОписанияGTIN.Скопировать();
	
	ТаблицаДляЗапроса.Колонки.Добавить("EAN", Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры.Измерения.Штрихкод.Тип);
	
	Для Каждого СтрокаТаблицы Из ТаблицаДляЗапроса Цикл
		
		СтрокаТаблицы.EAN = ШтрихкодированиеИСКлиентСервер.ШтрихкодEANИзGTIN(СтрокаТаблицы.GTIN);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.EAN,
	|	ИсходнаяТаблица.Номенклатура,
	|	ИсходнаяТаблица.Характеристика,
	|	ИсходнаяТаблица.Упаковка
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.EAN            КАК Штрихкод,
	|	ИсходнаяТаблица.Номенклатура   КАК Номенклатура,
	|	ИсходнаяТаблица.Характеристика КАК Характеристика,
	|	ИсходнаяТаблица.Упаковка       КАК Упаковка
	|ИЗ
	|	ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ИсходнаяТаблица.EAN = ШтрихкодыНоменклатуры.Штрихкод
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ТаблицаДляЗапроса);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
		
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаДетальныеЗаписи);
		
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

#Область РасчетСтатусовОформленияИСМППереопределяемый

// Позволяет переопределить имена реквизитов документа-основания для документа ИСМП.
//
// Параметры:
//  МетаданныеДокументаОснования - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.Основание<Имя документа ИСМП>
//  МетаданныеДокументаИСМП - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.ДокументыИСМППоддерживающиеСтатусыОформления
//  Реквизиты  - Структура - имена реквизитов:
//  * Ключ  - служебное имя реквизита в ИСМП
//  * Значение - имя реквизита документа-основания, которое при необходимости надо переопределить
//  (см. РасчетСтатусовОформленияИСМП.СтруктураРеквизитовДляРасчетаСтатусаОформленияДокументов).
Процедура ПриОпределенииИменРеквизитовДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, Реквизиты) Экспорт
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.СписаниеЗапасов
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.СборкаЗапасов
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.ИнвентаризацияЗапасов
		ИЛИ МетаданныеДокументаОснования = Метаданные.Документы.КомплектацияЗапасов Тогда
		Реквизиты.Удалить("Ответственный");
	КонецЕсли;
	
	// Уточним имена реквизитов для конкретных документов при необходимости.
	Если МетаданныеДокументаОснования = Метаданные.Документы.ПроизводственнаяОперацияВЕТИС
		Или МетаданныеДокументаОснования = Метаданные.Документы.ВходящаяТранспортнаяОперацияВЕТИС Тогда
		Реквизиты.Контрагент = "";
	КонецЕсли;
	
КонецПроцедуры

// Позволяет переопределить текст запроса выборки данных из документов-основания для расчета статуса оформления.
//   Требования к тексту запроса:
//     Если данные из документа выбирать не требуется, то данному параметру надо присвоить значение "" (пустая строка).
//     Результат запроса обязательно должен содержать следующие поля:
//      * Ссылка - ОпределяемыйТип.Основание<Имя документа ИСМП> - ссылка на документ-основание
//      * ЭтоДвижениеПриход - Булево - вид движения ТМЦ (Истина - приход, Ложь - расход)
//      * Номенклатура - ОпределяемыйТип.Номенклатура - номенклатура
//      * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - характеристика номенклатуры
//      * Серия - ОпределяемыйТип.СерияНоменклатуры - серия номенклатуры
//      * Количество - Число - количество номенклатуры в ее основной единице измерения
//     В результат запроса нужно включать только подконтрольную номенклатуру ИСМП (табак, обувь)
//     Для отбора документов-основания в запросе нужно использовать отбор "В (&МассивДокументов)"
//     Выбранные данные необходимо поместить во временную таблицу (См. РасчетСтатусовОформленияИСМП.ИмяВременнойТаблицыДляВыборкиДанныхДокумента).
//
// Параметры:
//   МетаданныеДокументаОснования - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.Основание<Имя документа ИСМП>
//   МетаданныеДокументаИСМП - ОбъектМетаданных - метаданные документа из ОпределяемыйТип.ДокументыИСМППоддерживающиеСтатусыОформления
//   ТекстЗапроса - Строка - текст запроса выборки данных, который надо переопределить
//   ДополнительныеПараметрыЗапроса - Структура - дополнительные параметры запроса, требуемые для выполнения запроса 
//       конкретного документа; при необходимости можно дополнить данную структуру
//     Ключ     - имя параметры
//     Значение - значение параметра.
//
Процедура ПриОпределенииТекстаЗапросаДляРасчетаСтатусаОформления(
			Знач МетаданныеДокументаОснования, Знач МетаданныеДокументаИСМП, ТекстЗапроса, ДополнительныеПараметрыЗапроса) Экспорт
	
	ПараметрыФормированияЗапроса = ИнтеграцияИСУНФ.ПараметрыФормированияЗапросаЗаполнения(МетаданныеДокументаОснования, МетаданныеДокументаИСМП);
	ЗаполнитьПараметрыФормированияЗапроса(ПараметрыФормированияЗапроса);
	Если ПараметрыФормированияЗапроса.ТабличныеЧасти.Количество() Тогда
		ТекстЗапроса = ИнтеграцияИСУНФ.ТекстЗапросаТоварыДокументаОснования(ПараметрыФормированияЗапроса, ДополнительныеПараметрыЗапроса);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ШтрихкодированиеИСМППереопределяемый

// Подготавливает структуру для печати этикеток
// 
Процедура СтруктураНастроекЭтикеткаОбувь(СтруктураНастроек) Экспорт
	
	ОбязательныеПоля = Новый Массив;
	
	ОбязательныеПоля.Добавить("Номенклатура");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		ОбязательныеПоля.Добавить("Характеристика");
	КонецЕсли;
	ОбязательныеПоля.Добавить("ШаблонЭтикетки");
	ОбязательныеПоля.Добавить("Количество");
	ОбязательныеПоля.Добавить("НомерВГруппе");
	ОбязательныеПоля.Добавить("СодержимоеКоличество");
	ОбязательныеПоля.Добавить("КодМаркировки");
	
	СтруктураНастроек.Вставить("ОбязательныеПоля", ОбязательныеПоля);
	
КонецПроцедуры

// По таблице с колонками Номенклатура и Характеристика формирует массив GTIN 
//   и соответствие конкретных кодов GTIN номенклатуре/характеристике
// 
// Параметры:
//   ТаблицаПроверки  - ТаблицаЗначений - входящий, проверяемые данные с колонками
//    * Номенклатура   - ОпределяемыйТип.Номенклатура
//    * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры
//   ПроверяемыеGTIN  - Массив          - исходящий, все GTIN привязанные к одной из строк таблицы
//   СоответствиеGTIN - Соответствие    - исходящий, пара (GTIN - (Номенклатура,Характеристика))
Процедура ЗаполнитьПроверяемыеGTIN(ТаблицаПроверки, ПроверяемыеGTIN, СоответствиеGTIN) Экспорт
	
	СоответствиеGTIN = Новый Соответствие;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО Товары.Номенклатура = Штрихкоды.Номенклатура
	|			И Товары.Характеристика = Штрихкоды.Характеристика");
	
	Запрос.УстановитьПараметр("Товары", ТаблицаПроверки);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(Выборка.Штрихкод) Тогда
			СоответствиеGTIN.Вставить(Выборка.Штрихкод, 
				Новый Структура("Номенклатура,Характеристика", Выборка.Номенклатура, Выборка.Характеристика));
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из СоответствиеGTIN Цикл
		ПроверяемыеGTIN.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПечатьЭтикетокИСМППереопределяемый

// В данной процедуре определяется метод печати этикеток ИС МП
// 
// Параметры:
// 	ТаблицаДляПечати - Массив - Массив строк таблицы (см. РегистрыСведений.ПулКодовМаркировкиСУЗ.НоваяТаблицаДанныхДляПечатиЭтикеток)
// 	ТабличныйДокумент - ТабличныйДокумент - результат печати
// 	СтруктураНастроек - Структура - Дополнительные параметры для печати
// 	СтандартнаяОбработка - Булево - Признак использования библиотечной печати
Процедура ПечатьЭтикетокИСМП(ТаблицаДляПечати, ТабличныйДокумент, СтруктураНастроек, СтандартнаяОбработка) Экспорт
	
	Если ТаблицаДляПечати.Колонки.Найти("ЭтоКодМаркировки") = Неопределено Тогда
		ТаблицаДляПечати.Колонки.Добавить("ЭтоКодМаркировки");
		ТаблицаДляПечати.ЗаполнитьЗначения(Истина, "ЭтоКодМаркировки");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронноеВзаимодействиеИСМППереопределяемый

// При записи документа ЭДО может измениться статус оформления документа, по которому происходит электронный документооборот:
//   * Для прямого обмена с ИС МП (документ "Отгрузка товаров") указание кодов маркировки требуется или в ЭДО, или в отгрузке.
//   * Подразумевается, что документ ЭДО не может изменить свой документ-основание.
//
// Параметры:
//  ЭлектронныйДокументИсходящийОбъект - ДокументОбъект.ЭлектронныйДокументИсходящий - записываемый документ.
//  ДокументыТребующиеПересчета - Массив Из см. РасчетСтатусовОформленияИСМП.РассчитатьСтатусыОформленияДокументов - 
//   документы, связанные с записываемым электронным, для которых требуется пересчитать статус оформления.
//
Процедура ТребуетсяПересчетСтатусовОформления(ЭлектронныйДокументИсходящийОбъект, ДокументыТребующиеПересчета) Экспорт

	ЭлектронныеДокументы = Новый Массив;
	ЭлектронныеДокументы.Добавить(ЭлектронныйДокументИсходящийОбъект.Ссылка);
	Основания = ИнтеграцияЭДО.ОбъектыУчетаЭлектронныхДокументов(ЭлектронныеДокументы);
	Основания.Колонки.ОбъектУчета.Имя = "ДокументОснование";
	
	ДокументыПоТипам = Новый Соответствие;
	Для Каждого Строка Из Основания Цикл
		ТипОснования = ТипЗнч(Строка.ДокументОснование);
		Элемент = ДокументыПоТипам.Получить(ТипОснования);
		Если Элемент = Неопределено Тогда
			Элемент = Новый Массив;
		КонецЕсли;
		Элемент.Добавить(Строка.ДокументОснование);
		ДокументыПоТипам.Вставить(ТипОснования, Элемент);
	КонецЦикла;
	
	СчетаФактуры = ДокументыПоТипам.Получить(Тип("ДокументСсылка.СчетФактура"));
	Если СчетаФактуры<>Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", СчетаФактуры);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураДокументыОснования.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	Документ.СчетФактура.ДокументыОснования КАК СчетФактураДокументыОснования
		|ГДЕ
		|	СчетФактураДокументыОснования.Ссылка В(&Ссылка)";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ТипОснования = ТипЗнч(Выборка.ДокументОснование);
			Элемент = ДокументыПоТипам.Получить(ТипОснования);
			Если Элемент = Неопределено Тогда
				Элемент = Новый Массив;
			КонецЕсли;
			Элемент.Добавить(Выборка.ДокументОснование);
			ДокументыПоТипам.Вставить(ТипОснования, Элемент);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.ОснованиеОтгрузкаТоваровИСМП.Тип.Типы() Цикл
		ДокументыТипа = ДокументыПоТипам.Получить(Тип);
		Если ДокументыТипа<>Неопределено Тогда
			ДокументыТребующиеПересчета.Добавить(ДокументыТипа);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСлужебныеРеквизитыИСМПВСтрокеТЧ(СтрокаТабличнойЧасти) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТабличнойЧасти, "МаркируемаяПродукция")
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТабличнойЧасти, "ВидПродукцииИС") Тогда
		
		СтруктураДанные = Новый Структура("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСлужебныеРеквизитыИСМП(СтруктураДанные);
		
		СписокСвойств = "МаркируемаяПродукция, ВидПродукцииИС";
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураДанные, СписокСвойств);
		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТабличнойЧасти, "АвтоматическийОСУИС") Тогда
		ПроверкаИПодборПродукцииИСМП.ЗаполнитьПризнакАвтоматическийОСУИСВСтроке(СтрокаТабличнойЧасти);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыИСМП(СтруктураДанные) Экспорт
	
	МаркируемаяПродукция = Ложь;
	ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ПустаяСсылка();
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтруктураДанные, "Номенклатура")
		И ЗначениеЗаполнено(СтруктураДанные.Номенклатура) Тогда
		
		ДанныеНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураДанные.Номенклатура,
			"ВидПродукцииИС, ВидАлкогольнойПродукции, ПродаетсяВРозлив");
		
		Если СтруктураДанные.Свойство("ДатаОбработки") И ЗначениеЗаполнено(СтруктураДанные.ДатаОбработки) Тогда
			ДатаПроверки = СтруктураДанные.ДатаОбработки;
		Иначе
			ДатаПроверки = ТекущаяДатаСеанса();
		КонецЕсли;
		
		ВидПродукцииИС       = ДанныеНоменклатуры.ВидПродукцииИС;
		МаркируемаяПродукция = ИнтеграцияИСМПКлиентСерверПовтИсп.ОбязательнаяРегистрацияОборотаМаркируемойПродукции(ДанныеНоменклатуры.ВидПродукцииИС, ДатаПроверки);
		Если НЕ МаркируемаяПродукция И ЗначениеЗаполнено(ДанныеНоменклатуры.ВидАлкогольнойПродукции) Тогда
			МаркируемаяПродукция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеНоменклатуры.ВидАлкогольнойПродукции, "Маркируемый")
				И Не ДанныеНоменклатуры.ПродаетсяВРозлив;
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураДанные.Вставить("МаркируемаяПродукция", МаркируемаяПродукция);
	СтруктураДанные.Вставить("ВидПродукцииИС",       ВидПродукцииИС);
	
КонецПроцедуры

//Возвращает сформированный ранее Акт о расхождениях для переданного документа.
//
//Параметры:
//   ДокументОснование - ДокументСсылка - ссылка на документ, для которого необходимо получить Акт о расхождениях.
//
//Возвращаемое значение:
//   ДокументСсылка.АктОРасхождениях, Неопределено - Акт о расхождениях (если он есть).
//
Функция СформированныйАктОРасхождениях(ДокументОснование) Экспорт
	
	АктОРасхождениях      = Неопределено;
	ТипДокументаОснования = ТипЗнч(ДокументОснование);
	
	Если ТипДокументаОснования <> Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		Возврат АктОРасхождениях;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АктОРасхождениях.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АктОРасхождениях КАК АктОРасхождениях
	|ГДЕ
	|	НЕ АктОРасхождениях.ПометкаУдаления
	|	И АктОРасхождениях.ДокументОснование = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОРасхождениях.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	АктОРасхождениях.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		АктОРасхождениях = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат АктОРасхождениях;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Заменяет не полные данные по кодам маркировки для передачи в ККТ.
// 
// Параметры:
// 	Коллекция - ТаблицаЗначений - Таблица штрихкодов.
Процедура УстановитьШтрихкодДляПечатиККТ(Коллекция) Экспорт
	
	ПолныйКодМОТП = Новый Соответствие();
	
	Для Каждого СтрокаКоллекции Из Коллекция Цикл
		
		Если СтрокаКоллекции.ВидПродукции = Перечисления.ВидыПродукцииИС.Табак
			И СтрокаКоллекции.ВидУпаковки = Перечисления.ВидыУпаковокИС.Потребительская
			Или СтрокаКоллекции.ВидПродукции = Перечисления.ВидыПродукцииИС.АльтернативныйТабак
			И СтрокаКоллекции.ТипШтрихкода = Перечисления.ТипыШтрихкодов.DataMatrix Тогда
			
			Если СтрокаКоллекции.ДанныеРазбора = Неопределено
				Или (СтрокаКоллекции.ДанныеРазбора.СоставКодаМаркировки.Свойство("ВключаетМРЦ")
				 И СтрокаКоллекции.ДанныеРазбора.СоставКодаМаркировки.ВключаетМРЦ) Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыНормализацииКМ = РазборКодаМаркировкиИССлужебныйКлиентСервер.НастройкиРазбораКодаМаркировкиДляСохраненияВПул();
			НормализованныйКод = РазборКодаМаркировкиИССлужебныйКлиентСервер.НормализоватьКодМаркировки(
				СтрокаКоллекции.ДанныеРазбора, СтрокаКоллекции.ВидПродукции, ПараметрыНормализацииКМ);
			
			ПолныйКодМОТП.Вставить(НормализованныйКод, СтрокаКоллекции);
			СтрокаКоллекции.ТребуетсяПолныйКод = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПолныйКодМОТП.Количество() > 0 Тогда 
		
		УстановитьШтрихкодДляПечатиККТ(ПолныйКодМОТП);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуШтрихкодовИзДереваУпаковокРекурсивно(ТаблицаПриемник, ДеревоУпаковок) Экспорт
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		Если СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Логистическая Тогда
			ЗаполнитьТаблицуШтрихкодовИзДереваУпаковокРекурсивно(ТаблицаПриемник, СтрокаДерева);
			Продолжить;
		ИначеЕсли Не ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(СтрокаДерева.ВидПродукции, Истина) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
		НоваяСтрока.Количество         = 1;
		НоваяСтрока.КоличествоУпаковок = 0;
		
		Если СтрокаДерева.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
			
			НоваяСтрока.Приоритет          = 5;
			НоваяСтрока.КоличествоУпаковок = 1;
			Если ИнтеграцияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДерева.ВидПродукции) Тогда
				НоваяСтрока.Количество = СтрокаДерева.КоличествоПачек;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеНаОсновании

#Область ЗаказНаЭмиссию

Процедура ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаНаПроизводство(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	ДокументСсылка.СтруктурнаяЕдиница КАК ПроизводственныйОбъект,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Производство) КАК СпособВводаВОборот,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьЗаказНаЭмиссиюКодовМаркировкиСУЗНаОснованииЗаказаПоставщику(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	ДокументСсылка.СтруктурнаяЕдиница КАК ПроизводственныйОбъект,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.СтранаРегистрации.УчастникЕАЭС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.Импорт)
	|	КОНЕЦ КАК СпособВводаВОборот,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратВОборот

Процедура ЗаполнитьВозвратВОборотИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВозвратВОборотПриРозничнойРеализации) КАК Операция
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВозвратВОборотИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВозвратВОборотПриРозничнойРеализации) КАК Операция
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Если Основание.ШтрихкодыУпаковок.Количество() Тогда
		// Распределим штрихкоды упаковок на Товары.
		ДанныеИСМП = РозничныеПродажиСервер.ДанныеДляИСМП(Основание);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеИСМП.Колонки, "ШтрихкодУпаковки") Тогда
			ДанныеИСМП.Колонки.ШтрихкодУпаковки.Имя = "КодМаркировки";
		КонецЕсли;
		ДанныеИСМП.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка." + Основание.Метаданные().Имя));
		ДанныеИСМП.ЗаполнитьЗначения(Основание, "Ссылка");
		
		ПараметрыФормированияЗапроса.ТабличныеЧасти[0].ТаблицаТовары = ДанныеИСМП;
	КонецЕсли;
	
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
	ДокументПродажи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ДокументОснование");
	Если ЗначениеЗаполнено(ДокументПродажи) И ТипЗнч(ДокументПродажи) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		ДанныеДокументаПродажи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПродажи, "Номер, Дата");
		Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
			СтрокаТовары.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументовИСМП.ТоварныйЧек;
			СтрокаТовары.ПервичныйДокумент = ДокументПродажи;
			СтрокаТовары.НомерПервичногоДокумента = ДанныеДокументаПродажи.Номер;
			СтрокаТовары.ДатаПервичногоДокумента  = ДанныеДокументаПродажи.Дата;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВозвратВОборотИСМПНаОснованииЧекаККМВозврат(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВозвратВОборотПриРозничнойРеализации) КАК Операция
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Если Основание.АкцизныеМарки.Количество() Тогда
		// Распределим штрихкоды упаковок на Товары.
		ДанныеИСМП = РозничныеПродажиСервер.ДанныеДляИСМП(Основание);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеИСМП.Колонки, "ШтрихкодУпаковки") Тогда
			ДанныеИСМП.Колонки.ШтрихкодУпаковки.Имя = "КодМаркировки";
		КонецЕсли;
		ДанныеИСМП.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка." + Основание.Метаданные().Имя));
		ДанныеИСМП.ЗаполнитьЗначения(Основание, "Ссылка");
		
		ПараметрыФормированияЗапроса.ТабличныеЧасти[0].ТаблицаТовары = ДанныеИСМП;
	КонецЕсли;
	
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
	// Заполним дополнительные поля.
	ДанныеПервичногоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Номер, Дата");
	Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
		СтрокаТовары.ВидПервичногоДокумента = Перечисления.ВидыПервичныхДокументовИСМП.КассовыйЧек;
		СтрокаТовары.ПервичныйДокумент = Основание;
		СтрокаТовары.НомерПервичногоДокумента = ДанныеПервичногоДокумента.Номер;
		СтрокаТовары.ДатаПервичногоДокумента  = ДанныеПервичногоДокумента.Дата;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВыводИзОборота

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииРасходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(ФискальныеОперации.НомерЧекаККМ, ДокументСсылка.Номер) КАК НомерПервичногоДокумента,
	|	ЕСТЬNULL(ФискальныеОперации.Дата, ДокументСсылка.Дата) КАК ДатаПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику)
	|				И Контрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаВозвратФизическомуЛицу)
	|		КОГДА НЕ ФискальныеОперации.НомерЧекаККМ ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа)
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА ВЫБОР
	|					КОГДА Контрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаДистанционнаяПродажа)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия)
	|				КОНЕЦ
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортВСтраныЕАЭС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортЗаПределыСтранЕАЭС)
	|	КОНЕЦ КАК Операция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Контрагенты.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ДокументСсылка.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА НЕ ФискальныеОперации.НомерЧекаККМ ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек)
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА ВЫБОР
	|					КОГДА Контрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.ТоварнаяНакладная)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.УПД)
	|	КОНЕЦ КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.РасходнаяНакладная КАК ДокументСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО (ФискальныеОперации.ДокументОснование = &ДокументОснование)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО (Контрагенты.Ссылка = ДокументСсылка.Контрагент)
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииСборкиЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее) КАК ВидПервичногоДокумента,
	|	ДокументСсылка.Дата КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Номер КАК НомерПервичногоДокумента
	|ИЗ
	|	Документ.СборкаЗапасов КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииСписанияЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаУтратаПовреждениеТовара) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее) КАК ВидПервичногоДокумента,
	|	ДокументСсылка.Дата КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Номер КАК НомерПервичногоДокумента
	|ИЗ
	|	Документ.СписаниеЗапасов КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииЧекаККМ(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерПервичногоДокумента,
	|	ФискальныеОперации.Дата КАК ДатаПервичногоДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ЧекККМ КАК ДокументСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО (ФискальныеОперации.ДокументОснование = &ДокументОснование)
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииКомплектацииЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияЗапасов.Разборка)
	|				И ДокументСсылка.Номенклатура.Весовой
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаФасовка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия)
	|	КОНЕЦ КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.Прочее) КАК ВидПервичногоДокумента,
	|	ДокументСсылка.Дата КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Номер КАК НомерПервичногоДокумента
	|ИЗ
	|	Документ.КомплектацияЗапасов КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьВыводИзОборотаИСМПНаОснованииЗаказаПокупателя(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерПервичногоДокумента,
	|	ФискальныеОперации.Дата КАК ДатаПервичногоДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа) КАК Операция,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПервичныхДокументовИСМП.КассовыйЧек) КАК ВидПервичногоДокумента
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ДокументСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО (ФискальныеОперации.ДокументОснование = &ДокументОснование)
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область МаркировкаТоваров

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииИнвентаризацииЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИнвентаризацияЗапасов.Ссылка КАК ДокументОснование,
	|	ИнвентаризацияЗапасов.Организация КАК Организация,
	|	ИнвентаризацияЗапасов.СтруктурнаяЕдиница КАК Склад,
	|	ЛОЖЬ КАК ЕстьОшибкиПроведен,
	|	Значение(Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков) КАК Операция
	|ИЗ
	|	Документ.ИнвентаризацияЗапасов КАК ИнвентаризацияЗапасов
	|ГДЕ
	|	ИнвентаризацияЗапасов.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииКомплектацияЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КомплектацияЗапасов.Ссылка КАК ДокументОснование,
	|	КомплектацияЗапасов.Организация КАК Организация,
	|	ИСТИНА КАК ОперацияНанесенияТолькоДляНаборов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ) КАК Операция,
	|	НЕ КомплектацияЗапасов.Проведен КАК ЕстьОшибкиПроведен,
	|	КомплектацияЗапасов.Дата КАК ДатаПроизводства
	|ИЗ
	|	Документ.КомплектацияЗапасов КАК КомплектацияЗапасов
	|ГДЕ
	|	КомплектацияЗапасов.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииОтчетОПереработке(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтчетОПереработке.Ссылка КАК ДокументОснование,
	|	ОтчетОПереработке.Организация КАК Организация,
	|	ОтчетОПереработке.СтруктурнаяЕдиница КАК Склад,
	|	НЕ ОтчетОПереработке.Проведен КАК ЕстьОшибкиПроведен,
	|	ОтчетОПереработке.Дата КАК ДатаПроизводства,
	|	ОтчетОПереработке.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговору) КАК Операция
	|ИЗ
	|	Документ.ОтчетОПереработке КАК ОтчетОПереработке
	|ГДЕ
	|	ОтчетОПереработке.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииОтчетПереработчика(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтчетПереработчика.Ссылка КАК ДокументОснование,
	|	ОтчетПереработчика.Организация КАК Организация,
	|	ОтчетПереработчика.СтруктурнаяЕдиница КАК Склад,
	|	НЕ ОтчетПереработчика.Проведен КАК ЕстьОшибкиПроведен,
	|	ОтчетПереработчика.Дата КАК ДатаПроизводства,
	|	ОтчетПереработчика.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговоруНаСторонеЗаказчика) КАК Операция
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПолучениеПродукцииОтФизическихЛиц)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДокументСсылка.Контрагент.СтранаРегистрации.УчастникЕАЭС
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС)
	|			КОНЕЦ
	|	КОНЕЦ КАК Операция,
	|	ДокументСсылка.Контрагент.СтранаРегистрации КАК СтранаПроисхождения,
	|	ДокументСсылка.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

Процедура ЗаполнитьМаркировкаТоваровИСМПНаОснованииСборкиЗапасов(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ) КАК Операция,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ДокументСсылка.Дата КАК ДатаПроизводства
	|ИЗ
	|	Документ.СборкаЗапасов КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОтчетПроизводственнойЛинии")
		И ДокументОбъект.Товары.Количество() Тогда
		// Данные по товарам хранятся в шапке документа.
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ДокументОбъект.Товары[0], "Номенклатура, Характеристика, Серия, КодТНВЭД");
		ДокументОбъект.Товары.Очистить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтгрузкаТоваров

Процедура ЗаполнитьОтгрузкаТоваровИСМПНаОснованииРасходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	ДокументСсылка.Контрагент КАК Контрагент,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ДокументСсылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтгрузкаКомиссия)
	|		КОГДА НЕ ДокументСсылка.Контрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|				И ЕСТЬNULL(ДокументСсылка.Контрагент.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтгрузкаЕАЭССПризнаниемКИ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ОтгрузкаДляСобственныхНуждПокупателя)
	|	КОНЕЦ КАК Операция,
	|	ДокументСсылка.Контрагент.СтранаРегистрации КАК СтранаНазначения,
	|	ДокументСсылка.Номер КАК НомерПервичногоДокумента,
	|	ДокументСсылка.Дата КАК ДатаПервичногоДокумента,
	|	ДокументСсылка.Комментарий КАК Комментарий,
	|	ДокументСсылка.Дата КАК ДатаОтгрузки,
	|	ДокументСсылка.Дата КАК ДатаВыводаИзОборота
	|ИЗ
	|	Документ.РасходнаяНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область ПеремаркировкаТоваров

Процедура ЗаполнитьПеремаркировкаТоваровИСМПНаОснованииПриходнойНакладной(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка           КАК ДокументОснование,
	|	ДокументСсылка.Организация      КАК Организация,
	|	НЕ ДокументСсылка.Проведен      КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ПриходнаяНакладная КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование
	|");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Если Основание.ШтрихкодыУпаковок.Количество() Тогда
		// Распределим штрихкоды упаковок на Товары.
		ДанныеИСМП = РозничныеПродажиСервер.ДанныеДляИСМП(Основание);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеИСМП.Колонки, "ШтрихкодУпаковки") Тогда
			ДанныеИСМП.Колонки.ШтрихкодУпаковки.Имя = "КодМаркировки";
		КонецЕсли;
		ДанныеИСМП.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка." + Основание.Метаданные().Имя));
		ДанныеИСМП.ЗаполнитьЗначения(Основание, "Ссылка");
		
		ПараметрыФормированияЗапроса.ТабличныеЧасти[0].ТаблицаТовары = ДанныеИСМП;
	КонецЕсли;
	
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
	// Заполним дополнительные поля.
	Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
		СтрокаТовары.НоваяНоменклатура = СтрокаТовары.Номенклатура;
		СтрокаТовары.НоваяХарактеристика = СтрокаТовары.Характеристика;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПеремаркировкаТоваровИСМПНаОснованииЧекаККМВозврат(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументСсылка.Ссылка КАК ДокументОснование,
	|	ДокументСсылка.Организация КАК Организация,
	|	НЕ ДокументСсылка.Проведен КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ДокументСсылка
	|ГДЕ
	|	ДокументСсылка.Ссылка = &ДокументОснование");
	
	ИнтеграцияИСУНФ.ЗаполнитьШапкуДокумента(ДокументОбъект, ДанныеЗаполнения, Запрос);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Основание = ДанныеЗаполнения.Основание;
	Иначе
		Основание = ДанныеЗаполнения;
	КонецЕсли;
	
	Если Основание.АкцизныеМарки.Количество() Тогда
		// Распределим штрихкоды упаковок на Товары.
		ДанныеИСМП = РозничныеПродажиСервер.ДанныеДляИСМП(Основание);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеИСМП.Колонки, "ШтрихкодУпаковки") Тогда
			ДанныеИСМП.Колонки.ШтрихкодУпаковки.Имя = "КодМаркировки";
		КонецЕсли;
		ДанныеИСМП.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка." + Основание.Метаданные().Имя));
		ДанныеИСМП.ЗаполнитьЗначения(Основание, "Ссылка");
		
		ПараметрыФормированияЗапроса.ТабличныеЧасти[0].ТаблицаТовары = ДанныеИСМП;
	КонецЕсли;
	
	ИнтеграцияИСУНФ.ЗаполнитьТабличныеЧастиДокументаИС(ДокументОбъект, ДанныеЗаполнения, ПараметрыФормированияЗапроса);
	
	// Заполним дополнительные поля.
	Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
		СтрокаТовары.НоваяНоменклатура = СтрокаТовары.Номенклатура;
		СтрокаТовары.НоваяХарактеристика = СтрокаТовары.Характеристика;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыФормированияЗапроса)
	
	ИмяТабличнойЧасти = ПараметрыФормированияЗапроса.ИмяТабличнойЧастиТоварыИС;
	
	ТЧТовары = ПараметрыФормированияЗапроса.МетаданныеДокументаИСМП.ТабличныеЧасти.Найти(ИмяТабличнойЧасти);
	Если ТЧТовары = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТЧТовары.Реквизиты.Найти("Серия") = Неопределено
		ИЛИ ТЧТовары.Реквизиты.Найти("СтатусУказанияСерий") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из ДокументОбъект[ИмяТабличнойЧасти] Цикл
		ИмяФормы = "Документ." + ПараметрыФормированияЗапроса.МетаданныеДокументаИСМП.Имя + ".Форма.ФормаДокумента";
		ИнтеграцияИСМПУНФВызовСервера.ПроверитьСериюРассчитатьСтатус(СтрокаТЧ, ИмяФормы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеМаркируемойПродукцииДокумента

Функция ЗапросGTINпоТаблицеМаркируемойПродукции(ТаблицаМаркируемойПродукции) Экспорт
	
	ПоляВЫБРАТЬ = Новый Массив;
	ПоляБезGTIN = Новый Массив;
	Для Каждого Колонка Из ТаблицаМаркируемойПродукции.Колонки Цикл
		Если Колонка.Имя <> "КодыGTIN"
				И Колонка.Имя <> "ИдентификаторыПроисхожденияВЕТИС" Тогда
			ПоляВЫБРАТЬ.Добавить(СтрШаблон("МаркируемаяПродукция.%1 КАК %1", Колонка.Имя));
			Если Колонка.Имя <> "GTIN" Тогда
				ПоляБезGTIN.Добавить(СтрШаблон("МаркируемаяПродукция.%1 КАК %1", Колонка.Имя));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ПоляВЫБРАТЬ
	|ПОМЕСТИТЬ ВТМаркируемаяПродукция
	|ИЗ
	|	&МаркируемаяПродукция КАК МаркируемаяПродукция
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	&ПоляБезGTIN
	|ИЗ
	|	ВТМаркируемаяПродукция КАК МаркируемаяПродукция
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО МаркируемаяПродукция.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|		 И МаркируемаяПродукция.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ГДЕ
	|	МаркируемаяПродукция.Номенклатура <> Значение(Справочник.Номенклатура.ПустаяСсылка)
	|	И МаркируемаяПродукция.GTIN = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Номенклатура не заполнена только для строк с остаточными GTIN
	|	&ПоляВЫБРАТЬ
	|ИЗ
	|	ВТМаркируемаяПродукция КАК МаркируемаяПродукция
	|ГДЕ
	|	МаркируемаяПродукция.Номенклатура = Значение(Справочник.Номенклатура.ПустаяСсылка)
	|	И МаркируемаяПродукция.GTIN <> """"
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МаркируемаяПродукция", ТаблицаМаркируемойПродукции);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляВЫБРАТЬ", СтрСоединить(ПоляВЫБРАТЬ, ",
	|"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляБезGTIN", СтрСоединить(ПоляБезGTIN, ",
	|"));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииДанныеФормыСтруктура(ДанныеФормыСтруктура, ВидМаркируемойПродукции)
	
	Если ТипЗнч(ДанныеФормыСтруктура.Ссылка) = Тип("ДокументСсылка.ЧекККМ")
		ИЛИ ТипЗнч(ДанныеФормыСтруктура.Ссылка) = Тип("ДокументСсылка.ЧекККМВозврат")
		ИЛИ ТипЗнч(ДанныеФормыСтруктура.Ссылка) = Тип("ДокументСсылка.ПриходнаяНакладная")
		ИЛИ ТипЗнч(ДанныеФормыСтруктура.Ссылка) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		
		ДанныеФормыКоллекцияТовары = ДанныеФормыСтруктура["Запасы"];
		ДанныеФормыКоллекцияСерии = ДанныеФормыСтруктура["СерииНоменклатуры"];
		
		Результат = ЗапросМаркируемойПродукцииДанныеФормыКоллекцияССоединениемСерий(ДанныеФормыКоллекцияТовары, ДанныеФормыКоллекцияСерии, ВидМаркируемойПродукции);
		
	Иначе
		
		ДанныеФормыКоллекцияТовары = ДанныеФормыСтруктура["Товары"];
		ДанныеФормыКоллекцияСерии = Неопределено;
		
		Результат = ЗапросМаркируемойПродукцииДанныеФормыКоллекция(ДанныеФормыКоллекцияТовары, ВидМаркируемойПродукции);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЗапросМаркируемойПродукцииДанныеФормыКоллекция(ДанныеФормыКоллекция, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ВТ_Товары.Серия КАК Серия,
	|	СУММА(ВТ_Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_МаркируемаяПродукция
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	&УсловиеМаркируемаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Товары.Номенклатура,
	|	ВТ_Товары.Характеристика,
	|	ВТ_Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	МаркируемаяПродукция.Номенклатура КАК Номенклатура,
	|	МаркируемаяПродукция.Характеристика КАК Характеристика,
	|	МаркируемаяПродукция.Серия КАК Серия,
	|	МаркируемаяПродукция.Количество КАК Количество
	|ИЗ
	|	ВТ_МаркируемаяПродукция КАК МаркируемаяПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО МаркируемаяПродукция.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И МаркируемаяПродукция.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Товары", ДанныеФормыКоллекция.Выгрузить());
	
	Возврат Запрос.Выполнить();	
	
КонецФункции

Функция ЗапросМаркируемойПродукцииДанныеФормыКоллекцияССоединениемСерий(ДанныеФормыКоллекцияТовары, ДанныеФормыКоллекцияСерии, ВидМаркируемойПродукции)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Товары.КлючСвязи КАК КлючСвязи,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Серия КАК Серия,
	|	Серии.КлючСвязи КАК КлючСвязи,
	|	Серии.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Номенклатура КАК Номенклатура,
	|	ВТ_Товары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Товары.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ТоварыСерии
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Товары.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Товары.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыСерии.Характеристика КАК Характеристика,
	|	ВТ_ТоварыСерии.Серия КАК Серия,
	|	СУММА(ВТ_ТоварыСерии.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_МаркируемаяПродукция
	|ИЗ
	|	ВТ_ТоварыСерии КАК ВТ_ТоварыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ТоварыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	&УсловиеМаркируемаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТоварыСерии.Номенклатура,
	|	ВТ_ТоварыСерии.Характеристика,
	|	ВТ_ТоварыСерии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	МаркируемаяПродукция.Номенклатура КАК Номенклатура,
	|	МаркируемаяПродукция.Характеристика КАК Характеристика,
	|	МаркируемаяПродукция.Серия КАК Серия,
	|	МаркируемаяПродукция.Количество КАК Количество
	|ИЗ
	|	ВТ_МаркируемаяПродукция КАК МаркируемаяПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО МаркируемаяПродукция.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И МаркируемаяПродукция.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Товары", ДанныеФормыКоллекцияТовары.Выгрузить());
	Запрос.УстановитьПараметр("Серии", ДанныеФормыКоллекцияСерии.Выгрузить());
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииПриходнаяНакладная(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	ПриходнаяНакладнаяЗапасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПриходнаяНакладнаяЗапасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриходнаяНакладнаяСерииНоменклатуры.Серия КАК Серия,
	|	ПриходнаяНакладнаяСерииНоменклатуры.Количество КАК Количество,
	|	ПриходнаяНакладнаяСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ПриходнаяНакладная.СерииНоменклатуры КАК ПриходнаяНакладнаяСерииНоменклатуры
	|ГДЕ
	|	ПриходнаяНакладнаяСерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросМаркируемойПродукцииЧекККМ(ФормаОбъект, ВидМаркируемойПродукции)
	
	Если ТипЗнч(ФормаОбъект) = Тип("ФормаКлиентскогоПриложения") Тогда
		Запасы = ФормаОбъект.Объект.Запасы;
		Серии = ФормаОбъект.Объект.СерииНоменклатуры;
	Иначе
		Запасы = ФормаОбъект.Запасы;
		Серии = ФормаОбъект.Серии;
	КонецЕсли;
	
	Возврат ЗапросМаркируемойПродукцииДанныеФормыКоллекцияССоединениемСерий(Запасы, Серии, ВидМаркируемойПродукции);
	
КонецФункции

Функция ЗапросМаркируемойПродукцииРасходнаяНакладная(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	РасходнаяНакладнаяЗапасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РасходнаяНакладнаяЗапасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	РасходнаяНакладнаяЗапасы.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходнаяНакладнаяСерииНоменклатуры.Серия КАК Серия,
	|	РасходнаяНакладнаяСерииНоменклатуры.Количество КАК Количество,
	|	РасходнаяНакладнаяСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.РасходнаяНакладная.СерииНоменклатуры КАК РасходнаяНакладнаяСерииНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииКорректировкаРеализации(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Количество КАК Количество,
	|	Запасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.КорректировкаРеализации.Запасы КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Запасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Запасы.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СерииНоменклатуры.Серия КАК Серия,
	|	СерииНоменклатуры.Количество КАК Количество,
	|	СерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.КорректировкаРеализации.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииПередачаТоваровМеждуОрганизациями(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Количество КАК Количество,
	|	Запасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Запасы КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Запасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Запасы.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СерииНоменклатуры.Серия КАК Серия,
	|	СерииНоменклатуры.Количество КАК Количество,
	|	СерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииСборкаЗапасов(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СборкаЗапасовПродукция.Номенклатура КАК Номенклатура,
	|	СборкаЗапасовПродукция.Характеристика КАК Характеристика,
	|	СборкаЗапасовПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СборкаЗапасовПродукция.Количество КАК Количество,
	|	СборкаЗапасовПродукция.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК СборкаЗапасовПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СборкаЗапасовПродукция.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СборкаЗапасовПродукция.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СборкаЗапасовСерииНоменклатурыПродукция.Серия КАК Серия,
	|	СборкаЗапасовСерииНоменклатурыПродукция.Количество КАК Количество,
	|	СборкаЗапасовСерииНоменклатурыПродукция.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.СборкаЗапасов.СерииНоменклатурыПродукция КАК СборкаЗапасовСерииНоменклатурыПродукция
	|ГДЕ
	|	СборкаЗапасовСерииНоменклатурыПродукция.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииОтчетОПереработке(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетОпереработкеПродукция.Номенклатура КАК Номенклатура,
	|	ОтчетОпереработкеПродукция.Характеристика КАК Характеристика,
	|	ОтчетОпереработкеПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОтчетОпереработкеПродукция.Количество КАК Количество,
	|	ОтчетОпереработкеПродукция.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.ОтчетОПереработке.Продукция КАК ОтчетОпереработкеПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ОтчетОпереработкеПродукция.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ОтчетОпереработкеПродукция.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетОпереработкеСерииНоменклатуры.Серия КАК Серия,
	|	ОтчетОпереработкеСерииНоменклатуры.Количество КАК Количество,
	|	ОтчетОпереработкеСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ОтчетОПереработке.СерииНоменклатуры КАК ОтчетОпереработкеСерииНоменклатуры
	|ГДЕ
	|	ОтчетОпереработкеСерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииОтчетПереработчика(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетПереработчикаПродукция.Номенклатура КАК Номенклатура,
	|	ОтчетПереработчикаПродукция.Характеристика КАК Характеристика,
	|	ОтчетПереработчикаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОтчетПереработчикаПродукция.Количество КАК Количество,
	|	ОтчетПереработчикаПродукция.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ОтчетПереработчикаПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ОтчетПереработчикаПродукция.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ОтчетПереработчикаПродукция.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетПереработчикаСерииНоменклатуры.Серия КАК Серия,
	|	ОтчетПереработчикаСерииНоменклатуры.Количество КАК Количество,
	|	ОтчетПереработчикаСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ОтчетПереработчика.СерииНоменклатуры КАК ОтчетПереработчикаСерииНоменклатуры
	|ГДЕ
	|	ОтчетПереработчикаСерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ЗапросМаркируемойПродукцииЗаказПокупателя(Документ, ВидМаркируемойПродукции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Запасы
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЗаказПокупателяЗапасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Ссылка = &ДокументСсылка
	|	И &УсловиеМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяСерииНоменклатуры.Серия КАК Серия,
	|	ЗаказПокупателяСерииНоменклатуры.Количество КАК Количество,
	|	ЗаказПокупателяСерииНоменклатуры.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ЗаказПокупателя.СерииНоменклатуры КАК ЗаказПокупателяСерииНоменклатуры
	|ГДЕ
	|	ЗаказПокупателяСерииНоменклатуры.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Запасы.Номенклатура КАК Номенклатура,
	|	ВТ_Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_Серии.Серия
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВТ_Серии.Серия ЕСТЬ NULL
	|			ТОГДА ВТ_Запасы.Количество * ЕСТЬNULL(ЕдиницыИзмерения.Коэффициент, 1)
	|		ИНАЧЕ ВТ_Серии.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_ЗапасыСерии
	|ИЗ
	|	ВТ_Запасы КАК ВТ_Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО ВТ_Запасы.КлючСвязи = ВТ_Серии.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ВТ_Запасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК GTIN,
	|	ВТ_ЗапасыСерии.Номенклатура КАК Номенклатура,
	|	ВТ_ЗапасыСерии.Характеристика КАК Характеристика,
	|	ВТ_ЗапасыСерии.Серия КАК Серия,
	|	ВТ_ЗапасыСерии.Количество КАК Количество
	|ИЗ
	|	ВТ_ЗапасыСерии КАК ВТ_ЗапасыСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВТ_ЗапасыСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ВТ_ЗапасыСерии.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ВТ_ЗапасыСерии.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ИТОГИ ПО
	|	Номенклатура,
	|	Характеристика,
	|	GTIN";
	
	УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, ВидМаркируемойПродукции);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", Документ);
	
	Возврат Запрос.Выполнить();

КонецФункции

#КонецОбласти

Процедура ЗаполнитьПараметрыФормированияЗапроса(ПараметрыФормированияЗапроса)
	
	МетаданныеДокументаОснования = ПараметрыФормированияЗапроса.МетаданныеДокументаОснования;
	МетаданныеДокументаИСМП      = ПараметрыФормированияЗапроса.МетаданныеДокументаИСМП;
	
	ТабличнаяЧастьЗапасы = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента();
	Если МетаданныеДокументаОснования.ТабличныеЧасти.Найти("СерииНоменклатуры") <> Неопределено Тогда
		ТабличнаяЧастьЗапасы.ИмяТабличнойЧастиСерии = "СерииНоменклатуры";
	КонецЕсли;
	
	ВидыПродукцииИСМП = ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции();
	ОтборНоменклатуры = ИнтеграцияИСУНФ.ОпределениеВидаПродукцииТекстаЗапроса("СправочникНоменклатура", ВидыПродукцииИСМП);
	
	ОтборНоменклатурыНеТабак      = СтрЗаменить(ОтборНоменклатуры, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак),", "");
	ОтборНоменклатурыНеТабакНеАТП = СтрЗаменить(ОтборНоменклатурыНеТабак, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак),", "");
	ОтборНоменклатурыНеВЕТИС      = СтрЗаменить(ОтборНоменклатуры, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),", "");
	ОтборНоменклатурыВЕТИС        = "СправочникНоменклатура.ВидыПродукцииИС  = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС)";
	
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатуры, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак),", "");
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатурыОтгрузкаТоваров, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.АльтернативныйТабак),", "");
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатурыОтгрузкаТоваров, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция),", "");
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатурыОтгрузкаТоваров, "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС),", "");
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатурыОтгрузкаТоваров,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС),", "");
	ОтборНоменклатурыОтгрузкаТоваров = СтрЗаменить(ОтборНоменклатурыОтгрузкаТоваров,  "ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Пиво),", "");
	
	ВидыПродукцииОстатки = "";
	ВидыПродукцииПеремаркировка = "";
	ВидыПродукцииВозвратВОборот = "";
	ВидыПродукцииОтгрузкаЕАЭС = "";
	ВидыПродукцииМолочная = "";
	Для Каждого ВидПродукцииИСМП Из ВидыПродукцииИСМП Цикл
		
		Если ВидПродукцииИСМП = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха
			И НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИнтеграцияИСКлиентСервер.ВидПродукцииПодлежитМаркировкеОстатков(ВидПродукцииИСМП) Тогда
			ВидыПродукцииОстатки = ВидыПродукцииОстатки
				+ ?(ВидыПродукцииОстатки = "", "", ", ")
				+ "ЗНАЧЕНИЕ(" + ПолучитьПолноеИмяПредопределенногоЗначения(ВидПродукцииИСМП) + ")";
		КонецЕсли;
		Если ИнтеграцияИСКлиентСервер.ВидПродукцииПодлежитПеремаркировке(ВидПродукцииИСМП) Тогда
			ВидыПродукцииПеремаркировка = ВидыПродукцииПеремаркировка
				+ ?(ВидыПродукцииПеремаркировка = "", "", ", ")
				+ "ЗНАЧЕНИЕ(" + ПолучитьПолноеИмяПредопределенногоЗначения(ВидПродукцииИСМП) + ")";
		КонецЕсли;
		Если ИнтеграцияИСКлиентСервер.ВидПродукцииПодлежитВозвратуВОборот(ВидПродукцииИСМП) Тогда
			ВидыПродукцииВозвратВОборот = ВидыПродукцииВозвратВОборот
				+ ?(ВидыПродукцииВозвратВОборот = "", "", ", ")
				+ "ЗНАЧЕНИЕ(" + ПолучитьПолноеИмяПредопределенногоЗначения(ВидПродукцииИСМП) + ")";
		КонецЕсли;
		Если ВидПродукцииИСМП = Перечисления.ВидыПродукцииИС.Обувь
			Или ВидПродукцииИСМП = Перечисления.ВидыПродукцииИС.ЛегкаяПромышленность
			Или ВидПродукцииИСМП = Перечисления.ВидыПродукцииИС.Шины
			Или ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукцииИСМП) Тогда
			ВидыПродукцииОтгрузкаЕАЭС = ВидыПродукцииОтгрузкаЕАЭС
				+ ?(ВидыПродукцииОтгрузкаЕАЭС = "", "", ", ")
				+ "ЗНАЧЕНИЕ(" + ПолучитьПолноеИмяПредопределенногоЗначения(ВидПродукцииИСМП) + ")";
		КонецЕсли;
		Если ИнтеграцияИСКлиентСервер.ЭтоМолочнаяПродукцияИСМП(ВидПродукцииИСМП) Тогда
			ВидыПродукцииМолочная = ВидыПродукцииМолочная
				+ ?(ВидыПродукцииМолочная = "", "", ", ")
				+ "ЗНАЧЕНИЕ(" + ПолучитьПолноеИмяПредопределенногоЗначения(ВидПродукцииИСМП) + ")";
		КонецЕсли;
	КонецЦикла;
	ОтборНоменклатурыМаркировкаОстатков = ?(ВидыПродукцииОстатки="", "ЛОЖЬ",
		СтрШаблон("СправочникНоменклатура.ВидПродукцииИС В (%1)", ВидыПродукцииОстатки));
	ОтборНоменклатурыПеремаркировка     = ?(ВидыПродукцииПеремаркировка="", "ЛОЖЬ",
		СтрШаблон("СправочникНоменклатура.ВидПродукцииИС В (%1)", ВидыПродукцииПеремаркировка));
	ОтборНоменклатурыВозвратВОборот     = ?(ВидыПродукцииВозвратВОборот="", "ЛОЖЬ",
		СтрШаблон("СправочникНоменклатура.ВидПродукцииИС В (%1)", ВидыПродукцииВозвратВОборот));
	ОтборНоменклатурыОтгрузкаЕАЭС       = ?(ВидыПродукцииОтгрузкаЕАЭС="", "ЛОЖЬ",
		СтрШаблон("СправочникНоменклатура.ВидПродукцииИС В (%1)", ВидыПродукцииОтгрузкаЕАЭС));
	ОтборНоменклатурыМолочнаяПродукция  = ?(ВидыПродукцииМолочная="", "ЛОЖЬ",
		СтрШаблон("СправочникНоменклатура.ВидПродукцииИС В (%1)", ВидыПродукцииМолочная));
	
	ОтборНоменклатурыДокумента = "";
	
	Если МетаданныеДокументаОснования = Метаданные.Документы.ЗаказНаПроизводство Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ Тогда
			
			ТабличнаяЧастьЗапасы.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Продукция");
			ТабличнаяЧастьПродукция.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийЗаказНаПроизводство.Сборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПокупателя Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП
			И ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
			// Не участвует в расчете статусов. Основной сценарий - пробивается Чек ККМ.
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ОтборНоменклатурыДокумента = ОтборНоменклатурыНеТабакНеАТП;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЗаказПоставщику Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = "НЕ Товары.Ссылка.Контрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)";
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ИнвентаризацияЗапасов Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыМаркировкаОстатков;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.КомплектацияЗапасов Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			// Комплектация набора.
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Ссылка");
			ТабличнаяЧастьПродукция.ДопСоединения = ТекстСоединенияGTINНаборов();
			ТабличнаяЧастьПродукция.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийКомплектацияЗапасов.Сборка)
			| И ОписаниеGTINИС.ВидУпаковки = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковокИС.Набор)
			|	И ОписаниеGTINИС.КоличествоПотребительскихУпаковок < 2";
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыНеВЕТИС;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТабличнаяЧастьЗапасы.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийКомплектацияЗапасов.Сборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Ссылка");
			ТабличнаяЧастьПродукция.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийКомплектацияЗапасов.Разборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОприходованиеЗапасов Тогда
		
		// Не поддерживается. Доступна маркировка остаков, либо производства.
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОПереработке Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Продукция", "СерииНоменклатуры");
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетПереработчика Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Продукция", "СерииНоменклатуры");
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = 
				" НЕ(1 В (ВЫБРАТЬ 1 Из РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
				|	ГДЕ ОбъектыУчетаДокументовЭДО.ОбъектУчета = Товары.Ссылка
				|	И ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент.СодержитМаркируемыеТовары))";
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ОтчетОРозничныхПродажах Тогда
		// Основной сценарий - документы формируются на основании Чека.
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВозвратВОборотИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = "Товары.Количество < 0";
			
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = ПараметрыФормированияЗапроса.ДопОтборы_Общие + "
				| И Товары.Ссылка.КассаККМ.ТипКассы В (ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.АвтономнаяККМ), ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ККМOffline))
				| И Товары.Ссылка.КассаККМ В (ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)";
			КонецЕсли;
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыВозвратВОборот;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = "Товары.Количество > 0";
			
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = ПараметрыФормированияЗапроса.ДопОтборы_Общие + "
				| И Товары.Ссылка.КассаККМ.ТипКассы В (ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.АвтономнаяККМ), ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.ККМOffline))
				| И Товары.Ссылка.КассаККМ В (ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)";
			КонецЕсли;
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыНеТабакНеАТП;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ПриходнаяНакладная Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВозвратВОборотИСМП
			И ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
			// Не участвует в расчете статусов. Основной сценарий - пробивается Чек ККМ
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя)
			| И Товары.Ссылка.Контрагент.ВидКонтрагента = Значение(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)";
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыВозвратВОборот;
			
			ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "";
			ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "";
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = "(Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика)
			| И НЕ Товары.Ссылка.Контрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
			| ИЛИ Товары.Ссылка.Контрагент.ВидКонтрагента = Значение(Перечисление.ВидыКонтрагентов.ФизическоеЛицо))";
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ПеремаркировкаТоваровИСМП
			И ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
			// Не участвует в расчете статусов. Ручной ввод.
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыПеремаркировка;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.РасходнаяНакладная Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "ШтрихкодыУпаковок";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
		
		// Основной сценарий - ЭДО. Проверка осуществляется перед записью документа.
		// См. доп. свойство "НеВыполнятьРасчетСтатусаГосИС".
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие =
			"(Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю)";
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = ПараметрыФормированияЗапроса.ДопОтборы_Общие + "
				| И (НЕ ЕстьNull(Товары.Ссылка.Контрагент.СтранаРегистрации.УчастникЕАЭС, Ложь)
				| ИЛИ Товары.Ссылка.Контрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия))";
			КонецЕсли;
			ПараметрыФормированияЗапроса.ДопОтборы_Общие = ПараметрыФормированияЗапроса.ДопОтборы_Общие + "
			| ИЛИ Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику)
			| И Товары.Ссылка.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
			| ИЛИ Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию))";
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ОтгрузкаТоваровИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			ПараметрыФормированияЗапроса.ДопОтборы_Общие =
			"НЕ Товары.Ссылка.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)";
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = ПараметрыФормированияЗапроса.ДопОтборы_Общие + "
				| И ЕстьNull(Товары.Ссылка.Контрагент.СтранаРегистрации.УчастникЕАЭС, Ложь)
				| И НЕ Товары.Ссылка.Контрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)";
			КонецЕсли;
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыОтгрузкаТоваров;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СборкаЗапасов Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ТабличнаяЧастьЗапасы.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Продукция", "СерииНоменклатурыПродукция");
			ТабличнаяЧастьПродукция.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийСборкаЗапасов.Разборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.МаркировкаТоваровИСМП Тогда
			
			ТабличнаяЧастьЗапасы.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийСборкаЗапасов.Разборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ТабличнаяЧастьПродукция = ИнтеграцияИСУНФ.ШаблонОписанияТабличнойЧастиДокумента(
				Истина, "Продукция", "СерииНоменклатурыПродукция");
			ТабличнаяЧастьПродукция.ДопОтборы_Товары = "Товары.Ссылка.ВидОперации = Значение(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)";
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьПродукция);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.СписаниеЗапасов Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатуры;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЧекККМ Тогда
		
		ПараметрыФормированияЗапроса.ИмяТаблицыШтрихкодыУпаковок = "АкцизныеМарки";
		ПараметрыФормированияЗапроса.ИмяКолонкиШтрихкодУпаковки = "АкцизнаяМарка";
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВыводИзОборотаИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = "Товары.Ссылка.КассаККМ В (ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)";
			КонецЕсли;
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыНеТабакНеАТП;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеДокументаОснования = Метаданные.Документы.ЧекККМВозврат Тогда
		
		Если МетаданныеДокументаИСМП = Метаданные.Документы.ВозвратВОборотИСМП Тогда
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			Если НЕ ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
				ПараметрыФормированияЗапроса.ДопОтборы_Общие = "Товары.Ссылка.КассаККМ В (ВЫБРАТЬ Касса Из РегистрСведений.КассыНеПередающиеДанныеВИСМП)";
			КонецЕсли;
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыВозвратВОборот;
			
		ИначеЕсли МетаданныеДокументаИСМП = Метаданные.Документы.ПеремаркировкаТоваровИСМП
			И ПараметрыФормированияЗапроса.ВключатьОпциональныеПоля Тогда
			// Не участвует в расчете статусов. Ручной ввод.
			
			ПараметрыФормированияЗапроса.ТабличныеЧасти.Добавить(ТабличнаяЧастьЗапасы);
			
			ОтборНоменклатурыДокумента = ОтборНоменклатурыПеремаркировка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыФормированияЗапроса.ОтборНоменклатуры = "ИСТИНА" Тогда
		ПараметрыФормированияЗапроса.ОтборНоменклатуры = ОтборНоменклатурыДокумента;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСвойстваСтрокиШтрихкодовУпаковокПоШтрихкоду(Строка, Штрихкод) ЭКспорт
	
	ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкода(Штрихкод);
	
	Если ПараметрыШтрихкода.Результат <> Неопределено Тогда
		
		СвойстваШтрихкода = ПараметрыШтрихкода.Результат;
		
		Если ПараметрыШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.SSCC Тогда
			
			Строка.ПрефиксКомпанииGS1 = Формат(СвойстваШтрихкода.ПрефиксКомпанииGS1, "ЧГ=0");
			Строка.ЦифраРасширения    = Формат(СвойстваШтрихкода.ЦифраРасширения, "ЧГ=0");
			Строка.СерийныйНомерSSCC  = Формат(СвойстваШтрихкода.СерийныйНомерSSCC, "ЧГ=0");
			Строка.КонтрольноеЧисло   = Формат(СвойстваШтрихкода.КонтрольноеЧисло, "ЧГ=0");
			
		ИначеЕсли ПараметрыШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.Code128 Тогда
			
			Если СвойстваШтрихкода.НомерФорматаCode128 = 1 Тогда
				Строка.ИдентификаторОрганизации = СвойстваШтрихкода.ИдентификаторОрганизации;
				Строка.ДатаМаркировки           = СвойстваШтрихкода.ДатаМаркировки;
				Строка.НомерПоПорядку           = Формат(СвойстваШтрихкода.НомерПоПорядку, "ЧГ=0");
			ИначеЕсли СвойстваШтрихкода.НомерФорматаCode128 = 2 Тогда
				Строка.ИдентификаторОрганизации = СвойстваШтрихкода.ИдентификаторОрганизации;
				Строка.ТипЛогистическойЕдиницы  = СвойстваШтрихкода.ТипЛогистическойЕдиницы;
				Строка.НомерПлощадкиМаркировки  = СвойстваШтрихкода.НомерПлощадкиМаркировки;
				Строка.ГодГенерацииШтрихкода    = Формат(СвойстваШтрихкода.ГодГенерацииШтрихкода, "ДФ=yyyy");
				Строка.НомерПоПорядку           = Формат(СвойстваШтрихкода.НомерПоПорядку, "ЧГ=0");
			ИначеЕсли СвойстваШтрихкода.НомерФорматаCode128 = 3 Тогда
				Строка.ИдентификаторОрганизации = СвойстваШтрихкода.ИдентификаторОрганизации;
				Строка.НомерПоПорядку           = Формат(СвойстваШтрихкода.НомерПоПорядку, "ЧГ=0");
			КонецЕсли;
			
		ИначеЕсли ПараметрыШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_128
			Или ПараметрыШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked
			Или ПараметрыШтрихкода.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_DataMatrix Тогда
			
			Для каждого СтрокаПараметровПримененияGS1 Из ПараметрыШтрихкода.Результат Цикл
				ИмяКолонкиПечати = "ИдентификаторПрименения_" + СтрокаПараметровПримененияGS1.КлючИдентификатора;
				Строка[ИмяКолонкиПечати] = СтрокаПараметровПримененияGS1.Значение;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗакрытииФормыПроверкиИПодбораВФормеРМК(Форма, Результат, ВидПродукцииИС)
	
	ДанныеПроверкиИПодбора = ПолучитьИзВременногоХранилища(Результат);
	
	ПараметрыСканирования = ШтрихкодированиеИС.ПараметрыСканирования(Форма);
	
	ПараметрыШтрихкодаДляОбработки = ИнтеграцияИСУНФ.ПараметрыШтрихкодаДляОбработки(Форма, ПараметрыСканирования);
	ПараметрыШтрихкодаДляОбработки.Вставить("ВидПродукцииИС", ВидПродукцииИС);
	
	Если ИнтеграцияИСКлиентСерверПовтИсп.ПоддерживаетсяЧастичноеВыбытие(ВидПродукцииИС, ПараметрыСканирования.ВидОперацииИСМП) Тогда
		
		ШтрихкодыУпаковок = Новый Массив;
		
		Для Каждого ШтрихкодУпаковки Из ДанныеПроверкиИПодбора.ТаблицаШтрихкодовВерхнегоУровня Цикл
			
			НовыйЭлемент = ШтрихкодированиеИСМП.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ШтрихкодУпаковки);
			
			ШтрихкодыУпаковок.Добавить(НовыйЭлемент);
			
		КонецЦикла;
	Иначе
		ШтрихкодыУпаковок = ДанныеПроверкиИПодбора.ТаблицаШтрихкодовВерхнегоУровня.ВыгрузитьКолонку("ШтрихкодУпаковки");
	КонецЕсли;
	
	РезультатРазбора = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковок(ШтрихкодыУпаковок, ПараметрыСканирования);
	
	ИнтеграцияИСУНФ.ЗафиксироватьРезультатПроверкиИПодбора(РезультатРазбора.ДеревоУпаковок, ПараметрыШтрихкодаДляОбработки, Форма);
	
	СуммаТоваров = Форма.Объект.Запасы.Итог("Всего");
	Если Не Форма.Объект.ОперацияСДенежнымиСредствами Тогда
		Форма.Объект.СуммаДокумента = СуммаТоваров;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "СуммаСдачи") Тогда
		Форма.СуммаСдачи = Форма.Объект.БезналичнаяОплата.Итог("Сумма")
						 + Форма.Объект.Предоплата.Итог("СуммаРасчетов")
						 + Форма.Объект.ПолученоНаличными
						 - Форма.Объект.СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаМаркируемойПродукции(ДокументСсылка, ИмяТабличнойЧасти = "Запасы")
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	
	ТекстЗапросаШаблон =
	"ВЫБРАТЬ
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Запасы.Партия = ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Запасы.Партия
	|	КОНЕЦ КАК Серия,
	|	СУММА(Запасы.Количество) КАК Количество
	|ИЗ
	|	Документ.%1.%2 КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Запасы.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Запасы.Ссылка = &Документ
	|	И &УсловиеМаркируемаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.Номенклатура,
	|	Запасы.Характеристика,
	|	Запасы.Партия";
	
	ТекстЗапроса = СтрШаблон(ТекстЗапросаШаблон, МетаданныеДокумента.Имя, ИмяТабличнойЧасти);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает в тексте запроса условие отбора по маркируемой продукции требуемого вида.
// Текст запроса должен содержать строку вида "&УсловиеМаркируемаяПродукция" в условии соединения или условии отбора
// и таблицу с синонимом "СправочникНоменклатура", которой как правило является таблица справочника "Номенклатура".
// Параметры:
//   ТекстЗапроса - Строка - строка с текстом запроса, удовлетворяющая приведенным выше условиям.
//   ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Массив Из ПеречислениеСсылка.ВидыПродукцииИС - 
//     вид или виды маркируемой продукции, условие отбора по которым необходимо установить.
Процедура УстановитьУсловиеПоМаркируемойПродукции(ТекстЗапроса, Знач ВидМаркируемойПродукции) Экспорт
	
	ПутьКПолюНоменклатура = "СправочникНоменклатура";
	ЗаменяемыйПараметр = "&УсловиеМаркируемаяПродукция";
	ВидыМаркируемойПродукции = Новый Массив();
	
	Если ТипЗнч(ВидМаркируемойПродукции) = Тип("Массив") Или ТипЗнч(ВидМаркируемойПродукции) = Тип("ФиксированныйМассив") Тогда
		
		Для Каждого ВидПродукции Из ВидМаркируемойПродукции Цикл
			ВидыМаркируемойПродукции.Добавить(СтрШаблон("ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.%1)", XMLСтрока(ВидПродукции)));
		КонецЦикла;
		
	Иначе
		
		Если ЗначениеЗаполнено(ВидМаркируемойПродукции) Тогда
			ВидыМаркируемойПродукции.Добавить(СтрШаблон("ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.%1)", XMLСтрока(ВидМаркируемойПродукции)));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидыМаркируемойПродукции.Количество() > 0 Тогда
		УсловиеМаркируемаяПродукция = СтрШаблон("%1.ВидПродукцииИС В (%2)", ПутьКПолюНоменклатура, СтрСоединить(ВидыМаркируемойПродукции, ","))
	Иначе
		УсловиеМаркируемаяПродукция = "ЛОЖЬ"
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйПараметр, УсловиеМаркируемаяПродукция);
	
КонецПроцедуры

Функция ТекстСоединенияGTINНаборов()
	
	ТекстСоединенияGTINНаборов = 
	"	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО Штрихкоды.Номенклатура = Товары.Номенклатура
	|		И Штрихкоды.Характеристика = Товары.Характеристика
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОписаниеGTINИС КАК ОписаниеGTINИС
	|		ПО  ОписаниеGTINИС.GTIN = Штрихкоды.Штрихкод
	|		Или ОписаниеGTINИС.GTIN = ""0""+Штрихкоды.Штрихкод
	|		Или ОписаниеGTINИС.GTIN = ""00""+Штрихкоды.Штрихкод
	|		Или ОписаниеGTINИС.GTIN = ""000000""+Штрихкоды.Штрихкод";
	
	Возврат ТекстСоединенияGTINНаборов;
	
КонецФункции

// Определяет, находится ли переданная форма формой документа ВыводИзОборотаИСМП в состоянии после завершения обмена данными
// В этом случае элемент формы СтраницаТовары находится в режиме ТолькоПросмотр
//
// Параметры:
//  Форма                  - УправляемаяФорма - форма, в которой произошло событие
//
// Возвращаемое значение:
//   Булево   - Истина, если передана форма документа ВыводИзОборотаИСМП в состоянии после завершения обмена данными 
//
Функция ЭтоЗавершениеОбменаДаннымиВыводИзОборотаИСМП(Форма)
	
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект")
	И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "Ссылка")
	И ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ВыводИзОборотаИСМП")
	И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "СтраницаТовары")
	И Форма.Элементы.СтраницаТовары.ТолькоПросмотр;
	
КонецФункции // ЭтоЗавершениеОбменаДаннымиВыводИзОборотаИСМП()

#КонецОбласти