#Область СлужебныйПрограммныйИнтерфейс

Процедура СортироватьМассивПоРасхождению(МассивСтрок) Экспорт
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МинимальноеРасхождение = 999999;
	СсылкаНаЭлемент = Неопределено;
	Для Каждого ЭлементМассива Из МассивСтрок Цикл
		Если ЭлементМассива.Расхождение >= 0 Тогда
			Продолжить;
		КонецЕсли;
		МинимальноеРасхождение = Мин(МинимальноеРасхождение, -ЭлементМассива.Расхождение);
		Если МинимальноеРасхождение = -ЭлементМассива.Расхождение Тогда
			СсылкаНаЭлемент = ЭлементМассива;
		КонецЕсли;
	КонецЦикла;
	
	Если СсылкаНаЭлемент <> Неопределено Тогда
		МассивСтрок.Вставить(0, СсылкаНаЭлемент);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьЭлементовФормыСборкиЗаказа(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ЗапасыКоличествоСобрано",
		"Видимость",
		Форма.РежимСборки ИЛИ Форма.ЕстьСобранныеПозиции);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ЗапасыКоличествоСобрано",
		"ЦветФонаЗаголовка",
		?(Форма.РежимСборки, Форма.ЦветСборкиЗаказа, Новый Цвет));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ЗапасыРасхождениеПредставление",
		"Видимость",
		Форма.РежимСборки);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ОчиститьСобрано",
		"Видимость",
		Форма.Элементы.ЗапасыКоличествоСобрано.Видимость);
	
КонецПроцедуры

Процедура РассчитатьРасхождения(Форма, СтрокаТабличнойЧасти, ПереустановитьОтбор = Истина) Экспорт
	
	Если Не Форма.РежимСборки Тогда
		ПереустановитьОтбор = Ложь;
	КонецЕсли;
	
	БылоРасхождение = СтрокаТабличнойЧасти.ЕстьРасхождение;
	
	СтрокаТабличнойЧасти.Расхождение = СтрокаТабличнойЧасти.КоличествоСобрано - СтрокаТабличнойЧасти.Количество;
	
	СтрокаТабличнойЧасти.РасхождениеПредставление = РасхождениеПредставление(СтрокаТабличнойЧасти.Расхождение);
	
	СтрокаТабличнойЧасти.ЕстьРасхождение = СтрокаТабличнойЧасти.Расхождение <> 0;
	
	Если БылоРасхождение <> СтрокаТабличнойЧасти.ЕстьРасхождение
		И ПереустановитьОтбор Тогда
		ПереустановитьОтбор(Форма.Элементы);
		Для Каждого СтрокаЗапасы Из Форма.Объект.Запасы Цикл
			Если СтрокаЗапасы.Расхождение <> 0 Тогда
				Форма.Элементы.Запасы.ТекущаяСтрока = СтрокаЗапасы.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереустановитьОтбор(Элементы) Экспорт
	
	Если Элементы.Запасы.ОтборСтрок <> Неопределено И Элементы.Запасы.ОтборСтрок.Количество() > 0 Тогда
		
		СтарыйОтборСтрок = Элементы.Запасы.ОтборСтрок;
		Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура);
		Элементы.Запасы.ОтборСтрок = СтарыйОтборСтрок;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьСобранныеПозиции(ТабличнаяЧасть, ТекущийВариантКП = 0) Экспорт
	
	НайденыПозиции = Ложь;
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		Если ТекущийВариантКП <> 0 Тогда
			Если СтрокаТЧ.НомерВариантаКП <> ТекущийВариантКП Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТЧ.КоличествоСобрано <> 0 Тогда
			НайденыПозиции = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденыПозиции;
	
КонецФункции

Процедура УстановитьОтборСобранныхПозиций(Элементы, ЕстьУслуги = Ложь, ТекущийВариантКП = 0) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЕстьРасхождение", Истина);
	Если ЕстьУслуги Тогда
		ПараметрыОтбора.Вставить("ТипНоменклатурыЗапас", Истина);
	КонецЕсли;
	Если ТекущийВариантКП <> 0 Тогда
		ПараметрыОтбора.Вставить("НомерВариантаКП", ТекущийВариантКП);
	КонецЕсли;
	
	Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура(ПараметрыОтбора);
	
	Элементы.ДекорацияОтборПоРасхождениям.Заголовок = НоменклатураВДокументахСервер.ИнформацияОбОтборе(
		НСтр("ru = 'К сборке'"));
	
	Элементы.ДекорацияОтборПоРасхождениям.Видимость = Истина;
	
КонецПроцедуры

Процедура СборкаЗаказовПриОткрытии(Форма, ТекущийВариантКП = 0) Экспорт
	
	Запасы = Форма.Объект.Запасы;
	
	Форма.ЕстьСобранныеПозиции = ЕстьСобранныеПозиции(Запасы, ТекущийВариантКП);
	УстановитьВидимостьЭлементовФормыСборкиЗаказа(Форма);
	Для Каждого СтрокаЗапасы Из Запасы Цикл
		РассчитатьРасхождения(Форма, СтрокаЗапасы, Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РасхождениеПредставление(Знач Расхождение)
	
	Результат = Формат(Расхождение, "ЧЦ=15; ЧДЦ=3; ЧН=0");
	
	Если Расхождение > 0 Тогда
		Возврат СтрШаблон("+%1", Результат);
	Иначе
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

#КонецОбласти