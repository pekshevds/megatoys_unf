
#Область ПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	ПодключитьОбработчикФормированияКомандОбсуждений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриФормированииКоманд(Параметры, Команды, КомандаПОУмолчанию, ДополнительныеПараметры) Экспорт
	
	Если Параметры.Источник = ИсточникКомандСистемыВзаимодействия.Сообщение Тогда
		ПараметрыКоманды = НовыйПараметрыКоманды();
		ПараметрыКоманды.Сообщение = Параметры.Сообщение;
		ПараметрыКоманды.Обсуждение = Параметры.Обсуждение;
		
		Обработчик = Новый ОписаниеОповещения("ОбработкаКомандыСоздатьЗадачу", ЗадачиСотрудникаКлиент, ПараметрыКоманды);
		Команда = Новый ОписаниеКомандыСистемыВзаимодействия(Обработчик, НСтр("ru='Создать задачу'"));
		Команда.Картинка = БиблиотекаКартинок.ЗадачаСотрудника;
		Команды.Добавить(Команда);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаКомандыСоздатьЗадачу(ПараметрыКоманды) Экспорт
	
	Если ПараметрыКоманды.Сообщение <> Неопределено Тогда
		НавигационнаяСсылкаСообщения = ПолучитьНавигационнуюСсылку(ПараметрыКоманды.Сообщение);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
		ПараметрыФормы.ЗначенияЗаполнения.Вставить("Основание", НавигационнаяСсылкаСообщения);
		ПараметрыФормы.ЗначенияЗаполнения.Вставить("ОснованиеПредставление", ПараметрыКоманды.Сообщение.Текст);
		ОткрытьФорму("Документ.ЗадачаСотрудника.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры выполнения команды, которая добавляется в СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд 
// 
// Возвращаемое значение:
//  Структура - параметры команды
//   * Сообщение - Неопределено, СообщениеСистемыВзаимодействия
//   * Обсуждение - Неопределено, ОбсуждениеСистемыВзаимодействия
//
Функция НовыйПараметрыКоманды()
	
	Результат = Новый Структура;
	Результат.Вставить("Сообщение", Неопределено);
	Результат.Вставить("Обсуждение", Неопределено);
	Возврат Результат;
	
КонецФункции

Процедура ПодключитьОбработчикФормированияКомандОбсуждений()
	
	Вызов = Новый ОписаниеОповещения("ПриФормированииКоманд", ЗадачиСотрудникаКлиент);
	СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд(Вызов);
	
КонецПроцедуры

#КонецОбласти