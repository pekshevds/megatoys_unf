
#Область СлужебныйПрограммныйИнтерфейс

Функция ЗагрузитьСтатусыОплатыЗапускЗадания(УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Загрузка статусов операций.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиУНФ.ПолучитьСтатусыОперацияC2B",
		,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Процедура ЗаписатьДанныеКассовойСсылки(КассаККМ, КассовыеСсылки, Отказ = Ложь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если КассовыеСсылки = Неопределено Тогда
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(КассаККМ, "КассовыеСсылкиСБП");
	Иначе
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(КассаККМ, КассовыеСсылки, "КассовыеСсылкиСБП");
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьКассовыеСсылки(СпособОплаты, НастройкиКассовыхСсылокСБП, Отказ) Экспорт

	Для Каждого НастройкаПодключения Из НастройкиКассовыхСсылокСБП.НастройкиПодключения Цикл
		
		Если НастройкаПодключения.СпособОплаты = СпособОплаты Тогда
			
			РезультатСоздания = ПереводыСБПc2b.КассоваяСсылка(НастройкаПодключения.Интеграция);
			
			Если ЗначениеЗаполнено(РезультатСоздания.КодОшибки) Тогда
				
				Отказ 				= Истина;
				РезультатСоздания 	= РезультатСоздания.СообщениеОбОшибке;
				
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
    Возврат РезультатСоздания;
	
КонецФункции

Функция ШаблонСообщенияОплатаСБП(Предмет = Неопределено, ПлатежнаяСсылка = "") Экспорт

    Возврат ИнтеграцияСПлатежнымиСистемамиУНФ.ШаблонСообщенияОплатаСБП(Предмет, ПлатежнаяСсылка);
	
КонецФункции

#КонецОбласти
