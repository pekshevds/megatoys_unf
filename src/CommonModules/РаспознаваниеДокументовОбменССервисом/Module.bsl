#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьОбменССервисомРаспознаванияДокументов() Экспорт
	
	// Параметры обмена
	ПланыОбмена.ОбменССервисомРаспознаванияДокументов.СоздатьУзлыСервисаРаспознаванияДокументов();
	
	Узел = ПланыОбмена.ОбменССервисомРаспознаванияДокументов.УзелСервисаРаспознаванияДокументов();
	НомерПоследнегоУспешногоОбмена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Узел, "НомерОтправленного");
	НомерТекущегоПакета = НомерПоследнегоУспешногоОбмена + 1;
	ОтправкаУдалась = Ложь;
	
	// Цикл обменов
	
	ВыполнитьОтправкуОбратнойСвязиRecognitionResult(Узел, НомерТекущегоПакета, ОтправкаУдалась);
	
	// Фиксация номера последнего успешного обмена
	Если ОтправкаУдалась Тогда
		УзелОбъект = Узел.ПолучитьОбъект();
		УзелОбъект.НомерОтправленного = НомерТекущегоПакета;
		УзелОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОтправкуОбратнойСвязиRecognitionResult(Узел, НомерТекущегоПакета, ОтправкаУдалась)
	
	// Шаг 1: Выбрать распознанные документы из очереди
	// Шаг 2: Присвоить этим документам номер отправленного пакета
	// Шаг 3: Сформировать пакеты отправки
	// Шаг 4: Выполнить отправку пакетов в сервис
	// Шаг 5: Подтвердить получение сервисом пакетов
	
	// Шаг 1 + Шаг 2
	Выборка = ПланыОбмена.ВыбратьИзменения(Узел, НомерТекущегоПакета, Метаданные.Документы.РаспознанныйДокумент);
	
	ТипРаспознанныйДокументОбъект = Тип("ДокументОбъект.РаспознанныйДокумент");
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Получить();
		
		// Шаг 3 + Шаг 4
		Если ТипЗнч(Объект) = ТипРаспознанныйДокументОбъект Тогда
			Попытка
				РаспознаваниеДокументов.ОтправитьОбратнуюСвязьRecognitionResult(Объект);
			Исключение
				ЗаписьЖурналаРегистрации(
					РаспознаваниеДокументов.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Ошибка,
					,
					Объект.Ссылка,
					СтрШаблон(НСтр("ru = 'Произошла ошибка отправки обратной связи RecognitionResult
					                |по причине:
					                |%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
					)
				);
			КонецПопытки;
		Иначе
			// Возможна ситуация, когда объект удален (т.е. стоит <Объект не найден>) и ТипЗнч(Объект) = Тип("УдалениеОбъекта")
			// В таком случае не нужно отправлять обратную связь
		КонецЕсли;
		
		// Шаг 5
		ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Объект.Ссылка);
		
		ОтправкаУдалась = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
