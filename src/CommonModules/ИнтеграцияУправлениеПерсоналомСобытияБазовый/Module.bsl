
#Область СлужебныйПрограммныйИнтерфейс

#Область РегистрацияПубликуемыхОбъектов

Процедура ТекущиеКадровыеДанныеСотрудниковПередЗаписью(НаборЗаписей) Экспорт

	Сотрудники = НаборЗаписей.ВыгрузитьКолонку("Сотрудник");
	ТекущиеДанные = ТекущиеКадровыеДанныеСотрудников(Сотрудники);
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПрежниеДанные", ТекущиеДанные);

КонецПроцедуры

Процедура ТекущиеКадровыеДанныеСотрудниковПриЗаписи(НаборЗаписей) Экспорт
	
	ПрежниеДанные = Неопределено;
	НаборЗаписей.ДополнительныеСвойства.Свойство("ПрежниеДанные", ПрежниеДанные);
	Если ПрежниеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(НаборЗаписей, "Сотрудник", Истина);
	НовыеДанные = ТекущиеКадровыеДанныеСотрудников(Сотрудники);
	
	Отбор = Новый Структура("Сотрудник");
	СписокСотрудников = Новый Массив;
	
	Для каждого Сотрудник Из Сотрудники Цикл
		Отбор.Сотрудник = Сотрудник;
		ПрежниеДанныеПоСотруднику 	= ПрежниеДанные.Скопировать(Отбор);
		НовыеДанныеПоСотруднику 	= НовыеДанные.Скопировать(Отбор);
		Если Не ОбщегоНазначения.КоллекцииИдентичны(ПрежниеДанныеПоСотруднику, НовыеДанныеПоСотруднику) Тогда
			СписокСотрудников.Добавить(Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокСотрудников.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&СписокСотрудников)
		|	И Сотрудники.ГоловнойСотрудник = Сотрудники.Ссылка";
		ТаблицаСотрудников  = Запрос.Выполнить().Выгрузить();
		
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		ИнтеграцияУправлениеПерсоналом.ЗарегистрироватьИзменениеКадровойИстории(ТаблицаСотрудников, Приложение);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИменаКонтролируемыхРеквизитов(Объект) Экспорт

	ИменаКонтролируемыхРеквизитов = "";
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.СтруктураПредприятия") Тогда
		ИменаКонтролируемыхРеквизитов = "Наименование,Код,Родитель";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ШтатноеРасписание") Тогда
		ИменаКонтролируемыхРеквизитов = "Наименование,ПометкаУдаления";
	КонецЕсли;
	
	Возврат ИменаКонтролируемыхРеквизитов;

КонецФункции

#КонецОбласти

Функция ТекущиеКадровыеДанныеСотрудников(Сотрудники)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаПриема КАК ДатаПриема,
	|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения КАК ДатаУвольнения,
	|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация КАК ТекущаяОрганизация,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение КАК ТекущееПодразделение,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность КАК ТекущаяДолжность,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости КАК ТекущийВидЗанятости
	|ИЗ
	|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|ГДЕ
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник В(&Сотрудники)
	|	И ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

#КонецОбласти
