#Область ПрограммныйИнтерфейс

// Процедура настраивает форму помощника кассового места
// Параметры:
//  Форма - форма нового РМК
//  ОписаниеОшибки - строка - текст ошибки для вывода на форме
//
Процедура НастроитьПояснениеКФормеПомощника(Форма, ОписаниеОшибки = "") Экспорт

	НастройкиКассовыхСсылокСБП 	= Форма.НастройкиКассовыхСсылокСБП;
	КассовыеСсылки				= НастройкиКассовыхСсылокСБП.КассовыеСсылки;
	КассоваяСсылка				= КассовыеСсылки.СпособыОплаты.Получить(Форма.СпособОплатыКассовойСсылкиСБП);
	СписокСпособовОплаты		= НастройкиКассовыхСсылокСБП.СписокСпособовОплаты;
	
	ПодключениеКассовойСсылки = Ложь;
	
	Если НастройкиКассовыхСсылокСБП.НастройкиПодключения <> Неопределено Тогда
	
		Для Каждого НастройкаПодключения Из НастройкиКассовыхСсылокСБП.НастройкиПодключения Цикл
			
			Если НастройкаПодключения.СпособОплаты = Форма.СпособОплатыКассовойСсылкиСБП Тогда
				ПодключениеКассовойСсылки = НастройкаПодключения.НастройкиТорговойТочки.КассовыеСсылки;
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;

	Форма.Элементы.ГруппаИспользоватьКассовыйQRКодСБП.Видимость 	= Форма.НастройкиКассовыхСсылокСБП.НастройкиПодключения <> Неопределено;
	Форма.Элементы.ИспользоватьКассовыйQRКодСБП.Доступность 		= СписокСпособовОплаты.Количество() > 0;
	Форма.Элементы.ГруппаСБП.Видимость 								= Форма.ИспользоватьКассовыйQRКодСБП;
	Форма.Элементы.СпособОплатыКассовойСсылкиСБП.Видимость 			= СписокСпособовОплаты.Количество() > 0;
	Форма.Элементы.СпособОплатыКассовойСсылкиСБП.ТолькоПросмотр		= СписокСпособовОплаты.Количество() = 1;

	Если КассоваяСсылка <> Неопределено Тогда
		Форма.NFCСсылкаСБП = ИнтеграцияСПлатежнымиСистемамиКлиентСервер.ФункциональнаяСсылкаNFC(КассоваяСсылка.КассоваяСсылка);
	Иначе
		Форма.NFCСсылкаСБП = "";
	КонецЕсли;
	
	Форма.Элементы.Подключить.Видимость 									= ПодключениеКассовойСсылки;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		Форма.Элементы.ДекорацияПояснениеКФорме.Заголовок 	= ОписаниеОшибки;
		Форма.Элементы.ДекорацияПояснениеКФорме.ЦветТекста	= WebЦвета.Красный;
				
	Иначе
		
		Форма.Элементы.ДекорацияПояснениеКФорме.Заголовок  = "";
		Форма.Элементы.ДекорацияПояснениеКФорме.ЦветТекста	= WebЦвета.Черный;
				
	КонецЕсли;	
	
КонецПроцедуры

// Функция возвращает структуру хранения кассовых ссылок
//
// Возвращаемое значение:
//  Структура - информация о доступных операциях:
//    *СпособОплатыДляПечати - ОпределяемыйТип.СпособОплатыПлатежныхСистемРМК - способ оплаты, который выводится на форме
//    *СпособыОплаты - Соответствие - Соответствие способов оплаты и кассовых ссылок
//
Функция КассовыеСсылкиСБП() Экспорт

	КассовыеСсылки = Новый Структура();
	КассовыеСсылки.Вставить("СпособОплатыДляПечати");	
	КассовыеСсылки.Вставить("ИспользоватьКассовыйQRКодСБП", Ложь);	
	КассовыеСсылки.Вставить("СпособыОплаты", Новый Соответствие);	

	Возврат КассовыеСсылки;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьПараметры(Форма, ПереданныеПараметры, ВидыОплат) Экспорт
	
	ЗаполнитьЗначенияСвойств(Форма, ПереданныеПараметры);
	
	Форма.ПлатежнаяСистема_НоваяЗаявка = ПереданныеПараметры;
	
	Если ВидыОплат.Количество() > 0  Тогда
		
		Если ИнтерфейсРМКСлужебныйКлиентСервер.ИспользоватьСлои(Форма) Тогда
			ИнтерфейсРМКСлужебныйКлиентСервер.УстановитьТекущийСлойГруппы(Форма.Элементы.ГруппаОсновнаяСтраница);
		Иначе
			УстановитьТекущуюСтраницу(Форма.Элементы.ГруппаОсновнаяСтраница);
		КонецЕсли;
	
		Если НЕ Форма.ПлатежнаяСистема_НоваяЗаявка.ВидОперацииПродажа Тогда
			
			Если ЗначениеЗаполнено(Форма.ПлатежнаяСистема_НоваяЗаявка.ДокументОплаты) Тогда
				Форма.Элементы.ИдентификаторОплаты.Видимость = Ложь;
			Иначе
				Форма.Элементы.ИдентификаторОплаты.Видимость = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Форма.ПлатежнаяСистема_НоваяЗаявка.ВидОплатыПлатежнойСистемы) Тогда
			НастроитьФормуПоВидуОплаты(Форма, ВидыОплат);
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыИнициализации() Экспорт
	
	ПереданныеПараметры = Новый Структура;
	
	// обязательные параметры
	ПереданныеПараметры.Вставить("ДокументОплаты");
	ПереданныеПараметры.Вставить("ИтогПоОрганизации", 0);
	ПереданныеПараметры.Вставить("СуммаОперации", 0); // фактическая сумма оплаты, возврата
	ПереданныеПараметры.Вставить("НомерДокумента", "");
	ПереданныеПараметры.Вставить("Оплачивается", Ложь);
	ПереданныеПараметры.Вставить("ИтогПоОрганизации", 0);
	ПереданныеПараметры.Вставить("Интеграция");
	ПереданныеПараметры.Вставить("ВидОплатыПлатежнойСистемы");    // устаревший параметр
	ПереданныеПараметры.Вставить("СпособОплатыПлатежнойСистемы"); // новый параметры, расширенный тип
	ПереданныеПараметры.Вставить("ИдентификаторПС", "");
	ПереданныеПараметры.Вставить("РазрешитьОплатуВозвратБезПодтвержденияОтПлатежнойСистемы", Ложь);
	ПереданныеПараметры.Вставить("Товары", Новый Массив);
	ПереданныеПараметры.Вставить("СсылочныйНомер", "");
	ПереданныеПараметры.Вставить("ВидОперацииПродажа", 	Истина);
	ПереданныеПараметры.Вставить("КассоваяСсылка", 		Неопределено); // данные кассового qr-кода
	ПереданныеПараметры.Вставить("ОтменаЗаказа", 		Неопределено); // данные для отмены заказа
	
	// параметры оплаты через чек ККМ
	ПереданныеПараметры.Вставить("Магазин");
	ПереданныеПараметры.Вставить("Организация");
	ПереданныеПараметры.Вставить("КассаККМ");
	ПереданныеПараметры.Вставить("КлючСвязиТПЧеков", 0);
	
	// параметры возврата
	ПереданныеПараметры.Вставить("ИдентификаторОплаты", "");
	ПереданныеПараметры.Вставить("ВидОплатыВозврата");
	ПереданныеПараметры.Вставить("ДокументВозврата");
	
	// интерфейс
	ПереданныеПараметры.Вставить("НадписьЗаголовок", "");
	ПереданныеПараметры.Вставить("ЦветТекстаЗаголовка");
	ПереданныеПараметры.Вставить("КраткоеПредставлениеКоманд", Истина);
	
	// служебные
	ПереданныеПараметры.Вставить("ОпределитьСтатусВозвратаПозже", 	Ложь);
	ПереданныеПараметры.Вставить("ОпределитьСтатусОплатыПозже", 	Ложь);
	ПереданныеПараметры.Вставить("НастройкиФормыИнтеграции",		Новый Структура());
	ПереданныеПараметры.Вставить("НастройкиИнтеграции");
	ПереданныеПараметры.Вставить("ТипПлатежнойСистемы");
	ПереданныеПараметры.Вставить("ИдентификаторQRКода");
	ПереданныеПараметры.Вставить("КартинкаQRКода");
	ПереданныеПараметры.Вставить("ДанныеQRКода");
	ПереданныеПараметры.Вставить("ИдентификаторЗаданияВозврата");
	ПереданныеПараметры.Вставить("ИдентификаторЗаданияПроверкиСтатуса");
	ПереданныеПараметры.Вставить("ИдентификаторЗаданияФормированияQRКода");
	ПереданныеПараметры.Вставить("ИдентификаторЗаданияОтменаЗаказа");
	ПереданныеПараметры.Вставить("БылоПодтверждениеКодомАдминистратора", Ложь);
	ПереданныеПараметры.Вставить("БылоПодтверждениеПлатежнойСистемы", Ложь);
	ПереданныеПараметры.Вставить("ПодключаемоеОборудование");
    ПереданныеПараметры.Вставить("АвтономнаяККТ", Ложь);
	
	// устаревшие параметры
	ПереданныеПараметры.Вставить("ВидОперации");
	
	Возврат ПереданныеПараметры;	
	
КонецФункции

Функция НовыйНастройкиТорговойТочки() Экспорт
	
	Операции = Новый Структура;
	Операции.Вставить("ОтменаЗаказа",                  	Ложь); 
	Операции.Вставить("ОтменаОплаты",                  	Ложь);
	Операции.Вставить("СрокЖизниQRКода",               	Ложь);
	Операции.Вставить("ПлатежнаяСсылка",               	Ложь);
	Операции.Вставить("ВыборПлатежнойСистемыВозврата", 	Ложь);
	Операции.Вставить("КассовыеСсылки", 				Ложь);
	Операции.Вставить("Идентификатор",                 	Ложь);
	Операции.Вставить("СинонимСистемы",                	Ложь);
	Операции.Вставить("Используется",                  	Ложь);
	Операции.Вставить("РазрешенВозврат",               	Истина);
	Операции.Вставить("ЗаписыватьДокументПередОплатой", Ложь);
	Операции.Вставить("ОтменаЗаказаДанные",             Неопределено); // данные для отмены заказа
	
	Возврат Операции;
	
КонецФункции

Функция СтрокаТоваров() Экспорт
	
	ТоварыУслуги = Новый Структура;
	ТоварыУслуги.Вставить("Артикул");
	ТоварыУслуги.Вставить("Наименование");
	ТоварыУслуги.Вставить("ЕдиницаИзмерения");
	ТоварыУслуги.Вставить("Количество");
	ТоварыУслуги.Вставить("Цена");
	ТоварыУслуги.Вставить("Сумма");
	ТоварыУслуги.Вставить("СуммаСкидки");
	
	Возврат ТоварыУслуги;	
	
КонецФункции

Функция ТекущаяСтраница(Страница, ТекущийСлой = "") Экспорт
	
	РезультатФункции = Ложь;
	Если ТекущийСлой = "" Тогда
		РезультатФункции = (Страница.Родитель.ТекущаяСтраница = Страница);
	Иначе
		РезультатФункции = (Страница = ТекущийСлой);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура УстановитьТекущуюСтраницу(Страница) Экспорт
	
	Страница.Родитель.ТекущаяСтраница 				= Страница;
	Страница.Родитель.ТекущаяСтраница.Доступность   = Истина;
	
КонецПроцедуры

Процедура НастроитьФормуПоВидуОплаты(Форма, ВидыОплат) Экспорт
	
	ПараметрыВидаОплат 	= Новый Структура;
	КлючПоиска			= Новый Структура("ИдентификаторПС", Форма.ПлатежнаяСистема_НоваяЗаявка.ИдентификаторПС);
	
	СтрокиОплаты		= ВидыОплат.НайтиСтроки(КлючПоиска);
	
	ПараметрыВидаОплат.Вставить("Интеграция", 					СтрокиОплаты[0].Интеграция);
	ПараметрыВидаОплат.Вставить("ТипПлатежнойСистемы",			СтрокиОплаты[0].ПлатежнаяСистема);
	
	НастройкиИнтеграции = ИнтеграцияСПлатежнымиСистемамиРМКВызовСервера.НастройкиТорговойТочки(
		СтрокиОплаты[0].Интеграция,
		Форма.ПлатежнаяСистема_НоваяЗаявка.КассаККМ);
	
	ПараметрыВидаОплат.Вставить("НастройкиИнтеграции", НастройкиИнтеграции);
	
	ЗаполнитьЗначенияСвойств(Форма.ПлатежнаяСистема_НоваяЗаявка, ПараметрыВидаОплат);
	
	Если Не Форма.ПлатежнаяСистема_НоваяЗаявка.ВидОперацииПродажа Тогда
		
		Форма.Элементы.ПлатежнаяСистема.СписокВыбора.Очистить();
			
		Если НастройкиИнтеграции.ВыборПлатежнойСистемыВозврата Тогда
			
			Для Каждого КлючЗначение Из НастройкиИнтеграции.ПлатежныеСистемыВозврата Цикл
				
				Форма.Элементы.ПлатежнаяСистема.СписокВыбора.Добавить(
					КлючЗначение.Ключ,
					КлючЗначение.Значение);
					
				Форма.Элементы.ПлатежнаяСистема.СписокВыбора.СортироватьПоПредставлению();
				
			КонецЦикла;
			
		КонецЕсли;
		
		Форма.Элементы.ПлатежнаяСистема.Видимость 		= Форма.Элементы.ПлатежнаяСистема.СписокВыбора.Количество() > 0;
		
	КонецЕсли;
	
	НастроитьФормуПоИнтеграции(Форма);
	
КонецПроцедуры

Процедура НастроитьФормуПоИнтеграции(Форма, Знач ОписаниеОшибки = "", Знач ТекстДлительнойОперации = "") Экспорт
	
	Элементы				= Форма.Элементы;
	НастройкиИнтеграции		= Форма.ПлатежнаяСистема_НоваяЗаявка.НастройкиИнтеграции;
	ЭтоПродажа 				= Форма.ПлатежнаяСистема_НоваяЗаявка.ВидОперацииПродажа;
	ЕстьКомандаОтложить		= Форма.Элементы.Найти("ФормаОтложить") <> Неопределено;
    ЕстьГруппаПовтор		= Форма.Элементы.Найти("ГруппаQRКодПовтор") <> Неопределено;
	
	ВывестиИнструкцию		= Ложь;
	ВывестиПечатьПречека	= Ложь;
	СкрытьКомандуОтложить	= Ложь;
	
	ПустаяПодсказка			= "";
	ПодсказкаВозврата		= НСтр("ru = 'Денежные средства будут возвращены
								|на тот же банковский счет покупателя.'");
	
	ИспользоватьСлои 		= ИнтерфейсРМКСлужебныйКлиентСервер.ИспользоватьСлои(Форма);
	
	ТекущийСлойПанелиПлатежнойСистемы = ?(ИспользоватьСлои, 
		ИнтерфейсРМКСлужебныйКлиентСервер.ПолучитьТекущийСлойГруппы(Форма.Элементы.ГруппаСлоевПанелиДействийПлатежнойСистемы), 
		"");
		
	Если ТекущаяСтраница(Форма.Элементы.ГруппаОсновнаяСтраница, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		Если Форма.Элементы.Найти("ИтогПоОрганизации") <> Неопределено Тогда
			Элементы.ИтогПоОрганизации.Заголовок = ?(ЭтоПродажа, НСтр("ru = 'К оплате'"), НСтр("ru = 'К возврату'"));
		КонецЕсли;
		
		Элементы.ГруппаВозврат.Видимость		 		= Не ЭтоПродажа;
		Элементы.ГруппаПродажа.Видимость		 		= ЭтоПродажа;
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость			= Ложь;
		КонецЕсли;
		
		Если Не ЭтоПродажа Тогда
			Элементы.ФормаЗавершитьОплату.Видимость		= Истина;
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Доступность		= Истина;
		Элементы.ФормаЗавершитьОплату.Заголовок  		= ?(ЭтоПродажа, НСтр("ru = 'Создать QR-код'"), НСтр("ru = 'Вернуть'"));
		Элементы.ФормаОтменитьОплату.Видимость  		= Ложь;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ?(ЭтоПродажа, ПустаяПодсказка, ПодсказкаВозврата);
		Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		
		Если ЗначениеЗаполнено(Форма.ПлатежнаяСистема_НоваяЗаявка.ВидОплатыПлатежнойСистемы) Тогда
			Форма.ТекущийЭлемент						= Форма.Элементы.ФормаЗавершитьОплату;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаДлительнаяОперация, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость			= Ложь;
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Доступность 		= Ложь;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.Оплачивается Тогда 
			
			Элементы.ДекорацияНадписьДлительнаяОперация.Заголовок = ?(ЭтоПродажа, 
																		НСтр("ru = 'Получение подтверждения оплаты. Пожалуйста, подождите...'"),
																		НСтр("ru = 'Получения подтверждения возврата. Пожалуйста, подождите...'"));
																		
		Иначе
																		
			Элементы.ДекорацияНадписьДлительнаяОперация.Заголовок = ?(ЭтоПродажа, 
																		НСтр("ru = 'Получение QR-кода оплаты платежной системы. Пожалуйста, подождите...'"),
																		НСтр("ru = 'Выполнение возврата в платежной системе. Пожалуйста, подождите...'"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстДлительнойОперации) Тогда
			Элементы.ДекорацияНадписьДлительнаяОперация.Заголовок = ТекстДлительнойОперации; 
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаQRКод, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		ЕстьКомандаИнструкция	= Форма.Элементы.Найти("ФормаИнструкция") <> Неопределено;
		
		Если ЕстьКомандаОтложить Тогда
			
			Элементы.ФормаОтложить.Видимость		= Не ЕстьКомандаИнструкция;
			Элементы.ФормаОтложить.Заголовок 		= НСтр("ru = 'Инструкция'");
			
			ВывестиИнструкцию 						= Истина;   
			СкрытьКомандуОтложить                   = Истина;
			
		КонецЕсли;
		
		ВывестиПечатьПречека  = Истина;
		
		Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Пречек'");
		Элементы.ФормаЗавершитьОплату.Видимость			= Истина;
		Элементы.ФормаЗавершитьОплату.Доступность 		= Истина;
		Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаПодтверждение, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость			= Ложь;
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= Истина;
		Элементы.ФормаЗавершитьОплату.Доступность		= Истина;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.КраткоеПредставлениеКоманд Тогда
			Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Подтв.'");// АПК:1036 для обратной совместимости
		Иначе
			Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Подтвердить'");
		КонецЕсли;
		
		Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		
		Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		
	ИначеЕсли ЕстьГруппаПовтор
		И ТекущаяСтраница(Форма.Элементы.ГруппаQRКодПовтор, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		Если ЕстьКомандаОтложить Тогда
			
			Элементы.ФормаОтложить.Видимость		= Не Форма.ПлатежнаяСистема_НоваяЗаявка.Оплачивается;
			Элементы.ФормаОтложить.Заголовок 		= НСтр("ru = 'Отложить чек'");

			ВывестиИнструкцию 	  					= Истина;
			
		КонецЕсли;
		
		ВывестиПечатьПречека  = Истина;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= Ложь;
		Элементы.ФормаЗавершитьОплату.Доступность		= Ложь;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.РазрешитьОплатуВозвратБезПодтвержденияОтПлатежнойСистемы Тогда
			Элементы.ФормаЗавершитьПринудительно.Видимость	= Истина;
		Иначе
			Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		КонецЕсли;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.КраткоеПредставлениеКоманд Тогда
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтв. оплату'"), // АПК:1036 для обратной совместимости
																	НСтр("ru = 'Подтв. возврат'")); // АПК:1036 для обратной совместимости
																	
		Иначе
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтвердить оплату'"), 
																	НСтр("ru = 'Подтвердить возврат'"));
																	
		КонецЕсли;
		
		
		Форма.ТекущийЭлемент 							= Элементы.ФормаЗавершитьПринудительно;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаВыполняется, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		Элементы.ДекорацияНадписьВыполняется.Заголовок 	= ?(ЭтоПродажа, 
																НСтр("ru = 'Платежная система не подтвердила оплату.'"),
																НСтр("ru = 'Платежная система не подтвердила возврат.'"));
		
		
		Если ЕстьКомандаОтложить Тогда
			
			Элементы.ФормаОтложить.Видимость		= Не Форма.ПлатежнаяСистема_НоваяЗаявка.Оплачивается;
			Элементы.ФормаОтложить.Заголовок 		= НСтр("ru = 'Отложить чек'");

		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= Истина;
		Элементы.ФормаЗавершитьОплату.Доступность		= Истина;
		Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Повторить'");
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.РазрешитьОплатуВозвратБезПодтвержденияОтПлатежнойСистемы Тогда
			Элементы.ФормаЗавершитьПринудительно.Видимость	= Истина;
		Иначе
			Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		КонецЕсли;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.КраткоеПредставлениеКоманд Тогда
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтв. оплату'"), // АПК:1036 для обратной совместимости
																	НСтр("ru = 'Подтв. возврат'")); // АПК:1036 для обратной совместимости
																	
		Иначе
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтвердить оплату'"), 
																	НСтр("ru = 'Подтвердить возврат'"));
																	
		КонецЕсли;
		
		
		Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаЗавершение, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		АвтоматическаяФискализация = Форма.Элементы.Найти("ДекорацияАвтоматическаяФискализация") <> Неопределено;
		
		Если НЕ Форма.ПлатежнаяСистема_НоваяЗаявка.БылоПодтверждениеПлатежнойСистемы Тогда
			
			Элементы.ДекорацияНадписьУспешноеЗавершение.Заголовок = ?(ЭтоПродажа, 
																		НСтр("ru = 'Ручное подтверждение оплаты.'"), 
																		НСтр("ru = 'Ручное подтверждение возврата.'"));
			
		Иначе
			
			Элементы.ДекорацияНадписьУспешноеЗавершение.Заголовок = ?(ЭтоПродажа, 
																		НСтр("ru = 'Оплата успешно выполнена.'"), 
																		НСтр("ru = 'Возврат успешно выполнен.'"));
																		
		КонецЕсли;
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость			= Ложь;
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= Не АвтоматическаяФискализация;
		Элементы.ФормаЗавершитьОплату.Доступность		= Истина;
		Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Готово'");
		Элементы.ФормаЗавершитьПринудительно.Видимость	= Ложь;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Элементы.ФормаОтменитьОплату.Видимость  		= Не АвтоматическаяФискализация 
															И НастройкиИнтеграции.ОтменаОплаты 
															И Форма.ПлатежнаяСистема_НоваяЗаявка.БылоПодтверждениеПлатежнойСистемы;
															
		Если Не АвтоматическаяФискализация Тогда
			Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаОшибка, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
		ЕстьКомандаЗакрыть = Форма.Элементы.Найти("ДекорацияЗакрытьФормуПлатежныеСистемы") <> Неопределено;
        ОписаниеОшибки     = ?(ЗначениеЗаполнено(ОписаниеОшибки), ОписаниеОшибки, НСтр("ru = 'Неизвестная ошибка'"));
		
		ЧастиСтрок = Новый Массив;
		ЧастиСтрок.Добавить(ОписаниеОшибки);
		ЧастиСтрок.Добавить(Символы.ПС);
		ЧастиСтрок.Добавить(Символы.ПС);
		
		Элементы.ДекорацияОписаниеОшибки.Заголовок		= Новый ФорматированнаяСтрока(ЧастиСтрок);
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость			= Ложь;
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= НЕ ЕстьКомандаЗакрыть;
		Элементы.ФормаЗавершитьОплату.Доступность		= Истина;
		Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Закрыть'");
		Элементы.ФормаЗавершитьПринудительно.Видимость 	= Ложь;
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		
	ИначеЕсли ТекущаяСтраница(Форма.Элементы.ГруппаОшибкаПолученияСтатуса, ТекущийСлойПанелиПлатежнойСистемы) Тогда
		
        ОписаниеОшибки     = ?(ЗначениеЗаполнено(ОписаниеОшибки), ОписаниеОшибки, НСтр("ru = 'Неизвестная ошибка'"));

		ЧастиСтрок = Новый Массив;
		ЧастиСтрок.Добавить(ОписаниеОшибки);
		ЧастиСтрок.Добавить(Символы.ПС);
		ЧастиСтрок.Добавить(Символы.ПС);
		
		Элементы.ДекорацияОписаниеОшибкиПолученияСтатуса.Заголовок 	= Новый ФорматированнаяСтрока(ЧастиСтрок);
		
		Если ЕстьКомандаОтложить Тогда
			Элементы.ФормаОтложить.Видимость		= Не Форма.ПлатежнаяСистема_НоваяЗаявка.Оплачивается;
			Элементы.ФормаОтложить.Заголовок 		= НСтр("ru = 'Отложить чек'");
		КонецЕсли;
		
		Элементы.ФормаЗавершитьОплату.Видимость			= Истина;
		Элементы.ФормаЗавершитьОплату.Доступность 		= Истина;
		Элементы.ФормаЗавершитьОплату.Заголовок 		= НСтр("ru = 'Повторить'");
		Элементы.ФормаЗавершитьОплатуРасширеннаяПодсказка.Заголовок = ПустаяПодсказка;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.РазрешитьОплатуВозвратБезПодтвержденияОтПлатежнойСистемы Тогда
			Элементы.ФормаЗавершитьПринудительно.Видимость 	= Истина;
		Иначе
			Элементы.ФормаЗавершитьПринудительно.Видимость 	= Ложь;
		КонецЕсли;
		
		Если Форма.ПлатежнаяСистема_НоваяЗаявка.КраткоеПредставлениеКоманд Тогда
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтв. оплату'"),// АПК:1036 для обратной совместимости 
																	НСтр("ru = 'Подтв. возврат'"));// АПК:1036 для обратной совместимости
																	
		Иначе
			
			Элементы.ФормаЗавершитьПринудительно.Заголовок	= ?(ЭтоПродажа, 
																	НСтр("ru = 'Подтвердить оплату'"), 
																	НСтр("ru = 'Подтвердить возврат'"));
																	
		КонецЕсли;
		
		Форма.ТекущийЭлемент 							= Форма.Элементы.ФормаЗавершитьОплату;
		
	КонецЕсли;
	
	ВывестиИнструкциюИПечатьПречекРМК(Форма, ВывестиИнструкцию, ВывестиПечатьПречека, СкрытьКомандуОтложить);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВывестиИнструкциюИПечатьПречекРМК(Форма, ВывестиИнструкцию, ВывестиПечатьПречека, СкрытьКомандуОтложить)

	Если Форма.Элементы.Найти("КомандаПлатежнаяСистема_Инструкция") <> Неопределено Тогда
		
		Форма.Элементы.КомандаПлатежнаяСистема_Инструкция.Видимость = ВывестиИнструкцию;
		
		Если ВывестиИнструкцию
			И СкрытьКомандуОтложить Тогда
			Форма.Элементы.ФормаОтложить.Видимость					= Ложь;
		КонецЕсли;
		
	КонецЕсли;

	Если Форма.Элементы.Найти("КомандаПлатежнаяСистема_Пречек") <> Неопределено Тогда
		
		Форма.Элементы.КомандаПлатежнаяСистема_Пречек.Видимость 	= ВывестиПечатьПречека;
		
		Если ВывестиПечатьПречека Тогда
			Форма.Элементы.ФормаЗавершитьОплату.Видимость			= Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

