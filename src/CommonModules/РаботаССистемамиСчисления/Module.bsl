///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает строчное представление десятичного числа в другой системе счисления
// 
// Параметры:
//  ЧислоЗначение - Число
//  НовоеОснование - Число
// 
// Возвращаемое значение:
//  Строка - Представление числа в другой системе счисления
//
Функция ДругуюИзДесятичной(Знач ЧислоЗначение, НовоеОснование) Экспорт
	
	СимволыСистем = СимволыСистем();
	
	Результат = "";
	
	Пока ЧислоЗначение > 0 Цикл
		
		НужныйСимвол = Сред(СимволыСистем, ЧислоЗначение % НовоеОснование + 1, 1);
		Результат = СтрШаблон("%1%2", НужныйСимвол, Результат);
		
		ЧислоЗначение = Цел(ЧислоЗначение / НовоеОснование);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает десятичное число из его строчного представления в другой системе счисления
// 
// Параметры:
//  ПредставлениеЧисла - Строка
//  ОснованиеСистемы - Число
// 
// Возвращаемое значение:
//  Число - десятичное число
//
Функция ДесятичнуюИзДругой(Знач ПредставлениеЧисла, ОснованиеСистемы) Экспорт
	
	ПредставлениеЧисла = ВРег(ПредставлениеЧисла);
	
	СимволыСистем = СимволыСистем();
	
	Результат = 0;
	
	Множитель = 1;
	Индекс = СтрДлина(ПредставлениеЧисла);
	Пока Индекс >= 1 Цикл
		ЧисловоеЗначениеСимвола = СтрНайти(СимволыСистем, Сред(ПредставлениеЧисла, Индекс, 1)) - 1;
		Результат = Результат + Множитель * ЧисловоеЗначениеСимвола;
		
		Множитель = Множитель * ОснованиеСистемы;
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СимволыСистем()
	Возврат "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ";
КонецФункции

#КонецОбласти
