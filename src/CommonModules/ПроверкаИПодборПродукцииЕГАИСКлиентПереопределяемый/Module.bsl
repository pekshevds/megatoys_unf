#Область СлужебныйПрограммныйИнтерфейс

// Заполняет специфичные параметры открытия формы проверки и подбора табачной продукции в зависимости от точки вызова
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма из которой происходит открытие формы проверки и подбора
//  Параметры - (См. ПроверкаИПодборПродукцииЕГАИСКлиент.ПараметрыОткрытияФормыПроверкиИПодбора)
Процедура ПриУстановкеПараметровОткрытияФормыПроверкиИПодбора(Форма, Параметры) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ЧекККМ.Форма.ФормаДокумента_РМК"
		Или Форма.ИмяФормы = "Документ.ЧекККМВозврат.Форма.ФормаДокумента" Тогда
		
		Параметры.ОрганизацияЕГАИС = ИнтеграцияЕГАИСУНФВызовСервера.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
			Форма.Объект.Организация,
			Форма.Объект.СтруктурнаяЕдиница);
		
		Параметры.НачальныйСтатусПроверки  = ПредопределенноеЗначение("Перечисление.СтатусыПроверкиНаличияПродукцииИС.ВНаличии");
		
		ПараметрыОповещенияПриЗакрытии = Новый Структура;
		ПараметрыОповещенияПриЗакрытии.Вставить("Форма", Форма);
		ПараметрыОповещенияПриЗакрытии.Вставить("ВидПродукции", ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная"));
		
		Параметры.ОписаниеОповещенияПриЗакрытии = Новый ОписаниеОповещения(
			"ПриЗакрытииФормыПроверкиИПодбора",
			СобытияФормИСУНФКлиент,
			ПараметрыОповещенияПриЗакрытии);
		
		Объект = Новый Структура;
		Объект.Вставить("ОрганизацияЕГАИС", Параметры.ОрганизацияЕГАИС);
		Объект.Вставить("Ссылка",           Форма.Объект.Ссылка);
		Объект.Вставить("АкцизныеМарки",    Форма.Объект.АкцизныеМарки);
		Объект.Вставить("Запасы",           Форма.Объект.Запасы);
		
		Параметры.АдресДанныхПроверяемойАлкогольнойПродукции =
			ИнтеграцияЕГАИСУНФВызовСервера.АдресДанныхПроверкиАлкогольнойПродукции(
				Неопределено,
				Объект,
				Форма.УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
