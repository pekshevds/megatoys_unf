
#Область ПрограммныйИнтерфейс

Функция ИмяЭлементаДляРазмещения(Форма, Элемент) Экспорт
	
	ОтборРеквизитов = Новый Структура("ИмяРеквизита", Элемент.Имя);
	ОписаниеРеквизитаКИ = Форма["КонтактнаяИнформацияОписаниеДополнительныхРеквизитов"].НайтиСтроки(ОтборРеквизитов);
	Если ОписаниеРеквизитаКИ.Количество() <> 0 Тогда
		ИмяЭлементаДляРазмещения = ОписаниеРеквизитаКИ[0].ИмяЭлементаДляРазмещения;
	Иначе
		ИмяЭлементаДляРазмещения = Неопределено;
	КонецЕсли;
	
	Возврат ИмяЭлементаДляРазмещения;
	
КонецФункции

Функция ОбработчикДействияПозвонитьПоТелефону(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	Если ПустаяСтрока(КонтактнаяИнформация.Представление)
		И ЗначениеЗаполнено(ДополнительныеПараметры.ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для совершения звонка заполните номер телефона.'"), , ДополнительныеПараметры.ИмяРеквизита);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТелефонияКлиент.ПоддерживаютсяВызовы() Тогда
		ТелефонияКлиент.ПозвонитьПоНомеру(КонтактнаяИнформация.Представление, ДополнительныеПараметры.ВладелецКонтактнойИнформации);
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ТипСобытия", ТипСобытияПоТипуКонтактнойИнформации(КонтактнаяИнформация.Тип));
		ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
		ОткрытьФорму("Документ.Событие.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), ДополнительныеПараметры.Форма);
	КонецЕсли;
	
КонецФункции

Функция ОбработчикДействияНаписатьTelegram(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	НомерТелефона = УправлениеКонтактнойИнформациейУНФКлиентСервер.НомерТелефонаВМеждународномФормате(КонтактнаяИнформация.Представление);
	Ссылка = "https://t.me/%1";
	Ссылка = СтрШаблон(Ссылка, НомерТелефона);
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Ссылка);
	
КонецФункции

Функция ОбработчикДействияНаписатьWhatsApp(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	НомерТелефона = УправлениеКонтактнойИнформациейУНФКлиентСервер.НомерТелефонаВМеждународномФормате(КонтактнаяИнформация.Представление);
	Ссылка = "https://wa.me/%1";
	Ссылка = СтрШаблон(Ссылка, НомерТелефона);
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Ссылка);
	
КонецФункции

Функция ОбработчикДействияЛичнаяВстреча(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСобытия", ТипСобытияПоТипуКонтактнойИнформации(КонтактнаяИнформация.Тип));
	ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), ДополнительныеПараметры.Форма);
	
КонецФункции

Функция ОбработчикДействияПрочееСобытие(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСобытия", ТипСобытияПоТипуКонтактнойИнформации(КонтактнаяИнформация.Тип));
	ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ДополнительныеПараметры.ВладелецКонтактнойИнформации);
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения), ДополнительныеПараметры.Форма);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФукнции

Функция ТипСобытияПоТипуКонтактнойИнформации(ТипКИ)
	
	Если ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Skype") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЛичнаяВстреча");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЭлектронноеПисьмо");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.ВебСтраница") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Другое") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок");
	ИначеЕсли ТипКИ = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Факс") Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок");
	Иначе
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ПустаяСсылка");
	КонецЕсли;
	
	Возврат ТипСобытия;
	
КонецФункции

#КонецОбласти
