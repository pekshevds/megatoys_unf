#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьУсловноеОформлениеСборкиЗаказов(УсловноеОформление, ЕстьУслуги = Ложь) Экспорт
	
	// Красная строка
	УсловноеОформлениеКраснаяСтрока = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеКраснаяСтрока, "ЦветТекста", WebЦвета.Красный);
	РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеКраснаяСтрока,
		"ЗапасыКоличествоСобрано,ЗапасыРасхождениеПредставление");
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеКраснаяСтрока.Отбор,
		"Объект.Запасы.Расхождение", 0, ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеКраснаяСтрока.Отбор,
		"Объект.Запасы.КоличествоСобрано", 0, ВидСравненияКомпоновкиДанных.Равно);
	
	// Оранжевая строка
	УсловноеОформлениеОранжеваяСтрока = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеОранжеваяСтрока, "ЦветТекста", ЦветаСтиля.ЦветТекстаПредупреждениеОбОкончанииДоговора);
	РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеОранжеваяСтрока,
		"ЗапасыКоличествоСобрано,ЗапасыРасхождениеПредставление");
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеОранжеваяСтрока.Отбор,
		"Объект.Запасы.Расхождение", 0, ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеОранжеваяСтрока.Отбор,
		"Объект.Запасы.КоличествоСобрано", 0, ВидСравненияКомпоновкиДанных.НеРавно);
	
	// Колонка Расхождение
	УсловноеОформлениеКолонкаРасхождение = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеКолонкаРасхождение.Отбор,
		"Объект.Запасы.Расхождение", 0, ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеКолонкаРасхождение, "ЗапасыРасхождениеПредставление");
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеКолонкаРасхождение, "Шрифт", ПолужирныйШрифт());
	
	// Зеленая строка
	УсловноеОформлениеЗеленаяСтрока = УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеЗеленаяСтрока, "ЦветТекста", WebЦвета.Зеленый);
	РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеЗеленаяСтрока, "ЗапасыКоличествоСобрано");
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеЗеленаяСтрока.Отбор,
		"Объект.Запасы.Расхождение", 0, ВидСравненияКомпоновкиДанных.Равно);
	
	// Услуги
	Если ЕстьУслуги Тогда
		УсловноеОформлениеУслуга = УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеУслуга.Отбор, "Объект.Запасы.ТипНоменклатурыЗапас", Ложь);
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(УсловноеОформлениеУслуга.Отбор, "Объект.Запасы.ЭтоРазделитель", Ложь);
		РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеУслуга, "ЗапасыРасхождениеПредставление");
		РаботаСФормой.ДобавитьОформляемыеПоля(УсловноеОформлениеУслуга, "ЗапасыКоличествоСобрано");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеУслуга, "ЦветТекста", ЦветаСтиля.ТекстВторостепеннойНадписи);
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеУслуга, "Текст", НСтр("ru = '<Для товаров>'"));
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(УсловноеОформлениеУслуга, "Доступность", Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСтатусСборкиЗаказа(ДокументОбъект, ЕстьУслуги = Ложь, ОсновнойВариантКП = 0) Экспорт
	
	СборкаНачата = Ложь;
	СборкаЗавершена = Истина;
	
	Если ЕстьУслуги Или ОсновнойВариантКП <> 0 Тогда
		ОтборСтрок = Новый Структура;
		Если ЕстьУслуги Тогда
			ОтборСтрок.Вставить("ТипНоменклатурыЗапас", Истина);
		КонецЕсли;
		Если ОсновнойВариантКП <> 0 Тогда
			ОтборСтрок.Вставить("НомерВариантаКП", ОсновнойВариантКП);
		КонецЕсли;
		СтрокиЗаказа = ДокументОбъект.Запасы.НайтиСтроки(ОтборСтрок);
	Иначе
		СтрокиЗаказа = ДокументОбъект.Запасы;
	КонецЕсли;
	
	
	Для Каждого СтрокаЗаказа Из СтрокиЗаказа Цикл
		Если СтрокаЗаказа.КоличествоСобрано > 0 Тогда
			СборкаНачата = Истина;
		КонецЕсли;
		Если СтрокаЗаказа.Количество > СтрокаЗаказа.КоличествоСобрано Тогда
			СборкаЗавершена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Не СборкаНачата Тогда
		ДокументОбъект.СтатусСборки = 0;
	ИначеЕсли Не СборкаЗавершена Тогда
		ДокументОбъект.СтатусСборки = 1;
	Иначе
		ДокументОбъект.СтатусСборки = 2;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолужирныйШрифт()
	Возврат Новый Шрифт(, , Истина);
КонецФункции

#КонецОбласти