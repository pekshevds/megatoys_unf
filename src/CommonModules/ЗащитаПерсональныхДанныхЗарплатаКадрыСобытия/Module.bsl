#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗарегистрироватьИзменениеКадровойИсторииДляПерсональныхДанныхПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗащитаПерсональныхДанных.ИспользоватьУничтожениеПерсональныхДанныхСубъектов() Тогда
		Возврат;
	КонецЕсли;
	
	ИзменившиесяДанные = Источник.ТаблицаИзменившихсяДанныхНабора();
	Если ИзменившиесяДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.Количество() > 0 Тогда
		ЕстьОбрабатываемоеСобытие = Ложь;
		Для Каждого ЗаписьРегистра Из Источник Цикл
			Если ЗаписьРегистра.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
				Или ЗаписьРегистра.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
				ЕстьОбрабатываемоеСобытие = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьОбрабатываемоеСобытие Тогда
			Возврат;
		КонецЕсли;
		
		ДатаСобытия = Источник[0].Период;
	Иначе
		ДатаСобытия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Субъекты = ОбщегоНазначения.ВыгрузитьКолонку(ИзменившиесяДанные, "ФизическоеЛицо", Истина);

	ЗащитаПерсональныхДанных.ДобавитьСубъектыДляРасчетаСроковХранения(Субъекты, ДатаСобытия);
	
КонецПроцедуры

Процедура ЗарегистрироватьИзменениеФизическихЛицКандидатовДляПерсональныхДанныхПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗащитаПерсональныхДанных.ИспользоватьУничтожениеПерсональныхДанныхСубъектов() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		МодульПодборПерсонала.ДобавитьСубъектыДляРасчетаСроковХранения(Источник);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти