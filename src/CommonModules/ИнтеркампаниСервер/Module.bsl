
#Область ПрограммныйИнтерфейс

// Формирует таблицу движений по учету резервов товаров организаций
// Таблицы значений сохраняет в свойствах структуры "СтруктураДополнительныеСвойства".
//
// Параметры:
//  ДокументСсылка					 - 	ДокументСсылка - Ссылка на проводимый документ
//  СтруктураДополнительныеСвойства	 - 	Структура - Структура данных для проведения 
//
Процедура СформироватьТаблицаРезервыТоваровОрганизаций(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ПередачаТоваровМеждуОрганизациями") Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", 
			Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли; 
	
	ОрганизацииИнтеркампани = ОрганизацииОстатков(
		СтруктураДополнительныеСвойства.ДляПроведения.Организация);
		
	Если ОрганизацииИнтеркампани.Количество() < 2 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", 
			Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ВидОперацииЗаказа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ВидОперации", Истина);
		ЭтоЗаказПокупателя = (ВидОперацииЗаказа = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу);
		ПервичноеПроведение = ?(ЭтоЗаказПокупателя, СтруктураДополнительныеСвойства.ПервичноеПроведение, Ложь);
	Иначе
		ЭтоЗаказПокупателя = Ложь; 
		ПервичноеПроведение = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));
	Запрос.УстановитьПараметр("УчетГТД", ПолучитьФункциональнуюОпцию("УчетГТД"));
	Запрос.УстановитьПараметр("УчетСерий", ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры"));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииИнтеркампани);
	Запрос.УстановитьПараметр("ДатаДокумента", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("ЭтоЗаказНаПродажу", ЭтоЗаказПокупателя);
	Запрос.УстановитьПараметр("ПервичноеПроведение", ПервичноеПроведение);
	Запрос.УстановитьПараметр("ПолучатьОстаткиПоСериям", Не ЭтоЗаказПокупателя Или (ЭтоЗаказПокупателя И ПервичноеПроведение));
	Запрос.Текст = ТекстЗапросаРезервыТоваровОрганизаций();
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаОстатковСклад = РезультатЗапроса[РезультатЗапроса.Количество() - 6].Выгрузить();
	ТаблицаОстатковСклад.Индексы.Добавить(СтрШаблон("%1, %2", ПоляСкладскойУчет(), "ОстатокБезПередачи"));
	ТаблицаОстатковЗапасов = РезультатЗапроса[РезультатЗапроса.Количество() - 5].Выгрузить();
	ТаблицаОстатковЗапасов.Индексы.Добавить(СтрШаблон("%1, %2", ПоляЗапасы(), "ОстатокБезПередачи"));
	ТаблицаОстатковЗапасов.Индексы.Добавить(СтрШаблон("%1, %2", ПоляЗапасы(), "Организация"));
	ТаблицаОстатковГТД = РезультатЗапроса[РезультатЗапроса.Количество() - 4].Выгрузить();
	ТаблицаОстатковГТД.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетГТД(), "ОстатокБезПередачи"));
	ТаблицаОстатковГТД.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетГТД(), "Организация"));
	ТаблицаОстатковСерий = РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ТаблицаОстатковСерий.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетСерий(), "ОстатокБезПередачи"));
	ТаблицаОстатковСерий.Индексы.Добавить(СтрШаблон("%1, %2", ПоляУчетСерий(), "Организация"));
	ТаблицаРезервы = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	ТаблицаРезервы.Индексы.Добавить(СтрШаблон("%1, %2", ПоляРезервы(), "ОстатокБезПередачи"));
	ТаблицыТоваров = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	ТаблицаРезервыТоваровОрганизаций = ТаблицыТоваров.СкопироватьКолонки();
	
	ПараметрыПодбораОстатков = Новый Структура;
	ПараметрыПодбораОстатков.Вставить("ТаблицаРезервы", ТаблицаРезервы);
	ПараметрыПодбораОстатков.Вставить("ТаблицаОстатковСклад", ТаблицаОстатковСклад);
	ПараметрыПодбораОстатков.Вставить("ТаблицаОстатковЗапасов", ТаблицаОстатковЗапасов);
	ПараметрыПодбораОстатков.Вставить("ТаблицаОстатковГТД", ТаблицаОстатковГТД);
	ПараметрыПодбораОстатков.Вставить("ТаблицаОстатковСерий", ТаблицаОстатковСерий);
	ПараметрыПодбораОстатков.Вставить("ТаблицаРезервыТоваровОрганизаций", ТаблицаРезервыТоваровОрганизаций);
		
	Ошибки = Новый Массив;
	КонтролироватьОстатки = Константы.КонтролироватьОстаткиПриПроведении.Получить();
	Для Каждого СтрокаТовара Из ТаблицыТоваров Цикл
		
		СтруктураОтбора = Новый Структура(ПоляЗапасы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиЗапасы = ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиСклад = ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетГТД());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиГТД = ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетСерий());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиСерии = ТаблицаОстатковСерий.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляРезервы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Истина);
		СтрокиРезервы = ТаблицаРезервы.НайтиСтроки(СтруктураОтбора);
		
		ПараметрыВызова = Новый Структура;
		ПараметрыВызова.Вставить("СтрокаТовара", СтрокаТовара);
		ПараметрыВызова.Вставить("СтрокиЗапасы", СтрокиЗапасы);
		ПараметрыВызова.Вставить("СтрокиСклад", СтрокиСклад);
		ПараметрыВызова.Вставить("СтрокиГТД", СтрокиГТД);
		ПараметрыВызова.Вставить("СтрокиСерии", СтрокиСерии);
		ПараметрыВызова.Вставить("СтрокиРезервы", СтрокиРезервы);
		ПараметрыВызова.Вставить("СкладскойУчет", 
			НЕ ЭтоЗаказПокупателя И НЕ СтрокаТовара.ОрдерныйСклад);
		
		КоличествоТовараНеХватило = КоличествоРезерва(ПараметрыВызова);
		Если КоличествоТовараНеХватило <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Первая итерация: сначала обрабатываются организации, от которых запланирована или выполнена передача товаров
		// Организации отсортированы в порядке уменьшения резерва, который ранее выполнялся документом
		ПараметрыПодбораОстатков.Вставить("СкладскойУчет", НЕ СтрокаТовара.ОрдерныйСклад);
		ПараметрыПодбораОстатков.Вставить("СтрокаТовара", СтрокаТовара);
		ПараметрыПодбораОстатков.Вставить("КоличествоТовараНеХватило", КоличествоТовараНеХватило);
		ПодборПоТекущимДвижениям(ПараметрыПодбораОстатков);
		
		Если ПараметрыПодбораОстатков.КоличествоТовараНеХватило <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Вторая итерация: прочие организации, на которых есть остатки
		ПодборПоОстаткам(ПараметрыПодбораОстатков);
		
		Если ПараметрыПодбораОстатков.КоличествоТовараНеХватило > 0 И КонтролироватьОстатки Тогда
			// Не удалось выполнить резерв товаров организаций
			ОшибкаПодбораОстатков(СтрокаТовара, ПараметрыПодбораОстатков.КоличествоТовараНеХватило, Ошибки);
		КонецЕсли; 
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРезервыТоваровОрганизаций", 
		ПараметрыПодбораОстатков.ТаблицаРезервыТоваровОрганизаций);
	Для каждого ТекстОшибки Из Ошибки Цикл
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументСсылка);
	КонецЦикла; 
	СтруктураДополнительныеСвойства.Вставить("ЕстьОшибкиПередачиТоваров", Ошибки.Количество() > 0);
	
КонецПроцедуры

// Договор передачи между организациями по умолчанию для денежного документа
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации	 - Организация документа
//  ВидОперации	 - ПеречислениеСсылка			 - Вид операции документа
// 
// Возвращаемое значение:
//  СправочникСсылка.ДоговорыКонтрагентов - Договор передачи по умолчанию
//
Функция ДоговорПередачиПоУмолчанию(Организация, ВидОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	(ДоговорыКонтрагентов.Владелец = &Контрагент
	|			ИЛИ ДоговорыКонтрагентов.Организация = &Организация)
	|	И ДоговорыКонтрагентов.ЭтоДоговорПередачиТоваровМеждуОрганизациями
	|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
	
	Если ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.НашейОрганизации
		ИЛИ ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НашейОрганизации Тогда
		Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПустаяСсылка());
		Запрос.УстановитьПараметр("Контрагент", Организация);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтНашейОрганизации
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтНашейОрганизации Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Договор;
	Иначе
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли; 
	
КонецФункции

// Возвращает перечень организаций для анализа остатков с учетом настроек передачи товаров
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация остатка
// 
// Возвращаемое значение:
//  Массив - Организации остатков
Функция ОрганизацииОстатков(Организация) Экспорт
	
	МассивОрганизаций = Новый Массив;
	ОрганизацияПродавец = Константы.УчетПоКомпании.Компания(Организация);
	МассивОрганизаций.Добавить(ОрганизацияПродавец);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец КАК ОрганизацияВладелец
	|ИЗ
	|	РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|ГДЕ
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &Организация
	|	И НастройкаПередачиТоваровМеждуОрганизациями.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.Продажа)";
	Запрос.УстановитьПараметр("Организация", ОрганизацияПродавец);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивОрганизаций.Добавить(Выборка.ОрганизацияВладелец);
	КонецЦикла;
	
	Возврат МассивОрганизаций;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРезервыТоваровОрганизаций()
	
	Возврат
	"ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999) КАК Порядок,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	ОстаткиИРезервы.Ячейка КАК Ячейка,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыНаСкладахОстатки.Организация КАК Организация,
	|		ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ЗапасыНаСкладахОстатки.Партия КАК Партия,
	|		ЗапасыНаСкладахОстатки.Ячейка КАК Ячейка,
	|		ЗапасыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК ЗапасыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыНаСкладах.Организация,
	|		ЗапасыНаСкладах.СтруктурнаяЕдиница,
	|		ЗапасыНаСкладах.Номенклатура,
	|		ЗапасыНаСкладах.Характеристика,
	|		ЗапасыНаСкладах.Партия,
	|		ЗапасыНаСкладах.Ячейка,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ЗапасыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЗапасыНаСкладах.Количество
	|			ИНАЧЕ -ЗапасыНаСкладах.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах КАК ЗапасыНаСкладах
	|	ГДЕ
	|		ЗапасыНаСкладах.Регистратор = &Ссылка
	|		И ЗапасыНаСкладах.Организация В(&МассивОрганизаций)
	|		И (ЗапасыНаСкладах.СтруктурнаяЕдиница, ЗапасыНаСкладах.Ячейка, ЗапасыНаСкладах.Номенклатура, ЗапасыНаСкладах.Характеристика, ЗапасыНаСкладах.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		РезервыТоваровОрганизацийОстатки.Ячейка,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		РезервыТоваровОрганизаций.Ячейка,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Ячейка, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)) КАК ОстаткиИРезервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|		ПО ОстаткиИРезервы.Организация = НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец
	|			И (НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Ячейка,
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999),
	|	ОстаткиИРезервы.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999) КАК Порядок,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия, 
	|							ЗапасыДляРезервовТоваровОрганизаций.ЗаказРезерваСписание
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Запасы.Организация,
	|		Запасы.СтруктурнаяЕдиница,
	|		Запасы.Номенклатура,
	|		Запасы.Характеристика,
	|		Запасы.Партия,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Запасы.Количество
	|			ИНАЧЕ -Запасы.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.Запасы КАК Запасы
	|	ГДЕ
	|		Запасы.Регистратор = &Ссылка
	|		И Запасы.Организация В(&МассивОрганизаций)
	|		И (Запасы.СтруктурнаяЕдиница, Запасы.Номенклатура, Запасы.Характеристика, Запасы.Партия, Запасы.ЗаказПокупателя) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия,
	|					ЗапасыДляРезервовТоваровОрганизаций.ЗаказРезерваСписание
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций)) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций)) КАК ОстаткиИРезервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|		ПО ОстаткиИРезервы.Организация = НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец
	|			И (НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &Организация)
	|ГДЕ
	|	&РезервированиеЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999),
	|	ОстаткиИРезервы.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999) КАК Порядок,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.НомерГТД КАК НомерГТД,
	|	ОстаткиИРезервы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыВРазрезеГТДОстатки.Организация КАК Организация,
	|		ЗапасыВРазрезеГТДОстатки.НомерГТД КАК НомерГТД,
	|		ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения КАК СтранаПроисхождения,
	|		ЗапасыВРазрезеГТДОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыВРазрезеГТДОстатки.Характеристика КАК Характеристика,
	|		ЗапасыВРазрезеГТДОстатки.Партия КАК Партия,
	|		ЗапасыВРазрезеГТДОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (НомерГТД, СтранаПроисхождения, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций
	|						ГДЕ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))) КАК ЗапасыВРазрезеГТДОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыВРазрезеГТД.Организация,
	|		ЗапасыВРазрезеГТД.НомерГТД,
	|		ЗапасыВРазрезеГТД.СтранаПроисхождения,
	|		ЗапасыВРазрезеГТД.Номенклатура,
	|		ЗапасыВРазрезеГТД.Характеристика,
	|		ЗапасыВРазрезеГТД.Партия,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ЗапасыВРазрезеГТД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЗапасыВРазрезеГТД.Количество
	|			ИНАЧЕ -ЗапасыВРазрезеГТД.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.ЗапасыВРазрезеГТД КАК ЗапасыВРазрезеГТД
	|	ГДЕ
	|		ЗапасыВРазрезеГТД.Регистратор = &Ссылка
	|		И ЗапасыВРазрезеГТД.Организация В(&МассивОрганизаций)
	|		И (ЗапасыВРазрезеГТД.НомерГТД, ЗапасыВРазрезеГТД.СтранаПроисхождения, ЗапасыВРазрезеГТД.Номенклатура, ЗапасыВРазрезеГТД.Характеристика, ЗапасыВРазрезеГТД.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций
	|				ГДЕ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.НомерГТД,
	|		РезервыТоваровОрганизацийОстатки.СтранаПроисхождения,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (НомерГТД, СтранаПроисхождения, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций
	|						ГДЕ
	|							ЗапасыДляРезервовТоваровОрганизаций.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.НомерГТД,
	|		РезервыТоваровОрганизаций.СтранаПроисхождения,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.НомерГТД, РезервыТоваровОрганизаций.СтранаПроисхождения, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций
	|				ГДЕ
	|					ЗапасыДляРезервовТоваровОрганизаций.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))) КАК ОстаткиИРезервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|		ПО ОстаткиИРезервы.Организация = НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец
	|			И (НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &Организация)
	|ГДЕ
	|	&УчетГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999),
	|	ОстаткиИРезервы.НомерГТД,
	|	ОстаткиИРезервы.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИРезервы.Организация КАК Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999) КАК Порядок,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	ОстаткиИРезервы.Серия КАК Серия,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Ячейка КАК Ячейка,
	|	ОстаткиИРезервы.Номенклатура КАК Номенклатура,
	|	ОстаткиИРезервы.Характеристика КАК Характеристика,
	|	ОстаткиИРезервы.Партия КАК Партия,
	|	СУММА(ОстаткиИРезервы.КоличествоОстаток) + СУММА(ОстаткиИРезервы.КоличествоПоДокументу) + СУММА(ОстаткиИРезервы.КоличествоРезерв) + СУММА(ОстаткиИРезервы.РезервПоДокументу) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		СерииНоменклатурыОстатки.Организация КАК Организация,
	|		СерииНоменклатурыОстатки.Серия КАК Серия,
	|		СерииНоменклатурыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		СерииНоменклатурыОстатки.Ячейка КАК Ячейка,
	|		СерииНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|		СерииНоменклатурыОстатки.Характеристика КАК Характеристика,
	|		СерииНоменклатурыОстатки.Партия КАК Партия,
	|		СерииНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|		0 КАК КоличествоРезерв,
	|		0 КАК КоличествоПоДокументу,
	|		0 КАК РезервПоДокументу
	|	ИЗ
	|		РегистрНакопления.СерииНоменклатуры.Остатки(
	|				&МоментКонтроля,
	|				&ПолучатьОстаткиПоСериям
	|					И Организация В (&МассивОрганизаций)
	|					И (Серия, СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.Серия,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций
	|						ГДЕ
	|							ЗапасыДляРезервовТоваровОрганизаций.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))) КАК СерииНоменклатурыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СерииНоменклатуры.Организация,
	|		СерииНоменклатуры.Серия,
	|		СерииНоменклатуры.СтруктурнаяЕдиница,
	|		СерииНоменклатуры.Ячейка,
	|		СерииНоменклатуры.Номенклатура,
	|		СерииНоменклатуры.Характеристика,
	|		СерииНоменклатуры.Партия,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА СерииНоменклатуры.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА СерииНоменклатуры.Количество
	|			ИНАЧЕ -СерииНоменклатуры.Количество
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		РегистрНакопления.СерииНоменклатуры КАК СерииНоменклатуры
	|	ГДЕ
	|		СерииНоменклатуры.Регистратор = &Ссылка
	|		И СерииНоменклатуры.Организация В(&МассивОрганизаций)
	|		И (СерииНоменклатуры.Серия, СерииНоменклатуры.СтруктурнаяЕдиница, СерииНоменклатуры.Ячейка, СерииНоменклатуры.Номенклатура, СерииНоменклатуры.Характеристика, СерииНоменклатуры.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.Серия,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций
	|				ГДЕ
	|					ЗапасыДляРезервовТоваровОрганизаций.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|		И НЕ &ЭтоЗаказНаПродажу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗапасыНаСкладах.Организация,
	|		ЗапасыНаСкладах.Серия,
	|		ЗапасыНаСкладах.СтруктурнаяЕдиница,
	|		ВЫБОР
	|			КОГДА ЗапасыНаСкладах.Ячейка = НЕОПРЕДЕЛЕНО
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|			ИНАЧЕ ЗапасыНаСкладах.Ячейка
	|		КОНЕЦ,
	|		ЗапасыНаСкладах.Номенклатура,
	|		ЗапасыНаСкладах.Характеристика,
	|		ЗапасыНаСкладах.Партия,
	|		ЗапасыНаСкладах.Количество,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ЗапасыДляРезервовТоваровОрганизаций КАК ЗапасыНаСкладах
	|	ГДЕ
	|		ЗапасыНаСкладах.Количество > 0
	|		И &ЭтоЗаказНаПродажу
	|		И ЗапасыНаСкладах.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И НЕ &ПервичноеПроведение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизацийОстатки.Организация,
	|		РезервыТоваровОрганизацийОстатки.Серия,
	|		РезервыТоваровОрганизацийОстатки.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизацийОстатки.Ячейка,
	|		РезервыТоваровОрганизацийОстатки.Номенклатура,
	|		РезервыТоваровОрганизацийОстатки.Характеристика,
	|		РезервыТоваровОрганизацийОстатки.Партия,
	|		0,
	|		РезервыТоваровОрганизацийОстатки.КоличествоОстаток,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|				&МоментКонтроля,
	|				Организация В (&МассивОрганизаций)
	|					И (Серия, СтруктурнаяЕдиница, Ячейка, Номенклатура, Характеристика, Партия) В
	|						(ВЫБРАТЬ
	|							ЗапасыДляРезервовТоваровОрганизаций.Серия,
	|							ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|							ВЫБОР
	|								КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|								ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|							КОНЕЦ,
	|							ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|							ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|							ЗапасыДляРезервовТоваровОрганизаций.Партия
	|						ИЗ
	|							ЗапасыДляРезервовТоваровОрганизаций
	|						ГДЕ
	|							ЗапасыДляРезервовТоваровОрганизаций.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))) КАК РезервыТоваровОрганизацийОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Организация,
	|		РезервыТоваровОрганизаций.Серия,
	|		РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|		РезервыТоваровОрганизаций.Ячейка,
	|		РезервыТоваровОрганизаций.Номенклатура,
	|		РезервыТоваровОрганизаций.Характеристика,
	|		РезервыТоваровОрганизаций.Партия,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|	ГДЕ
	|		РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|		И РезервыТоваровОрганизаций.Организация В(&МассивОрганизаций)
	|		И (РезервыТоваровОрганизаций.Серия, РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Ячейка, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия) В
	|				(ВЫБРАТЬ
	|					ЗапасыДляРезервовТоваровОрганизаций.Серия,
	|					ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|					ВЫБОР
	|						КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|						ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|					КОНЕЦ,
	|					ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|					ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|					ЗапасыДляРезервовТоваровОрганизаций.Партия
	|				ИЗ
	|					ЗапасыДляРезервовТоваровОрганизаций
	|				ГДЕ
	|					ЗапасыДляРезервовТоваровОрганизаций.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))) КАК ОстаткиИРезервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|		ПО ОстаткиИРезервы.Организация = НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец
	|			И (НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &Организация)
	|ГДЕ
	|	&УчетСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИРезервы.Партия,
	|	ОстаткиИРезервы.Номенклатура,
	|	ОстаткиИРезервы.Характеристика,
	|	ОстаткиИРезервы.Организация,
	|	ЕСТЬNULL(НастройкаПередачиТоваровМеждуОрганизациями.Приоритет, 99999),
	|	ОстаткиИРезервы.Серия,
	|	ОстаткиИРезервы.СтруктурнаяЕдиница,
	|	ОстаткиИРезервы.Ячейка,
	|	ВЫБОР
	|		КОГДА ОстаткиИРезервы.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервыТоваровОрганизаций.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА РезервыТоваровОрганизаций.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОстатокБезПередачи,
	|	РезервыТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика КАК Характеристика,
	|	РезервыТоваровОрганизаций.Партия КАК Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка КАК Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|	РезервыТоваровОрганизаций.Серия КАК Серия,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.Регистратор = &Ссылка
	|				ТОГДА 0
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) КАК Остаток,
	|	СУММА(ВЫБОР
	|			КОГДА РезервыТоваровОрганизаций.Регистратор <> &Ссылка
	|				ТОГДА 0
	|			КОГДА РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РезервыТоваровОрганизаций.Количество
	|			ИНАЧЕ -РезервыТоваровОрганизаций.Количество
	|		КОНЕЦ) КАК ДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|ГДЕ
	|	РезервыТоваровОрганизаций.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ) И КОНЕЦПЕРИОДА(&ДатаДокумента, ДЕНЬ)
	|	И (РезервыТоваровОрганизаций.СтруктурнаяЕдиница, РезервыТоваровОрганизаций.Ячейка, РезервыТоваровОрганизаций.НомерГТД, РезервыТоваровОрганизаций.СтранаПроисхождения, РезервыТоваровОрганизаций.Номенклатура, РезервыТоваровОрганизаций.Характеристика, РезервыТоваровОрганизаций.Партия, РезервыТоваровОрганизаций.Серия) В
	|			(ВЫБРАТЬ
	|				ЗапасыДляРезервовТоваровОрганизаций.СтруктурнаяЕдиница,
	|				ВЫБОР
	|					КОГДА ЗапасыДляРезервовТоваровОрганизаций.Ячейка = НЕОПРЕДЕЛЕНО
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|					ИНАЧЕ ЗапасыДляРезервовТоваровОрганизаций.Ячейка
	|				КОНЕЦ,
	|				ЗапасыДляРезервовТоваровОрганизаций.НомерГТД,
	|				ЗапасыДляРезервовТоваровОрганизаций.СтранаПроисхождения,
	|				ЗапасыДляРезервовТоваровОрганизаций.Номенклатура,
	|				ЗапасыДляРезервовТоваровОрганизаций.Характеристика,
	|				ЗапасыДляРезервовТоваровОрганизаций.Партия,
	|				ЗапасыДляРезервовТоваровОрганизаций.Серия
	|			ИЗ
	|				ЗапасыДляРезервовТоваровОрганизаций)
	|	И (РезервыТоваровОрганизаций.Организация = &Организация
	|			ИЛИ РезервыТоваровОрганизаций.КорОрганизация = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	РезервыТоваровОрганизаций.Организация,
	|	ВЫБОР
	|		КОГДА РезервыТоваровОрганизаций.Организация = &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	РезервыТоваровОрганизаций.Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика,
	|	РезервыТоваровОрганизаций.Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	РезервыТоваровОрганизаций.Серия,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	РезервыТоваровОрганизаций.Номенклатура,
	|	РезервыТоваровОрганизаций.Характеристика,
	|	РезервыТоваровОрганизаций.Партия,
	|	РезервыТоваровОрганизаций.СтруктурнаяЕдиница,
	|	РезервыТоваровОрганизаций.Ячейка,
	|	РезервыТоваровОрганизаций.НомерГТД,
	|	РезервыТоваровОрганизаций.Серия,
	|	РезервыТоваровОрганизаций.СтранаПроисхождения,
	|	ОстатокБезПередачи,
	|	РезервыТоваровОрганизаций.Организация,
	|	ДвиженияДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыНаСкладах.НомерСтроки КАК НомерСтроки,
	|	ЗапасыНаСкладах.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЗапасыНаСкладах.Организация КАК Организация,
	|	ЗапасыНаСкладах.Организация КАК КорОрганизация,
	|	ЗапасыНаСкладах.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыНаСкладах.ОрдерныйСклад КАК ОрдерныйСклад,
	|	ЗапасыНаСкладах.Номенклатура КАК Номенклатура,
	|	ЗапасыНаСкладах.Характеристика КАК Характеристика,
	|	ЗапасыНаСкладах.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ЗапасыНаСкладах.Ячейка = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
	|		ИНАЧЕ ЗапасыНаСкладах.Ячейка
	|	КОНЕЦ КАК Ячейка,
	|	ЗапасыНаСкладах.НомерГТД КАК НомерГТД,
	|	ЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗапасыНаСкладах.Серия КАК Серия,
	|	ЗапасыНаСкладах.СерияСОстатком КАК СерияСОстатком,
	|	ЗапасыНаСкладах.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗапасыНаСкладах.Количество КАК Количество
	|ИЗ
	|	ЗапасыДляРезервовТоваровОрганизаций КАК ЗапасыНаСкладах
	|ГДЕ
	|	ЗапасыНаСкладах.Количество > 0";
	
КонецФункции

Процедура ПодборПоТекущимДвижениям(ПараметрыПодбораОстатков)
	
	СтрокаТовара = ПараметрыПодбораОстатков.СтрокаТовара;
	
	СтруктураОтбора = Новый Структура(ПоляРезервы());
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
	СтруктураОтбора.Вставить("ОстатокБезПередачи", Ложь);
	СтрокиРезервы = ПараметрыПодбораОстатков.ТаблицаРезервы.НайтиСтроки(СтруктураОтбора);
	Для каждого НайденнаяСтрока Из СтрокиРезервы Цикл
		
		ДоступныйРезерв = -Мин(НайденнаяСтрока.ДвиженияДокумента, НайденнаяСтрока.Остаток);
		Если ДоступныйРезерв <=0 Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиСклад = ПараметрыПодбораОстатков.ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляЗапасы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиЗапасы = ПараметрыПодбораОстатков.ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетГТД());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиГТД = ПараметрыПодбораОстатков.ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетСерий());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиСерии = ПараметрыПодбораОстатков.ТаблицаОстатковСерий.НайтиСтроки(СтруктураОтбора);
		
		ПараметрыВызова = Новый Структура;
		ПараметрыВызова.Вставить("СтрокаТовара", СтрокаТовара);
		ПараметрыВызова.Вставить("ДоступныйРезерв", ДоступныйРезерв);
		ПараметрыВызова.Вставить("СтрокиЗапасы", СтрокиЗапасы);
		ПараметрыВызова.Вставить("СтрокиСклад", СтрокиСклад);
		ПараметрыВызова.Вставить("СтрокиГТД", СтрокиГТД);
		ПараметрыВызова.Вставить("СтрокиСерии", СтрокиСерии);
		ПараметрыВызова.Вставить("СкладскойУчет", ПараметрыПодбораОстатков.СкладскойУчет);
		
		Остаток = МинимальныйОстатокПоУчетам(ПараметрыВызова);
		Если Остаток <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ПерваяСтрока = ПараметрыПодбораОстатков.ТаблицаРезервыТоваровОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(ПерваяСтрока, СтрокаТовара);
		ВтораяСтрока = ПараметрыПодбораОстатков.ТаблицаРезервыТоваровОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(ВтораяСтрока, ПерваяСтрока);
		ПерваяСтрока.КорОрганизация = НайденнаяСтрока.Организация;
		ВтораяСтрока.Организация = НайденнаяСтрока.Организация;
		ВтораяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
		КПередаче = Мин(ПараметрыПодбораОстатков.КоличествоТовараНеХватило, Остаток);
		ПерваяСтрока.Количество = КПередаче;
		ВтораяСтрока.Количество = ПерваяСтрока.Количество;
		НайденнаяСтрока.Остаток = НайденнаяСтрока.Остаток + КПередаче;
		НайденнаяСтрока.ДвиженияДокумента = НайденнаяСтрока.ДвиженияДокумента + КПередаче;
		ЗачестьПоОстаткам(СтрокиСклад, КПередаче);
		Если ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
			ЗачестьПоОстаткам(СтрокиЗапасы, КПередаче);
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД) Тогда
			ЗачестьПоОстаткам(СтрокиГТД, КПередаче);
		КонецЕсли; 
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") 
			И ЗначениеЗаполнено(СтрокаТовара.Серия) Тогда
			ЗачестьПоОстаткам(СтрокиСерии, КПередаче);
		КонецЕсли; 
		ПараметрыПодбораОстатков.КоличествоТовараНеХватило = ПараметрыПодбораОстатков.КоличествоТовараНеХватило - КПередаче;
		Если ПараметрыПодбораОстатков.КоличествоТовараНеХватило <= 0 Тогда
			Прервать;
		КонецЕсли; 
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодборПоОстаткам(ПараметрыПодбораОстатков)
	
	СтрокаТовара = ПараметрыПодбораОстатков.СтрокаТовара;
	
	УчетРезервов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД);
	УчетСерий = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") И ЗначениеЗаполнено(СтрокаТовара.Серия);

	Если ПараметрыПодбораОстатков.СкладскойУчет Тогда
		СтруктураОтбора = Новый Структура(ПоляСкладскойУчет());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Ложь);
		СтрокиСклад = ПараметрыПодбораОстатков.ТаблицаОстатковСклад.НайтиСтроки(СтруктураОтбора);
	Иначе
		СтруктураОтбора = Новый Структура(ПоляЗапасы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("ОстатокБезПередачи", Ложь);
		СтрокиСклад = ПараметрыПодбораОстатков.ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	
	Для Каждого НайденнаяСтрока Из СтрокиСклад Цикл
		
		СтруктураОтбора = Новый Структура(ПоляЗапасы());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиЗапасы = ПараметрыПодбораОстатков.ТаблицаОстатковЗапасов.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетГТД());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиГТД = ПараметрыПодбораОстатков.ТаблицаОстатковГТД.НайтиСтроки(СтруктураОтбора);
		
		СтруктураОтбора = Новый Структура(ПоляУчетСерий());
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТовара);
		СтруктураОтбора.Вставить("Организация", НайденнаяСтрока.Организация);
		СтрокиСерии = ПараметрыПодбораОстатков.ТаблицаОстатковСерий.НайтиСтроки(СтруктураОтбора);
		
		ПараметрыВызова = Новый Структура;
		ПараметрыВызова.Вставить("СтрокаТовара", СтрокаТовара);
		ПараметрыВызова.Вставить("ДоступныйРезерв", НайденнаяСтрока.Остаток);
		ПараметрыВызова.Вставить("СтрокиЗапасы", СтрокиЗапасы);
		ПараметрыВызова.Вставить("СтрокиСклад", Новый Массив);
		ПараметрыВызова.Вставить("СтрокиГТД", СтрокиГТД);
		ПараметрыВызова.Вставить("СтрокиСерии", СтрокиСерии);
		ПараметрыВызова.Вставить("СкладскойУчет", Ложь);
		
		Остаток = МинимальныйОстатокПоУчетам(ПараметрыВызова);
		Если Остаток <= 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ПерваяСтрока = ПараметрыПодбораОстатков.ТаблицаРезервыТоваровОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(ПерваяСтрока, СтрокаТовара);
		ВтораяСтрока = ПараметрыПодбораОстатков.ТаблицаРезервыТоваровОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(ВтораяСтрока, ПерваяСтрока);
		ПерваяСтрока.КорОрганизация = НайденнаяСтрока.Организация;
		ВтораяСтрока.Организация = НайденнаяСтрока.Организация;
		ВтораяСтрока.ВидДвижения = ВидДвиженияНакопления.Расход;
		КПередаче = Мин(ПараметрыПодбораОстатков.КоличествоТовараНеХватило, Остаток);
		ПерваяСтрока.Количество = КПередаче;
		ВтораяСтрока.Количество = ПерваяСтрока.Количество;
		НайденнаяСтрока.Остаток = НайденнаяСтрока.Остаток - КПередаче;
		Если УчетРезервов И ПараметрыПодбораОстатков.СкладскойУчет Тогда
			ЗачестьПоОстаткам(СтрокиЗапасы, КПередаче);
		КонецЕсли;
		Если УчетГТД Тогда
			ЗачестьПоОстаткам(СтрокиГТД, КПередаче);
		КонецЕсли; 
		Если УчетРезервов И СтрокаТовара.СерияСОстатком Тогда
			ЗачестьПоОстаткам(СтрокиСерии, КПередаче);
		КонецЕсли; 
		ПараметрыПодбораОстатков.КоличествоТовараНеХватило = ПараметрыПодбораОстатков.КоличествоТовараНеХватило - КПередаче;
		Если ПараметрыПодбораОстатков.КоличествоТовараНеХватило <= 0 Тогда
			Прервать;
		КонецЕсли; 
		
	КонецЦикла;
		
КонецПроцедуры

Функция КоличествоРезерва(ПараметрыВызова)
	
	СтрокаТовара = ПараметрыВызова.СтрокаТовара;
	
	УчетРезервов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД);
	УчетСерий = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") И ЗначениеЗаполнено(СтрокаТовара.Серия);
	
	// Определение остатков разным учетам
	ОстатокЗапасы = 0;
	Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиЗапасы Цикл
		ОстатокЗапасы = ОстатокЗапасы + СтрокаТаблицы.Остаток;
	КонецЦикла;  
	ОстатокСклад = 0;
	Если ПараметрыВызова.СкладскойУчет Тогда
		Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиСклад Цикл
			ОстатокСклад = ОстатокСклад + СтрокаТаблицы.Остаток;
		КонецЦикла;  
	КонецЕсли;
	ОстатокГТД = 0;
	Если УчетГТД Тогда
		Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиГТД Цикл
			ОстатокГТД = ОстатокГТД + СтрокаТаблицы.Остаток;
		КонецЦикла;
	КонецЕсли;
	ОстатокСерий = 0;
	Если УчетСерий И СтрокаТовара.СерияСОстатком Тогда
		Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиСерии Цикл
			ОстатокСерий = ОстатокСерий + СтрокаТаблицы.Остаток;
		КонецЦикла;
	КонецЕсли;
	ОстатокРезерв = 0;
	
	// Проверки перепроведения документа, выполнявшего резерв
	// Обеспеченный передачей товаров резерв отменять нельзя
	Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиРезервы Цикл
		ОстатокРезерв = ОстатокРезерв + Мин(СтрокаТаблицы.Остаток, СтрокаТаблицы.ДвиженияДокумента);
	КонецЦикла;  
	Если ОстатокРезерв < 0 Тогда
		ОстатокРезерв = 0;
	КонецЕсли;
	ОстатокРезерв = Мин(ОстатокРезерв, СтрокаТовара.Количество);
	
	// Расчет количества, отгружаемого из остатков
	Если ПараметрыВызова.СкладскойУчет Тогда
		Остаток = ОстатокСклад;
		Если УчетРезервов Тогда
			Остаток = Мин(Остаток, ОстатокЗапасы);
		КонецЕсли;
	Иначе
		Остаток = ОстатокЗапасы;
	КонецЕсли;
	Если УчетГТД Тогда
		Остаток = Мин(Остаток, ОстатокГТД);
	КонецЕсли;
	Если УчетСерий И СтрокаТовара.СерияСОстатком Тогда
		Остаток = Мин(Остаток, ОстатокСерий);
	КонецЕсли;
	КоличествоТовараНеХватило = СтрокаТовара.Количество - Остаток;
	Если ОстатокРезерв > 0 И ОстатокРезерв > КоличествоТовараНеХватило Тогда
		КоличествоТовараНеХватило = ОстатокРезерв;
		Зачтено = СтрокаТовара.Количество - ОстатокРезерв;
	ИначеЕсли КоличествоТовараНеХватило <= 0 Тогда
		Зачтено = СтрокаТовара.Количество;
	Иначе
		Зачтено = Остаток;
	КонецЕсли;
	
	// Уменьшение остатков на зачтенное количество
	Если Зачтено > 0 Тогда
		ЗачестьПоОстаткам(ПараметрыВызова.СтрокиСклад, Зачтено);
		Если УчетРезервов Тогда
			ЗачестьПоОстаткам(ПараметрыВызова.СтрокиЗапасы, Зачтено);
		КонецЕсли;
		Если УчетГТД Тогда
			ЗачестьПоОстаткам(ПараметрыВызова.СтрокиГТД, Зачтено);
		КонецЕсли; 
		Если УчетСерий И СтрокаТовара.СерияСОстатком Тогда
			ЗачестьПоОстаткам(ПараметрыВызова.СтрокиСерии, Зачтено);
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Мин(КоличествоТовараНеХватило, СтрокаТовара.Количество);
		
КонецФункции

Процедура ЗачестьПоОстаткам(СтрокиОстатков, Зачтено)
	
	ОстатокЗачета = Зачтено;
	Для каждого СтрокаТаблицы Из СтрокиОстатков Цикл
		ПоПозиции = Мин(СтрокаТаблицы.Остаток, ОстатокЗачета);
		СтрокаТаблицы.Остаток = СтрокаТаблицы.Остаток - ПоПозиции;
		ОстатокЗачета = ОстатокЗачета - ПоПозиции;
		Если ОстатокЗачета <= 0 Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;  
	
КонецПроцедуры

Функция МинимальныйОстатокПоУчетам(ПараметрыВызова)
	
	СтрокаТовара = ПараметрыВызова.СтрокаТовара;
	
	УчетРезервов = ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД") И ЗначениеЗаполнено(СтрокаТовара.НомерГТД);
	УчетСерий = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") И ЗначениеЗаполнено(СтрокаТовара.Серия);

	ОстатокЗапасы = 0;
	Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиЗапасы Цикл
		ОстатокЗапасы = ОстатокЗапасы + СтрокаТаблицы.Остаток;
	КонецЦикла;  
	ОстатокГТД = 0;
	Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиГТД Цикл
		ОстатокГТД = ОстатокГТД + СтрокаТаблицы.Остаток;
	КонецЦикла;
	ОстатокСерии = 0;
	Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиСерии Цикл
		ОстатокСерии = ОстатокСерии + СтрокаТаблицы.Остаток;
	КонецЦикла;
	ОстатокСклад = 0;
	Если НЕ ПараметрыВызова.СкладскойУчет Тогда
		ОстатокСклад = ПараметрыВызова.ДоступныйРезерв;
	Иначе
		Для каждого СтрокаТаблицы Из ПараметрыВызова.СтрокиСклад Цикл
			ОстатокСклад = ОстатокСклад + СтрокаТаблицы.Остаток;
		КонецЦикла;  
	КонецЕсли;
	
	Если НЕ УчетРезервов Тогда
		ОстатокЗапасы = ПараметрыВызова.ДоступныйРезерв;
	КонецЕсли;
	Если НЕ УчетГТД Тогда
		ОстатокГТД = ПараметрыВызова.ДоступныйРезерв;
	КонецЕсли;
	Если НЕ УчетСерий ИЛИ НЕ СтрокаТовара.СерияСОстатком Тогда
		ОстатокСерии = ПараметрыВызова.ДоступныйРезерв;
	КонецЕсли;
	Возврат Мин(ПараметрыВызова.ДоступныйРезерв, ОстатокСклад, ОстатокЗапасы, ОстатокГТД, ОстатокСерии);
	
КонецФункции
 
// Сообщает об ошибках подбора остатков по регистру РезервыТоваровОрганизаций.
// 
// Параметры:
//  СтрокаТовара - СтрокаТабличнойЧасти - строка с данными номенклатуры
//  Нехватка - Число - количество, для которого недостаточно остатка
//  Ошибки - Массив - данные об ошибках контроля остатков
Процедура ОшибкаПодбораОстатков(СтрокаТовара, Нехватка, Ошибки)
	
	Если Ошибки.Количество() = 0 Тогда
		ТекстЗаголовкаСообщения = НСтр("ru = 'Ошибка:
	    	|Не хватает остатка для передачи товаров организаций'");
		Ошибки.Добавить(ТекстЗаголовкаСообщения);
	КонецЕсли; 
	
	ШаблонСообщения = НСтр("ru = 'Номенклатура: %1,
		|недостаточно %2 %3'");
	
	ЧастиНаименования = Новый Массив;
	ПредставлениеНоменклатуры = Справочники.Номенклатура.Представление(СтрокаТовара.Номенклатура,
		СтрокаТовара.Характеристика, СтрокаТовара.Партия);
	ЧастиНаименования.Добавить(ПредставлениеНоменклатуры);	
	Если ЗначениеЗаполнено(СтрокаТовара.Серия) Тогда
		ЧастиНаименования.Добавить(Строка(СтрокаТовара.Серия));
	КонецЕсли;
	ТекстСообщения = СтрШаблон(ШаблонСообщения, 
		СтрСоединить(ЧастиНаименования, ","), 
		Нехватка,
		СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТовара.Номенклатура, "ЕдиницаИзмерения")));
	Ошибки.Добавить(ТекстСообщения);
		
КонецПроцедуры

#Область ФиксированныеСтроки

Функция ПоляРезервы()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница, Ячейка, НомерГТД, СтранаПроисхождения, Серия";		
КонецФункции
 
Функция ПоляСкладскойУчет()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница, Ячейка";		
КонецФункции
 
Функция ПоляЗапасы()
	Возврат "Номенклатура, Характеристика, Партия, СтруктурнаяЕдиница";		
КонецФункции
 
Функция ПоляУчетГТД()
	Возврат "Номенклатура, Характеристика, Партия, НомерГТД, СтранаПроисхождения";		
КонецФункции

Функция ПоляУчетСерий()
	Возврат "Номенклатура, Характеристика, Партия, Серия, СтруктурнаяЕдиница, Ячейка";		
КонецФункции

#КонецОбласти 

#КонецОбласти 


 