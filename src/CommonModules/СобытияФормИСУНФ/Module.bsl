#Область СобытияФормИСПереопределяемый

// Устанавливает условное оформление для поля "Характеристика".
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой нужно установить условное оформление,
//  ИмяПоляВводаХарактеристики - Строка - имя элемента формы "Характеристика",
//  ПутьКПолюОтбора - Строка - полный путь к реквизиту "Характеристики используются".
//
Процедура УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
	Форма,
	ИмяПоляВводаХарактеристики,
	ПутьКПолюОтбора) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		Возврат;
	КонецЕсли;
																
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаХарактеристики].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<характеристики не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ИмяТЧ = "";
	
	Если ИмяПоляВводаХарактеристики = "ТоварыХарактеристика" Тогда
		ПутьКТЧ = "Объект.Товары";
		ИмяТЧ = "Товары";
		
	ИначеЕсли ИмяПоляВводаХарактеристики = "СырьеХарактеристика" Тогда
		ПутьКТЧ = "Объект.Сырье";
		ИмяТЧ = "Сырье";
	Иначе Возврат;
	КонецЕсли;
	
	НовыеРеквизиты = Новый Массив;
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПроверятьЗаполнениеХарактеристики", Новый ОписаниеТипов("Булево"), ПутьКТЧ, "ПроверятьЗаполнениеХарактеристики", Истина));
	Форма.ИзменитьРеквизиты(НовыеРеквизиты);

	НоваяКолонка = Форма.Элементы.Добавить(ИмяТЧ+"ПроверятьЗаполнениеХарактеристики", Тип("ПолеФормы"), Форма.Элементы[ИмяТЧ]);
	НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода; 
	НоваяКолонка.ПутьКДанным = ПутьКТЧ + "." + "ПроверятьЗаполнениеХарактеристики";
	НоваяКолонка.Видимость = Ложь;
	
	ИмяПоляПроверятьЗаполнениеХарактеристики = "Объект."+ИмяТЧ+ ".ПроверятьЗаполнениеХарактеристики";
	
	ИмяПоляХарактеристики = ИмяПоляВводаХарактеристики;
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеХарактеристики, Истина, ВидСравненияКомпоновкиДанных.Равно);
	
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеХарактеристики, Ложь, ВидСравненияКомпоновкиДанных.Равно);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект."+ИмяТЧ+".Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляХарактеристики);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Устанавливаем условное оформление для единиц измерения номенклатуры
//
// Параметры:
// 		Форма - Форма - Содержит данную форму
// 		ИмяПоляВводаЕдиницИзмерения - Строка - Наименование элемента формы, содержащего ед. измерения номенклатуры,
//									   			если оно отличается от "ТоварыНоменклатураЕдиницаИзмерения"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "Упаковка",
//									если он отличается от "Объект.Товары.Упаковка".
//
Процедура УстановитьУсловноеОформлениеЕдиницИзмерения(Форма,
													  ИмяПоляВводаЕдиницИзмерения,
													  ПутьКПолюОтбора) Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаЕдиницИзмерения].Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);	
	
КонецПроцедуры

// Устанавливает условное оформление для поля "Серия".
//
// Параметры:
//	Форма - УправляемаяФорма - Форма, в которой нужно установить условное оформление,
//
Процедура УстановитьУсловноеОформлениеСерийНоменклатуры(Форма,
														ИмяПоляВводаСерии,
														ПутьКПолюОтбораСтатусУказанияСерий,
														ПутьКПолюОтбораТипНоменклатуры) Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаСерии].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбораСтатусУказанияСерий);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<серии не используются>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ИмяТЧ = "";
	
	Если ИмяПоляВводаСерии = "ТоварыСерия" Тогда
		ПутьКТЧ = "Объект.Товары";
		ИмяТЧ = "Товары";
		
	ИначеЕсли ИмяПоляВводаСерии = "СырьеСерия" Тогда
		ПутьКТЧ = "Объект.Сырье";
		ИмяТЧ = "Сырье";
	Иначе Возврат;
	КонецЕсли;
	
	НовыеРеквизиты = Новый Массив;
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПроверятьЗаполнениеСерии", Новый ОписаниеТипов("Булево"), ПутьКТЧ, "ПроверятьЗаполнениеСерии", Истина));
	Форма.ИзменитьРеквизиты(НовыеРеквизиты);

	НоваяКолонка = Форма.Элементы.Добавить(ИмяТЧ+"ПроверятьЗаполнениеСерии", Тип("ПолеФормы"), Форма.Элементы[ИмяТЧ]);
	НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода; 
	НоваяКолонка.ПутьКДанным = ПутьКТЧ + "." + "ПроверятьЗаполнениеСерии";
	НоваяКолонка.Видимость = Ложь;
	
	ИмяПоляПроверятьЗаполнениеСерии = "Объект."+ИмяТЧ+ ".ПроверятьЗаполнениеСерии";
	
	ИмяПоляСерии = ИмяПоляВводаСерии;
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеСерии, Истина, ВидСравненияКомпоновкиДанных.Равно);
	
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляСерии);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Истина);
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, ИмяПоляПроверятьЗаполнениеСерии, Ложь, ВидСравненияКомпоновкиДанных.Равно);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляСерии);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект."+ИмяТЧ+".Серия", Справочники.ПартииНоменклатуры.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.НеРавно);
	РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, ИмяПоляСерии);
	РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Переопределяемая часть обработки проверки заполнения формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма.
//   Отказ - Булево - Истина если проверка заполнения не пройдена
//   ПроверяемыеРеквизиты - Массив Из Строка - реквизиты формы, отмеченные для проверки
Процедура ОбработкаПроверкиЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Если Форма.ИмяФормы = "ОбщаяФорма.ФормаУточненияДанныхИС"
		Или Форма.ИмяФормы = "ОбщаяФорма.ФормаУточненияПодобраннойПродукцииИСМП" Тогда
		
		Номенклатура = Форма.Номенклатура;
		НепроверяемыеРеквизиты = Новый Массив;
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			
			ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, 
				"ИспользоватьПартии, ПроверятьЗаполнениеПартий, ИспользоватьХарактеристики, ПроверятьЗаполнениеХарактеристики");
			
			Если НЕ (ЗначениеРеквизитов.ИспользоватьПартии И ЗначениеРеквизитов.ПроверятьЗаполнениеПартий) Тогда
				
				НепроверяемыеРеквизиты.Добавить("Серия");
			
			КонецЕсли;
			
			Если НЕ (ЗначениеРеквизитов.ИспользоватьХарактеристики И ЗначениеРеквизитов.ПроверятьЗаполнениеХарактеристики) Тогда
				
				НепроверяемыеРеквизиты.Добавить("Характеристика");
				
			КонецЕсли;
			
		Иначе
			
			НепроверяемыеРеквизиты.Добавить("Серия");
			НепроверяемыеРеквизиты.Добавить("Характеристика");
			
		КонецЕсли;
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
		
	КонецЕсли;
	
КонецПроцедуры

// Возникает на сервере при записи константы в формах настроек
// если запись одной константы может повлечь изменение других отображаемых в этой же форме.
//
// Параметры:
//  Форма             - ФормаКлиентскогоПриложения - форма,
//  КонстантаИмя      - Строка           - записываемая константа,
//  КонстантаЗначение - Произвольный     - значение константы.
Процедура ОбновитьФормуНастройкиПриЗаписиПодчиненныхКонстант(Форма, КонстантаИмя, КонстантаЗначение) Экспорт

	Если КонстантаИмя = "ВестиУчетМаркировкиПродукцииВГИСМ" И КонстантаЗначение Тогда
		
		Константы.ИспользоватьСерииНоменклатуры.Установить(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет действия при изменении номенклатуры в объекте (форме, строке табличной части итп).
//
// Параметры:
//  Форма                  - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока          - Произвольный - контекст редактирования (текущая строка таблицы, шапка объекта, форма)
//  КэшированныеЗначения   - Неопределено, Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - Произвольный - параметры указания серий формы
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения = Неопределено, ПараметрыУказанияСерий = Неопределено) Экспорт
	
	Если Форма.ИмяФормы = "ОбщаяФорма.ФормаУточненияДанныхИС" 
		ИЛИ Форма.ИмяФормы = "Документ.МаркировкаТоваровИСМП.Форма.ФормаДокумента" Тогда
		
		ТекущаяСтрока.ХарактеристикиИспользуются = 
			ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") 
			И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) 
			И ТекущаяСтрока.Номенклатура.ИспользоватьХарактеристики;
		
		ТекущаяСтрока.СтатусУказанияСерий = ?(
			ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") 
			И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) 
			И ТекущаяСтрока.Номенклатура.ИспользоватьСерииНоменклатуры,
			1,
			0);
	
	КонецЕсли;
	
	СлужебныеРеквизиты = Новый Структура;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ТребуетВзвешивания") Тогда
		СлужебныеРеквизиты.Вставить("ТребуетВзвешивания");
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ПроизвольнаяЕдиницаУчета") Тогда
		СлужебныеРеквизиты.Вставить("ПроизвольнаяЕдиницаУчета");
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "СкоропортящаясяПродукция") Тогда
		СлужебныеРеквизиты.Вставить("СкоропортящаясяПродукция", ТекущаяСтрока.СкоропортящаясяПродукция);
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "ЕдиницаИзмерения") Тогда
		СлужебныеРеквизиты.Вставить("ЕдиницаИзмерения");
	КонецЕсли;
	
	Если СлужебныеРеквизиты.Количество() Тогда
		ИнтеграцияИСУНФКлиентСервер.ЗаполнитьСлужебныеРеквизитыСтроки(Форма, ТекущаяСтрока, СлужебныеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	Если Форма.КэшДанныхГОСИС = Неопределено Тогда
		Форма.КэшДанныхГОСИС = Новый Структура;
	КонецЕсли;
	
	СобытияФормИС.ПриЧтенииНаСервере(Форма, ТекущийОбъект);
	
КонецПроцедуры

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	Если Форма.КэшДанныхГОСИС =  Неопределено Тогда
		Форма.КэшДанныхГОСИС = Новый Структура;
	КонецЕсли;
	
	Если Форма.ИмяФормы = "Документ.ПередачаТоваровМеждуОрганизациями.Форма.ФормаДокумента"
		ИЛИ Форма.ИмяФормы = "Документ.РасходнаяНакладная.Форма.ФормаДокумента" Тогда
		ФОВестиУчетПодконтрольныхТоваровВЕТИС = ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС");
		Если ФОВестиУчетПодконтрольныхТоваровВЕТИС Тогда
			Форма.КэшДанныхГОСИС.Вставить("ИнтеграцияВЕТИС");
			ИнтеграцияИС.ПриСозданииНаСервереВФормеДокументаОснования(
				Форма,
				Форма.Объект,
				ИнтеграцияИС.ПараметрыИнтеграцииВФорме("ВетИС",ИнтеграцияИС.ИмяЭлементаДляРазмещения()));
		КонецЕсли;
		
	ИначеЕсли Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда
		
		Если НЕ ИнтеграцияИСУНФ.ИнициализацияФормыВыполнена(Форма, Ложь)
			И Форма.КэшДанныхГОСИС.Свойство("ИнтеграцияИСМП") Тогда
			ИмяЭлементаНастройки = "НастройкаПараметровНоменклатурыИСМП";
			ДобавляемыеРеквизиты = Новый Массив;
			ДобавляемыеРеквизиты.Добавить(
				Новый РеквизитФормы(ИмяЭлементаНастройки, Новый ОписаниеТипов("ФорматированнаяСтрока")));
			Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
			
			Элемент = Форма.Элементы.Вставить(
				ИмяЭлементаНастройки,
				Тип("ПолеФормы"),
				Форма.Элементы.ОсновныеРеквизиты,
				Форма.Элементы.ГруппаГабариты);
			Элемент.ПутьКДанным            = ИмяЭлементаНастройки;
			Элемент.ПоложениеЗаголовка     = ПоложениеЗаголовкаЭлементаФормы.Нет;
			
			ИнтеграцияИСМПУНФКлиентСервер.ЗаполнитьДанныеНастройкиПараметровНоменклатурыИСМП(Форма);
		КонецЕсли;
		
		// Заполним список выбора доступных видов продукции.
		Форма.Элементы.ВидПродукцииИС.СписокВыбора.Очистить();
		Если Форма.КэшДанныхГОСИС.Свойство("ИнтеграцияЕГАИС") Тогда
			Форма.Элементы.ВидПродукцииИС.СписокВыбора.Добавить(Перечисления.ВидыПродукцииИС.Алкогольная);
		КонецЕсли;
		Если Форма.КэшДанныхГОСИС.Свойство("ИнтеграцияИСМП") Тогда
			УчитываемыеВидыМаркируемойПродукции = ИнтеграцияИСМПКлиентСерверПовтИсп.УчитываемыеВидыМаркируемойПродукции();
			Для Каждого ВидПродукции Из УчитываемыеВидыМаркируемойПродукции Цикл
				Форма.Элементы.ВидПродукцииИС.СписокВыбора.Добавить(ВидПродукции);
			КонецЦикла;
		КонецЕсли;
		Если Форма.КэшДанныхГОСИС.Свойство("ИнтеграцияВЕТИС") Тогда
			Форма.Элементы.ВидПродукцииИС.СписокВыбора.Добавить(Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияВЕТИС);
		КонецЕсли;
		Если Форма.КэшДанныхГОСИС.Свойство("ИнтеграцияЗЕРНО") Тогда
			УчитываемыеВидыПродукцииЗЕРНО = ИнтеграцияЗЕРНОКлиентСерверПовтИсп.УчитываемыеВидыПродукции();
			Для Каждого ВидПродукции Из УчитываемыеВидыПродукцииЗЕРНО Цикл
				Форма.Элементы.ВидПродукцииИС.СписокВыбора.Добавить(ВидПродукции);
			КонецЦикла;
		КонецЕсли;
		
		// Сохраним доступность ранее указанного значения.
		Если ЗначениеЗаполнено(Форма.Объект.ВидПродукцииИС)
			И Форма.Элементы.ВидПродукцииИС.СписокВыбора.НайтиПоЗначению(Форма.Объект.ВидПродукцииИС) = Неопределено Тогда
			Форма.Элементы.ВидПродукцииИС.СписокВыбора.Добавить(Форма.Объект.ВидПродукцииИС);
		КонецЕсли;
		Форма.Элементы.ВидПродукцииИС.СписокВыбора.СортироватьПоЗначению();
		
	КонецЕсли;
	
	СобытияФормИС.ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПослеЗаписиНаСервере(Форма) Экспорт
	
	СобытияФормИС.ПослеЗаписиНаСервере(Форма);
	
КонецПроцедуры

Процедура ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ Форма.КэшДанныхГОСИС.Свойство("Инициализация") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	
	Если Элемент = "ТабличнаяЧасть" Тогда
		ИдентификаторСтроки = Неопределено;
		Элемент = Форма.КэшДанныхГОСИС.ИмяТабличнойЧасти;
		
		Если ДополнительныеПараметры.Свойство("ТребуетсяЗаполнениеСлужебныхРеквизитов", ИдентификаторСтроки) Тогда
			СлужебныеРеквизиты = Новый Структура("МаркируемаяПродукция, ВидПродукцииИС, КоэффициентЕдиницыИзмеренияГосИС, КоличествоВБазовыхЕдиницахГосИС, АвтоматическийОСУИС");
			Если ИдентификаторСтроки = Неопределено Тогда
				ИнтеграцияИСУНФ.ЗаполнитьСлужебныеРеквизитыВКоллекции(Форма, Форма.Объект[Элемент], СлужебныеРеквизиты);
			Иначе
				СтрокаТЧ = Форма.Объект[Элемент].НайтиПоИдентификатору(ИдентификаторСтроки);
				ИнтеграцияИСУНФКлиентСервер.ЗаполнитьСлужебныеРеквизитыСтроки(Форма, СтрокаТЧ, СлужебныеРеквизиты);
			КонецЕсли;
		КонецЕсли;
		
		Если ДополнительныеПараметры.Свойство("ТребуетсяЗаполнениеКэша") Тогда
			НастройкиКэша = ИнтеграцияИСУНФ.НастройкиИсточникаКешаЧека();
			ПроверкаИПодборПродукцииИС.ЗаполнитьКешШтрихкодовУпаковок(Форма, НастройкиКэша);
		КонецЕсли;
		
	КонецЕсли;
	
	СобытияФормИС.ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти