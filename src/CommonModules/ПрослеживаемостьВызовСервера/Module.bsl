#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьВыгружаемыеДанныеУведомленияПоПрослеживаемости(Ссылка) Экспорт

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
		Возврат 
			ПрослеживаемостьБРУ.ВыгрузитьУведомлениеОбОстаткахПрослеживаемыхТоваров(Ссылка);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		Возврат 
			ПрослеживаемостьБРУ.ВыгрузитьУведомлениеОВвозеПрослеживаемыхТоваров(Ссылка);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров") Тогда
		Возврат 
			ПрослеживаемостьБРУ.ВыгрузитьУведомлениеОПеремещенииПрослеживаемыхТоваров(Ссылка);
		
	КонецЕсли;
		
	Возврат Неопределено;

КонецФункции

Функция МожноДобавлятьУведомленияОПеремещении() Экспорт
	
	Возврат ПравоДоступа("Добавление", Метаданные.Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров);

КонецФункции

Функция МожноДобавлятьУведомленияОбОстатках() Экспорт
	
	Возврат ПравоДоступа("Добавление", Метаданные.Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров);

КонецФункции

Функция ОбновитьТекстПоТекущейСтрокеОС(ПараметрыТоваров) Экспорт
	
	ДанныеПоСоставнымТоварам = Новый Структура("КоличествоСоставныхОбъектовВОС,Состав");
	
	ДанныеПоСоставнымТоварам.КоличествоСоставныхОбъектовВОС = 
		ПрослеживаемостьПереопределяемый.КоличествоТоваровВСоставеОС(ПараметрыТоваров);

	ДанныеПоСоставнымТоварам.Состав =
		ТекстСоставаТоваровВСоставеОС(ДанныеПоСоставнымТоварам.КоличествоСоставныхОбъектовВОС);
	
	Возврат ДанныеПоСоставнымТоварам;

КонецФункции

Функция ТекстСоставаТоваровВСоставеОС(КоличествоОбъектов) Экспорт
	
	Если КоличествоОбъектов = 0 Тогда 
		
		ТекстСостава = НСтр("ru='Указать состав'");
		
	Иначе
			
		СтрокаСКоличеством = ПолучитьСклоненияСтрокиПоЧислу(
			НСтр("ru='объект'"), КоличествоОбъектов,, "ЧС=Количественное", "ПД=Именительный; ПЧ=Число")[0];
			
		ТекстСостава = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='В составе: %1'"), СтрокаСКоличеством)
			
	КонецЕсли;
		
	Возврат ТекстСостава;
КонецФункции

#КонецОбласти
