#Область ПрограммныйИнтерфейс

Функция ИспользоватьВнешнийМодуль() Экспорт
	
	Возврат Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль.Получить();
	
КонецФункции

Функция ИнициализироватьКонтекстДокументооборотаСНалоговымиОрганами(ИндексСледующейОбработкиЭДО = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("КонтекстИнициализирован", 	Ложь);
	Результат.Вставить("ИндексОбработкиЭДО", 		0);
	
	ИмяОбработки_ДокументооборотСКО = Неопределено;
	КонтекстЭДО						= Неопределено;
	ЭтоПерваяИтерация				= Истина;
	ИнициализироватьКонтекст 		= Истина;
	
	Пока ИнициализироватьКонтекст Цикл
		
		ИнициализироватьКонтекст = Ложь;
		
		Если ИндексСледующейОбработкиЭДО = Неопределено Тогда
			// определяем свободный индекс для формирования уникального имени при подключени новой внешней обработки,
			// поскольку подключение новой внешней обработки с тем же именем приводит к ошибкам в уже открытых формах
			// из подключенной раньше внешней обработки
			ИнформацияОбОбработкеЭДО = СоздатьВнешнююОбработкуЭДО();
			Результат.ИндексОбработкиЭДО = ?(ИнформацияОбОбработкеЭДО.ОбработкаЭДО = Неопределено,
				0, ИнформацияОбОбработкеЭДО.ИндексОбработкиЭДО + 1);
			
		Иначе
			Результат.ИндексОбработкиЭДО = ИндексСледующейОбработкиЭДО;
		КонецЕсли;
		ИндексОбработкиЭДОСтрокой = Формат(Результат.ИндексОбработкиЭДО, "ЧДЦ=; ЧГ=");
		
		ВнешниеОбъектыХранилище = Константы.ДокументооборотСКонтролирующимиОрганами_ВнешнийМодуль;
		ДвоичныеДанныеОбработки = ВнешниеОбъектыХранилище.Получить().Получить();
		Если ДвоичныеДанныеОбработки <> Неопределено Тогда
			Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанныеОбработки);
			Попытка
				МенеджерВнешнихОбработок = ВнешниеОбработки;
				ОписаниеЗащиты = Новый ОписаниеЗащитыОтОпасныхДействий;
				ОписаниеЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;
				
				ИмяОбработки_ДокументооборотСКО = МенеджерВнешнихОбработок.Подключить(
					Адрес,
					"Обработка_ДокументооборотСКО" + ИндексОбработкиЭДОСтрокой,
					Ложь,
					ОписаниеЗащиты);
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(
					"Электронный документооборот с контролирующими органами.Подключение внешнего модуля",
					УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(НСтр("ru = 'Не удалось загрузить внешний модуль для документооборота с налоговыми органами:
									|%1
									|Будет использован модуль, встроенный в конфигурацию.'"), КраткоеПредставлениеОшибки(ИнформацияОбОшибке)));
				ДвоичныеДанныеОбработки = Неопределено;
			КонецПопытки;
			УдалитьИзВременногоХранилища(Адрес);
		КонецЕсли;
		Если ДвоичныеДанныеОбработки = Неопределено И Результат.ИндексОбработкиЭДО > 0 Тогда
			Результат.ИндексОбработкиЭДО = Результат.ИндексОбработкиЭДО - 1;
			ИндексОбработкиЭДОСтрокой = Формат(Результат.ИндексОбработкиЭДО, "ЧДЦ=; ЧГ=");
		КонецЕсли;
		
		Попытка
			ОбъектОбработка = ВнешниеОбработки.Создать("Обработка_ДокументооборотСКО" + ИндексОбработкиЭДОСтрокой);
			ОбъектОбработка.ПутьКОбъекту = "ВнешняяОбработка.Обработка_ДокументооборотСКО" + ИндексОбработкиЭДОСтрокой;
			КонтекстЭДО = НЕ (ИмяОбработки_ДокументооборотСКО = Неопределено);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ЗаписьЖурналаРегистрации(
				"Электронный документооборот с контролирующими органами.Подключение внешнего модуля",
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			ИмяОбработки_ДокументооборотСКО = Неопределено;
			КонтекстЭДО = Неопределено;
		КонецПопытки;
		
		ЭтоПерваяИтерация = Ложь;
		
	КонецЦикла;
	
	Результат.КонтекстИнициализирован = КонтекстЭДО <> Неопределено;
	Возврат Результат;
	
КонецФункции

Функция ЕстьПравоНаДОсКО(ПравоНаВнешнюю) Экспорт 
	
	Если ПравоНаВнешнюю Тогда
		Возврат Истина;
		
	Иначе
		Возврат ПравоДоступа("Использование", Метаданные.Обработки.ДокументооборотСКонтролирующимиОрганами)
			ИЛИ ПривилегированныйРежим();
	КонецЕсли;
	
КонецФункции

Функция ЕстьДоступНаЧтение()
	
	Возврат ПравоДоступа("Чтение", Метаданные.Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль)
		И ПравоДоступа("Чтение", Метаданные.Константы.ДокументооборотСКонтролирующимиОрганами_ВнешнийМодуль)
		ИЛИ ПривилегированныйРежим();
	
КонецФункции
 
Функция ПодключатьВнешнююОбработкуЭДО() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Подключать", 			Ложь);
	Результат.Вставить("ИндексОбработкиЭДО", 	0);
	
	// если есть доступ
	Если ЕстьДоступНаЧтение() Тогда
		Если Константы.ДокументооборотСКонтролирующимиОрганами_ИспользоватьВнешнийМодуль.Получить() Тогда
			ИнформацияОбОбработкеЭДО = СоздатьВнешнююОбработкуЭДО();
			
			Если ИнформацияОбОбработкеЭДО.ОбработкаЭДО = Неопределено Тогда
				ИнформацияОбИнициализации = ИнициализироватьКонтекстДокументооборотаСНалоговымиОрганами(0);
				Результат.Подключать 			= ИнформацияОбИнициализации.КонтекстИнициализирован;
				Результат.ИндексОбработкиЭДО 	= ИнформацияОбИнициализации.ИндексОбработкиЭДО;
				
			Иначе
				Результат.Подключать 			= Истина;
				Результат.ИндексОбработкиЭДО 	= ИнформацияОбОбработкеЭДО.ИндексОбработкиЭДО;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьВнешнююОбработкуЭДО()
	
	Результат = Новый Структура;
	Результат.Вставить("ОбработкаЭДО", 			Неопределено);
	Результат.Вставить("ИндексОбработкиЭДО", 	0);
	
	ИндексСледующейОбработкиЭДО = 0;
	Пока Истина Цикл
		ИндексСледующейОбработкиЭДОСтрокой = Формат(ИндексСледующейОбработкиЭДО, "ЧДЦ=; ЧГ=");
		Попытка
			СледующаяОбработкаЭДО = ВнешниеОбработки.Создать("Обработка_ДокументооборотСКО"
				+ ИндексСледующейОбработкиЭДОСтрокой);
		Исключение
			СледующаяОбработкаЭДО = Неопределено;
		КонецПопытки;
		
		Если СледующаяОбработкаЭДО = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		Результат.ОбработкаЭДО 			= СледующаяОбработкаЭДО;
		Результат.ИндексОбработкиЭДО 	= ИндексСледующейОбработкиЭДО;
		
		ИндексСледующейОбработкиЭДО = ИндексСледующейОбработкиЭДО + 1;
	КонецЦикла;
	
КонецФункции

Процедура ЗаполнитьПараметрыФормы(ПараметрыФормы, Параметры) Экспорт
	
	ВозможныеПредопределенныеПараметры = Новый Структура("ВыборГруппИЭлементов, ЗакрыватьПриВыборе, ЗакрыватьПриЗакрытииВладельца, КлючНазначенияИспользования, МножественныйВыбор, Отбор, ПараметрыФормы, ПараметрыФункциональныхОпций, РазрешитьВыборКорня, РежимВыбора, ТекущаяСтрока, ТолькоПросмотр, ФиксированныеНастройки");
	ЗаполнитьЗначенияСвойств(ВозможныеПредопределенныеПараметры, Параметры);
	Для Каждого ВозможныйПредопределенныйПараметр Из ВозможныеПредопределенныеПараметры Цикл
		Если ЗначениеЗаполнено(ВозможныйПредопределенныйПараметр.Значение) Тогда
			ПараметрыФормы.Добавить(ВозможныйПредопределенныйПараметр.Значение, ВозможныйПредопределенныйПараметр.Ключ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекущемуПользователюАОДоступен() Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиДокументооборота) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекущиеУчетныеЗаписи = ДокументооборотСКО.ТекущиеУчетныеЗаписиНалогоплательщика();
	Если ТекущиеУчетныеЗаписи.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеЗаписиДокументооборота.Ссылка КАК УчетнаяЗапись
	|ПОМЕСТИТЬ УчетныеЗаписи
	|ИЗ
	|	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	|ГДЕ
	|	УчетныеЗаписиДокументооборота.Ссылка В(&ТекущиеУчетныеЗаписи)
	|	И НЕ УчетныеЗаписиДокументооборота.ОтключитьАвтообмен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВЫРАЗИТЬ(Организации.УчетнаяЗаписьОбмена КАК Справочник.УчетныеЗаписиДокументооборота) КАК УчетнаяЗапись
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УчетныеЗаписи КАК УчетныеЗаписи
	|		ПО Организации.УчетнаяЗаписьОбмена = УчетныеЗаписи.УчетнаяЗапись
	|ГДЕ
	|	Организации.ВидОбменаСКонтролирующимиОрганами = ЗНАЧЕНИЕ(Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате)
	|	И НЕ Организации.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ТекущиеУчетныеЗаписи", ТекущиеУчетныеЗаписи);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ТекущемуПользователюЭДОДоступен() Экспорт
	
	Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ТекущемуПользователюАОДоступен();
	
КонецФункции

Функция ПолучитьВыбранныйCSPИзВременныхНастроек() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"МастерФормированияЗаявкиНаПодключение",
		"ВыбранныйКриптопровайдер",
		,,
		
	);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////

Процедура ПриИнициализацииФормыРегламентированногоОтчета(Отчет, ОрганизацияОтчета, КонтролирующийОрган, ПараметрыПрорисовкиКнопокОтправки, ПараметрыПрорисовкиПанели) Экспорт
	
	// прорисовываем кнопки отправки
	ПараметрыПрорисовкиКнопокОтправки 	= ПараметрыПрорисовкиКнопокОтправки(ОрганизацияОтчета, КонтролирующийОрган, Отчет);
	// прорисовываем панель отправки
	ПараметрыПрорисовкиПанели 			= ПараметрыПрорисовкиПанелиОтправки(Отчет, ОрганизацияОтчета, КонтролирующийОрган);
	
КонецПроцедуры

Функция ПараметрыПрорисовкиКнопокОтправки(ОрганизацияОтчета, КонтролирующийОрган, Отчет = Неопределено) Экспорт
	
	ПараметрыПрорисовкиКнопокОтправки = Новый Структура;
	
	Попытка
		
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
		
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ГруппаОтправкаВКонтролирующийОрган", Истина);
		
		СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияОтчета,
			"ВидОбменаСКонтролирующимиОрганами, УчетнаяЗаписьОбмена");
		
		// прорисовываем кнопки отправки
		ПредназначенаДляДокументооборотаСКонтролирующимОрганом = Истина;
		УчетнаяЗаписьДокументооборота = СвойстваОрганизации.УчетнаяЗаписьОбмена;
		
		ДокументооборотНастроенВУнивесальномФормате = 
			СвойстваОрганизации.ВидОбменаСКонтролирующимиОрганами = ПредопределенноеЗначение("Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате")
			И ЗначениеЗаполнено(УчетнаяЗаписьДокументооборота);
		
		Если ДокументооборотНастроенВУнивесальномФормате Тогда
			
			Если (КонтролирующийОрган = "ФНС" И УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗаписьДокументооборота) <> Истина)
				ИЛИ (КонтролирующийОрган = "ПФР" И УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗаписьДокументооборота) <> Истина)
				ИЛИ (КонтролирующийОрган = "ФСГС" И УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗаписьДокументооборота) <> Истина) Тогда
				ПредназначенаДляДокументооборотаСКонтролирующимОрганом = Ложь;
			КонецЕсли;
			//
			ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);		
			Если НЕ ПредназначенаДляДокументооборотаСКонтролирующимОрганом Тогда
				ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
				
			Иначе
				Попытка
					ОбменИлиОнлайнПроверкаОтключена = УчетнаяЗаписьДокументооборота.ОбменНапрямую ИЛИ НЕ УчетнаяЗаписьДокументооборота.ИспользоватьСервисОнлайнПроверкиОтчетов;
				Исключение
					ОбменИлиОнлайнПроверкаОтключена = Истина;
				КонецПопытки;
				
				Если ОбменИлиОнлайнПроверкаОтключена Тогда
					ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
					
				Иначе
					Если УчетнаяЗаписьДокументооборота.СпецоператорСвязи = Перечисления.СпецоператорыСвязи.Такском Тогда
						ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);
						
					ИначеЕсли КонтекстЭДОСервер = Неопределено Тогда
						ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
						
					Иначе
						ОнлайнПроверкаКонтролирующиеОрганы =
							ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(
							УчетнаяЗаписьДокументооборота.СпецоператорСвязи, "ОнлайнПроверкаКонтролирующиеОрганы");
						ОнлайнПроверкаКонтролирующиеОрганы = СокрЛП(ОнлайнПроверкаКонтролирующиеОрганы);
						ОнлайнПроверкаКонтролирующиеОрганы = ВРег(ОнлайнПроверкаКонтролирующиеОрганы);
						КонтролирующийОрганОператора = ?(КонтролирующийОрган = "ЦПРР", "ФНС", КонтролирующийОрган);
						КонтролирующийОрганОператора = ВРег(КонтролирующийОрганОператора);
						
						Если СтрНайти(";" + ОнлайнПроверкаКонтролирующиеОрганы + ";", ";" + КонтролирующийОрганОператора + ";") = 0 Тогда
							ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
						Иначе
							ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ИмяТипаДокументаСведенияДляИсчисленияДоплатыКПенсииСЗВ_ДСО = "СведенияДляИсчисленияДоплатыКПенсииСЗВ_ДСО";
			Если Метаданные.Документы.Найти(ИмяТипаДокументаСведенияДляИсчисленияДоплатыКПенсииСЗВ_ДСО) <> Неопределено
				И ТипЗнч(Отчет) = Тип("ДокументСсылка." + ИмяТипаДокументаСведенияДляИсчисленияДоплатыКПенсииСЗВ_ДСО)
				И НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отправка СЗВ-ДСО") Тогда
				
				ПараметрыПрорисовкиКнопокОтправки.Вставить("ОтправитьВКонтролирующийОрган", Ложь);
				ПараметрыПрорисовкиКнопокОтправки.Вставить("ФормаОтправитьВПФР", Ложь);
			КонецЕсли;
			
		Иначе
			// Если нет учетной записи, то оставляем меню ПроверитьВИнтернете, чтобы по нему показывать предложение подключиться
			ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Истина);
		КонецЕсли;
		
	Исключение
		
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ГруппаОтправкаВКонтролирующийОрган", Истина);
		ПараметрыПрорисовкиКнопокОтправки.Вставить("ПроверитьВИнтернете", Ложь);
		
	КонецПопытки;
	
	Возврат ПараметрыПрорисовкиКнопокОтправки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСФНС
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСФНС;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в ФНС для учетной записи'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСПФР
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСПФР;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в ПФР для учетной записи'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));

		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСФСГС
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСФСГС;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в Росстат для учетной записи'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция УчетнаяЗаписьПредназначенаДляДокументооборотаСЦБ(УчетнаяЗапись) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	УчетныеЗаписиДокументооборота.ПредназначенаДляДокументооборотаСЦБ
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиДокументооборота КАК УчетныеЗаписиДокументооборота
	                      |ГДЕ
	                      |	УчетныеЗаписиДокументооборота.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗапись);
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ПредназначенаДляДокументооборотаСЦБ;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Проверка наличия возможности отправки в ЦБ РФ для учетной записи'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция ПолучитьСообщенияЦиклаОбмена(ЦиклОбмена, ТипыСообщений = Неопределено, ПомеченныеНаУдаление = Ложь) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ТранспортноеСообщение.Ссылка,
	                      |	ТранспортноеСообщение.Статус,
	                      |	ТранспортноеСообщение.Тип,
	                      |	ТранспортноеСообщение.ПометкаУдаления,
	                      |	ТранспортноеСообщение.Дата,
	                      |	ТранспортноеСообщение.ИдентификаторСообщения,
	                      |	ТранспортноеСообщение.ДатаТранспорта,
	                      |	ТранспортноеСообщение.Основание,
	                      |	ТранспортноеСообщение.ПротоколСОшибкой,
	                      |	ТранспортноеСообщение.Тема
	                      |ИЗ
	                      |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	                      |ГДЕ
	                      |	ТранспортноеСообщение.ЦиклОбмена = &ЦиклОбмена");
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	
	Если НЕ ПомеченныеНаУдаление Тогда
		Запрос.Текст = Запрос.Текст + "
		                      |	И ТранспортноеСообщение.ПометкаУдаления = &ПометкаУдаления";
		Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	КонецЕсли;
	
	Если ТипыСообщений <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		                      |	И ТранспортноеСообщение.Тип В (&ТипыСообщений)";
		Запрос.УстановитьПараметр("ТипыСообщений", ТипыСообщений);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СообщениеЗашифровано(Сообщение) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ИСТИНА КАК Поле1
	                      |ИЗ
	                      |	РегистрСведений.ТранспортныеКонтейнеры КАК ТранспортныеКонтейнеры
	                      |ГДЕ
	                      |	ТранспортныеКонтейнеры.ТранспортноеСообщение = &ТранспортноеСообщение");
	Запрос.УстановитьПараметр("ТранспортноеСообщение", Сообщение);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СообщениеРасшифровано(Сообщение) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ИСТИНА КАК Поле1
	                      |ИЗ
	                      |	РегистрСведений.СодержимоеТранспортныхКонтейнеров КАК СодержимоеТранспортныхКонтейнеров
	                      |ГДЕ
	                      |	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение = &ТранспортноеСообщение");
	Запрос.УстановитьПараметр("ТранспортноеСообщение", Сообщение);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ПолучитьПоследнийЦиклОбмена(Объект) Экспорт
	
	Если Объект <> Неопределено Тогда
		Ссылка = Объект.Ссылка;
		Если Не ЗначениеЗаполнено(Ссылка) Тогда
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Документы.ТранспортноеСообщение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
		Ссылка = ПолучитьОписьВходящихДокументовПоТребованию(Ссылка);
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ЦиклОбмена.Ссылка КАК Ссылка,
	                      |	ЦиклОбмена.ДатаСоздания КАК ДатаСоздания
	                      |ПОМЕСТИТЬ ЦиклыОбмена
	                      |ИЗ
	                      |	Справочник.ЦиклыОбмена КАК ЦиклОбмена
	                      |ГДЕ
	                      |	ЦиклОбмена.Предмет = &Объект
	                      |	И ЦиклОбмена.ПометкаУдаления = ЛОЖЬ
	                      |	И ЦиклОбмена.Тип В(&Тип)
	                      |
	                      |ОБЪЕДИНИТЬ
	                      |
	                      |ВЫБРАТЬ
	                      |	ЦиклОбмена.Ссылка,
	                      |	ЦиклОбмена.ДатаСоздания
	                      |ИЗ
	                      |	Справочник.ЦиклыОбмена КАК ЦиклОбмена
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЦиклыОбмена.ДополнительныеПредметы КАК ТЧЦиклОбмена
	                      |		ПО (ТЧЦиклОбмена.Ссылка = ЦиклОбмена.Ссылка)
	                      |ГДЕ
	                      |	ЦиклОбмена.Предмет = &Объект
	                      |	И ЦиклОбмена.ПометкаУдаления = ЛОЖЬ
	                      |	И ЦиклОбмена.Тип В(&Тип)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ЦиклыОбмена.Ссылка КАК ЦиклОбмена,
	                      |	ЦиклыОбмена.ДатаСоздания КАК ДатаСоздания
	                      |ИЗ
	                      |	ЦиклыОбмена КАК ЦиклыОбмена
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	                      |		ПО (ТранспортноеСообщение.ЦиклОбмена = ЦиклыОбмена.Ссылка)
	                      |ГДЕ
	                      |	(ТранспортноеСообщение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПисем.Полученное)
	                      |			ИЛИ ТранспортноеСообщение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПисем.Отправленное)
	                      |			ИЛИ ТранспортноеСообщение.Тип В (&ТипПервичныхСообщений)
	                      |				И ТранспортноеСообщение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПисем.Исходящее))
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаСоздания УБЫВ");

	Запрос.УстановитьПараметр("Объект", 				Ссылка);
	Запрос.УстановитьПараметр("Тип", 					ТипыЦикловОбмена());
	Запрос.УстановитьПараметр("ТипПервичныхСообщений", 	ТипПервичныхСообщений());
	
	Попытка
		РезультатЗапроса = Запрос.Выполнить();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.ЦиклОбмена) = Тип("СправочникСсылка.ЦиклыОбмена") Тогда
			Возврат Выборка.ЦиклОбмена;
		КонецЕсли;
	КонецЦикла;
	Возврат Справочники.ЦиклыОбмена.ПустаяСсылка();
	
КонецФункции

Функция ПолучитьСсылкуНаПротоколПоЦиклуОбмена(ЦиклОбмена)
	
	ТипыПротоколов = Новый Массив;
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.ПротоколПФР);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.ПротоколВходногоКонтроляОтчетностиФСГС);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаДекларацияНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатОбработкиДекларацияНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаЗаявлениеНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатОбработкиЗаявлениеНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаФорма2НДФЛНО);
	ТипыПротоколов.Добавить(Перечисления.ТипыТранспортныхСообщений.РезультатПриемаПредставлениеНО);
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ТранспортноеСообщение.Ссылка
	                      |ИЗ
	                      |	Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	                      |ГДЕ
	                      |	ТранспортноеСообщение.ЦиклОбмена = &ЦиклОбмена
	                      |	И ТранспортноеСообщение.Тип В(&ТипыПротоколов)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ТранспортноеСообщение.Тип.Порядок УБЫВ");
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	Запрос.УстановитьПараметр("ТипыПротоколов", ТипыПротоколов);
	
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСвойстваАктуальногоПротокола(СсылкаНаОтчет) Экспорт
	
	ЦиклОбмена = ПолучитьПоследнийЦиклОбмена(СсылкаНаОтчет);
	Если НЕ ЗначениеЗаполнено(ЦиклОбмена) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Протокол = ПолучитьСсылкуНаПротоколПоЦиклуОбмена(ЦиклОбмена);
	
	Возврат Новый Структура("Дата, Протокол", ЦиклОбмена.ДатаСоздания, Протокол);
	
КонецФункции

Процедура УстановитьПараметрСеансаТекущиеУчетныеЗаписиНалогоплательщика(ИмяПараметра = Неопределено, УстановленныеПараметры = Неопределено)Экспорт
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.Справочники.УчетныеЗаписиДокументооборота) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПользователиУчетныхЗаписейДокументооборота.УчетнаяЗапись.Ссылка КАК УчетнаяЗапись
	|ИЗ
	|	РегистрСведений.ПользователиУчетныхЗаписейДокументооборота КАК ПользователиУчетныхЗаписейДокументооборота
	|ГДЕ
	|	ПользователиУчетныхЗаписейДокументооборота.Пользователь = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	МассивУчетныхЗаписей = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивУчетныхЗаписей.Добавить(Выборка.УчетнаяЗапись);
	КонецЦикла;
	
	Если Пользователи.ЭтоПолноправныйПользователь(,,Ложь) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.УчетнаяЗаписьОбмена КАК УчетнаяЗаписьОбмена
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ВидОбменаСКонтролирующимиОрганами = ЗНАЧЕНИЕ(Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате)
		|	И НЕ Организации.ПометкаУдаления
		|	И НЕ Организации.УчетнаяЗаписьОбмена.ЭтоМультиРежим
		|	И НЕ Организации.УчетнаяЗаписьОбмена.ЭлектроннаяПодписьВМоделиСервиса";
		УчетныеЗаписиПолныхПрав = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УчетнаяЗаписьОбмена");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивУчетныхЗаписей, УчетныеЗаписиПолныхПрав, Истина);
		
	КонецЕсли;
	
	МассивУчетныхЗаписейФиксированныйМассив = Новый ФиксированныйМассив(МассивУчетныхЗаписей);
	ПараметрыСеанса.ТекущиеУчетныеЗаписиНалогоплательщика = МассивУчетныхЗаписейФиксированныйМассив;
	Если УстановленныеПараметры <> Неопределено Тогда 
		УстановленныеПараметры.Добавить("ТекущиеУчетныеЗаписиНалогоплательщика");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ОрганизацияОтчета, КонтролирующийОрган, НастроенОбменВУниверсальномФормате, УчетнаяЗаписьПредназначенаДляДокументооборотаСКО) Экспорт
	
	НастроенОбменВУниверсальномФормате = Ложь;
	УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = Ложь;
	
	СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОрганизацияОтчета, "ВидОбменаСКонтролирующимиОрганами, УчетнаяЗаписьОбмена");
	
	УчетнаяЗаписьДокументооборота = СвойстваОрганизации.УчетнаяЗаписьОбмена;
	Если СвойстваОрганизации.ВидОбменаСКонтролирующимиОрганами = ПредопределенноеЗначение("Перечисление.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате")
	И ЗначениеЗаполнено(УчетнаяЗаписьДокументооборота) Тогда
		НастроенОбменВУниверсальномФормате = Истина;
		Если КонтролирующийОрган = "ФНС" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСФНС(УчетнаяЗаписьДокументооборота);
		ИначеЕсли КонтролирующийОрган = "ПФР" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСПФР(УчетнаяЗаписьДокументооборота);
		ИначеЕсли КонтролирующийОрган = "ФСГС" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСФСГС(УчетнаяЗаписьДокументооборота);
		ИначеЕсли КонтролирующийОрган = "ЦБ" Тогда
			УчетнаяЗаписьПредназначенаДляДокументооборотаСКО = УчетнаяЗаписьПредназначенаДляДокументооборотаСЦБ(УчетнаяЗаписьДокументооборота);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНастройкиФСС(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ОнлайнПроверкаДоступна = Истина;
	ИспользоватьОбмен = КонтекстЭДО.ОрганизацияИспользуетОбменСФСС(ОрганизацияСсылка, , ОнлайнПроверкаДоступна);
	
	Результат.Вставить("ИспользоватьОбмен", ИспользоватьОбмен);
	Результат.Вставить("ОнлайнПроверкаДоступна", ОнлайнПроверкаДоступна);
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиФСРАР(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Результат = КонтекстЭДО.НастройкиФСРАР(ОрганизацияСсылка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиРПН(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Результат = КонтекстЭДО.НастройкиРПН(ОрганизацияСсылка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНастройкиФТС(ОрганизацияСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Результат = КонтекстЭДО.НастройкиФТС(ОрганизацияСсылка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция УчетнаяЗаписьОрганизации(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДО = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДО.УчетнаяЗаписьОрганизации(Организация);
	
КонецФункции

#Область НоваяФормаРегламентированнойОтчетности

Функция ПараметрыПрорисовкиПанелиОтправки(Знач Ссылка, Знач Организация = Неопределено, Знач КонтролирующийОрган = "ФНС") Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получение параметров прорисовки
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Истина);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Истина);
	ДополнительныеПараметры.Вставить("ВозвращатьДокументыРеализацииПолномочийНО", Истина);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Ссылка, КонтролирующийОрган, ДополнительныеПараметры);
	
	Возврат ТекущееСостояние;
	
КонецФункции

Функция ПараметрыПрорисовкиСтатуса(Знач Ссылка, КонтролирующийОрган = "ФНС", ПолучатьДаты = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", ПолучатьДаты);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Ложь);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Ссылка, КонтролирующийОрган, ДополнительныеПараметры);
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Возврат ТекущееСостояние;
	
КонецФункции

// Обновление ИБ
Процедура ЗаполнитьРегистрЖурналОтправокВКонтролирующиеОрганы(Параметры = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документы.ЗапросНаИнформационноеОбслуживаниеСтрахователя.ПерезаполнитьЗначениеРеквизитаНаДату();
	
	ВыборкаДетальныеЗаписи = ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы();
		
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Предмет = ВыборкаДетальныеЗаписи.Объект;
		Отказ 	= Ложь;
		
		Попытка
			
			// Запись в регистр
			ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаписьОбъектовРегламентированнойОтчетности(Предмет, Отказ);
			
		Исключение
		
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при заполнении регистра ЖурналОтправокВКонтролирующиеОрганы'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				УровеньЖурналаРегистрации.Ошибка,, Предмет,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецПопытки;

	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция КоличествоОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыборкаОбъектовНеЗаписанныхВРегистр	= ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы();
	КоличествоОбъектов					= ВыборкаОбъектовНеЗаписанныхВРегистр.Количество();
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат  КоличествоОбъектов;
	
КонецФункции

Функция ВыборкаОбъектовНеЗаписанныхВРегистрЖурналОтправокВКонтролирующиеОрганы() Экспорт
	
	МассивТипов = ТипыОбъектовЭДООтображаемыхВФорме1СОтчетность();
	
	// Дополняем таблицу типов объектами, не входящими в БРО
	ТаблицаОписанияОбъектовНеВходящихВБРО 	= РегламентированнаяОтчетность.ТаблицаОписанияОбъектовРегламентированнойОтчетности();
	ОписанияОбъектовНеВходящихВБРО = ТаблицаОписанияОбъектовНеВходящихВБРО.НайтиСтроки(
		Новый Структура("ВидДокумента", Перечисления.СтраницыЖурналаОтчетность.Уведомления));
	
	Для каждого Описание Из ОписанияОбъектовНеВходящихВБРО Цикл
		МассивТипов.Добавить(Описание.ТипОбъекта);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	// Составляем запрос
	КоличествоТипов = МассивТипов.Количество();
	ИндексЭлемента = 0;
	Для каждого ТипОбъекта Из МассивТипов Цикл
		
		ОбъектМетаданных 	= Метаданные.НайтиПоТипу(ТипОбъекта);
		
		Если ОбъектМетаданных <> Неопределено Тогда
		
			ПолноеИмяТипа 		= ОбъектМетаданных.ПолноеИмя();
			ИмяТипа 			= ОбъектМетаданных.Имя;
			
			// Формируем основную часть запроса
			Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
				|	%1.Ссылка КАК Объект
				|ИЗ
				|	%2 КАК %1
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
				|		ПО (ЖурналОтправокВКонтролирующиеОрганы.Ссылка = %1.Ссылка)
				|ГДЕ
				|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка Есть NULL";
				
			// Берем не все электронные представление, а только относящиеся к уведомлениям
			Если ТипОбъекта = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				Запрос.Текст = Запрос.Текст + "
					|И (%1.ВидОтчета В (&ВидыОтчетовРазделаУведомления))";
					
				ВидыОтчетовРазделаУведомления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ВидыЭлектронныхПредставленийВРазделеУведомления();
				Запрос.УстановитьПараметр("ВидыОтчетовРазделаУведомления", ВидыОтчетовРазделаУведомления);
			КонецЕсли;
				
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ИмяТипа);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%2", ПолноеИмяТипа);
				
			// Добавляем объединение между запросами
			Если ИндексЭлемента <> КоличествоТипов - 1 Тогда
				  Запрос.Текст = Запрос.Текст + "
				  |
				  |ОБЪЕДИНИТЬ ВСЕ
				  |
				  |";
				  
			КонецЕсли;
			
		КонецЕсли;
		
		ИндексЭлемента = ИндексЭлемента + 1;
		
	КонецЦикла;
	
	// Выполнение полученного запроса
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ТипыОбъектовЭДООтображаемыхВФорме1СОтчетность() Экспорт
	
	// Здесь нет МакетыПенсионныхДел и ЗаявлениеОНазначенииПенсии, потому что они отображаются в другом регистре сведений  
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика"));
	МассивТипов.Добавить(Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения"));
	МассивТипов.Добавить(Тип("ДокументСсылка.УведомлениеОПолучателеДокументов"));
	МассивТипов.Добавить(Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр"));
	МассивТипов.Добавить(Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ЗаявленияОбОтзывеМЧДФНС"));
	МассивТипов.Добавить(Тип("СправочникСсылка.МашиночитаемыеДоверенностиФСС"));
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
		МассивТипов.Добавить(Тип("СправочникСсылка." + ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ));
	КонецЕсли;
	МассивТипов.Добавить(Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаявленияПоЭлДокументооборотуСПФР"));
	МассивТипов.Добавить(Тип("ДокументСсылка.УведомлениеОПредоставленииПолномочийПредставителю"));
	МассивТипов.Добавить(Тип("ДокументСсылка.УведомлениеОПрекращенииПолномочийПредставителя"));
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦПРРМЧДСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
		МассивТипов.Добавить(Тип("СправочникСсылка." + ИмяТипаСправочникаМашиночитаемыеДоверенности));
	КонецЕсли;
	
	Возврат МассивТипов;
	
КонецФункции

Функция ЦветФонаПанелиОтправкиПоСтатусу(СостояниеСдачиОтчетности) Экспорт
	
	ЦветФона = ЦветаСтиля.ЦветФонаНеначавшейсяОтправки; 
	Если СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНачат 
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ТребуетсяПодтверждениеПриема Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаТекущейОтправки;// желтый
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ОтправленоИзКонтролирующегоОргана
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНеНачат
		ИЛИ СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПриемПодтвержден Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаНеначавшейсяОтправки;// серый
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ОтрицательныйРезультатДокументооборота Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаОшибкиОтправки;// красный
	ИначеЕсли СостояниеСдачиОтчетности = Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота Тогда
		ЦветФона = ЦветаСтиля.ЦветФонаУдачнойОтправки;// зеленый
	КонецЕсли;
	
	Возврат ЦветФона;
	
КонецФункции

Функция ЕстьДоступККонтекстуЭДО() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер <> Неопределено;
	
КонецФункции

Функция ПолучитьОписьВходящихДокументовПоТребованию(ТребованиеСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы.Ссылка 
						  |ИЗ
		|	Справочник.ОписиВходящихДокументовИзНалоговыхОрганов.ВходящиеДокументы КАК ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы
						  |ГДЕ
		|	ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы.СсылкаНаОбъект = &ТребованиеСсылка";

	Запрос.УстановитьПараметр("ТребованиеСсылка", ТребованиеСсылка);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;

	Возврат Неопределено;
	
КонецФункции

Функция ТипыЦикловОбмена() Экспорт
	
	ТипМассив = Новый Массив;
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НалоговаяИлиБухгалтерскаяОтчетность);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОтчетностьПФР);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НеформализованнаяПерепискаПФРИсходящие);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.НеформализованнаяПерепискаПФРВходящие);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.РассылкаПФР);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Форма2НДФЛ);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОбращениеНП);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Представление);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИОН);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Документ);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Заявление);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОтчетностьФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ПисьменноеОбращениеВФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИОС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ПисьмоНО);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.Рассылка);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.РассылкаГрупповая);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ИндивидуальноеИнформированиеФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.РассылкаФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.РассылкаШаблоновФСГС);
	ТипМассив.Добавить(Перечисления.ТипыЦикловОбмена.ОтчетностьЦБ);
	
	Возврат ТипМассив;
	
КонецФункции

Функция ТипПервичныхСообщений() Экспорт
	
	ТипМассивПервичное = Новый Массив;
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ДекларацияНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееОтчетностьПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоИсходящееПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоВходящееПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.Форма2НДФЛНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ОбращениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПредставлениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗапросНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ДокументНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗаявлениеНП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееОтчетностьФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоИсходящееФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееЗапросПФР);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ЗапросНаВыпискуЕРГЮЛ_ЕГРИП);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.РассылкаНО);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПисьмоВходящееФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.РассылкаФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.РассылкаШаблоновФСГС);
	ТипМассивПервичное.Добавить(Перечисления.ТипыТранспортныхСообщений.ПервичноеСообщениеСодержащееОтчетностьЦБ);
	
	Возврат ТипМассивПервичное;
	
КонецФункции

#КонецОбласти

// Функцию нельзя переносить в повторное использование, так как режим могут поменять,
// а функция будет возвращать прежнее значение.
Функция ИспользуетсяРежимТестирования() Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.Константы.ДокументооборотСКонтролирующимиОрганами_РежимТестирования) Тогда
		Возврат Константы.ДокументооборотСКонтролирующимиОрганами_РежимТестирования.Получить();
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ТипыСодержимогоТранспортногоКонтейнераПервичныхСообщений() Экспорт
	
	ТипыВложений = Новый Массив;
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ФайлОтчетности);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ФайлОтчетностиПФР);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ФайлОтчетностиФСГС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Заявление);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Форма2НДФЛ);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ЗапросИОН);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ЗапросИОС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Представление);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Письмо);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ПисьмоФСГС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.РассылкаФСГС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ШаблонФСГС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ОписаниеРассылкиШаблоновФСГС);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Документ);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Рассылка);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Обращение);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Запрос);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.НеформализованныйДокумент);
	ТипыВложений.Добавить(Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ФайлОтчетностиЦБ);
	
	Возврат ТипыВложений;
	
КонецФункции

Функция ЭтоРегламентированныйОтчетРеестрНДС(СсылкаНаОтчет) Экспорт
	
	Если ТипЗнч(СсылкаНаОтчет)  = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ИсточникОтчета = СсылкаНаОтчет.ИсточникОтчета;
		Возврат Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрНДСПриложение") > 0
			ИЛИ Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрНДСДекларации") > 0;
		
	Иначе
		
		Возврат Ложь;
		
	Конецесли;
	
КонецФункции

Функция ЭтоРегламентированныйОтчетРеестрАкцизы(СсылкаНаОтчет) Экспорт
	
	Если ТипЗнч(СсылкаНаОтчет)  = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ИсточникОтчета = СсылкаНаОтчет.ИсточникОтчета;
		Возврат Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрАкцизыПриложение") > 0
			ИЛИ Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрАкцизыВычетыДенатурированныйЭтиловыйСпирт") > 0
			ИЛИ Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрАкцизыВычетыВиноматериалыВиногрИФруктСусло") > 0
			ИЛИ Найти(ИсточникОтчета, "РегламентированныйОтчетРеестрАкцизыВычетыВиноград") > 0;
		
	Иначе
		
		Возврат Ложь;
		
	Конецесли;
	
КонецФункции

Функция ОрганизацияПодключенаКИнспекции(СсылкаНаОбъект) Экспорт

	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер.ОрганизацияПодключенаКИнспекции(СсылкаНаОбъект);
	
КонецФункции
	
Функция ПутьКОбъекту() Экспорт

	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		// Нет прав
		Возврат Неопределено;
	Иначе
		Возврат КонтекстЭДОСервер.ПутьКОбъекту;
	КонецЕсли;

КонецФункции

Процедура ВФонеОбработатьИзменившиесяКодыФСГС() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер <> Неопределено Тогда 
		Если КонтекстЭДОСервер.ЭлектронныйДокументооборотИспользуется() = Ложь Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Задание = "ЭлектронныйДокументооборотСКонтролирующимиОрганами.ФоноваяЗаменаВОрганизацияхИПодразделенияхКодОрганаФСГС";
	
	ПараметрыЗапуска = Новый Массив;
	ПараметрыЗапуска.Добавить(Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "Замена кодов ТОГС";
	
	ДлительныеОперации.ВыполнитьВФоне(Задание, ПараметрыЗапуска, ПараметрыВыполнения);	
	
КонецПроцедуры

#Область КонтактнаяИнформация

Функция ПредставлениеКонтактнойИнформации(ЗначениеАдреса) Экспорт
	
	Результат = ЗначениеАдреса;
	Попытка
		Результат = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЗначениеАдреса);
	Исключение
		ТекстОшибки = НСтр("ru = 'Электронный документооборот с контролирующими органами. Получение представления адреса'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(
			ТекстОшибки,
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Результат;
	
Конецфункции

Процедура ПреобразоватьАдресВФорматXML(Адрес, ВидКонтактнойИнформации) Экспорт
	
	ОбработкаЗаявленийАбонента.ПреобразоватьАдресВФорматXML(Адрес, ВидКонтактнойИнформации);
	
КонецПроцедуры

#КонецОбласти

Функция ПолучитьКодВидаДокументаФизическогоЛица(ДокументФизическогоЛица) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = "";
	
	Если ЗначениеЗаполнено(ДокументФизическогоЛица) Тогда
		Результат = РегламентированнаяОтчетностьПереопределяемый.ПолучитьКодВидаДокументаФизическогоЛица(
			ДокументФизическогоЛица);
		
		Если НЕ ЗначениеЗаполнено(Результат)
			И ТипЗнч(ДокументФизическогоЛица) = Тип("СправочникСсылка.ВидыДокументовФизическихЛиц")
			И Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодМВД") <> Неопределено Тогда
			
			Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументФизическогоЛица, "КодМВД");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьВидДокументаПоКодуИлиНаименованию(КодВидаДокумента, НаименованиеВидаДокумента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(КодВидаДокумента)
		И Метаданные.Справочники.ВидыДокументовФизическихЛиц.Реквизиты.Найти("КодМВД") <> Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
			|ГДЕ
			|	ВидыДокументовФизическихЛиц.КодМВД = &КодВидаДокумента
			|	И ВидыДокументовФизическихЛиц.ПометкаУдаления = ЛОЖЬ");
		Запрос.УстановитьПараметр("КодВидаДокумента", Строка(КодВидаДокумента));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВидыДокументовФизическихЛиц.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыДокументовФизическихЛиц КАК ВидыДокументовФизическихЛиц
		|ГДЕ
		|	ВидыДокументовФизическихЛиц.Наименование = &НаименованиеВидаДокумента
		|	И ВидыДокументовФизическихЛиц.ПометкаУдаления = ЛОЖЬ");
	Запрос.УстановитьПараметр("НаименованиеВидаДокумента", Строка(НаименованиеВидаДокумента));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодВидаДокумента = ПолучитьКодВидаДокументаФизическогоЛица(Выборка.Ссылка);
		Если ЗначениеЗаполнено(КодВидаДокумента) Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	// нет видов документа с таким наименованием и заполненным кодом
	Возврат Справочники.ВидыДокументовФизическихЛиц.НайтиПоНаименованию(Строка(НаименованиеВидаДокумента), Ложь);

КонецФункции

Функция РазложитьФИО(Знач ФамилияИмяОтчество) Экспорт
	
	Возврат РегламентированнаяОтчетность.РазложитьФИО(ФамилияИмяОтчество);
	
КонецФункции

Функция ПолучитьСвойстваОрганизации(Организация) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЭтоЮридическоеЛицо", Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		Результат.ЭтоЮридическоеЛицо = РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация);
	КонецЕсли;
	
	Результат.Вставить("ЭтоРоссийскаяОрганизация", Истина);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Результат.ЭтоРоссийскаяОрганизация = НЕ Результат.ЭтоЮридическоеЛицо
			ИЛИ РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация,, "ТипНП").ТипНП <> 5;
		
		Если Результат.ЭтоЮридическоеЛицо Тогда
			Если Результат.ЭтоРоссийскаяОрганизация Тогда
				РеквизитыОрганизации = "НаимЮЛПол, ИННЮЛ, КППЮЛ, ОГРН, АдрЮР, АдрЮР_XML, АдрЮР_JSON, КодНО, "
					+ "ФИОРук, ФамилияРук, ИмяРук, ОтчествоРук, ИННРук, ГраждРук, ПолРук, ДатаРождРук, ДолжнРук, "
					+ "КодУдЛичнРук, ВидУдЛичнРук, СерияУдЛичнРук, НомерУдЛичнРук, ДатаУдЛичнРук, ОрганВыданУдЛичнРук, "
					+ "КодПодрУдЛичнРук";
				
			Иначе
				РеквизитыОрганизации = "НаимЮЛПол, ИННЮЛ, КППЮЛ, ОГРН, АдрИО, АдрИО_XML, АдрИО_JSON, НаимИОПол, СтрИО, "
					+ "КодИОСтрРег, КодНО, "
					+ "ФИОРук, ФамилияРук, ИмяРук, ОтчествоРук, ИННРук, ГраждРук, ПолРук, ДатаРождРук, МестоРождРук, ДолжнРук, "
					+ "КодУдЛичнРук, ВидУдЛичнРук, СерияУдЛичнРук, НомерУдЛичнРук, ДатаУдЛичнРук, ОрганВыданУдЛичнРук, "
					+ "КодПодрУдЛичнРук";
			КонецЕсли;
			
		Иначе
			РеквизитыОрганизации = "ФИО, ФамилияИП, ИмяИП, ОтчествоИП, ИННФЛ, ОГРН, Гражд, ДатаРожд, КодНО, "
				+ "КодУдЛичн, ВидУдЛичн, СерияУдЛичн, НомерУдЛичн, ДатаУдЛичн, ОрганВыданУдЛичн, КодПодрУдЛичн";
		КонецЕсли;
		
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация,,
			РеквизитыОрганизации);
		
		Для каждого ЗначениеРеквизита Из СведенияОбОрганизации Цикл
			Результат.Вставить(ЗначениеРеквизита.Ключ, ЗначениеРеквизита.Значение);
		КонецЦикла;
		
		Если Результат.ЭтоЮридическоеЛицо Тогда
			Если НЕ ЗначениеЗаполнено(Результат.ФамилияРук) ИЛИ НЕ ЗначениеЗаполнено(Результат.ИмяРук)
				ИЛИ Результат.ИмяРук = Результат.ФИОРук Тогда
				
				СтруктураФИО = РегламентированнаяОтчетность.РазложитьФИО(Результат.ФИОРук);
				Результат.ФамилияРук 	= СокрЛП(СтруктураФИО.Фамилия);
				Результат.ИмяРук 		= СокрЛП(СтруктураФИО.Имя);
				Результат.ОтчествоРук 	= СокрЛП(СтруктураФИО.Отчество);
			КонецЕсли;
			
			Если НЕ Результат.ЭтоРоссийскаяОрганизация Тогда
				МестоРождения = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПредставлениеМестаРождения(
					Результат.МестоРождРук);
				Результат.Вставить("МестоРождения", МестоРождения);
			КонецЕсли;
			
			ВидУдостоверения = ДокументооборотСКОВызовСервера.ПолучитьВидДокументаПоКодуИлиНаименованию(
				Результат.КодУдЛичнРук, Результат.ВидУдЛичнРук);
			
		Иначе
			Если НЕ ЗначениеЗаполнено(Результат.ФамилияИП) ИЛИ НЕ ЗначениеЗаполнено(Результат.ИмяИП)
				ИЛИ Результат.ИмяИП = Результат.ФИО Тогда
				
				СтруктураФИО = РегламентированнаяОтчетность.РазложитьФИО(Результат.ФИО);
				Результат.ФамилияИП 	= СокрЛП(СтруктураФИО.Фамилия);
				Результат.ИмяИП 		= СокрЛП(СтруктураФИО.Имя);
				Результат.ОтчествоИП 	= СокрЛП(СтруктураФИО.Отчество);
			КонецЕсли;
			
			ВидУдостоверения = ДокументооборотСКОВызовСервера.ПолучитьВидДокументаПоКодуИлиНаименованию(
				Результат.КодУдЛичн, Результат.ВидУдЛичн);
		КонецЕсли;
		Результат.Вставить("ВидУдостоверения", ВидУдостоверения);
		
		КодСтраныГражданства = ?(Результат.ЭтоЮридическоеЛицо, Результат.ГраждРук, Результат.Гражд);
		Гражданство = ?(ЗначениеЗаполнено(КодСтраныГражданства), Справочники.СтраныМира.НайтиПоКоду(КодСтраныГражданства),
			Справочники.СтраныМира.ПустаяСсылка());
		Результат.Вставить("Гражданство", Гражданство);
		
		СНИЛС = "";
		Должность = "";
		Руководитель = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.Руководитель(
			Организация);
		Если ЗначениеЗаполнено(Руководитель) Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ФИОФизЛица(Руководитель);
			ДанныеИсполнителя =
				ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ПолучитьДанныеИсполнителя(
				Руководитель, Организация);
			Если ЗначениеЗаполнено(ДанныеИсполнителя) И ДанныеИсполнителя.Свойство("СНИЛС") Тогда
				СНИЛС = ДанныеИсполнителя.СНИЛС;
			КонецЕсли;
			Если ЗначениеЗаполнено(ДанныеИсполнителя) И ДанныеИсполнителя.Свойство("Должность") Тогда
				Должность = ДанныеИсполнителя.Должность;
			КонецЕсли;
		КонецЕсли;
		Результат.Вставить("Руководитель", Руководитель);
		Результат.Вставить("СНИЛС", СНИЛС);
		Результат.Вставить("Должность", Должность);
		
		Если Результат.ЭтоЮридическоеЛицо И НЕ Результат.ЭтоРоссийскаяОрганизация Тогда
			СтранаРегистрацииИностраннойОрганизации = Справочники.СтраныМира.НайтиПоРеквизиту("НаименованиеПолное",
				СведенияОбОрганизации.СтрИО);
			Результат.Вставить("СтранаРегистрацииИностраннойОрганизации", СтранаРегистрацииИностраннойОрганизации);
			Если СтранаРегистрацииИностраннойОрганизации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
				Результат.ЭтоРоссийскаяОрганизация = Истина;
				Если ЗначениеЗаполнено(Результат.НаимИОПол) Тогда
					Результат.НаимЮЛПол = Результат.НаимИОПол;
				КонецЕсли;
				Результат.Вставить("АдрЮР", 		Результат.АдрИО);
				Результат.Вставить("АдрЮР_XML", 	Результат.АдрИО_XML);
				Результат.Вставить("АдрЮР_JSON", 	Результат.АдрИО_JSON);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСвойстваКонтрагента(Контрагент) Экспорт
	
	РеквизитНаименованиеПолноеОтсутствует 			= Истина; // допустимо отсутствие реквизита
	РеквизитЮридическоеФизическоеЛицоОтсутствует 	= Истина; // допустимо отсутствие реквизита
	Если ЭлектронныйДокументооборотСКонтролирующимиОрганами.РеквизитыСправочникаКонтрагентовДоступны(
		РеквизитНаименованиеПолноеОтсутствует,
		РеквизитЮридическоеФизическоеЛицоОтсутствует) Тогда
		
		РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент,
			?(РеквизитНаименованиеПолноеОтсутствует, "Наименование", "НаименованиеПолное")
			+ ?(РеквизитЮридическоеФизическоеЛицоОтсутствует, "", ", ЮридическоеФизическоеЛицо")
			+ ", ИНН, КПП");
		
	Иначе
		РеквизитыКонтрагента = Новый Структура;
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ПолучитьРеквизитыКонтрагента(
			Контрагент,
			РеквизитыКонтрагента);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("НаименованиеПолное", 		"");
	Результат.Вставить("ЮридическоеФизическоеЛицо", Неопределено);
	Результат.Вставить("ИНН", 						"");
	Результат.Вставить("КПП", 						"");
	Если ЗначениеЗаполнено(РеквизитыКонтрагента) Тогда
		ЗаполнитьЗначенияСвойств(Результат, РеквизитыКонтрагента);
		Если НЕ ЗначениеЗаполнено(Результат.НаименованиеПолное) И РеквизитыКонтрагента.Свойство("Наименование") Тогда
			Результат.НаименованиеПолное = РеквизитыКонтрагента.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Вставить("ЭтоЮридическоеЛицо", Неопределено);
	ИмяПеречисленияЮридическоеФизическоеЛицо = "ЮридическоеФизическоеЛицо";
	Если Метаданные.Перечисления.Найти(ИмяПеречисленияЮридическоеФизическоеЛицо) <> Неопределено И
		ТипЗнч(Результат.ЮридическоеФизическоеЛицо) =
		Тип("ПеречислениеСсылка." + ИмяПеречисленияЮридическоеФизическоеЛицо) Тогда
		
		Если Результат.ЮридическоеФизическоеЛицо =
			ПредопределенноеЗначение("Перечисление." + ИмяПеречисленияЮридическоеФизическоеЛицо + ".ФизическоеЛицо") Тогда
			
			Результат.ЭтоЮридическоеЛицо = Ложь;
			
		ИначеЕсли Результат.ЮридическоеФизическоеЛицо =
			ПредопределенноеЗначение("Перечисление." + ИмяПеречисленияЮридическоеФизическоеЛицо + ".ЮридическоеЛицо")
			ИЛИ ЗначениеЗаполнено(Результат.ИНН) И ЗначениеЗаполнено(Результат.КПП) Тогда
			
			Результат.ЭтоЮридическоеЛицо = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Результат.ИНН = СокрЛП(Результат.ИНН);
	Результат.КПП = СокрЛП(Результат.КПП);
	Если Результат.ЭтоЮридическоеЛицо = Неопределено Тогда
		Если СтрДлина(Результат.ИНН) = 10 Тогда
			Результат.ЭтоЮридическоеЛицо = Истина;
		ИначеЕсли СтрДлина(Результат.ИНН) = 12 Тогда
			Результат.ЭтоЮридическоеЛицо = Ложь;
		ИначеЕсли ЗначениеЗаполнено(Результат.КПП) Тогда
			Результат.ЭтоЮридическоеЛицо = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.ЭтоЮридическоеЛицо = Ложь Тогда
		ФамилияИмяОтчество = РегламентированнаяОтчетность.РазложитьФИО(Результат.НаименованиеПолное);
		Результат.Вставить("Фамилия", 	ФамилияИмяОтчество.Фамилия);
		Результат.Вставить("Имя", 		ФамилияИмяОтчество.Имя);
		Результат.Вставить("Отчество",	ФамилияИмяОтчество.Отчество);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСвойстваФизическогоЛица(ФизическоеЛицо) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ФИО", 				Новый Структура("Фамилия, Имя, Отчество", "", "", ""));
	Результат.Вставить("ИНН", 				"");
	Результат.Вставить("Серия", 			"");
	Результат.Вставить("Номер", 			"");
	Результат.Вставить("ДатаВыдачи", 		Неопределено);
	Результат.Вставить("КемВыдан", 			"");
	Результат.Вставить("ВидДокумента", 		Справочники.ВидыДокументовФизическихЛиц.ПустаяСсылка());
	Результат.Вставить("КодПодразделения", 	"");
	Результат.Вставить("Должность", 		"");
	Результат.Вставить("Подразделение", 	"");
	Результат.Вставить("СНИЛС", 			"");
	Результат.Вставить("ДатаРождения", 		Неопределено);
	Результат.Вставить("МестоРождения", 	"");
	Результат.Вставить("Пол", 				Перечисления.ПолФизическогоЛица.ПустаяСсылка());
	Результат.Вставить("Гражданство", 		Справочники.СтраныМира.ПустаяСсылка());
	Результат.Вставить("ТелефонРабочий", 	"");
	
	ДанныеИсполнителя = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ПолучитьДанныеИсполнителя(
		ФизическоеЛицо,
		Справочники.Организации.ПустаяСсылка());
	
	Если ТипЗнч(ДанныеИсполнителя) = Тип("Структура") ИЛИ ТипЗнч(ДанныеИсполнителя) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(Результат, ДанныеИсполнителя);
	КонецЕсли;
	
	ФамилияИмяОтчество = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	Если ЗначениеЗаполнено(Результат.ФИО) Тогда
		ЗаполнитьЗначенияСвойств(ФамилияИмяОтчество, Результат.ФИО);
	КонецЕсли;
	Результат.ФИО = ФамилияИмяОтчество;
	
	Если Результат.Пол = "Мужской" Тогда
		Результат.Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской");
	ИначеЕсли Результат.Пол = "Женский" Тогда
		Результат.Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский");
	КонецЕсли;
	
	МассивПоказателей = Новый Массив;
	МассивПоказателей.Добавить("ИНН");
	МассивПоказателей.Добавить("Фамилия");
	МассивПоказателей.Добавить("Имя");
	МассивПоказателей.Добавить("Отчество");
	ДатаЗначения = ТекущаяДатаСеанса();
	
	СведенияОФизическомЛице = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОФизЛице(
		ФизическоеЛицо,
		МассивПоказателей,
		ДатаЗначения);
	
	Если ТипЗнч(СведенияОФизическомЛице) = Тип("Структура")
		ИЛИ ТипЗнч(СведенияОФизическомЛице) = Тип("ФиксированнаяСтруктура") Тогда
		Если НЕ ЗначениеЗаполнено(Результат.ФИО.Фамилия) И НЕ ЗначениеЗаполнено(Результат.ФИО.Имя)
			И НЕ ЗначениеЗаполнено(Результат.ФИО.Отчество) Тогда
			ЗаполнитьЗначенияСвойств(Результат.ФИО, СведенияОФизическомЛице);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Результат.ИНН) И СведенияОФизическомЛице.Свойство("ИНН") Тогда
			Результат.ИНН = СведенияОФизическомЛице.ИНН;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоЮридическоеЛицо(ОрганизацияКонтрагентИлиФизическоеЛицо, ЗначениеПоУмолчанию = Истина) Экспорт
	
	Если ТипЗнч(ОрганизацияКонтрагентИлиФизическоеЛицо) = Тип("СправочникСсылка.Организации") Тогда
		Результат = РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(ОрганизацияКонтрагентИлиФизическоеЛицо);
		
	ИначеЕсли ТипЗнч(ОрганизацияКонтрагентИлиФизическоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Результат = Ложь;
		
	ИначеЕсли ОрганизацияКонтрагентИлиФизическоеЛицо = Неопределено
		ИЛИ ТипЗнч(ОрганизацияКонтрагентИлиФизическоеЛицо) = Тип("Строка") Тогда
		
		Результат = Неопределено;
		
	Иначе // ТипЗнч(ОрганизацияКонтрагентИлиФизическоеЛицо) = Тип("СправочникСсылка.Контрагенты")
		Результат = ПолучитьСвойстваКонтрагента(ОрганизацияКонтрагентИлиФизическоеЛицо).ЭтоЮридическоеЛицо;
	КонецЕсли;
	
	Возврат ?(Результат = Неопределено, ЗначениеПоУмолчанию, Результат);
	
КонецФункции

Функция НайтиФизическоеЛицо(ФИО) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ФизическиеЛица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Наименование = &Наименование
		|УПОРЯДОЧИТЬ ПО ФизическиеЛица.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Наименование", ФИО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область МашиночитаемыеДоверенности

Функция ВыгрузитьМЧДРР(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Истина,
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ВыгрузитьЭлементВФайлОбмена(
		СправочникСсылка,
		ИдентификаторФайла,
		КодНалоговогоОрганаПолучателя,
		ОпределятьКодНалоговогоОрганаПолучателя,
		ФорматДоверенностиПоПриказуФНС,
		ПроверитьВыгрузку,
		ПараметрыВозврата);
	
КонецФункции

Функция ЗагрузитьМЧДРР(
		ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка = Ложь,
		Подпись = Неопределено,
		ЗаявлениеНаОтзыв = Неопределено,
		ПодписьЗаявленияНаОтзыв = Неопределено,
		ДанныеАрхива = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ЗагрузитьЭлементИзФайлаОбмена(
		ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка,
		Подпись,
		ЗаявлениеНаОтзыв,
		ПодписьЗаявленияНаОтзыв,
		ДанныеАрхива);
	
КонецФункции

Функция ПроверитьМЧДРР(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Функция НайтиМЧДРР(НомерДоверенности, ИННДоверителя = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр КАК МашиночитаемыеДоверенностиРаспределенныйРеестр
		|ГДЕ
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.НомерДоверенности = &НомерДоверенности"
		+ ?(ИННДоверителя = Неопределено, "", "
		|	И (МашиночитаемыеДоверенностиРаспределенныйРеестр.ДоверительЮЛ_ИНН = &ИННДоверителя
		|		ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.ПредставительФЛ_ИНН = &ИННДоверителя)") + "
		|	И (МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования = """"
		|		ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования ЕСТЬ NULL)
		|	И НЕ МашиночитаемыеДоверенностиРаспределенныйРеестр.ПометкаУдаления
		|УПОРЯДОЧИТЬ ПО
		|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОтправки УБЫВ");
	
	Запрос.УстановитьПараметр("НомерДоверенности", НомерДоверенности);
	Если ИННДоверителя <> Неопределено Тогда
		Запрос.УстановитьПараметр("ИННДоверителя", ИННДоверителя);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЗарегистрированныеМЧДФНС(
		НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Неопределено,
		ПроверяемыеМЧДФНС = Неопределено,
		ВыбранныеОрганизации = Неопределено) Экспорт
	
	ИгнорируемыеЗарегистрированныеМЧДФНС = Неопределено;
	Если НоваяИгнорируемаяЗарегистрированнаяМЧДФНС <> Ложь Тогда
		ИгнорируемыеЗарегистрированныеМЧДФНС = ХранилищеОбщихНастроек.Загрузить(
			"ДокументооборотСКонтролирующимиОрганами_ИгнорируемыеЗарегистрированныеМЧДФНС");
	КонецЕсли;
	Если ИгнорируемыеЗарегистрированныеМЧДФНС = Неопределено Тогда
		ИгнорируемыеЗарегистрированныеМЧДФНС = Новый Массив;
	КонецЕсли;
	
	Если НоваяИгнорируемаяЗарегистрированнаяМЧДФНС <> Неопределено
		И НоваяИгнорируемаяЗарегистрированнаяМЧДФНС <> Ложь Тогда
		
		Если ИгнорируемыеЗарегистрированныеМЧДФНС.Найти(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС) = Неопределено Тогда
			ИгнорируемыеЗарегистрированныеМЧДФНС.Добавить(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС);
			ХранилищеОбщихНастроек.Сохранить("ДокументооборотСКонтролирующимиОрганами_ИгнорируемыеЗарегистрированныеМЧДФНС",,
				ИгнорируемыеЗарегистрированныеМЧДФНС);
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	// объединение запросов с колонками неограниченной длины невозможно
	МаксимальныйИндексЗапроса = 1;
	ТаблицыЗапросов = Новый Массив;
	Для ИндексЗапроса = 0 По МаксимальныйИндексЗапроса Цикл
		Если ИндексЗапроса = 0 Тогда
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка КАК СсылкаМЧДФНС,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаВыдачи КАК ДатаВыдачи,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОкончания КАК ДатаОкончания,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.СрокДействия КАК СрокДействия,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.НомерДоверенности КАК НомерДоверенности,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус КАК СтатусДоверенности,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Доверитель КАК Доверитель,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Представитель КАК Представитель,
				|	Организации.Ссылка КАК ОрганизацияСсылка,
				|	&НаименованиеОрганизации КАК НаименованиеОрганизации,
				|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияСсылка,
				|	РегистрацииВНалоговомОргане.Код КАК КодРегистрации,
				|	РегистрацииВНалоговомОргане.КПП КАК КППРегистрации,
				|	ЕСТЬNULL(РегистрацииВНалоговомОрганеПодписанты.Доверенность, РегистрацииВНалоговомОргане.Доверенность) КАК ДоверенностьРегистрации
				|ИЗ
				|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр КАК МашиночитаемыеДоверенностиРаспределенныйРеестр
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
				|		ПО (Организации.Ссылка = МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
				|		ПО (РегистрацииВНалоговомОргане.Владелец = МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец)
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане.Подписанты КАК РегистрацииВНалоговомОрганеПодписанты
				|		ПО (РегистрацииВНалоговомОрганеПодписанты.Ссылка = РегистрацииВНалоговомОргане.Ссылка)
				|			И (РегистрацииВНалоговомОрганеПодписанты.Представитель = МашиночитаемыеДоверенностиРаспределенныйРеестр.Представитель)
				|ГДЕ
				|	" + ?(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Ложь, "",
					"МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус =
				|		ЗНАЧЕНИЕ(Перечисление.СтатусыМашиночитаемойДоверенностиКО.Зарегистрировано)
				|	И ") + "НЕ МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка В (&ИгнорируемыеЗарегистрированныеМЧДФНС)
				|	И (&ПроверяемыеМЧДФНСНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка В (&ПроверяемыеМЧДФНС))
				|	И (&ВыбранныеОрганизацииНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец В (&ВыбранныеОрганизации))
				|	И (МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования = """"
				|		ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования ЕСТЬ NULL)
				|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления" + ?(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Ложь, "", "
				|	И НЕ МашиночитаемыеДоверенностиРаспределенныйРеестр.ПометкаУдаления
				|	И НЕ Организации.ПометкаУдаления") + "
				|
				|УПОРЯДОЧИТЬ ПО
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОкончания,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка,
				|	Организации.Ссылка,
				|	РегистрацииВНалоговомОргане.Код,
				|	РегистрацииВНалоговомОргане.КПП,
				|	РегистрацииВНалоговомОргане.Ссылка");
			
		Иначе
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	МашиночитаемыеДоверенностиФНС.Ссылка КАК СсылкаМЧДФНС,
				|	МашиночитаемыеДоверенностиФНС.ДатаВыдачи КАК ДатаВыдачи,
				|	МашиночитаемыеДоверенностиФНС.ДатаОкончания КАК ДатаОкончания,
				|	МашиночитаемыеДоверенностиФНС.СрокДействия КАК СрокДействия,
				|	МашиночитаемыеДоверенностиФНС.НомерДоверенности КАК НомерДоверенности,
				|	ЗНАЧЕНИЕ(Перечисление.СтатусыМашиночитаемойДоверенностиКО.Зарегистрировано) КАК СтатусДоверенности,
				|	МашиночитаемыеДоверенностиФНС.Доверитель КАК Доверитель,
				|	МашиночитаемыеДоверенностиФНС.Представитель КАК Представитель,
				|	Организации.Ссылка КАК ОрганизацияСсылка,
				|	&НаименованиеОрганизации КАК НаименованиеОрганизации,
				|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияСсылка,
				|	РегистрацииВНалоговомОргане.Код КАК КодРегистрации,
				|	РегистрацииВНалоговомОргане.КПП КАК КППРегистрации,
				|	ЕСТЬNULL(РегистрацииВНалоговомОрганеПодписанты.Доверенность, РегистрацииВНалоговомОргане.Доверенность) КАК ДоверенностьРегистрации
				|ИЗ
				|	Справочник.МашиночитаемыеДоверенностиФНС КАК МашиночитаемыеДоверенностиФНС
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
				|		ПО (Организации.Ссылка = МашиночитаемыеДоверенностиФНС.Организация)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
				|		ПО (РегистрацииВНалоговомОргане.Владелец = МашиночитаемыеДоверенностиФНС.Организация)
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане.Подписанты КАК РегистрацииВНалоговомОрганеПодписанты
				|		ПО (РегистрацииВНалоговомОрганеПодписанты.Ссылка = РегистрацииВНалоговомОргане.Ссылка)
				|			И (РегистрацииВНалоговомОрганеПодписанты.Представитель = МашиночитаемыеДоверенностиФНС.Представитель)"
						+ ?(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Ложь, "", "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЦиклыОбмена КАК ЦиклыОбмена
				|		ПО (ЦиклыОбмена.Предмет = МашиночитаемыеДоверенностиФНС.Ссылка)
				|			И (ЦиклыОбмена.ДатаСоздания >= ДАТАВРЕМЯ(2023, 1, 1, 0, 0, 0))
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
				|		ПО (ТранспортноеСообщение.ЦиклОбмена = ЦиклыОбмена.Ссылка)
				|			И (ТранспортноеСообщение.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыТранспортныхСообщений.РезультатПриемаПредставлениеНО))
				|			И (ТранспортноеСообщение.ПротоколСОшибкой <> ИСТИНА)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
				|		ПО (ЖурналОтправокВКонтролирующиеОрганы.Ссылка = МашиночитаемыеДоверенностиФНС.Ссылка)") + "
				|ГДЕ
				|	" + ?(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Ложь, "",
					"ЖурналОтправокВКонтролирующиеОрганы.СтатусОтправки <> &СтатусОтправки
				|	И ") + "НЕ МашиночитаемыеДоверенностиФНС.Ссылка В (&ИгнорируемыеЗарегистрированныеМЧДФНС)
				|	И (&ПроверяемыеМЧДФНСНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиФНС.Ссылка В (&ПроверяемыеМЧДФНС))
				|	И (&ВыбранныеОрганизацииНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиФНС.Организация В (&ВыбранныеОрганизации))
				|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления" + ?(НоваяИгнорируемаяЗарегистрированнаяМЧДФНС = Ложь, "", "
				|	И НЕ МашиночитаемыеДоверенностиФНС.ПометкаУдаления
				|	И НЕ Организации.ПометкаУдаления
				|	И НЕ ЦиклыОбмена.ПометкаУдаления
				|	И НЕ ТранспортноеСообщение.ПометкаУдаления") + "
				|
				|УПОРЯДОЧИТЬ ПО
				|	МашиночитаемыеДоверенностиФНС.ДатаОкончания,
				|	МашиночитаемыеДоверенностиФНС.Ссылка,
				|	Организации.Ссылка,
				|	РегистрацииВНалоговомОргане.Код,
				|	РегистрацииВНалоговомОргане.КПП,
				|	РегистрацииВНалоговомОргане.Ссылка");
			
			Если НоваяИгнорируемаяЗарегистрированнаяМЧДФНС <> Ложь Тогда
				Запрос.УстановитьПараметр("СтатусОтправки", "Отозвано");
			КонецЕсли;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ИгнорируемыеЗарегистрированныеМЧДФНС", ИгнорируемыеЗарегистрированныеМЧДФНС);
		УстановитьПараметрыПодключенияОтключенияМЧД(Запрос, ПроверяемыеМЧДФНС, ВыбранныеОрганизации);
		
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		ТаблицыЗапросов.Добавить(ТаблицаЗапроса);
	КонецЦикла;
	
	Результат = Новый Массив;
	
	Для ИндексЗапроса = 0 По МаксимальныйИндексЗапроса Цикл
		Для каждого СтрокаТаблицы Из ТаблицыЗапросов[ИндексЗапроса] Цикл
			КоличествоДоверенностей = Результат.Количество();
			СсылкаМЧДФНС = ?(КоличествоДоверенностей > 0, Результат[КоличествоДоверенностей - 1].СсылкаМЧДФНС, Неопределено);
			
			Если СсылкаМЧДФНС = СтрокаТаблицы.СсылкаМЧДФНС Тогда
				СвойстваДоверенности = Результат[КоличествоДоверенностей - 1];
				
			Иначе
				ФИОПредставителя = "";
				Если ТипЗнч(СтрокаТаблицы.Представитель) = Тип("СправочникСсылка.Организации")
					И СтрокаТаблицы.Представитель = СтрокаТаблицы.Доверитель
					ИЛИ ТипЗнч(СтрокаТаблицы.Представитель) = Тип("СправочникСсылка.Контрагенты") Тогда
					
					ФИОПредставителя = ФИОМЧДФНС(СтрокаТаблицы.СсылкаМЧДФНС,
						Перечисления.СубъектыДоверенностиНалогоплательщика.ПредставительФЛ);
					Если ЗначениеЗаполнено(ФИОПредставителя)
						И ТипЗнч(СтрокаТаблицы.Представитель) = Тип("СправочникСсылка.Организации") Тогда
						
						СтрокаТаблицы.Представитель = ДокументооборотСКОВызовСервера.НайтиФизическоеЛицо(ФИОПредставителя);
						ФИОПредставителя = "";
					КонецЕсли;
				КонецЕсли;
				
				ДоверенностьМожноПодставитьВРегистрацию = ТипЗнч(СтрокаТаблицы.Представитель) = Тип("СправочникСсылка.Контрагенты")
					ИЛИ ТипЗнч(СтрокаТаблицы.Представитель) = Тип("СправочникСсылка.ФизическиеЛица");
				
				СвойстваДоверенности = Новый Структура;
				СвойстваДоверенности.Вставить("СсылкаМЧДФНС", 								СтрокаТаблицы.СсылкаМЧДФНС);
				СвойстваДоверенности.Вставить("ДатаВыдачи", 								СтрокаТаблицы.ДатаВыдачи);
				СвойстваДоверенности.Вставить("ДатаОкончания", 								СтрокаТаблицы.ДатаОкончания);
				СвойстваДоверенности.Вставить("СрокДействия", 								СтрокаТаблицы.СрокДействия);
				СвойстваДоверенности.Вставить("НомерДоверенности", 							СтрокаТаблицы.НомерДоверенности);
				СвойстваДоверенности.Вставить("СтатусДоверенности", 						СтрокаТаблицы.СтатусДоверенности);
				СвойстваДоверенности.Вставить("Доверитель", 								СтрокаТаблицы.Доверитель);
				СвойстваДоверенности.Вставить("Представитель", 								СтрокаТаблицы.Представитель);
				СвойстваДоверенности.Вставить("ФИОПредставителя", 							ФИОПредставителя);
				СвойстваДоверенности.Вставить("ОрганизацияСсылка", 							СтрокаТаблицы.ОрганизацияСсылка);
				СвойстваДоверенности.Вставить("НаименованиеОрганизации", 					СтрокаТаблицы.НаименованиеОрганизации);
				СвойстваДоверенности.Вставить("РегистрацииВНалоговомОргане", 				Новый Массив);
				СвойстваДоверенности.Вставить("ДоверенностьЕстьВРегистрациях", 				Ложь);
				СвойстваДоверенности.Вставить("ДоверенностьМожноПодставитьВРегистрацию", 	ДоверенностьМожноПодставитьВРегистрацию);
				Результат.Добавить(СвойстваДоверенности);
				КоличествоДоверенностей = КоличествоДоверенностей + 1;
			КонецЕсли;
			
			КоличествоРегистрацийДоверенности = Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане.Количество();
			Если КоличествоРегистрацийДоверенности = 0
				ИЛИ Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане[
				КоличествоРегистрацийДоверенности - 1].РегистрацияСсылка <> СтрокаТаблицы.РегистрацияСсылка Тогда
				
				СвойстваРегистрации = Новый Структура;
				СвойстваРегистрации.Вставить("РегистрацияСсылка", 			СтрокаТаблицы.РегистрацияСсылка);
				СвойстваРегистрации.Вставить("КодРегистрации", 				СтрокаТаблицы.КодРегистрации);
				СвойстваРегистрации.Вставить("КППРегистрации", 				СтрокаТаблицы.КППРегистрации);
				СвойстваРегистрации.Вставить("ДоверенностьРегистрации", 	СтрокаТаблицы.ДоверенностьРегистрации);
				Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане.Добавить(СвойстваРегистрации);
				
				Если СтрокаТаблицы.ДоверенностьРегистрации = СтрокаТаблицы.СсылкаМЧДФНС Тогда
					Результат[КоличествоДоверенностей - 1].ДоверенностьЕстьВРегистрациях = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НедействительныеМЧДФНС(
		НоваяИгнорируемаяНедействительнаяМЧДФНС = Неопределено,
		ПроверяемыеМЧДФНС = Неопределено,
		ВыбранныеОрганизации = Неопределено) Экспорт
	
	ИгнорируемыеНедействительныеМЧДФНС = Неопределено;
	Если НоваяИгнорируемаяНедействительнаяМЧДФНС <> Ложь Тогда
		ИгнорируемыеНедействительныеМЧДФНС = ХранилищеОбщихНастроек.Загрузить(
			"ДокументооборотСКонтролирующимиОрганами_ИгнорируемыеНедействительныеМЧДФНС");
	КонецЕсли;
	Если ИгнорируемыеНедействительныеМЧДФНС = Неопределено Тогда
		ИгнорируемыеНедействительныеМЧДФНС = Новый Массив;
	КонецЕсли;
	
	Если НоваяИгнорируемаяНедействительнаяМЧДФНС <> Неопределено И НоваяИгнорируемаяНедействительнаяМЧДФНС <> Ложь Тогда
		Если ИгнорируемыеНедействительныеМЧДФНС.Найти(НоваяИгнорируемаяНедействительнаяМЧДФНС) = Неопределено Тогда
			ИгнорируемыеНедействительныеМЧДФНС.Добавить(НоваяИгнорируемаяНедействительнаяМЧДФНС);
			ХранилищеОбщихНастроек.Сохранить("ДокументооборотСКонтролирующимиОрганами_ИгнорируемыеНедействительныеМЧДФНС",,
				ИгнорируемыеНедействительныеМЧДФНС);
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	// объединение запросов с колонками неограниченной длины невозможно
	МаксимальныйИндексЗапроса = 1;
	ТаблицыЗапросов = Новый Массив;
	Для ИндексЗапроса = 0 По МаксимальныйИндексЗапроса Цикл
		Если ИндексЗапроса = 0 Тогда
			Запрос = Новый Запрос(
				ЗапросМЧДВРегистрациях() +
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка КАК СсылкаМЧДФНС,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаВыдачи КАК ДатаВыдачи,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОкончания КАК ДатаОкончания,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.СрокДействия КАК СрокДействия,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.НомерДоверенности КАК НомерДоверенности,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус КАК СтатусДоверенности,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец КАК ОрганизацияДоверенности,
				|	Организации.Ссылка КАК ОрганизацияСсылка,
				|	&НаименованиеОрганизации КАК НаименованиеОрганизации,
				|	РегистрацииВНО.Ссылка КАК РегистрацияСсылка,
				|	РегистрацииВНО.Код КАК КодРегистрации,
				|	РегистрацииВНО.КПП КАК КППРегистрации,
				|	РегистрацииВНО.Доверенность КАК ДоверенностьРегистрации
				|ИЗ
				|	&РегистрацииВНО КАК РегистрацииВНО
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр КАК МашиночитаемыеДоверенностиРаспределенныйРеестр
				|		ПО (МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка = РегистрацииВНО.Доверенность)"
							+ ?(НоваяИгнорируемаяНедействительнаяМЧДФНС = Ложь, "", "
				|			И (МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус =
				|				ЗНАЧЕНИЕ(Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отозвано)
				|				ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус =
				|				ЗНАЧЕНИЕ(Перечисление.СтатусыМашиночитаемойДоверенностиКО.ИстекСрокДействия))") + "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
				|		ПО (Организации.Ссылка = РегистрацииВНО.Владелец)
				|ГДЕ
				|	НЕ МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка В (&ИгнорируемыеНедействительныеМЧДФНС)
				|	И (&ПроверяемыеМЧДФНСНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка В (&ПроверяемыеМЧДФНС))
				|	И (&ВыбранныеОрганизацииНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.Владелец В (&ВыбранныеОрганизации))
				|	И (МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования = """"
				|		ИЛИ МашиночитаемыеДоверенностиРаспределенныйРеестр.РежимыИспользования ЕСТЬ NULL)"
					+ ?(НоваяИгнорируемаяНедействительнаяМЧДФНС = Ложь, "", "
				|	И НЕ МашиночитаемыеДоверенностиРаспределенныйРеестр.ПометкаУдаления
				|	И НЕ Организации.ПометкаУдаления") + "
				|
				|УПОРЯДОЧИТЬ ПО
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Статус,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.ДатаОкончания,
				|	МашиночитаемыеДоверенностиРаспределенныйРеестр.Ссылка,
				|	Организации.Ссылка,
				|	РегистрацииВНО.Код,
				|	РегистрацииВНО.КПП,
				|	РегистрацииВНО.Ссылка");
			
		Иначе
			Запрос = Новый Запрос(
				ЗапросМЧДВРегистрациях() + 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	МашиночитаемыеДоверенностиФНС.Ссылка КАК СсылкаМЧДФНС,
				|	МашиночитаемыеДоверенностиФНС.ДатаВыдачи КАК ДатаВыдачи,
				|	МашиночитаемыеДоверенностиФНС.ДатаОкончания КАК ДатаОкончания,
				|	МашиночитаемыеДоверенностиФНС.СрокДействия КАК СрокДействия,
				|	МашиночитаемыеДоверенностиФНС.НомерДоверенности КАК НомерДоверенности,
				|	ЗНАЧЕНИЕ(Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отозвано) КАК СтатусДоверенности,
				|	МашиночитаемыеДоверенностиФНС.Организация КАК ОрганизацияДоверенности,
				|	Организации.Ссылка КАК ОрганизацияСсылка,
				|	&НаименованиеОрганизации КАК НаименованиеОрганизации,
				|	РегистрацииВНО.Ссылка КАК РегистрацияСсылка,
				|	РегистрацииВНО.Код КАК КодРегистрации,
				|	РегистрацииВНО.КПП КАК КППРегистрации,
				|	РегистрацииВНО.Доверенность КАК ДоверенностьРегистрации
				|ИЗ
				|	&РегистрацииВНО КАК РегистрацииВНО
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МашиночитаемыеДоверенностиФНС КАК МашиночитаемыеДоверенностиФНС
				|		ПО (МашиночитаемыеДоверенностиФНС.Ссылка = РегистрацииВНО.Доверенность)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
				|		ПО (Организации.Ссылка = РегистрацииВНО.Владелец)" + ?(НоваяИгнорируемаяНедействительнаяМЧДФНС = Ложь, "", "
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЦиклыОбмена КАК ЦиклыОбмена
				|		ПО (ЦиклыОбмена.Предмет = МашиночитаемыеДоверенностиФНС.Ссылка)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
				|		ПО (ТранспортноеСообщение.ЦиклОбмена = ЦиклыОбмена.Ссылка)
				|			И (ТранспортноеСообщение.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыТранспортныхСообщений.РезультатПриемаПредставлениеНО))
				|			И (ТранспортноеСообщение.ПротоколСОшибкой <> ИСТИНА)
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
				|		ПО (ЖурналОтправокВКонтролирующиеОрганы.Ссылка = МашиночитаемыеДоверенностиФНС.Ссылка)") + "
				|ГДЕ
				|	" + ?(НоваяИгнорируемаяНедействительнаяМЧДФНС = Ложь, "",
					"ЖурналОтправокВКонтролирующиеОрганы.СтатусОтправки = &СтатусОтправки
				|	И ") + "НЕ МашиночитаемыеДоверенностиФНС.Ссылка В (&ИгнорируемыеНедействительныеМЧДФНС)
				|	И (&ПроверяемыеМЧДФНСНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиФНС.Ссылка В (&ПроверяемыеМЧДФНС))
				|	И (&ВыбранныеОрганизацииНеЗаданы
				|			ИЛИ МашиночитаемыеДоверенностиФНС.Организация В (&ВыбранныеОрганизации))"
					+ ?(НоваяИгнорируемаяНедействительнаяМЧДФНС = Ложь, "", "
				|	И НЕ МашиночитаемыеДоверенностиФНС.ПометкаУдаления
				|	И НЕ Организации.ПометкаУдаления
				|	И НЕ ЦиклыОбмена.ПометкаУдаления
				|	И НЕ ТранспортноеСообщение.ПометкаУдаления") + "
				|
				|УПОРЯДОЧИТЬ ПО
				|	МашиночитаемыеДоверенностиФНС.ДатаОкончания,
				|	МашиночитаемыеДоверенностиФНС.Ссылка,
				|	Организации.Ссылка,
				|	РегистрацииВНО.Код,
				|	РегистрацииВНО.КПП,
				|	РегистрацииВНО.Ссылка");
			
			Если НоваяИгнорируемаяНедействительнаяМЧДФНС <> Ложь Тогда
				Запрос.УстановитьПараметр("СтатусОтправки", "Отозвано");
			КонецЕсли;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ИгнорируемыеНедействительныеМЧДФНС", ИгнорируемыеНедействительныеМЧДФНС);
		УстановитьПараметрыПодключенияОтключенияМЧД(Запрос, ПроверяемыеМЧДФНС, ВыбранныеОрганизации);
		
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		ТаблицыЗапросов.Добавить(ТаблицаЗапроса);
	КонецЦикла;
	
	Результат = Новый Массив;
	
	Для ИндексЗапроса = 0 По МаксимальныйИндексЗапроса Цикл
		Для каждого СтрокаТаблицы Из ТаблицыЗапросов[ИндексЗапроса] Цикл
			КоличествоДоверенностей = Результат.Количество();
			СсылкаМЧДФНС = ?(КоличествоДоверенностей > 0, Результат[КоличествоДоверенностей - 1].СсылкаМЧДФНС, Неопределено);
			
			Если СсылкаМЧДФНС = СтрокаТаблицы.СсылкаМЧДФНС Тогда
				СвойстваДоверенности = Результат[КоличествоДоверенностей - 1];
				
			Иначе
				СвойстваДоверенности = Новый Структура;
				СвойстваДоверенности.Вставить("СсылкаМЧДФНС", 					СтрокаТаблицы.СсылкаМЧДФНС);
				СвойстваДоверенности.Вставить("ДатаВыдачи", 					СтрокаТаблицы.ДатаВыдачи);
				СвойстваДоверенности.Вставить("ДатаОкончания", 					СтрокаТаблицы.ДатаОкончания);
				СвойстваДоверенности.Вставить("СрокДействия", 					СтрокаТаблицы.СрокДействия);
				СвойстваДоверенности.Вставить("НомерДоверенности", 				СтрокаТаблицы.НомерДоверенности);
				СвойстваДоверенности.Вставить("СтатусДоверенности", 			СтрокаТаблицы.СтатусДоверенности);
				СвойстваДоверенности.Вставить("ОрганизацияСсылка", 				СтрокаТаблицы.ОрганизацияДоверенности);
				СвойстваДоверенности.Вставить("НаименованиеОрганизации", 		СтрокаТаблицы.НаименованиеОрганизации);
				СвойстваДоверенности.Вставить("Организации", 					Новый Массив);
				СвойстваДоверенности.Вставить("РегистрацииВНалоговомОргане", 	Новый Массив);
				Результат.Добавить(СвойстваДоверенности);
				КоличествоДоверенностей = КоличествоДоверенностей + 1;
			КонецЕсли;
			
			КоличествоОргаизаций = СвойстваДоверенности.Организации.Количество();
			ОрганизацияСсылка = ?(КоличествоОргаизаций > 0,
				СвойстваДоверенности.Организации[КоличествоОргаизаций - 1].ОрганизацияСсылка, Неопределено);
			
			Если ОрганизацияСсылка <> СтрокаТаблицы.ОрганизацияСсылка Тогда
				СвойстваОрганизации = Новый Структура;
				СвойстваОрганизации.Вставить("ОрганизацияСсылка", 			СтрокаТаблицы.ОрганизацияСсылка);
				СвойстваОрганизации.Вставить("НаименованиеОрганизации", 	СтрокаТаблицы.НаименованиеОрганизации);
				СвойстваОрганизации.Вставить("РегистрацииВНалоговомОргане", Новый Массив);
				Результат[КоличествоДоверенностей - 1].Организации.Добавить(СвойстваОрганизации);
				КоличествоОргаизаций = КоличествоОргаизаций + 1;
			КонецЕсли;
			
			КоличествоРегистрацийДоверенности = Результат[КоличествоДоверенностей - 1].Организации[
				КоличествоОргаизаций - 1].РегистрацииВНалоговомОргане.Количество();
			Если КоличествоРегистрацийДоверенности = 0 ИЛИ Результат[КоличествоДоверенностей - 1].Организации[
				КоличествоОргаизаций - 1].РегистрацииВНалоговомОргане[КоличествоРегистрацийДоверенности - 1].РегистрацияСсылка <>
				СтрокаТаблицы.РегистрацияСсылка Тогда
				
				СвойстваРегистрации = Новый Структура;
				СвойстваРегистрации.Вставить("РегистрацияСсылка", 	СтрокаТаблицы.РегистрацияСсылка);
				СвойстваРегистрации.Вставить("КодРегистрации", 		СтрокаТаблицы.КодРегистрации);
				СвойстваРегистрации.Вставить("КППРегистрации", 		СтрокаТаблицы.КППРегистрации);
				Результат[КоличествоДоверенностей - 1].Организации[КоличествоОргаизаций - 1].РегистрацииВНалоговомОргане.Добавить(
					СвойстваРегистрации);
			КонецЕсли;
			
			КоличествоРегистрацийДоверенности = Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане.Количество();
			Если КоличествоРегистрацийДоверенности = 0
				ИЛИ Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане[
				КоличествоРегистрацийДоверенности - 1].РегистрацияСсылка <> СтрокаТаблицы.РегистрацияСсылка Тогда
				
				СвойстваРегистрации = Новый Структура;
				СвойстваРегистрации.Вставить("РегистрацияСсылка", 			СтрокаТаблицы.РегистрацияСсылка);
				СвойстваРегистрации.Вставить("КодРегистрации", 				СтрокаТаблицы.КодРегистрации);
				СвойстваРегистрации.Вставить("КППРегистрации", 				СтрокаТаблицы.КППРегистрации);
				СвойстваРегистрации.Вставить("ДоверенностьРегистрации", 	СтрокаТаблицы.ДоверенностьРегистрации);
				СвойстваРегистрации.Вставить("ОрганизацияСсылка", 			СтрокаТаблицы.ОрганизацияСсылка);
				СвойстваРегистрации.Вставить("НаименованиеОрганизации", 	СтрокаТаблицы.НаименованиеОрганизации);
				Результат[КоличествоДоверенностей - 1].РегистрацииВНалоговомОргане.Добавить(СвойстваРегистрации);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РегистрацииВНалоговомОргане(ОрганизацияСсылка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияСсылка,
		|	РегистрацииВНалоговомОргане.Владелец КАК ОрганизацияСсылка,
		|	РегистрацииВНалоговомОргане.Код КАК КодРегистрации,
		|	РегистрацииВНалоговомОргане.КПП КАК КППРегистрации,
		|	РегистрацииВНалоговомОргане.НаименованиеИФНС КАК НаименованиеИФНС,
		|	ЕСТЬNULL(ТЧПодписанты.ДокументПредставителя, РегистрацииВНалоговомОргане.ДокументПредставителя) КАК ДокументПредставителя,
		|	ЕСТЬNULL(ТЧПодписанты.Представитель, РегистрацииВНалоговомОргане.Представитель) КАК Представитель,
		|	ЕСТЬNULL(ТЧПодписанты.УполномоченноеЛицоПредставителя, РегистрацииВНалоговомОргане.УполномоченноеЛицоПредставителя) КАК УполномоченноеЛицоПредставителя,
		|	ЕСТЬNULL(ТЧПодписанты.Доверенность, РегистрацииВНалоговомОргане.Доверенность) КАК Доверенность
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане.Подписанты КАК ТЧПодписанты
		|		ПО (ТЧПодписанты.Ссылка = РегистрацииВНалоговомОргане.Ссылка)
		|			И (ТЧПодписанты.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	РегистрацииВНалоговомОргане.Владелец В(&ОрганизацияСсылка)");
	
	Если ТипЗнч(ОрганизацияСсылка) = Тип("Массив") ИЛИ ТипЗнч(ОрганизацияСсылка) = Тип("ФиксированныйМассив") Тогда
		Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОрганизацияСсылка);
	Иначе
		Организации = ОрганизацияСсылка;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОрганизацияСсылка", Организации);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция КодыНалоговыхОргановМЧДФНС(ДоверенностьСсылка) Экспорт
	
	Если ТипЗнч(ДоверенностьСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НалоговыеОрганыДействия.КодНалоговогоОргана КАК КодНалоговогоОргана
			|ИЗ
			|	Справочник.МашиночитаемыеДоверенностиФНС.НалоговыеОрганыДействия КАК НалоговыеОрганыДействия
			|ГДЕ
			|	НалоговыеОрганыДействия.Ссылка = &СсылкаМЧДФНС");
		
	ИначеЕсли ТипЗнч(ДоверенностьСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НалоговыеОрганыДействия.КодНалоговогоОргана КАК КодНалоговогоОргана
			|ИЗ
			|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.НалоговыеОрганыДействия КАК НалоговыеОрганыДействия
			|ГДЕ
			|	НалоговыеОрганыДействия.Ссылка = &СсылкаМЧДФНС");
		
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СсылкаМЧДФНС", ДоверенностьСсылка);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КодНалоговогоОргана");
	
КонецФункции

Функция ФИОМЧДФНС(ДоверенностьСсылка, СубъектДоверенности) Экспорт
	
	Если ТипЗнч(ДоверенностьСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиФНС") Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ФИОМЧДФНС.Фамилия КАК Фамилия,
			|	ФИОМЧДФНС.Имя КАК Имя,
			|	ФИОМЧДФНС.Отчество КАК Отчество
			|ИЗ
			|	Справочник.МашиночитаемыеДоверенностиФНС.ФИО КАК ФИОМЧДФНС
			|ГДЕ
			|	ФИОМЧДФНС.Ссылка = &СсылкаМЧДФНС
			|	И ФИОМЧДФНС.Владелец = &СубъектДоверенности");
		
	ИначеЕсли ТипЗнч(ДоверенностьСсылка) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиРаспределенныйРеестр") Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ФИОМЧДФНС.Фамилия КАК Фамилия,
			|	ФИОМЧДФНС.Имя КАК Имя,
			|	ФИОМЧДФНС.Отчество КАК Отчество
			|ИЗ
			|	Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр.ФИО КАК ФИОМЧДФНС
			|ГДЕ
			|	ФИОМЧДФНС.Ссылка = &СсылкаМЧДФНС
			|	И ФИОМЧДФНС.Владелец = &СубъектДоверенности");
		
	Иначе
		Возврат "";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СсылкаМЧДФНС", 			ДоверенностьСсылка);
	Запрос.УстановитьПараметр("СубъектДоверенности", 	СубъектДоверенности);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Фамилия + ?(ЗначениеЗаполнено(Выборка.Фамилия) И ЗначениеЗаполнено(Выборка.Имя),
			" ", "") + Выборка.Имя + ?((ЗначениеЗаполнено(Выборка.Фамилия) ИЛИ ЗначениеЗаполнено(Выборка.Имя))
			И ЗначениеЗаполнено(Выборка.Отчество), " ", "") + Выборка.Отчество;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция ВыгрузитьЗаявлениеНаОтменуМЧДРР(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Истина,
		ПроверитьВыгрузку = Ложь,
		ПараметрыОперации = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиРаспределенныйРеестр.ВыгрузитьЭлементВФайлОбменаДляОтмены(
		СправочникСсылка,
		ИдентификаторФайла,
		КодНалоговогоОрганаПолучателя,
		ОпределятьКодНалоговогоОрганаПолучателя,
		ФорматДоверенностиПоПриказуФНС,
		ПроверитьВыгрузку,
		ПараметрыОперации);
	
КонецФункции

Функция АвторизоватьсяНаСервереМЧДРР() Экспорт
	
	ПараметрыРезультатаАвторизации = ПараметрыСеанса.РезультатАвторизацииНаСервереМЧДРРСКО;
	Если ПараметрыРезультатаАвторизации <> Неопределено И ПараметрыРезультатаАвторизации.ДатаСеанса <> Неопределено
		И ПараметрыРезультатаАвторизации.РезультатАвторизации <> Неопределено Тогда
		
		ДатаСеанса = ТекущаяДатаСеанса();
		Если ПараметрыРезультатаАвторизации.ДатаСеанса >= ДатаСеанса - 60 * 60
			И ПараметрыРезультатаАвторизации.ДатаСеанса <= ДатаСеанса Тогда
			
			Возврат Новый Структура(ПараметрыРезультатаАвторизации.РезультатАвторизации);
		КонецЕсли;
	КонецЕсли;
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 	Ложь);
	Результат.Вставить("АдресСервера", 			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("ТокенДоступа", 			"");
	Результат.Вставить("ТекстОтвета", 			"");
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен токен доступа при авторизации на сервере МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось получить данные при авторизации на сервере МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось получить данные при авторизации на сервере МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при получении токена доступа при авторизации на сервере МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/invalid_grant",
		НСтр("ru = 'Некорректная авторизация на сервере МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unsupported_grant_type",
		НСтр("ru = 'Некорректный тип авторизации на сервере МЧД распределенного реестра'"));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/token", "/vst-oauth2/oauth/token");
	ПараметрыРесурсаНаСервере = "";
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
		ТикетАутентификацииИлиДанныеПользователя = ТикетАутентификацииИлиДанныеПользователяНаПорталеПоддержки();
		
		Если ТикетАутентификацииИлиДанныеПользователя = Неопределено Тогда
			ВывестиИЗаписатьОшибкуМЧДРР(,,
				Новый Структура("ТекстОшибкиПоУмолчанию",
					НСтр("ru = 'Авторизация на сервере МЧД распределенного реестра невозможна, так как не заданы логин и пароль авторизации на Портале 1С:ИТС'")));
			Возврат Результат;
		КонецЕсли;
		
		Если ТикетАутентификацииИлиДанныеПользователя.Свойство("Тикет") Тогда
			ПараметрыРесурсаНаСервере = "?ticket=" + КодироватьСтроку(ТикетАутентификацииИлиДанныеПользователя.Тикет,
				СпособКодированияСтроки.КодировкаURL);
		Иначе
			ПараметрыРесурсаНаСервере = "?login=" + КодироватьСтроку(ТикетАутентификацииИлиДанныеПользователя.Логин,
				СпособКодированияСтроки.КодировкаURL)
				+ "&password=" + КодироватьСтроку(ТикетАутентификацииИлиДанныеПользователя.Пароль,
				СпособКодированияСтроки.КодировкаURL)
		КонецЕсли;
	КонецЕсли;
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере + ПараметрыРесурсаНаСервере, ЗаголовкиHTTP);
	Если НЕ СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
		СтруктураЗапроса = Новый Структура;
		СтруктураЗапроса.Вставить("username", СвойстваСервераМЧДРР.ЛогинОператора);
		СтруктураЗапроса.Вставить("password", СвойстваСервераМЧДРР.ПарольОператора);
		СтруктураЗапроса.Вставить("grant_type", "password");
		
		ИмяФайлаЗапроса = ПолучитьИмяВременногоФайла();
		ЗапросJSON = Новый ЗаписьJSON;
		ЗапросJSON.ОткрытьФайл(ИмяФайлаЗапроса, "utf-8");
		ЗаписатьJSON(ЗапросJSON, СтруктураЗапроса);
		ЗапросJSON.Закрыть();
		
		ЗапросHTTP.УстановитьИмяФайлаТела(ИмяФайлаЗапроса);
	КонецЕсли;
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(СвойстваСервераМЧДРР.АдресСервера);
		
		Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
			ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP, ИмяФайлаОтвета);
		Иначе
			ОтветHTTP = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP, ИмяФайлаОтвета);
		КонецЕсли;
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если НЕ СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		КонецЕсли;
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.ТокенДоступа = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("access_token"),
			СтруктураОтвета.access_token, "");
		
		КодСостоянияПриНеверномЛогинеИлиПароле = 403;
		Если ОтветHTTP.КодСостояния = КодСостоянияПриНеверномЛогинеИлиПароле И СтруктураОтвета.Свойство("message") Тогда
			СброситьДанныеАутентификацииНаПорталеПоддержки();
		КонецЕсли;
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если НЕ СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		КонецЕсли;
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при получении данных доверенности с сервера МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.ТокенДоступа) Тогда
		Если НЕ СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		КонецЕсли;
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	Если НЕ СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI Тогда
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
	КонецЕсли;
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	ПараметрыРезультатаАвторизации = ПараметрыРезультатаАвторизацииНаСервереМЧДРРСКО();
	ПараметрыРезультатаАвторизации.ДатаСеанса 			= ТекущаяДатаСеанса();
	ПараметрыРезультатаАвторизации.РезультатАвторизации = Новый ФиксированнаяСтруктура(Результат);
	ПараметрыСеанса.РезультатАвторизацииНаСервереМЧДРРСКО = Новый ФиксированнаяСтруктура(ПараметрыРезультатаАвторизации);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНомерМЧДРР(ТокенДоступа = "") Экспорт
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 	Ложь);
	Результат.Вставить("АдресСервера", 			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("НомерДоверенности", 	"");
	Результат.Вставить("ТекстОтвета", 			"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен номер доверенности с сервера МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось получить данные номера доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось получить данные номера доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при получении номера доверенности с сервера МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/number", "/poar-webapp/integration/poa/generate-number");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(
			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.НомерДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("poaNumber"),
			СтруктураОтвета.poaNumber, "");
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать данные при получении номера доверенности с сервера МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.НомерДоверенности) Тогда
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	Возврат Результат;
	
КонецФункции

Функция ЗарегистрироватьМЧДРР(
		ИмяФайлаВыгрузки,
		ДанныеИлиАдресВыгрузки,
		ДанныеИлиАдресПодписи,
		ТокенДоступа = "",
		НомерДоверенности = "",
		СсылкаНаДоверенность = Неопределено,
		Настройки = Неопределено) Экспорт
	
	НастройкиВызова = Новый Структура;
	НастройкиВызова.Вставить("ЗаписатьСтатус", 					Ложь);
	НастройкиВызова.Вставить("ОтпечатокСертификатаАбонента", 	"");
	Если Настройки <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(НастройкиВызова, Настройки);
	КонецЕсли;
	
	ДанныеВыгрузки = ?(ТипЗнч(ДанныеИлиАдресВыгрузки) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресВыгрузки), ПолучитьИзВременногоХранилища(ДанныеИлиАдресВыгрузки),
		ДанныеИлиАдресВыгрузки);
	ДанныеПодписи = ?(ТипЗнч(ДанныеИлиАдресПодписи) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресПодписи), ПолучитьИзВременногоХранилища(ДанныеИлиАдресПодписи),
		ДанныеИлиАдресПодписи);
	
	Если ЗначениеЗаполнено(СсылкаНаДоверенность) Тогда
		ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
		ОбъектДоверенность.ИмяФайлаВыгрузки = ИмяФайлаВыгрузки;
		ОбъектДоверенность.ФайлВырузки = Новый ХранилищеЗначения(ДанныеВыгрузки,
			Новый СжатиеДанных(9));
		ОбъектДоверенность.ЭлектроннаяПодпись = Новый ХранилищеЗначения(ДанныеПодписи,
			Новый СжатиеДанных(9));
		ОбъектДоверенность.Записать();
	КонецЕсли;
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 		Ложь);
	Результат.Вставить("АдресСервера", 				СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("ИдентификаторТранзакции", 	"");
	Результат.Вставить("НомерДоверенности", 		"");
	Результат.Вставить("ХешДоверенности", 			"");
	Результат.Вставить("ИННДоверителя", 			"");
	Результат.Вставить("ТекстОтвета", 				"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен идентификатор загрузки доверенности на сервер МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось загрузить доверенность на сервер МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось загрузить доверенность на сервер МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при загрузке доверенности на сервер МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/contract/pre_validation/failed",
		СтрШаблон(
			НСтр("ru = 'Регистрационный номер или имя файла регистрируемой доверенности ""%1"" уже используется'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unknown-poa-type",
		СтрШаблон(
			НСтр("ru = 'Некорректный префикс имени файла доверенности ""%1"", невозможно определить тип доверенности'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.signature_is_invalid",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка валидности электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/certificate_cn_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнены фамилия, имя, отчество владельца в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.certificate_cn_is_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнены фамилия, имя, отчество владельца в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/certificate_snils_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен СНИЛС владельца в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.certificate_snils_is_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен СНИЛС владельца в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/certificate_ogrn_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен ОГРН организации, к которой принадлежит владелец, в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.certificate_ogrn_is_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен ОГРН организации, к которой принадлежит владелец, в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/certificate_ogrnip_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен ОГРНИП в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.certificate_ogrnip_is_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не заполнен ОГРНИП в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/individual_entrepreneur_signer_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка подписанта на соответствие данным сертификата электронной подписи доверенности ""%1"" или в качестве доверителя указан ИП'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/individual_entrepreneur_snils_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: СНИЛС подписанта доверенности не совпадает со СНИЛС в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/individual_entrepreneur_ogrnip_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: ОГРНИП доверителя не указан в доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/individual_entrepreneur_ogrnip_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: ОГРНИП доверителя в доверенности не совпадает с ОГРНИП в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.issuer_ogrnip_mismatch_with_certificate_ogrnip",
		СтрШаблон(
			НСтр("ru = 'ОГРНИП доверителя в доверенности не совпадает с ОГРНИП в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/russian_legal_entity_snils_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: СНИЛС подписанта доверенности не совпадает со СНИЛС в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.issuer_snils_mismatch_with_certificate_snils",
		СтрШаблон(
			НСтр("ru = 'СНИЛС подписанта доверенности не совпадает со СНИЛС в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/russian_legal_entity_ogrn_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: ОГРН доверителя в доверенности не совпадает с ОГРН в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/russian_legal_entity_inner_ogrn_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка правомерности загрузки: ОГРН организации-доверителя не совпадает с ОГРН в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.issuer_ogrn_mismatch_with_certificate_ogrn",
		СтрШаблон(
			НСтр("ru = 'ОГРН организации-доверителя не совпадает с ОГРН в сертификате электронной подписи доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/russian_legal_entity_signer_check_failed",
		СтрШаблон(
			НСтр("ru = 'Данные доверителя и подписанта в доверенности не соответствуют данным из ЕГРЮЛ для доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.issuer_signer_mismatch_with_egrul",
		СтрШаблон(
			НСтр("ru = 'Данные доверителя и подписанта в доверенности не соответствуют данным из ЕГРЮЛ для доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/nds_plus_api_exception",
		СтрШаблон(
			НСтр("ru = 'Ошибка сервиса NdsPlus API, может быть связана с временной недоступностью сервиса или неверным заполнением доверенности. Повторите попытку позже. Проверьте ИНН организации-доверителя для доверенности ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныПрефиксовДляКодовОшибок.Вставить("/errors/poa.schematron_validation_failed",
		СтрШаблон(
			НСтр("ru = 'Ошибка проверки по XSD-схеме XML-файла доверенности ""%1""'"),
			НомерДоверенности));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/poa", "/poar-webapp/integration/poa");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "multipart/form-data; boundary=My1cV8bNdr");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	// запись передаваемых файлов
	
	МассивИменФайлов = Новый Массив;
	
	СодержимоеФайла = "--My1cV8bNdr"
		+ Символы.ПС + "Content-Disposition: form-data; name=""poa""; filename=""" + ИмяФайлаВыгрузки + """"
		+ Символы.ПС + "Content-Type: text/xml"
		+ Символы.ПС
		+ Символы.ПС;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	ИмяВременногоФайлаДоверенности = ПолучитьИмяВременногоФайла();
	ТекстВыгрузки = ПолучитьСтрокуИзДвоичныхДанных(ДанныеВыгрузки, "windows-1251");
	ПолучитьДвоичныеДанныеИзСтроки(ТекстВыгрузки, "windows-1251", Истина).Записать(ИмяВременногоФайлаДоверенности);
	МассивИменФайлов.Добавить(ИмяВременногоФайлаДоверенности);
	
	СодержимоеФайла = Символы.ПС + "--My1cV8bNdr"
		+ Символы.ПС + "Content-Disposition: form-data; name=""signature""; filename=""" + ИмяФайлаВыгрузки + ".sig"""
		+ Символы.ПС + "Content-Type: application/octet-stream"
		+ Символы.ПС
		+ Символы.ПС;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	ИмяВременногоФайлаПодписи = ПолучитьИмяВременногоФайла();
	Подпись64 = Base64Строка(ДанныеПодписи);
	Подпись64 = СтрЗаменить(Подпись64, Символы.ВК, "");
	Подпись64 = СтрЗаменить(Подпись64, Символы.ПС, "");
	ПолучитьДвоичныеДанныеИзСтроки(Подпись64, "windows-1251").Записать(ИмяВременногоФайлаПодписи);
	МассивИменФайлов.Добавить(ИмяВременногоФайлаПодписи);
	
	СодержимоеФайла = Символы.ПС + "--My1cV8bNdr--";
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	// объединение передаваемых файлов
	
	ИмяФайлаЗапроса = ПолучитьИмяВременногоФайла();
	ОбъединитьФайлы(МассивИменФайлов, ИмяФайлаЗапроса);
	Для каждого ИмяВременногоФайла Из МассивИменФайлов Цикл
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяВременногоФайла);
	КонецЦикла;
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ЗапросHTTP.УстановитьИмяФайлаТела(ИмяФайлаЗапроса);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(
			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.ИдентификаторТранзакции = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("txId"),
			СтруктураОтвета.txId, "");
		Результат.НомерДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("poaNumber"),
			СтруктураОтвета.poaNumber, "");
		Результат.ХешДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("poaId"),
			СтруктураОтвета.poaId, "");
		Результат.ИННДоверителя = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("issuerInn"),
			СтруктураОтвета.issuerInn, "");
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при загрузке доверенности на сервер МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.ИдентификаторТранзакции) Тогда
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	Если ЗначениеЗаполнено(Результат.ИдентификаторТранзакции) И ЗначениеЗаполнено(СсылкаНаДоверенность)
		И НастройкиВызова.ЗаписатьСтатус Тогда
		
		ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
		ОбъектДоверенность.Статус =
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отправлено");
		ОбъектДоверенность.ДатаОтправки = ТекущаяДатаСеанса();
		ОбъектДоверенность.ДатаОбновленияСтатуса = ОбъектДоверенность.ДатаОтправки;
		ОбъектДоверенность.ИдентификаторТранзакции = Результат.ИдентификаторТранзакции;
		ОбъектДоверенность.ИмяФайлаВыгрузки = ИмяФайлаВыгрузки;
		ОбъектДоверенность.ОтпечатокСертификата = НастройкиВызова.ОтпечатокСертификатаАбонента;
		ОбъектДоверенность.Записать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтатусТранзакцииМЧДРР(ИдентификаторТранзакции, ТокенДоступа = "", НомерДоверенности = "") Экспорт
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 		Ложь);
	Результат.Вставить("АдресСервера", 				СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("СтатусТранзакции", 			"");
	Результат.Вставить("ИдентификаторТранзакции", 	"");
	Результат.Вставить("ДатаВремяТранзакции", 		Неопределено);
	Результат.Вставить("ТекстОтвета", 				"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен статус обработки с сервера МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось получить статус обработки с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось получить статус обработки с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при получении статус обработки с сервера МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/not-found",
		СтрШаблон(
			НСтр("ru = 'Не найдена загрузка доверенности номер ""%1"" с идентификатором ""%2""'"),
			НомерДоверенности,
			ИдентификаторТранзакции));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/transactions?txId=" + ИдентификаторТранзакции,
		"/poar-webapp/integration/poa/" + ИдентификаторТранзакции + "/status");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(
			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.СтатусТранзакции = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("status"),
			СтруктураОтвета.status, "");
		Результат.ИдентификаторТранзакции = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("txId"),
			СтруктураОтвета.txId, "");
		Результат.ДатаВремяТранзакции = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("timestamp"),
			ДатаИзСтрокиРазныхФорматов(СтруктураОтвета.timestamp), Неопределено);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при получении данных доверенности с сервера МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.ИдентификаторТранзакции) Тогда
		Результат.СтатусТранзакции = "";
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧДРР(НомерДоверенности, ТокенДоступа = "") Экспорт
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 	Ложь);
	Результат.Вставить("АдресСервера", 			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("СтатусДоверенности", 	"");
	Результат.Вставить("ХешДоверенности", 		"");
	Результат.Вставить("НомерДоверенности", 	"");
	Результат.Вставить("ДатаВыдачи", 			Неопределено);
	Результат.Вставить("ДатаОкончания", 		Неопределено);
	Результат.Вставить("ДатаРегистрации", 		Неопределено);
	Результат.Вставить("ДатаИзмененияСтатуса", 	Неопределено);
	Результат.Вставить("ПубличныйКлюч", 		"");
	Результат.Вставить("ТекстОтвета", 			"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен статус доверенности с сервера МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось получить статус доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось получить статус доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при получении статуса доверенности с сервера МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/not-found",
		СтрШаблон(
			НСтр("ru = 'Не найдена доверенность номер ""%1""'"),
			НомерДоверенности));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/poaopen?poaNumber=" + НомерДоверенности, "/poar-webapp/integration/poa/" + НомерДоверенности + "/public");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(
			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.СтатусДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("status"),
			СтруктураОтвета.status, "");
		Результат.ХешДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("id"),
			СтруктураОтвета.id, "");
		Результат.НомерДоверенности = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("poaNumber"),
			СтруктураОтвета.poaNumber, "");
		Результат.ДатаВыдачи = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("startDate"),
			ДатаИзСтрокиРазныхФорматов(СтруктураОтвета.startDate), Неопределено);
		Результат.ДатаОкончания = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("endDate"),
			ДатаИзСтрокиРазныхФорматов(СтруктураОтвета.endDate), Неопределено);
		Результат.ДатаРегистрации = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("createdDate"),
			ДатаИзСтрокиРазныхФорматов(СтруктураОтвета.createdDate), Неопределено);
		Результат.ДатаИзмененияСтатуса = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("statusDate"),
			ДатаИзСтрокиРазныхФорматов(СтруктураОтвета.statusDate), Неопределено);
		Результат.ПубличныйКлюч = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("issuerPublicKey"),
			СтруктураОтвета.issuerPublicKey, "");
		Если ИспользуетсяРежимТестирования() Тогда
			ПараметрыМЧДФНС = ХранилищеОбщихНастроек.Загрузить("ДокументооборотСКонтролирующимиОрганами_ПараметрыМЧДФНС");
			Если ПараметрыМЧДФНС <> Неопределено Тогда
				ПараметрыДоверенности = ПараметрыМЧДФНС[Результат.НомерДоверенности];
				Если ПараметрыДоверенности <> Неопределено И ПараметрыДоверенности.Свойство("ВозвращаемыйСтатус") Тогда
					Результат.СтатусДоверенности = ПараметрыДоверенности.ВозвращаемыйСтатус;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при получении статуса доверенности с сервера МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.ХешДоверенности) И НЕ ЗначениеЗаполнено(Результат.НомерДоверенности) Тогда
		Результат.СтатусДоверенности = "";
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПолныеДанныеДоверенностиНаСервереМЧДРР(НомерДоверенности, ИННДоверителя, ТокенДоступа = "") Экспорт
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 			Ложь);
	Результат.Вставить("АдресСервера", 					СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("ДанныеВыгрузки", 				Неопределено);
	Результат.Вставить("ДанныеПодписи", 				Неопределено);
	Результат.Вставить("ДанныеЗаявленияНаОтзыв", 		Неопределено);
	Результат.Вставить("ДанныеПодписиЗаявленияНаОтзыв", Неопределено);
	Результат.Вставить("ДанныеАрхива", 					Неопределено);
	Результат.Вставить("СтатусПолучения", 				"");
	Результат.Вставить("ТекстОтвета", 					"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен статус запроса данных доверенности с сервера МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось получить данные доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось получить данные доверенности с сервера МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при получении данных доверенности с сервера МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/not-found",
		СтрШаблон(
			НСтр("ru = 'Не найдена получаемая доверенность номер ""%1""'"),
			НомерДоверенности));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/poazip?poaNumber=" + НомерДоверенности + ?(ИННДоверителя = Неопределено, "", "&issuerInn=" + ИННДоверителя),
		"/poar-webapp/integration/poa/" + НомерДоверенности
			+ ?(ИННДоверителя = Неопределено, "", "/" + ИННДоверителя) + "/zip");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Если нрег(ОтветHTTP.Заголовки["Content-Type"]) = "application/zip"
		ИЛИ нрег(ОтветHTTP.Заголовки["content-type"]) = "application/zip"
		ИЛИ нрег(ОтветHTTP.Заголовки["Content-Type"]) = "multipart"
		ИЛИ нрег(ОтветHTTP.Заголовки["content-type"]) = "multipart"
		ИЛИ нрег(Лев(ОтветHTTP.Заголовки["Content-Disposition"], 10)) = "attachment"
		ИЛИ нрег(Лев(ОтветHTTP.Заголовки["content-disposition"], 10)) = "attachment" Тогда
		
		КаталогРаспаковки = "";
		Попытка
			Результат.ДанныеАрхива = Новый ДвоичныеДанные(ИмяФайлаОтвета);
			
			ОбъектЧтение = Новый ЧтениеZipФайла(ИмяФайлаОтвета);
			Если ОбъектЧтение.Элементы.Количество() <> 0 Тогда
				КаталогРаспаковки = ПолучитьИмяВременногоФайла();
				КаталогРаспаковки = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогРаспаковки);
				
				СоздатьКаталог(КаталогРаспаковки);
				Для каждого ЭлементАрхива Из ОбъектЧтение.Элементы Цикл
					РасширениеЭлемента = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ЭлементАрхива.Имя).Расширение;
					Если нрег(РасширениеЭлемента) = ".xml" ИЛИ нрег(РасширениеЭлемента) = ".sig"
						ИЛИ нрег(РасширениеЭлемента) = ".sign" Тогда
						
						ИмяЭлементаНРег = нрег(ЭлементАрхива.Имя);
						ЭтоОтзыв = СтрНайти(ИмяЭлементаНРег, "отм") <> 0 ИЛИ СтрНайти(ИмяЭлементаНРег, "отз") <> 0;
						ЭтоКвитанция = СтрНайти(ИмяЭлементаНРег, "квит") <> 0;
						
						Если НЕ ЭтоКвитанция Тогда
							ОбъектЧтение.Извлечь(ЭлементАрхива, КаталогРаспаковки, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
							Если нрег(РасширениеЭлемента) = ".xml" Тогда
								Если ЭтоОтзыв Тогда
									Результат.ДанныеЗаявленияНаОтзыв = Новый ДвоичныеДанные(КаталогРаспаковки + ЭлементАрхива.Имя);
								Иначе
									Результат.ДанныеВыгрузки = Новый ДвоичныеДанные(КаталогРаспаковки + ЭлементАрхива.Имя);
								КонецЕсли;
							ИначеЕсли нрег(РасширениеЭлемента) = ".sig" ИЛИ нрег(РасширениеЭлемента) = ".sign" Тогда
								ДанныеПодписи = Новый ДвоичныеДанные(КаталогРаспаковки + ЭлементАрхива.Имя);
								СтрокаПодписи64 = ПолучитьСтрокуИзДвоичныхДанных(ДанныеПодписи, "windows-1251");
								ДанныеПодписи = Base64Значение(СтрокаПодписи64);
								Если ЭтоОтзыв Тогда
									Результат.ДанныеПодписиЗаявленияНаОтзыв = ДанныеПодписи;
								Иначе
									Результат.ДанныеПодписи = ДанныеПодписи;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ОбъектЧтение.Закрыть();
		Исключение
			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(КаталогРаспаковки);
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
			ВывестиИЗаписатьОшибкуМЧДРР(,,
				Новый Структура("ШаблонОшибкиИзИсключения",
					НСтр("ru = 'Не удалось распаковать ответ при получении данных доверенности с сервера МЧД распределенного реестра: %1'")),,
				ПредставлениеОшибки);
			Возврат Результат;
		КонецПопытки;
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(КаталогРаспаковки);
		
		Если Результат.ДанныеВыгрузки = Неопределено Тогда
			ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
			ВывестиИЗаписатьОшибкуМЧДРР(,,
				Новый Структура("ТекстОшибкиПоУмолчанию",
					НСтр("ru = 'Не получены данные доверенности с сервера МЧД распределенного реестра'")));
			Возврат Результат;
		КонецЕсли;
		
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.СтатусПолучения = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("reqStatus"),
			СтруктураОтвета.reqStatus, "");
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при получении данных доверенности с сервера МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.СтатусПолучения) Тогда
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	Возврат Результат;
	
КонецФункции

Функция ОтменитьМЧДРР(
		ИмяФайлаВыгрузки,
		ДанныеИлиАдресВыгрузки,
		ДанныеИлиАдресПодписи,
		ТокенДоступа = "",
		НомерДоверенности = "",
		СсылкаНаДоверенность = Неопределено) Экспорт
	
	ДанныеВыгрузки = ?(ТипЗнч(ДанныеИлиАдресВыгрузки) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресВыгрузки), ПолучитьИзВременногоХранилища(ДанныеИлиАдресВыгрузки),
		ДанныеИлиАдресВыгрузки);
	ДанныеПодписи = ?(ТипЗнч(ДанныеИлиАдресПодписи) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресПодписи), ПолучитьИзВременногоХранилища(ДанныеИлиАдресПодписи),
		ДанныеИлиАдресПодписи);
	
	Если ЗначениеЗаполнено(СсылкаНаДоверенность) Тогда
		ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
		ОбъектДоверенность.ИмяФайлаЗаявленияНаОтзыв = ИмяФайлаВыгрузки;
		ОбъектДоверенность.ФайлЗаявленияНаОтзыв = Новый ХранилищеЗначения(ДанныеВыгрузки,
			Новый СжатиеДанных(9));
		ОбъектДоверенность.ЭлектроннаяПодписьЗаявленияНаОтзыв = Новый ХранилищеЗначения(ДанныеПодписи,
			Новый СжатиеДанных(9));
		ОбъектДоверенность.Записать();
	КонецЕсли;
	
	СвойстваСервераМЧДРР = СвойстваСервераМЧДРР();
	
	Результат = Новый Структура;
	Результат.Вставить("ПовторятьСоединение", 		Ложь);
	Результат.Вставить("АдресСервера", 				СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
	Результат.Вставить("ИдентификаторТранзакции", 	"");
	Результат.Вставить("ТекстОтвета", 				"");
	
	Если НЕ ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
		Если РезультатАвторизации.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение = Истина;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	ШаблоныОшибок = СтруктураШаблоновОшибокМЧДРР();
	ШаблоныОшибок.ТекстОшибкиПоУмолчанию =
		НСтр("ru = 'Не получен идентификатор отзыва доверенности с сервера МЧД распределенного реестра'");
	ШаблоныОшибок.ШаблонОшибкиИзИсключения =
		НСтр("ru = 'Не удалось отозвать доверенность на сервере МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиДляКодаСостояния =
		НСтр("ru = 'Не удалось отозвать доверенность на сервере МЧД распределенного реестра: %1'");
	ШаблоныОшибок.ШаблонОшибкиИзОтвета =
		НСтр("ru = 'Ошибка при отзыве доверенности на сервере МЧД распределенного реестра. %1'");
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthenticated",
		НСтр("ru = 'Запрос к серверу МЧД распределенного реестра выполнен от неаутентифицированного пользователя'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/unauthorized",
		НСтр("ru = 'Запрос выполнен не от пользователя с ролью для доступа к серверу МЧД распределенного реестра'"));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/not-found",
		СтрШаблон(
			НСтр("ru = 'Не найдена досрочно отзываемая доверенность номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/not-valid",
		СтрШаблон(
			НСтр("ru = 'Операция запрещена: данные в сертификате не совпадают с данными досрочно отзываемой доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/certificate_validation_exception",
		СтрШаблон(
			НСтр("ru = 'Электронная подпись запроса на досрочный отзыв доверенности номер ""%1"" невалидна'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/expired_poa",
		СтрШаблон(
			НСтр("ru = 'Досрочный отзыв недоступен: доверенность номер ""%1"" истекла'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoked_poa",
		СтрШаблон(
			НСтр("ru = 'Досрочный отзыв недоступен: доверенность номер ""%1"" уже отозвана'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_terminal_poa_status",
		СтрШаблон(
			НСтр("ru = 'Невозможен отзыв доверенности номер ""%1"", имеющей статус, отличный от ""Зарегистрировано""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/poa.revoke.revocable_poa_status_is_terminal",
		СтрШаблон(
			НСтр("ru = 'Невозможен отзыв доверенности номер ""%1"", имеющей статус, отличный от ""Зарегистрировано""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_signature_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка валидности электронной подписи отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_ogrn_or_ogrnip_check_failed",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка полномочий подписанта, так как ОГРН/ОГРНИП доверителя в отзываемой доверенности не соответствуют ОГРН/ОГРНИП в сертификате электронной подписи отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_certificate_ogrn_or_ogrnip_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка полномочий подписанта, так как в сертификате электронной подписи не заполнен ОГРН/ОГРНИП для отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_certificate_snils_not_specified",
		СтрШаблон(
			НСтр("ru = 'Не пройдена проверка полномочий подписанта, так как в сертификате электронной подписи не заполнен СНИЛС владельца для отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/revoke_snils_check_failed",
		СтрШаблон(
			НСтр("ru = 'ННе пройдена проверка полномочий подписанта, так как СНИЛС подписанта в отзываемой доверенности не соответствуют СНИЛС в сертификате электронной подписи для отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	ШаблоныОшибок.ШаблоныДляКодовОшибок.Вставить("/errors/invalid_poa_revoke_xml",
		СтрШаблон(
			НСтр("ru = 'XML-файл отзыва доверенности не соответствует XSD-схеме для отзыва доверенности номер ""%1""'"),
			НомерДоверенности));
	
	РесурсНаСервере = СвойстваСервераМЧДРР.РесурсКорняAPI + ?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI,
		"/poacancel", "/poar-webapp/integration/poa/revoke");
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "multipart/form-data; boundary=My1cV8bNdr");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	Если СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI И ИспользуетсяРежимТестирования() Тогда
		ЗаголовкиHTTP.Вставить("poaservertype", СвойстваСервераМЧДРР.ТестовыйСервер);
	КонецЕсли;
	ЗаголовкиHTTP.Вставить(?(СвойстваСервераМЧДРР.ИспользоватьРасширенияAPI, "authorizationtoken", "authorization"),
		"Bearer " + ТокенДоступа);
	
	// запись передаваемых файлов
	
	МассивИменФайлов = Новый Массив;
	
	СодержимоеФайла = "--My1cV8bNdr"
		+ Символы.ПС + "Content-Disposition: form-data; name=""poaRevoke""; filename=""" + ИмяФайлаВыгрузки + """"
		+ Символы.ПС + "Content-Type: text/xml"
		+ Символы.ПС
		+ Символы.ПС;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	ИмяВременногоФайлаОтмены = ПолучитьИмяВременногоФайла();
	ДанныеВыгрузки.Записать(ИмяВременногоФайлаОтмены);
	МассивИменФайлов.Добавить(ИмяВременногоФайлаОтмены);
	
	СодержимоеФайла = Символы.ПС + "--My1cV8bNdr"
		+ Символы.ПС + "Content-Disposition: form-data; name=""signature""; filename=""" + ИмяФайлаВыгрузки + ".sig"""
		+ Символы.ПС + "Content-Type: application/octet-stream"
		+ Символы.ПС
		+ Символы.ПС;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	ИмяВременногоФайлаПодписи = ПолучитьИмяВременногоФайла();
	Подпись64 = Base64Строка(ДанныеПодписи);
	Подпись64 = СтрЗаменить(Подпись64, Символы.ВК, "");
	Подпись64 = СтрЗаменить(Подпись64, Символы.ПС, "");
	ПолучитьДвоичныеДанныеИзСтроки(Подпись64, "windows-1251").Записать(ИмяВременногоФайлаПодписи);
	МассивИменФайлов.Добавить(ИмяВременногоФайлаПодписи);
	
	СодержимоеФайла = Символы.ПС + "--My1cV8bNdr--";
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ОбъектЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ОбъектЗаписьТекста.Записать(СодержимоеФайла);
	ОбъектЗаписьТекста.Закрыть();
	МассивИменФайлов.Добавить(ИмяВременногоФайла);
	
	// объединение передаваемых файлов
	
	ИмяФайлаЗапроса = ПолучитьИмяВременногоФайла();
	ОбъединитьФайлы(МассивИменФайлов, ИмяФайлаЗапроса);
	Для каждого ИмяВременногоФайла Из МассивИменФайлов Цикл
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяВременногоФайла);
	КонецЦикла;
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ЗапросHTTP.УстановитьИмяФайлаТела(ИмяФайлаЗапроса);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		СоединениеHTTP = ОбщегоНазначенияЭДКО.СоединениеССерверомИнтернета(
			СвойстваСервераМЧДРР.АдресСервераБезАутентификации);
		
		ОтветHTTP = СоединениеHTTP.ОтправитьДляОбработки(ЗапросHTTP, ИмяФайлаОтвета);
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		Результат.ПовторятьСоединение = НЕ ОбщегоНазначения.РазделениеВключено();
		ВывестиИЗаписатьОшибкуМЧДРР(,,
			ШаблоныОшибок,
			?(Результат.ПовторятьСоединение, "ТолькоЗаписатьВЖурналРегистрации", ""),
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	
	Попытка
		ФайлОтвета = Новый ЧтениеТекста(ИмяФайлаОтвета, "utf-8");
		Результат.ТекстОтвета = ФайлОтвета.Прочитать();
		ФайлОтвета.Закрыть();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьФайл(ИмяФайлаОтвета, "utf-8");
		СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
		
		Результат.ИдентификаторТранзакции = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("txId"),
			СтруктураОтвета.txId, "");
	Исключение
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ШаблоныОшибок.ШаблонОшибкиИзИсключения =
			НСтр("ru = 'Не удалось разобрать ответ при отмене доверенности на сервере МЧД распределенного реестра: %1'");
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,,
			ШаблоныОшибок,,
			ПредставлениеОшибки);
		Возврат Результат;
	КонецПопытки;
	ШаблоныОшибок.ШаблонОшибкиИзИсключения = "";
	
	Если НЕ ЗначениеЗаполнено(Результат.ИдентификаторТранзакции) Тогда
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
		ВывестиИЗаписатьОшибкуМЧДРР(
			ОтветHTTP,
			СтруктураОтвета,
			ШаблоныОшибок);
		Возврат Результат;
	КонецЕсли;
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаЗапроса);
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ИмяФайлаОтвета);
	
	Возврат Результат;
	
КонецФункции

Функция ОбновитьСостояниеМЧДРР(СсылкаНаДоверенность = Неопределено) Экспорт
	
	Результат = ДокументооборотСКОКлиентСервер.РезультатОбновитьСостояниеМЧДРР();
	Результат.Выполнено = Истина;
	
	ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
	
	Результат.СостояниеИзменено = Ложь;
	Результат.СтатусИзменился = Ложь;
	ТокенДоступа = "";
	Если ОбъектДоверенность.Статус =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отправлено")
		ИЛИ ОбъектДоверенность.Статус =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв") Тогда
		
		СведенияСтатусаТранзакции = ПолучитьСтатусТранзакцииМЧДРР(
			ОбъектДоверенность.ИдентификаторТранзакции,
			ТокенДоступа,
			ОбъектДоверенность.НомерДоверенности);
		Если СведенияСтатусаТранзакции.ПовторятьСоединение Тогда
			Результат.ПовторятьСоединение 	= Истина;
			Результат.АдресСервера 			= СведенияСтатусаТранзакции.АдресСервера;
			Возврат Результат;
		КонецЕсли;
		
		Если СведенияСтатусаТранзакции.СтатусТранзакции = "SUCCESS" Тогда
			Если ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв") Тогда
				
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтвержденияОтзыва");
				
			Иначе
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтверждения");
			КонецЕсли;
			
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			ОбъектДоверенность.ИдентификаторТранзакции = "";
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции = "FAILURE" Тогда
			Если ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв") Тогда
				
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОшибкаОтзыва");
				
			Иначе
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОшибкаРегистрации");
			КонецЕсли;
			
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			ОбъектДоверенность.ИдентификаторТранзакции = "";
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции <> "PENDING" Тогда // "ERROR"
			Результат.Выполнено = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбъектДоверенность.Статус <>
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ВРаботе")
		И ОбъектДоверенность.Статус <>
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отправлено")
		И ОбъектДоверенность.Статус <>
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОшибкаРегистрации") Тогда
		
		СведенияСтатусаДоверенности = ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧДРР(
			ОбъектДоверенность.НомерДоверенности,
			ТокенДоступа);
		Если СведенияСтатусаДоверенности.ПовторятьСоединение Тогда
			Если Результат.СостояниеИзменено Тогда
				ОбъектДоверенность.Записать();
			КонецЕсли;
			Результат.ПовторятьСоединение 	= Истина;
			Результат.АдресСервера 			= СведенияСтатусаДоверенности.АдресСервера;
			Возврат Результат;
		КонецЕсли;
		Результат.ХешДоверенности 		= СведенияСтатусаДоверенности.ХешДоверенности;
		Результат.НомерДоверенности 	= СведенияСтатусаДоверенности.НомерДоверенности;
		Результат.ДатаВыдачи 			= СведенияСтатусаДоверенности.ДатаВыдачи;
		Результат.ДатаОкончания 		= СведенияСтатусаДоверенности.ДатаОкончания;
		Результат.ДатаРегистрации 		= СведенияСтатусаДоверенности.ДатаРегистрации;
		Результат.ДатаИзмененияСтатуса 	= СведенияСтатусаДоверенности.ДатаИзмененияСтатуса;
		Результат.СтатусДоверенности 	= СведенияСтатусаДоверенности.СтатусДоверенности;
		Результат.ПубличныйКлюч 		= СведенияСтатусаДоверенности.ПубличныйКлюч;
		
		Если СведенияСтатусаДоверенности.СтатусДоверенности = "PROCESSING" Тогда
			Если ОбъектДоверенность.Статус <>
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтверждения")
				И ОбъектДоверенность.Статус <>
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтвержденияОтзыва") Тогда
				
				Если ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв") Тогда
					
					ОбъектДоверенность.Статус =
						ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтвержденияОтзыва");
					
				Иначе
					ОбъектДоверенность.Статус =
						ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтверждения");
				КонецЕсли;
				
				ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
				Результат.СостояниеИзменено = Истина;
				Результат.СтатусИзменился = Истина;
			КонецЕсли;
			Результат.Рассматривается = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "REJECTED"
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтказВРегистрации")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтказВРегистрацииОтзыва") Тогда
			
			Если ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв")
				ИЛИ ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтвержденияОтзыва") Тогда
				
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтказВРегистрацииОтзыва");
				
			Иначе
				ОбъектДоверенность.Статус =
					ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтказВРегистрации");
			КонецЕсли;
			
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "CREATED"
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ДатаНачалаДействияНеНаступила") Тогда
			
			ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ДатаНачалаДействияНеНаступила");
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "ACTIVE"
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Зарегистрировано")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтправленоЗаявлениеНаОтзыв")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОжиданиеПодтвержденияОтзыва")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОтказВРегистрацииОтзыва")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ОшибкаОтзыва") Тогда
			
			ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Зарегистрировано");
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "EXPIRED"
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ИстекСрокДействия") Тогда
			
			ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.ИстекСрокДействия");
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
			
		ИначеЕсли (СведенияСтатусаДоверенности.СтатусДоверенности = "DECLINED"
			ИЛИ СведенияСтатусаДоверенности.СтатусДоверенности = "REVOKED")
			И ОбъектДоверенность.Статус <>
			ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отозвано") Тогда
			
			ОбъектДоверенность.Статус =
				ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиКО.Отозвано");
			ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
			Результат.СостояниеИзменено = Истина;
			Результат.СтатусИзменился = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СведенияСтатусаДоверенности.ДатаРегистрации)
			И ОбъектДоверенность.ДатаРегистрации <> СведенияСтатусаДоверенности.ДатаРегистрации Тогда
			
			ОбъектДоверенность.ДатаРегистрации = СведенияСтатусаДоверенности.ДатаРегистрации;
			Если НЕ ЗначениеЗаполнено(ОбъектДоверенность.ДатаОтправки) Тогда
				ОбъектДоверенность.ДатаОтправки = СведенияСтатусаДоверенности.ДатаРегистрации;
			КонецЕсли;
			Результат.СостояниеИзменено = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СведенияСтатусаДоверенности.ДатаИзмененияСтатуса)
			И ОбъектДоверенность.ДатаИзмененияСтатуса <> СведенияСтатусаДоверенности.ДатаИзмененияСтатуса Тогда
			
			ОбъектДоверенность.ДатаИзмененияСтатуса = СведенияСтатусаДоверенности.ДатаИзмененияСтатуса;
			Результат.СостояниеИзменено = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.СостояниеИзменено Тогда
		ОбъектДоверенность.Записать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьМЧД(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Ложь,
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФНС.ВыгрузитьЭлементВФайлОбмена(
		СправочникСсылка,
		ИдентификаторФайла,
		КодНалоговогоОрганаПолучателя,
		ОпределятьКодНалоговогоОрганаПолучателя,
		ФорматДоверенностиПоПриказуФНС,
		ПроверитьВыгрузку,
		ПараметрыВозврата);
	
КонецФункции

Функция ЗагрузитьМЧД(ВходящиеДанные, ОбновлятьСуществующийИлиСсылка = Ложь, Подпись = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФНС.ЗагрузитьЭлементИзФайлаОбмена(ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка, Подпись);
	
КонецФункции

Функция ПроверитьМЧД(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФНС.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Функция ПроверитьЗаявлениеОбОтзывеМЧДФНС(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Справочники.ЗаявленияОбОтзывеМЧДФНС.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Функция ЗагрузитьЗаявлениеОбОтзывеМЧДФНС(ВходящиеДанные, ОбновлятьСуществующий = Ложь, Подпись = Неопределено) Экспорт
	
	Возврат Справочники.ЗаявленияОбОтзывеМЧДФНС.ЗагрузитьЭлементИзФайлаОбмена(ВходящиеДанные,
		ОбновлятьСуществующий, Подпись);
	
КонецФункции

Функция ВыгрузитьЗаявлениеОбОтзывеМЧДФНС(
		СправочникСсылка,
		ИдентификаторФайла = "",
		КодНалоговогоОрганаПолучателя = "",
		ОпределятьКодНалоговогоОрганаПолучателя = Ложь,
		ФорматДоверенностиПоПриказуФНС = Ложь,
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	Возврат Справочники.ЗаявленияОбОтзывеМЧДФНС.ВыгрузитьЭлементВФайлОбмена(
		СправочникСсылка,
		ИдентификаторФайла,
		КодНалоговогоОрганаПолучателя,
		ОпределятьКодНалоговогоОрганаПолучателя,
		ФорматДоверенностиПоПриказуФНС,
		ПроверитьВыгрузку,
		ПараметрыВозврата);
	
КонецФункции

Процедура СохранитьПодписьМЧДФНС(
		ИмяФайлаВыгрузки,
		ДанныеИлиАдресВыгрузки,
		ДанныеИлиАдресПодписи,
		ОтпечатокСертификатаАбонента,
		СсылкаНаДоверенность) Экспорт
	
	ДанныеВыгрузки = ?(ТипЗнч(ДанныеИлиАдресВыгрузки) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресВыгрузки), ПолучитьИзВременногоХранилища(ДанныеИлиАдресВыгрузки),
		ДанныеИлиАдресВыгрузки);
	ДанныеПодписи = ?(ТипЗнч(ДанныеИлиАдресПодписи) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресПодписи), ПолучитьИзВременногоХранилища(ДанныеИлиАдресПодписи),
		ДанныеИлиАдресПодписи);
	
	ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
	ОбъектДоверенность.ИмяФайлаВыгрузки = ИмяФайлаВыгрузки;
	ОбъектДоверенность.ФайлВырузки = Новый ХранилищеЗначения(ДанныеВыгрузки,
		Новый СжатиеДанных(9));
	ОбъектДоверенность.ЭлектроннаяПодпись = Новый ХранилищеЗначения(ДанныеПодписи,
		Новый СжатиеДанных(9));
	ОбъектДоверенность.ОтпечатокСертификата = ОтпечатокСертификатаАбонента;
	ОбъектДоверенность.Записать();
	
КонецПроцедуры

Функция ВыгрузитьМЧДФСС(
		СправочникСсылка,
		ИдентификаторФайла = "",
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФСС.ВыгрузитьЭлементВФайлОбмена(
		СправочникСсылка,
		ИдентификаторФайла,
		ПроверитьВыгрузку,
		ПараметрыВозврата);
	
КонецФункции

Функция ЗагрузитьМЧДФСС(ВходящиеДанные, ОбновлятьСуществующийИлиСсылка = Ложь) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФСС.ЗагрузитьЭлементИзФайлаОбмена(ВходящиеДанные,
		ОбновлятьСуществующийИлиСсылка);
	
КонецФункции

Функция ПроверитьМЧДФСС(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Справочники.МашиночитаемыеДоверенностиФСС.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Функция ПараметрыОтзываМашиночитаемойДоверенностиФСС(
		ОтчетСсылка,
		Организация,
		НомерДоверенности,
		Причина = "",
		ОтозватьЦепочку = Ложь,
		ВозвращатьОтправкуОтзываПриДействительностиМЧД = Неопределено,
		ДатаНачалаОтправки = Неопределено) Экспорт
	
	Если ВозвращатьОтправкуОтзываПриДействительностиМЧД <> Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	МашиночитаемыеДоверенностиФСС.Ссылка КАК ОтчетСсылка,
			|	МашиночитаемыеДоверенностиФСС.Статус КАК Статус,
			|	ОтправкиФСС.Ссылка КАК ОтправкаСсылка,
			|	ОтправкиФСС.СтатусОтправки КАК СтатусОтправки
			|ИЗ
			|	Справочник.МашиночитаемыеДоверенностиФСС КАК МашиночитаемыеДоверенностиФСС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОтправкиФСС КАК ОтправкиФСС
			|		ПО (ОтправкиФСС.ОтчетСсылка = МашиночитаемыеДоверенностиФСС.Ссылка)
			|ГДЕ
			|	МашиночитаемыеДоверенностиФСС.Ссылка = &ОтчетСсылка
			|	И МашиночитаемыеДоверенностиФСС.Организация = &Организация
			|	И НЕ ОтправкиФСС.ПометкаУдаления
			|УПОРЯДОЧИТЬ ПО
			|	ОтправкиФСС.ДатаОтправки УБЫВ";
		
		Запрос.УстановитьПараметр("ОтчетСсылка", ОтчетСсылка);
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Результат = Новый Структура;
		Результат.Вставить("ЭтоСведенияОбОтчетеОперации", 	Истина);
		Результат.Вставить("ОтчетСсылка", 					Неопределено);
		Результат.Вставить("ОтправкаСсылка", 				Неопределено);
		Результат.Вставить("СтатусОтправки", 				Неопределено);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() И Выборка.СтатусОтправки = Перечисления.СтатусыОтправки.НеПринят
				И Выборка.Статус = Перечисления.СтатусыМашиночитаемойДоверенностиКО.Отозвано Тогда
				
				Результат.ОтчетСсылка 		= Выборка.ОтчетСсылка;
				Результат.ОтправкаСсылка 	= Выборка.ОтправкаСсылка;
				Результат.СтатусОтправки 	= Выборка.СтатусОтправки;
				
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВозвращатьОтправкуОтзываПриДействительностиМЧД = Истина Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ОтправкиФСС.Ссылка КАК ОтправкаСсылка,
			|	ОтправкиФСС.ОтчетСсылка КАК ОтчетСсылка,
			|	ОтправкиФСС.СтатусОтправки КАК СтатусОтправки
			|ИЗ
			|	Справочник.ОтправкиФСС КАК ОтправкиФСС
			|ГДЕ
			|	ОтправкиФСС.ВидОтчета = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОтзывМашиночитаемойДоверенностиФСС)
			|	И ОтправкиФСС.Организация = &Организация
			|	И ОтправкиФСС.ИдентификаторОтправкиНаСервере ПОДОБНО &ИдентификаторОтправкиНаСервере
			|	И ОтправкиФСС.ДатаОтправки >= &ДатаНачалаОтправки
			|	И НЕ ОтправкиФСС.ПометкаУдаления
			|УПОРЯДОЧИТЬ ПО
			|	ОтправкиФСС.ДатаОтправки УБЫВ";
		
		Запрос.УстановитьПараметр("Организация", 					Организация);
		Запрос.УстановитьПараметр("ИдентификаторОтправкиНаСервере", "%" + НомерДоверенности + "%");
		Запрос.УстановитьПараметр("ДатаНачалаОтправки", 			ДатаНачалаОтправки);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() Тогда
				Результат.ЭтоСведенияОбОтчетеОперации 	= Ложь;
				Результат.ОтчетСсылка 					= Выборка.ОтчетСсылка;
				Результат.ОтправкаСсылка 				= Выборка.ОтправкаСсылка;
				Результат.СтатусОтправки 				= Выборка.СтатусОтправки;
				
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("utf-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("urn:revocationPowerOfAttorney");
	ЗаписьXML.ЗаписатьАтрибут("xmlns:urn", "urn:ru:fss:integration:types:mchd:v01");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("urn:uuid");
	ЗаписьXML.ЗаписатьТекст(НомерДоверенности);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("urn:revokeChain");
	Если ОтозватьЦепочку Тогда
		ЗаписьXML.ЗаписатьТекст("true");
	КонецЕсли;
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("urn:reason");
	ЗаписьXML.ЗаписатьТекст(Причина);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	СодержимоеСообщения = ЗаписьXML.Закрыть();
	
	ТипСообщения = ДокументооборотСФССКлиентСервер.ТипыСообщенийСЭДО().ТипСообщенияОтзывМашиночитаемойДоверенностиФСС;
	ОписаниеОшибки =
		НСтр("ru = 'Не удалось отправить отзыв машиночитаемой доверенности (ФСС).
				   |%1'");
	
	Результат = ЭлектронныйДокументооборотСФСС.ПараметрыОтправитьСообщениеСЭДО(
		ТипСообщения,
		СодержимоеСообщения,
		Организация,
		ОписаниеОшибки);
	ДанаСеанса = ТекущаяДатаСеанса();
	Результат.Вставить("ДатаСеансаНаСервере", ДанаСеанса);
	Возврат Результат;
	
КонецФункции

Функция ПараметрыЗапросаМашиночитаемойДоверенностиФСС(
		Организация,
		НомерДоверенности,
		ВозвращатьОтправкуЗапросаПриОтсутствииМЧД = Ложь,
		ДатаНачалаОтправки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	МашиночитаемыеДоверенностиФСС.Ссылка КАК ОтчетСсылка,
		|	ОтправкиФСС.Ссылка КАК ОтправкаСсылка,
		|	ОтправкиФСС.СтатусОтправки КАК СтатусОтправки
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиФСС КАК МашиночитаемыеДоверенностиФСС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОтправкиФСС КАК ОтправкиФСС
		|		ПО (ОтправкиФСС.ОтчетСсылка = МашиночитаемыеДоверенностиФСС.Ссылка)
		|ГДЕ
		|	МашиночитаемыеДоверенностиФСС.НомерДоверенности = &НомерДоверенности
		|	И МашиночитаемыеДоверенностиФСС.Организация = &Организация
		|	И НЕ МашиночитаемыеДоверенностиФСС.ПометкаУдаления
		|	И НЕ ОтправкиФСС.ПометкаУдаления
		|УПОРЯДОЧИТЬ ПО
		|	ОтправкиФСС.ДатаОтправки УБЫВ";
	
	Запрос.УстановитьПараметр("НомерДоверенности", 	НомерДоверенности);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоСведенияОбОтчетеОперации", 	Истина);
	Результат.Вставить("ОтчетСсылка", 					Неопределено);
	Результат.Вставить("ОтправкаСсылка", 				Неопределено);
	Результат.Вставить("СтатусОтправки", 				Неопределено);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Результат.ОтчетСсылка 		= Выборка.ОтчетСсылка;
			Результат.ОтправкаСсылка 	= Выборка.ОтправкаСсылка;
			Результат.СтатусОтправки 	= Выборка.СтатусОтправки;
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВозвращатьОтправкуЗапросаПриОтсутствииМЧД Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ОтправкиФСС.Ссылка КАК ОтправкаСсылка,
			|	ОтправкиФСС.ОтчетСсылка КАК ОтчетСсылка,
			|	ОтправкиФСС.СтатусОтправки КАК СтатусОтправки
			|ИЗ
			|	Справочник.ОтправкиФСС КАК ОтправкиФСС
			|ГДЕ
			|	ОтправкиФСС.ВидОтчета = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ЗапросМашиночитаемойДоверенностиФСС)
			|	И ОтправкиФСС.Организация = &Организация
			|	И ОтправкиФСС.ДатаОтправки >= &ДатаНачалаОтправки
			|	И НЕ ОтправкиФСС.ПометкаУдаления
			|УПОРЯДОЧИТЬ ПО
			|	ОтправкиФСС.ДатаОтправки УБЫВ";
		
		Запрос.УстановитьПараметр("Организация", 		Организация);
		Запрос.УстановитьПараметр("ДатаНачалаОтправки", ДатаНачалаОтправки);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() Тогда
				Результат.ЭтоСведенияОбОтчетеОперации 	= Ложь;
				Результат.ОтчетСсылка 					= Выборка.ОтчетСсылка;
				Результат.ОтправкаСсылка 				= Выборка.ОтправкаСсылка;
				Результат.СтатусОтправки 				= Выборка.СтатусОтправки;
				
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	ТипСообщения = ДокументооборотСФССКлиентСервер.ТипыСообщенийСЭДО().ТипСообщенияЗапросМашиночитаемойДоверенностиФСС;
	СодержимоеСообщения = СтрШаблон(
		"<urn:powerOfAttorneyRequest xmlns:urn=""urn:ru:fss:integration:types:mchd:v01"">
		|  <urn:uuid>%1</urn:uuid>
		|</urn:powerOfAttorneyRequest>",
		НомерДоверенности);
	ОписаниеОшибки =
		НСтр("ru = 'Не удалось отправить запрос машиночитаемой доверенности (ФСС).
				   |%1'");
	
	Результат = ЭлектронныйДокументооборотСФСС.ПараметрыОтправитьСообщениеСЭДО(
		ТипСообщения,
		СодержимоеСообщения,
		Организация,
		ОписаниеОшибки);
	ДанаСеанса = ТекущаяДатаСеанса();
	Результат.Вставить("ДатаСеансаНаСервере", ДанаСеанса);
	Возврат Результат;
	
КонецФункции

Функция ПараметрыЗапросаСправочникаПолномочийМашиночитаемыхДоверенностейФСС(Организация) Экспорт
	
	ТипыСообщений = ДокументооборотСФССКлиентСервер.ТипыСообщенийСЭДО();
	ТипСообщения = ТипыСообщений.ТипСообщенияЗапросСправочникаПолномочийМашиночитаемыхДоверенностейФСС;
	СодержимоеСообщения =
		"<urn:dicAuthorityListRequest xmlns:urn=""urn:ru:fss:integration:types:mchd:v01""/>";
	ОписаниеОшибки =
		НСтр("ru = 'Не удалось отправить запрос справочника полномочий машиночитаемых доверенностей (ФСС).
				   |%1'");
	
	Результат = ЭлектронныйДокументооборотСФСС.ПараметрыОтправитьСообщениеСЭДО(
		ТипСообщения,
		СодержимоеСообщения,
		Организация,
		ОписаниеОшибки);
	ДанаСеанса = ТекущаяДатаСеанса();
	Результат.Вставить("ДатаСеансаНаСервере", ДанаСеанса);
	Возврат Результат;
	
КонецФункции

Функция СведенияОбОтправкеСЭДО(АвтозапросПараметры) Экспорт
	
	ОтчетИлиСведенияОбОтправке = Неопределено;
	Если АвтозапросПараметры.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.ОтзывМашиночитаемойДоверенностиФСС Тогда
		ВозвращатьОтправкуОтзываПриДействительностиМЧД = Истина;
		Возврат ПараметрыОтзываМашиночитаемойДоверенностиФСС(
			АвтозапросПараметры.СсылкаНаОтчет,
			АвтозапросПараметры.Организация,
			АвтозапросПараметры.НомерОтчета,,,
			ВозвращатьОтправкуОтзываПриДействительностиМЧД,
			АвтозапросПараметры.ДатаНачалаОтправки);
		
	ИначеЕсли АвтозапросПараметры.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.ЗапросМашиночитаемойДоверенностиФСС Тогда
		ВозвращатьОтправкуЗапросаПриОтсутствииМЧД = Истина;
		Возврат ПараметрыЗапросаМашиночитаемойДоверенностиФСС(
			АвтозапросПараметры.Организация,
			АвтозапросПараметры.НомерОтчета,
			ВозвращатьОтправкуЗапросаПриОтсутствииМЧД,
			АвтозапросПараметры.ДатаНачалаОтправки);
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВыгрузитьМЧДЦБ(
		СправочникСсылка,
		ПроверитьВыгрузку = Ложь,
		ПараметрыВозврата = Неопределено) Экспорт
	
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
		Возврат Справочники[ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ].ВыгрузитьЭлементВФайлОбмена(
			СправочникСсылка,
			ПроверитьВыгрузку,
			ПараметрыВозврата);
	КонецЕсли;
	
КонецФункции

Функция ЗагрузитьМЧДЦБ(ВходящиеДанные, ОбновлятьСуществующий = Ложь, Подпись = Неопределено) Экспорт
	
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
		Возврат Справочники[ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ].ЗагрузитьЭлементИзФайлаОбмена(ВходящиеДанные,
			ОбновлятьСуществующий, Подпись);
	КонецЕсли;
	
КонецФункции

Функция ПроверитьМЧДЦБ(Знач ОбъектИлиСсылка) Экспорт
	
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦБСуществует() Тогда
		ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ = "МашиночитаемыеДоверенностиЦБ";
		Возврат Справочники[ИмяТипаСправочникаМашиночитаемыеДоверенностиЦБ].ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	КонецЕсли;
	
КонецФункции

Процедура ЗаписатьМЧДЦБСПодписью(
		ИмяФайлаВыгрузки,
		ДанныеИлиАдресВыгрузки,
		ДанныеИлиАдресПодписи,
		СсылкаНаДоверенность) Экспорт
	
	ДанныеВыгрузки = ?(ТипЗнч(ДанныеИлиАдресВыгрузки) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресВыгрузки), ПолучитьИзВременногоХранилища(ДанныеИлиАдресВыгрузки),
		ДанныеИлиАдресВыгрузки);
	ДанныеПодписи = ?(ТипЗнч(ДанныеИлиАдресПодписи) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(ДанныеИлиАдресПодписи), ПолучитьИзВременногоХранилища(ДанныеИлиАдресПодписи),
		ДанныеИлиАдресПодписи);
	
	Если ЗначениеЗаполнено(СсылкаНаДоверенность) Тогда
		ОбъектДоверенность = СсылкаНаДоверенность.ПолучитьОбъект();
		ОбъектДоверенность.ИмяФайлаВыгрузки = ИмяФайлаВыгрузки;
		ОбъектДоверенность.ФайлВырузки = Новый ХранилищеЗначения(ДанныеВыгрузки, Новый СжатиеДанных(9));
		ОбъектДоверенность.ЭлектроннаяПодпись = Новый ХранилищеЗначения(ДанныеПодписи, Новый СжатиеДанных(9));
		ОбъектДоверенность.Статус = Перечисления.СтатусыМашиночитаемойДоверенностиКО.Подписано;
		ОбъектДоверенность.ДатаОбновленияСтатуса = ТекущаяДатаСеанса();
		ОбъектДоверенность.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ВыгрузитьУведомлениеОПредоставленииПолномочийПредставителю(
		ДокументСсылка) Экспорт
	
	Возврат Документы.УведомлениеОПредоставленииПолномочийПредставителю.ВыгрузитьУведомление(
		ДокументСсылка);
	
КонецФункции

Функция ЗагрузитьУПУП(ВходящиеДанные, ОбновлятьСуществующий = Ложь) Экспорт
	
	Возврат Документы.УведомлениеОПредоставленииПолномочийПредставителю.ЗагрузитьДокументИзФайлаОбмена(ВходящиеДанные,
		ОбновлятьСуществующий);
	
КонецФункции
	
Функция ПроверитьУПУП(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Документы.УведомлениеОПредоставленииПолномочийПредставителю.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Функция ВыгрузитьУведомлениеОПрекращенииПолномочийПредставителя(
		ДокументСсылка) Экспорт
	
	Возврат Документы.УведомлениеОПрекращенииПолномочийПредставителя.ВыгрузитьУведомление(
		ДокументСсылка);
	
КонецФункции

Функция ЗагрузитьУПРУП(ВходящиеДанные, ОбновлятьСуществующий = Ложь) Экспорт
	
	Возврат Документы.УведомлениеОПрекращенииПолномочийПредставителя.ЗагрузитьДокументИзФайлаОбмена(ВходящиеДанные,
		ОбновлятьСуществующий);
	
КонецФункции
	
Функция ПроверитьУПРУП(Знач ОбъектИлиСсылка) Экспорт
	
	Возврат Документы.УведомлениеОПрекращенииПолномочийПредставителя.ПроверитьВозможностьВыгрузки(ОбъектИлиСсылка);
	
КонецФункции

Процедура УстановитьПараметрыПодключенияОтключенияМЧД(Запрос, ПроверяемыеМЧДФНС, ВыбранныеОрганизации) 
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РегистрацииВНО", "РегистрацииВНО");
	
	ЕстьРеквизитОрганизацииНаименованиеСокращенное =
		Метаданные.Справочники.Организации.Реквизиты.Найти("НаименованиеСокращенное") <> Неопределено;

	Если ЕстьРеквизитОрганизацииНаименованиеСокращенное Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НаименованиеОрганизации", "Организации.НаименованиеСокращенное");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НаименованиеОрганизации", "Организации.Наименование");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПроверяемыеМЧДФНСНеЗаданы",    ПроверяемыеМЧДФНС = Неопределено);
	Запрос.УстановитьПараметр("ВыбранныеОрганизацииНеЗаданы", ВыбранныеОрганизации = Неопределено);
	
	Если ТипЗнч(ПроверяемыеМЧДФНС) = Тип("Массив") ИЛИ ТипЗнч(ПроверяемыеМЧДФНС) = Тип("ФиксированныйМассив") Тогда
		МассивПроверяемыхМЧДФНС = ПроверяемыеМЧДФНС;
	Иначе
		ПроверяемаяМЧДФНС = ПроверяемыеМЧДФНС;
		МассивПроверяемыхМЧДФНС = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПроверяемаяМЧДФНС);
	КонецЕсли;
	Запрос.УстановитьПараметр("ПроверяемыеМЧДФНС", МассивПроверяемыхМЧДФНС);

	Если ТипЗнч(ВыбранныеОрганизации) = Тип("Массив") ИЛИ ТипЗнч(ВыбранныеОрганизации) = Тип("ФиксированныйМассив") Тогда
		МассивВыбранныхОрганизаций = ВыбранныеОрганизации;
	Иначе
		ВыбраннаяОрганизация = ВыбранныеОрганизации;
		МассивВыбранныхОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбраннаяОрганизация);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВыбранныеОрганизации", МассивВыбранныхОрганизаций);
	
КонецПроцедуры

Функция ЗапросМЧДВРегистрациях()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка,
		|	РегистрацииВНалоговомОргане.Код КАК Код,
		|	РегистрацииВНалоговомОргане.КПП КАК КПП,
		|	РегистрацииВНалоговомОргане.Владелец КАК Владелец,
		|	РегистрацииВНалоговомОргане.Доверенность КАК Доверенность
		|ПОМЕСТИТЬ РегистрацииВНО
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|ГДЕ
		|	НЕ РегистрацииВНалоговомОргане.ПометкаУдаления
		|	И (РегистрацииВНалоговомОргане.Доверенность ССЫЛКА Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр
		|			ИЛИ РегистрацииВНалоговомОргане.Доверенность ССЫЛКА Справочник.МашиночитаемыеДоверенностиФНС)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацииВНалоговомОрганеПодписанты.Ссылка,
		|	РегистрацииВНалоговомОрганеПодписанты.Ссылка.Код,
		|	РегистрацииВНалоговомОрганеПодписанты.Ссылка.КПП,
		|	РегистрацииВНалоговомОрганеПодписанты.Ссылка.Владелец,
		|	РегистрацииВНалоговомОрганеПодписанты.Доверенность
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане.Подписанты КАК РегистрацииВНалоговомОрганеПодписанты
		|ГДЕ
		|	НЕ РегистрацииВНалоговомОрганеПодписанты.Ссылка.ПометкаУдаления
		|	И (РегистрацииВНалоговомОрганеПодписанты.Доверенность ССЫЛКА Справочник.МашиночитаемыеДоверенностиРаспределенныйРеестр
		|			ИЛИ РегистрацииВНалоговомОрганеПодписанты.Доверенность ССЫЛКА Справочник.МашиночитаемыеДоверенностиФНС)";
	
	ТекстЗапроса = ТекстЗапроса + "; //////////////////////////////////////////////////////////////////////////////// " + Символы.ПС;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОрганизацияМЧДЕдиногоФормата(Ссылка) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	МашиночитаемыеДоверенностиДоверители.Доверитель КАК Доверитель
		|ИЗ
		|	&МашиночитаемыеДоверенностиДоверители КАК МашиночитаемыеДоверенностиДоверители
		|ГДЕ
		|	МашиночитаемыеДоверенностиДоверители.Ссылка = &Ссылка");
	ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&МашиночитаемыеДоверенностиДоверители",
		"Справочник." + ИмяТипаСправочникаМашиночитаемыеДоверенности + ".Доверители");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипЗнч(Выборка.Доверитель) = Тип("СправочникСсылка.Организации") Тогда
			Результат = Выборка.Доверитель;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставителиМЧДЕдиногоФормата(Ссылка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	МашиночитаемыеДоверенностиПредставители.Представитель КАК Представитель
		|ИЗ
		|	&МашиночитаемыеДоверенностиПредставители КАК МашиночитаемыеДоверенностиПредставители
		|ГДЕ
		|	МашиночитаемыеДоверенностиПредставители.Ссылка = &Ссылка");
	ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&МашиночитаемыеДоверенностиПредставители",
		"Справочник." + ИмяТипаСправочникаМашиночитаемыеДоверенности + ".Представители");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Представитель");
	
КонецФункции

Функция СвойстваМЧДЕдиногоФормата(Ссылка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МашиночитаемыеДоверенностиСтатусы.ТехническийСтатус КАК ТехническийСтатус,
		|	МашиночитаемыеДоверенностиСтатусы.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса
		|ИЗ
		|	&МашиночитаемыеДоверенностиСтатусы КАК МашиночитаемыеДоверенностиСтатусы
		|ГДЕ
		|	МашиночитаемыеДоверенностиСтатусы.МашиночитаемаяДоверенность = &Ссылка");
	ИмяТипаРегистраСведенийМашиночитаемыеДоверенностиСтатусы = "МашиночитаемыеДоверенностиСтатусы";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&МашиночитаемыеДоверенностиСтатусы",
		"РегистрСведений." + ИмяТипаРегистраСведенийМашиночитаемыеДоверенностиСтатусы);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяТипаПеречисленияТехническиеСтатусыМЧД = "ТехническиеСтатусыМЧД";
	ТехническийСтатус = Перечисления[ИмяТипаПеречисленияТехническиеСтатусыМЧД].ПустаяСсылка();
	ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
	Если Выборка.Следующий() Тогда
		ТехническийСтатус = Выборка.ТехническийСтатус;
		ДатаЗапросаСтатуса = Выборка.ДатаЗапросаСтатуса;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Ссылка", 				Ссылка);
	Результат.Вставить("ТехническийСтатус", 	ТехническийСтатус);
	Результат.Вставить("ДатаЗапросаСтатуса", 	ДатаЗапросаСтатуса);
	Возврат Результат;
	
КонецФункции

Функция ВыгрузитьМЧДЕдиногоФормата(Ссылка, ПараметрыВозврата = Неопределено) Экспорт
	
	ДоверенностьСсылка = Ссылка;
	
	ПараметрыВозвратаВызова = Новый Структура;
	ПараметрыВозвратаВызова.Вставить("СформироватьФайлДоверенностиПриОтсутствии", 	Истина);
	ПараметрыВозвратаВызова.Вставить("ИгнорироватьОтсутствиеФайлаДоверенности", 	Ложь);
	Если ПараметрыВозврата <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыВозвратаВызова, ПараметрыВозврата);
	КонецЕсли;
	
	Результат = Новый Массив;
	Результат.Добавить(Неопределено);
	
	Доверенности = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДоверенностьСсылка);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИСТИНА
		|ИЗ
		|	&МашиночитаемыеДоверенности КАК МашиночитаемыеДоверенности
		|ГДЕ
		|	МашиночитаемыеДоверенности.Ссылка В(&Доверенности)
		|	И МашиночитаемыеДоверенности.ДляНалоговыхОрганов");
	ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&МашиночитаемыеДоверенности",
		"Справочник." + ИмяТипаСправочникаМашиночитаемыеДоверенности);
	Запрос.УстановитьПараметр("Доверенности", Доверенности);
	РезультатЗапроса = Запрос.Выполнить();
	ДляНалоговыхОрганов = НЕ РезультатЗапроса.Пустой();
	
	МодульМашиночитаемыеДоверенностиФНС = ОбщегоНазначения.ОбщийМодуль("МашиночитаемыеДоверенностиФНС");
	ФайлыДоверенности = МодульМашиночитаемыеДоверенностиФНС.ФайлыДоверенностей(Доверенности, ДляНалоговыхОрганов)[
		ДоверенностьСсылка];
	
	ЕстьОшибкаВыгрузки = Ложь;
	ОписанияОшибокПодписи = Новый Массив;
	Если ФайлыДоверенности <> Неопределено Тогда
		Для каждого ФайлДоверенности Из ФайлыДоверенности Цикл
			Если ФайлДоверенности.ТипФайла = "Доверенность" Тогда
				Если ЗначениеЗаполнено(ФайлДоверенности.ОписаниеОшибки) Тогда
					ЕстьОшибкаВыгрузки = Истина;
					ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
						НСтр("ru = 'Не удалось выгрузить машиночитаемую доверенность (единый формат)'")
						+ ". " + ФайлДоверенности.ОписаниеОшибки);
					
				ИначеЕсли Результат[0] = Неопределено Тогда
					АдресФайла = ПоместитьВоВременноеХранилище(ФайлДоверенности.ДвоичныеДанные, Новый УникальныйИдентификатор);
					Результат[0] = Новый Структура("Имя, Адрес", ФайлДоверенности.ИмяФайла, АдресФайла);
				КонецЕсли;
				
			Иначе
				Если ЗначениеЗаполнено(ФайлДоверенности.ОписаниеОшибки) Тогда
					ОписанияОшибокПодписи.Добавить(ФайлДоверенности.ОписаниеОшибки);
					
				ИначеЕсли ФайлДоверенности.ТипФайла = "Подпись" Тогда
					АдресФайла = ПоместитьВоВременноеХранилище(ФайлДоверенности.ДвоичныеДанные, Новый УникальныйИдентификатор);
					Результат.Добавить(Новый Структура("Имя, Адрес", ФайлДоверенности.ИмяФайла, АдресФайла));
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьОшибкаВыгрузки ИЛИ ОписанияОшибокПодписи.Количество() <> 0 Тогда
		Для каждого ОписаниеОшибкиПодписи Из ОписанияОшибокПодписи Цикл
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
				НСтр("ru = 'Не удалось выгрузить подпись машиночитаемой доверенности (единый формат)'")
				+ ". " + ОписаниеОшибкиПодписи);
		КонецЦикла;
		Возврат Неопределено;
	КонецЕсли;
	
	Если Результат[0] = Неопределено И Результат.Количество() = 1
		И ПараметрыВозвратаВызова.СформироватьФайлДоверенностиПриОтсутствии Тогда
		
		ОбъектДоверенности = ДоверенностьСсылка.ПолучитьОбъект();
		Если ЭлектроннаяПодпись.УстановленныеПодписи(ОбъектДоверенности.ФайлДоверенности).Количество() = 0 Тогда
			Если ОбъектДоверенности.РегистрироватьВРеестре И НЕ ЗначениеЗаполнено(ОбъектДоверенности.НомерДоверенности) Тогда
				МодульМашиночитаемыеДоверенностиФНССлужебный = ОбщегоНазначения.ОбщийМодуль("МашиночитаемыеДоверенностиФНССлужебный");
				РезультатПолучения = МодульМашиночитаемыеДоверенностиФНССлужебный.ПолучитьНомерМЧДРР();
				Если ЗначениеЗаполнено(РезультатПолучения.НомерДоверенности) Тогда
					ОбъектДоверенности.НомерДоверенности = РезультатПолучения.НомерДоверенности;
				Иначе
					ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(НСтр("ru = 'Не удалось получить номер доверенности'"));
					Возврат Неопределено;
				КонецЕсли;
				
			ИначеЕсли НЕ ЗначениеЗаполнено(ОбъектДоверенности.НомерДоверенности) Тогда
				ОбъектДоверенности.НомерДоверенности = Строка(Новый УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ОбъектДоверенности.ПроверитьЗаполнение() Тогда
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(НСтр("ru = 'Найдены ошибки заполнения машиночитаемой доверенности (единый формат)'"));
			Возврат Неопределено;
		КонецЕсли;
		
		ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
		ОписаниеФайлаДоверенности = Справочники[ИмяТипаСправочникаМашиночитаемыеДоверенности].СформироватьФайлДоверенности(ОбъектДоверенности);
		
		ПотокВПамяти = Новый ПотокВПамяти;
		ЗаписьТекста = Новый ЗаписьТекста(ПотокВПамяти);
		ЗаписьТекста.Записать(ОписаниеФайлаДоверенности.Текст);
		ЗаписьТекста.Закрыть();
		ДвоичныеДанные = ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
		АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор);
		
		Если ЗначениеЗаполнено(ОбъектДоверенности.ФайлДоверенности) Тогда
			СвойстваФайла = Новый Структура;
			СвойстваФайла.Вставить("АдресФайлаВоВременномХранилище", АдресФайлаВоВременномХранилище);
			СвойстваФайла.Вставить("АдресВременногоХранилищаТекста", "");
			
			РаботаСФайлами.ОбновитьФайл(ОбъектДоверенности.ФайлДоверенности, СвойстваФайла);
			
		Иначе
			ПараметрыДобавления = РаботаСФайлами.ПараметрыДобавленияФайла();
			ПараметрыДобавления.ВладелецФайлов 		= ДоверенностьСсылка;
			ПараметрыДобавления.ИмяБезРасширения 	= ОписаниеФайлаДоверенности.ИмяБезРасширения;
			ПараметрыДобавления.РасширениеБезТочки 	= ОписаниеФайлаДоверенности.РасширениеБезТочки;
			ПараметрыДобавления.Служебный 			= Истина;
			ОбъектДоверенности.ФайлДоверенности = РаботаСФайлами.ДобавитьФайл(ПараметрыДобавления,
				АдресФайлаВоВременномХранилище);
		КонецЕсли;
		
		Если ОбъектДоверенности.Модифицированность() Тогда
			ОбъектДоверенности.Записать();
		КонецЕсли;
		
		ИмяФайла = ОписаниеФайлаДоверенности.ИмяБезРасширения + ?(ЗначениеЗаполнено(
			ОписаниеФайлаДоверенности.РасширениеБезТочки), "." + ОписаниеФайлаДоверенности.РасширениеБезТочки, "");
		Результат[0] = Новый Структура("Имя, Адрес", ИмяФайла, АдресФайлаВоВременномХранилище);
	КонецЕсли;
	
	Если Результат[0] = Неопределено Тогда
		Если ПараметрыВозвратаВызова.ИгнорироватьОтсутствиеФайлаДоверенности И Результат.Количество() = 1 Тогда
			Результат.Удалить(0);
			
		Иначе
			ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
				НСтр("ru = 'Не удалось выгрузить машиночитаемую доверенность (единый формат)'"));
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ОрганизацияПоОбъектуОтправки(ОбъектСсылка) Экспорт
	
	ИмяТипаСправочникаМашиночитаемыеДоверенности = "МашиночитаемыеДоверенности";
	Если ДокументооборотСКОКлиентСервер.ПодсистемаЦПРРМЧДСуществует()
		И ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка." + ИмяТипаСправочникаМашиночитаемыеДоверенности) Тогда
		
		Возврат ОрганизацияМЧДЕдиногоФормата(ОбъектСсылка);
		
	Иначе
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "Организация");
	КонецЕсли;
	
КонецФункции

Функция ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт
	
	МодульИнтернетПоддержкаПользователей =
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.МодульИнтернетПоддержкаПользователей();
	
	Возврат МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
КонецФункции

Функция ДатаИзСтрокиРазныхФорматов(СтрокаДаты) Экспорт
	
	Если ТипЗнч(СтрокаДаты) = Тип("Дата") Тогда
		Возврат СтрокаДаты;
	КонецЕсли;
	
	Результат = СокрЛП(СтрокаДаты);
	
	Попытка
		Возврат Дата(Результат);
	Исключение
	КонецПопытки;
	
	ПозицияЧасовогоПояса = СтрНайти(Результат, "+");
	Если ПозицияЧасовогоПояса <> 0 Тогда
		Результат = Лев(Результат, ПозицияЧасовогоПояса - 1);
		
		Попытка
			Возврат Дата(Результат);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Префикс = "\/Date(";
	Постфикс = ")\/";
	НачалоПрефикса = СтрНайти(Результат, Префикс);
	Если НачалоПрефикса <> 0 Тогда
		ДлинаПрефикса = СтрДлина(Префикс);
		ЧисловаяДата = Сред(Результат, НачалоПрефикса + ДлинаПрефикса);
		
		КонецПрефикса = СтрНайти(Результат, Постфикс);
		Если КонецПрефикса > 0 Тогда
			ЧисловаяДата = Лев(ЧисловаяДата, КонецПрефикса - 1);
		КонецЕсли;
		
		Попытка
			Возврат '00010101000000' + Число(ЧисловаяДата) / 1000 + ('19700101000000' - '00010101000000');
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Результат = СтрЗаменить(Результат, "T", " "); // поддержка формата "2015-03-13T18:34:13", "2015-03-13T18:34:13Z"
	Результат = СтрЗаменить(Результат, "Z", "");
	
	Если СтрДлина(Результат) = 10 Тогда
		Результат = Результат + " 00:00:00";
		
	ИначеЕсли СтрДлина(Результат) = 16 Тогда
		Результат = Результат + ":00"
	КонецЕсли;
	
	Если (СтрДлина(Результат) = 19 ИЛИ СтрДлина(Результат) = 23) И Сред(Результат, 11, 1) = " " И
		(Сред(Результат, 14, 1) = ":" ИЛИ Сред(Результат, 14, 1) = ".") И (Сред(Результат, 17, 1) = ":" ИЛИ Сред(Результат, 17, 1) = ".") Тогда
		
		Если (Сред(Результат, 3, 1) = "." ИЛИ Сред(Результат, 3, 1) = "-") И (Сред(Результат, 6, 1) = "." ИЛИ Сред(Результат, 6, 1) = "-") Тогда
			Результат = Сред(Результат, 7, 4) + Сред(Результат, 4, 2) + Сред(Результат, 1, 2) + Сред(Результат, 12, 2) + Сред(Результат, 15, 2) + Сред(Результат, 18, 2);
			
		ИначеЕсли (Сред(Результат, 5, 1) = "." ИЛИ Сред(Результат, 5, 1) = "-") И (Сред(Результат, 8, 1) = "." ИЛИ Сред(Результат, 8, 1) = "-") Тогда
			Результат = Сред(Результат, 1, 4) + Сред(Результат, 6, 2) + Сред(Результат, 9, 2) + Сред(Результат, 12, 2) + Сред(Результат, 15, 2) + Сред(Результат, 18, 2);
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		Возврат Дата(Результат);
	Исключение
	КонецПопытки;
	
	Возврат СтрокаДаты;
	
КонецФункции

Функция ЧислоВШестнадцатеричномПредставлении(Знач Число, Знач КоличествоБайт = 32) Экспорт
	
	Результат = "";
	СтрокаСимволов = "0123456789ABCDEF";
	Для НомерБайта = 1 По КоличествоБайт Цикл
		ПредставлениеБайта = "";
		Для Счетчик = 1 По 2 Цикл
			ПредставлениеБайта = Сред(СтрокаСимволов, Число % 16 + 1, 1) + ПредставлениеБайта;
			Число = Цел(Число / 16);
		КонецЦикла;
		// порядок байтов: начиная с младшего
		Результат = Результат + ПредставлениеБайта;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция АдресаСерверовМетокВремени(СтрокаИлиМассивПоУмолчанию = "http://dss.1stdss.1c.ru/TSP/tsp.srf") Экспорт
	
	АдресСервисаМеткиВремени = Константы.ДокументооборотСКонтролирующимиОрганами_АдресСервисаШтампаВремени.Получить();
	
	Если ЗначениеЗаполнено(АдресСервисаМеткиВремени) Тогда
		Результат = СтрРазделить(АдресСервисаМеткиВремени, Символы.ПС, Ложь);
		
		КоличествоЭлементов = Результат.Количество();
		Для ИндексЭлемента = 0 По КоличествоЭлементов - 1 Цикл
			Если НЕ ЗначениеЗаполнено(Результат[КоличествоЭлементов - ИндексЭлемента - 1]) Тогда
				Результат.Удалить(КоличествоЭлементов - ИндексЭлемента - 1);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(СтрокаИлиМассивПоУмолчанию) = Тип("Строка") Тогда
		Результат = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаИлиМассивПоУмолчанию);
		
	Иначе
		Результат = СтрокаИлиМассивПоУмолчанию;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьШтампыЭЦПКСодержимомуТабличногоДокумента(ТабличныйДокумент, ТабДокШтампыЭЦП) Экспорт
	
	Если ТабличныйДокумент.Области.Найти("СтрокаШтампов") <> Неопределено
	 ИЛИ ТабДокШтампыЭЦП.Области.Найти("СтрокаШтампов") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоследняяСтрокаСтраницы = ТабличныйДокумент.ПолучитьОбласть(
	ТабличныйДокумент.ВысотаТаблицы, , ТабличныйДокумент.ВысотаТаблицы);
	ТабличныйДокумент.УдалитьОбласть(ТабличныйДокумент.Область(
	ТабличныйДокумент.ВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы), ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).СоздатьФорматСтрок();
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).КонецСтраницы = Ложь;
	ПоследняяСтрокаСтраницы.Область(1, ,ПоследняяСтрокаСтраницы.ВысотаТаблицы).РастягиватьПоГоризонтали = Ложь;
	
	Отступ = ПоследняяСтрокаСтраницы.ПолучитьОбласть(1, 1, ПоследняяСтрокаСтраницы.ВысотаТаблицы, 1);
	Отступ.Область().Очистить( , , Истина);
	
	ТабличныйДокумент.Вывести(ПоследняяСтрокаСтраницы.ПолучитьОбласть(1, 1,
	ПоследняяСтрокаСтраницы.ВысотаТаблицы, ПоследняяСтрокаСтраницы.ШиринаТаблицы));
	
	КоличествоСтраниц = 0;
	ПринтерДоступен = Истина;
	Попытка
		КоличествоСтраниц = ТабличныйДокумент.КоличествоСтраниц();
	Исключение
		ПринтерДоступен = Ложь;
	КонецПопытки;
	
	Разделитель = Новый ТабличныйДокумент;
	
	СерыйЦвет = Новый Цвет(192, 192, 192);
	Граница  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	
	СекцияОгр = Разделитель.ПолучитьОбласть(1, 1, 2, 1);
	СекцияОгр.Область().ВысотаСтроки = 7.5;
	СекцияОгр.Область().ЦветРамки = СерыйЦвет;
	СекцияОгр.Область().РастягиватьПоГоризонтали = Ложь;
	СекцияОгр.Область().ШиринаКолонки = Отступ.Область(1, 1).ШиринаКолонки;
	
	СекцияКол = СекцияОгр.ПолучитьОбласть();
	СекцияКол.Область(1, 1).ГраницаСнизу = Граница;
	
	Разделитель.Вывести(СекцияОгр);
	Для НомКол = 2 По ПоследняяСтрокаСтраницы.ШиринаТаблицы Цикл
		Секция = ?(НомКол = ПоследняяСтрокаСтраницы.ШиринаТаблицы, СекцияОгр, СекцияКол);
		Секция.Область().ШиринаКолонки = ПоследняяСтрокаСтраницы.Область(1, НомКол).ШиринаКолонки;
		Разделитель.Присоединить(Секция);
	КонецЦикла;
	Разделитель.Область(1, ,Разделитель.ВысотаТаблицы).СоздатьФорматСтрок();
	
	СтрокаШтампов = ТабДокШтампыЭЦП.ПолучитьОбласть("СтрокаШтампов");
	СтрокаШтампов.Область(1, 1, СтрокаШтампов.ВысотаТаблицы, 1).ШиринаКолонки = Отступ.Область(1, 1).ШиринаКолонки;
	
	НачальнаяВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	
	Если ПринтерДоступен Тогда
		ПроверяемыеТабДок = Новый Массив();
		ПроверяемыеТабДок.Добавить(Разделитель);
		ПроверяемыеТабДок.Добавить(СтрокаШтампов);
		ПроверяемыеТабДок.Добавить(Разделитель); // не выводится
		
		ИндДопЯчейки = 0;
		Пока НЕ ТабличныйДокумент.ПроверитьВывод(ПроверяемыеТабДок) Цикл
			ТабличныйДокумент.Присоединить(Отступ);
			ИндДопЯчейки = ИндДопЯчейки + 1;
			Если ИндДопЯчейки > 300 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(Разделитель);
	ТабличныйДокумент.Вывести(СтрокаШтампов);
	
	ТабличныйДокумент.Область(НачальнаяВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы).ВместеСоСледующим = Истина;
	ТабличныйДокумент.Область(НачальнаяВысотаТаблицы, ,ТабличныйДокумент.ВысотаТаблицы).НачалоСтраницы = Ложь;
	
	Возврат Истина;
	
КонецФункции

Функция НастройкиОбменаЕГАИС(Организация, НастройкиОбмена = Неопределено) Экспорт
	
	Если НастройкиОбмена <> Неопределено Тогда
		НастройкиОбмена.Очистить();
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	ТаблицаНастроекОбменаЕГАИС = Неопределено;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ПриПолученииНастроекОбменаЕГАИС(
		Организация,
		ТаблицаНастроекОбменаЕГАИС,
		СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		ОбъектМетаданныхНастройкиОбменаЕГАИС = Метаданные.РегистрыСведений.Найти("НастройкиОбменаЕГАИС");
		
		Результат = ОбъектМетаданныхНастройкиОбменаЕГАИС <> Неопределено
			И ОбъектМетаданныхНастройкиОбменаЕГАИС.Измерения.Найти("ИдентификаторФСРАР") <> Неопределено
			И ОбъектМетаданныхНастройкиОбменаЕГАИС.Ресурсы.Найти("АдресУТМ") <> Неопределено
			И ОбъектМетаданныхНастройкиОбменаЕГАИС.Ресурсы.Найти("ПортУТМ") <> Неопределено
			И ОбъектМетаданныхНастройкиОбменаЕГАИС.Ресурсы.Найти("Таймаут") <> Неопределено
			И ОбъектМетаданныхНастройкиОбменаЕГАИС.Ресурсы.Найти("ОбменНаСервере") <> Неопределено;
		
		Если Результат Тогда
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР КАК ИдентификаторФСРАР,
				|	НастройкиОбменаЕГАИС.АдресУТМ КАК АдресУТМ,
				|	НастройкиОбменаЕГАИС.ПортУТМ КАК ПортУТМ,
				|	НастройкиОбменаЕГАИС.Таймаут КАК Таймаут,
				|	НастройкиОбменаЕГАИС.ОбменНаСервере КАК ОбменНаСервере
				|ИЗ
				|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
				|УПОРЯДОЧИТЬ ПО
				|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР");
			
			ТаблицаНастроекОбменаЕГАИС = Запрос.Выполнить().Выгрузить();
		КонецЕсли;
		
	Иначе
		Результат = (ТаблицаНастроекОбменаЕГАИС <> Неопределено);
	КонецЕсли;
	
	Если Результат Тогда
		Для каждого СтрокаНастроекОбмена Из ТаблицаНастроекОбменаЕГАИС Цикл
			ЭлементНастроекОбмена = Новый Структура();
			ЭлементНастроекОбмена.Вставить("АдресУТМ", СтрокаНастроекОбмена.АдресУТМ);
			ЭлементНастроекОбмена.Вставить("ПортУТМ", СтрокаНастроекОбмена.ПортУТМ);
			ЭлементНастроекОбмена.Вставить("Таймаут", СтрокаНастроекОбмена.Таймаут);
			ЭлементНастроекОбмена.Вставить("ОбменНаСервере", СтрокаНастроекОбмена.ОбменНаСервере);
			
			ПредставлениеЭлементаНастроекОбмена = СтрШаблон(
				НСтр("ru = '%1:%2 (таймаут %3 с., %4, код %5)'"),
				СтрокаНастроекОбмена.АдресУТМ,
				Формат(СтрокаНастроекОбмена.ПортУТМ, "ЧДЦ=; ЧН=; ЧГ="),
				Формат(СтрокаНастроекОбмена.Таймаут, "ЧДЦ=; ЧН=; ЧГ="),
				?(СтрокаНастроекОбмена.ОбменНаСервере, НСтр("ru = 'обмен на сервере 1С:Предприятия'"),
					НСтр("ru = 'обмен на локальном компьютере'")),
				СтрокаНастроекОбмена.ИдентификаторФСРАР);
			
			Если НастройкиОбмена <> Неопределено Тогда
				НастройкиОбмена.Добавить(ЭлементНастроекОбмена, ПредставлениеЭлементаНастроекОбмена);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область СлужебныйПрограммныйИнтерфейс

Функция ПредставлениеСверкиСФНС(ЗапросИОН, ТекстXML) Экспорт 
	ВизуализацияВходящихИзвещенийФНС = ОбщегоНазначения.ОбщийМодуль("Отчеты.ВизуализацияВходящихИзвещенийФНС");
	Возврат ВизуализацияВходящихИзвещенийФНС.ПредставлениеСверкиСФНС(ЗапросИОН, ТекстXML);
КонецФункции

#КонецОбласти

Функция КраткоеНаименованиеОрганизации(Организация) Экспорт
	
	Если Метаданные.Справочники.Организации.Реквизиты.Найти("НаименованиеСокращенное") = Неопределено Тогда
		Результат = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
			Организация,, "НаимЮЛСокр").НаимЮЛСокр;
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Наименование");
		КонецЕсли;
		
	Иначе
		СвойстваОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Организация, "Наименование, НаименованиеСокращенное");
		Результат = ?(ЗначениеЗаполнено(СвойстваОрганизации.НаименованиеСокращенное),
			СвойстваОрганизации.НаименованиеСокращенное, СвойстваОрганизации.Наименование);
	КонецЕсли;
	
	Результат = СокрЛП(Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьТекстОшибкиСертификатаКА(ТекстИсключения) Экспорт
	
	ДоверенныйКорневой = "http://x1.i.lencr.org/";
	Промежуточный      = "https://letsencrypt.org/certs/lets-encrypt-r3.der";
	
	УжеДобавлено = 
		СтрНайти(Строка(ТекстИсключения), ДоверенныйКорневой)
		ИЛИ СтрНайти(Строка(ТекстИсключения), Промежуточный);
		
	ДобавитьПояснения = 
		СтрНайти(Строка(ТекстИсключения), "Удаленный узел не прошел проверку")
		И НЕ УжеДобавлено;
	
	Если ДобавитьПояснения Тогда
	
		БазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
		Если БазаФайловая Тогда
			Текст1 = НСтр("ru = 'Возможно, причина в том, что на компьютере не установлены сертификаты сервера АО ""Калуга Астрал"".'");
		Иначе
			Текст1 = НСтр("ru = 'Возможно, причина в том, что на сервере 1С не установлены сертификаты сервера АО ""Калуга Астрал"".'");
		КонецЕсли;
		
		Текст2 = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Сертификат '"),
			Новый ФорматированнаяСтрока(ДоверенныйКорневой,,,,ДоверенныйКорневой),
			НСтр("ru = ' должен быть установлен в хранилище ""Доверенные корневые центры сертификации"".'"));
		
		Текст3 = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Сертификат '"),
			Новый ФорматированнаяСтрока(Промежуточный,,,,Промежуточный),
			НСтр("ru = ' должен быть установлен в хранилище ""Промежуточные центры сертификации"".'"));
			
		ТекстИсключения = Новый ФорматированнаяСтрока(
			ТекстИсключения,
			Символы.ПС,
			Текст1,
			Символы.ПС,
			Текст2,
			Символы.ПС,
			Текст3);
		
		// Записываем в журнал
		ЗаголовокОшибки = 
			НСтр("ru = 'Электронный документооборот с контролирующими органами. Удаленный узел не прошел проверку'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗаписьЖурналаРегистрации(
			ЗаголовокОшибки,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстИсключения);
		
	КонецЕсли;
	
	Возврат ТекстИсключения;
	
КонецФункции

Функция ЕстьУведомление(ИмяВида) Экспорт
	
	Уведомления = УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПолучитьСоответствиеВидовУведомленийИменамОтчетов();
	
	ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения[ИмяВида];
	
	Уведомление = Уведомления[ВидУведомления];
	
	Есть = Метаданные.Отчеты.Найти(Уведомление) <> Неопределено;
	
	Возврат Есть;
	
КонецФункции

Функция КНДФайлаПоАдресу(Адрес, Кодировка = Неопределено) Экспорт
	
	Возврат ЗначениеXMLФайлаПоАдресу(Адрес, "КНД", Кодировка);

КонецФункции

Функция ЗначениеXMLФайлаПоАдресу(Адрес, ИмяУзла, Кодировка = Неопределено) Экспорт
	
	ДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла();
	
	ДвДанные.Записать(ИмяФайла);
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ЧтениеТекста = КонтекстЭДОСервер.НовыйЧтениеТекстаНаСервере(ИмяФайла, Кодировка);
	Текст = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	Значение = ЗначениеXMLФайлаПоТексту(Текст, ИмяУзла);
	
	Возврат Значение;

КонецФункции

Функция ЗначениеXMLФайлаПоТексту(Текст, ИмяУзла) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ДеревоXML = КонтекстЭДОСервер.ЗагрузитьXMLВДеревоЗначений(, Текст); 
	
	Значение = КонтекстЭДОСервер.ИзвлечьДанное(ДеревоXML, ИмяУзла);
	
	Возврат Значение;

КонецФункции

#Область ЗаявленияВПФР

Функция ВидЗаявленияВПФР(ЗаявлениеПФР) Экспорт
	
	Возврат ЗаявлениеПФР.Вид;
	
КонецФункции

Функция ЗаявленияПоЭДООтправляютсяВПФР() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер.ЗаявленияПоЭДООтправляютсяВПФР(); 
	
КонецФункции

Функция ДопКомментарийЭтапаВЗаявленииПФР(Знач Ссылка, Знач Организация) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получение параметров прорисовки
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучатьДаты", Ложь);
	ДополнительныеПараметры.Вставить("ПолучатьОшибкиОтправки", Ложь);
	ТекущееСостояние = КонтекстЭДОСервер.ТекущееСостояниеОтправки(Ссылка, "ПФР", ДополнительныеПараметры);
	
	Если ТекущееСостояние = Неопределено ИЛИ ТекущееСостояние.ТекущийЭтапОтправки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СостояниеСдачиОтчетности = ТекущееСостояние.ТекущийЭтапОтправки.СостояниеСдачиОтчетности;
	ДокументооборотНеНачат   = Перечисления.СостояниеСдачиОтчетности.ДокументооборотНеНачат;
	Если НЕ СостояниеСдачиОтчетности = ДокументооборотНеНачат Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат Неопределено;
	Иначе
		Возврат КонтекстЭДОСервер.ПредложениеПодключить1СОтчетностьИзПанели();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция ЗначенияРеквизитовОбъекта(Ссылка, Знач Реквизиты) Экспорт
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);
КонецФункции

Функция ПолучитьРеквизит(ОбъектДляЧтения, ИмяРеквизита) Экспорт
	
	МетаданныеОбъекта = ОбъектДляЧтения.Метаданные();
	Если МетаданныеОбъекта.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда 
		Возврат ОбъектДляЧтения[ИмяРеквизита];
	ИначеЕсли ИмяРеквизита = "Наименование" Тогда
		Возврат ОбъектДляЧтения[ИмяРеквизита];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЭтоПакетСДопДокументами(ОписьИсхДок) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер.ЭтоПакетСДопДокументами(ОписьИсхДок);
	
КонецФункции

Функция ЗагрузитьНастройку(Ключ) Экспорт
	Возврат ХранилищеОбщихНастроек.Загрузить(Ключ);
КонецФункции

Функция СохранитьНастройку(Ключ, Значение) Экспорт
	ХранилищеОбщихНастроек.Сохранить(Ключ, , Значение);
КонецФункции

Функция ЭтоТарифКадровыеРешения(Организация) Экспорт
	
	Результат = ЭтоНужныйТариф(Организация, Перечисления.ТарифыОператораЭДО.КадровыеРешения);
	Возврат Результат;
	
КонецФункции

Функция ЭтоТарифПромоЕНС(Организация) Экспорт
	
	Результат = ЭтоНужныйТариф(Организация, Перечисления.ТарифыОператораЭДО.ПромоЕНС);
	Возврат Результат;
	
КонецФункции

Функция СобытиеПромоЕНСНаступило() Экспорт
	
	Поддерживает = Ложь;
	ДокументооборотСКОПереопределяемый.КонфигурацияПоддерживаетТарифПромоЕНС(Поддерживает);
	
	Возврат Поддерживает;
	
КонецФункции

Функция ЭтоНужныйТариф(Организация, Тариф) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	
	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Врег(УчетнаяЗапись.ЛицензияНаименование) = Врег(Строка(Тариф));
	
КонецФункции

Функция ТарифОператораЭДО(Организация) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	
	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат Перечисления.ТарифыОператораЭДО.ПустаяСсылка();
	КонецЕсли;
	
	ЛицензияНаименование = УчетнаяЗапись.ЛицензияНаименование;
	
	Тарифы = Метаданные.Перечисления.ТарифыОператораЭДО.ЗначенияПеречисления;
	Для каждого Тариф Из Тарифы Цикл
		Если Врег(Тариф.Синоним) = Врег(ЛицензияНаименование) Тогда
			Возврат Перечисления.ТарифыОператораЭДО[Тариф.Имя];
		КонецЕсли;
	КонецЦикла;
	
	Возврат Перечисления.ТарифыОператораЭДО.ПустаяСсылка();
	
КонецФункции

Функция ОтправкаОбъектаЗапрещенаНаТарифеПромоЕНС(СсылкаНаОбъект) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	СведенияПоОбъекту = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(СсылкаНаОбъект);
	
	Организация = СведенияПоОбъекту.Организация;
	
	Если НЕ ЭтоТарифПромоЕНС(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УчетнаяЗапись = КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
	
	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидыУведомлений = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения")
		И (СсылкаНаОбъект.ВидУведомления = ВидыУведомлений.ИсчисленныеСуммыНалогов
		ИЛИ СсылкаНаОбъект.ВидУведомления = ВидыУведомлений.ЗапросСуммЕНП
		ИЛИ СсылкаНаОбъект.ВидУведомления = ВидыУведомлений.ВозвратЕдиногоНалоговогоПлатежа
		ИЛИ СсылкаНаОбъект.ВидУведомления = ВидыУведомлений.СогласиеНаРаскрытиеНалоговойТайны
		ИЛИ СсылкаНаОбъект.ВидУведомления = ВидыУведомлений.ЗаявлениеОЗачетеНалога) Тогда
		
		Возврат Ложь;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
		
		Возврат Ложь;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
		
		Возврат Ложь;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
		
		Возврат Ложь;

	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#Область МашиночитаемыеДоверенности

Функция СвойстваСервераМЧДРР()
	
	Результат = НастройкиМЧД();
	
	Если ИспользуетсяРежимТестирования() Тогда
		СохраненныеНастройкиМЧД = ХранилищеОбщихНастроек.Загрузить("ДокументооборотСКонтролирующимиОрганами_НастройкиМЧД");
		Если СохраненныеНастройкиМЧД <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Результат, СохраненныеНастройкиМЧД);
		КонецЕсли;
		Если Результат.ИспользоватьРасширенияAPI Тогда
			Результат.ЛогинОператора 	= "";
			Результат.ПарольОператора 	= "";
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	URLСервера = Константы.ДокументооборотСКонтролирующимиОрганами_АдресСервисаМЧДРР.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Если НЕ ЗначениеЗаполнено(URLСервера) Тогда
		URLСервера = "https://1cpoagate.1c.ru/applications/MChD/api/clientpoa";
	КонецЕсли;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLСервера);
	ЛогинПароль = ?(ЗначениеЗаполнено(СтруктураURI.Логин) ИЛИ ЗначениеЗаполнено(СтруктураURI.Пароль),
		СтруктураURI.Логин + ":" + СтруктураURI.Пароль + "@", "");
	
	Результат.Вставить("АдресСервера", 					СтруктураURI.Схема + "://" + ЛогинПароль + СтруктураURI.ИмяСервера);
	Результат.Вставить("АдресСервераБезАутентификации", СтруктураURI.Схема + "://" + СтруктураURI.ИмяСервера);
	Результат.Вставить("РесурсКорняAPI", 				"/" + СтруктураURI.ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции

Функция НастройкиМЧД() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЛогинОператора", 			"");
	Результат.Вставить("ПарольОператора", 			"");
	Результат.Вставить("ИспользоватьРасширенияAPI", Истина);
	Результат.Вставить("ТестовыйСервер", 			"test");
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеОшибкиHTTP(КодСостояния) Экспорт
	
	Если КодСостояния < 300 Тогда
		Возврат "";
	ИначеЕсли КодСостояния = 300 Тогда
		Возврат НСтр("ru = 'Множественный выбор при отправке ответа сервера'");
	ИначеЕсли КодСостояния = 301 Тогда
		Возврат НСтр("ru = 'Ресурс перемещен'");
	ИначеЕсли КодСостояния = 302 Тогда
		Возврат НСтр("ru = 'Ресурс временно перемещен'");
	ИначеЕсли КодСостояния = 303 Тогда
		Возврат НСтр("ru = 'Ресурс перемещен на другой адрес'");
	ИначеЕсли КодСостояния = 304 Тогда
		Возврат НСтр("ru = 'Неожиданный ответ об отсутствии изменений страницы'");
	ИначеЕсли КодСостояния = 305 Тогда
		Возврат НСтр("ru = 'Для доступа к ресурсу требуется прокси'");
	ИначеЕсли КодСостояния = 306 Тогда
		Возврат НСтр("ru = 'Неиспользуемый код перенаправления запроса'");
	ИначеЕсли КодСостояния = 307 Тогда
		Возврат НСтр("ru = 'Временное перенаправление'");
	ИначеЕсли КодСостояния < 400 Тогда
		Возврат СтрШаблон(
			НСтр("ru = 'Ошибка по перенаправлению запроса с кодом %1'"),
			Формат(КодСостояния, "ЧДЦ=; ЧН=; ЧГ="));
	ИначеЕсли КодСостояния = 400 Тогда
		Возврат НСтр("ru = 'Неверный формат запроса'");
	ИначеЕсли КодСостояния = 401 Тогда
		Возврат НСтр("ru = 'Требуется аутентификация'");
	ИначеЕсли КодСостояния = 402 Тогда
		Возврат НСтр("ru = 'Требуется оплата'");
	ИначеЕсли КодСостояния = 403 Тогда
		Возврат НСтр("ru = 'Доступ к ресурсу запрещен'");
	ИначеЕсли КодСостояния = 404 Тогда
		Возврат НСтр("ru = 'Запрошенная страница не найдена'");
	ИначеЕсли КодСостояния = 405 Тогда
		Возврат НСтр("ru = 'Используемый метод запрещен'");
	ИначеЕсли КодСостояния = 406 Тогда
		Возврат НСтр("ru = 'Отсутствуют подходящие ответы'");
	ИначеЕсли КодСостояния = 407 Тогда
		Возврат НСтр("ru = 'Требуется аутентификация прокси'");
	ИначеЕсли КодСостояния = 408 Тогда
		Возврат НСтр("ru = 'Лимит времени сервера при ожидании запроса исчерпан'");
	ИначеЕсли КодСостояния = 409 Тогда
		Возврат НСтр("ru = 'Конфликт с текущим состоянием ресурса, требуется больше информации'");
	ИначеЕсли КодСостояния = 410 Тогда
		Возврат НСтр("ru = 'Ресурс более недоступен'");
	ИначеЕсли КодСостояния = 411 Тогда
		Возврат НСтр("ru = 'Требуется задание длины соержимого'");
	ИначеЕсли КодСостояния = 412 Тогда
		Возврат НСтр("ru = 'Ошибочные условия заголовочных полей'");
	ИначеЕсли КодСостояния = 413 Тогда
		Возврат НСтр("ru = 'Слишком большая длина запроса'");
	ИначеЕсли КодСостояния = 414 Тогда
		Возврат НСтр("ru = 'Запрошенный идентификатор слишком велик'");
	ИначеЕсли КодСостояния = 415 Тогда
		Возврат НСтр("ru = 'Неподдерживаемый тип данных запроса'");
	ИначеЕсли КодСостояния = 416 Тогда
		Возврат НСтр("ru = 'Запрошенный промежуток невыполним'");
	ИначеЕсли КодСостояния = 417 Тогда
		Возврат НСтр("ru = 'Несоответстие ожиданиям'");
	ИначеЕсли КодСостояния = 422 Тогда
		Возврат НСтр("ru = 'Необрабатываемый объект'");
	ИначеЕсли КодСостояния = 423 Тогда
		Возврат НСтр("ru = 'Заблокировано'");
	ИначеЕсли КодСостояния = 424 Тогда
		Возврат НСтр("ru = 'Сбой взаимосвязанного вызова'");
	ИначеЕсли КодСостояния = 449 Тогда
		Возврат НСтр("ru = 'Возврат запроса после необходимого действия'");
	ИначеЕсли КодСостояния < 500 Тогда
		Возврат СтрШаблон(
			НСтр("ru = 'Ошибка клиента с кодом %1'"),
			Формат(КодСостояния, "ЧДЦ=; ЧН=; ЧГ="));
	ИначеЕсли КодСостояния = 500 Тогда
		Возврат НСтр("ru = 'Внутренняя ошибка сервера'");
	ИначеЕсли КодСостояния = 501 Тогда
		Возврат НСтр("ru = 'Процесс для данного запроса не поддерживается сервером'");
	ИначеЕсли КодСостояния = 502 Тогда
		Возврат НСтр("ru = 'Gateway-сервер получил ошибочный ответ'");
	ИначеЕсли КодСостояния = 503 Тогда
		Возврат НСтр("ru = 'Сервер временно недоступен'");
	ИначеЕсли КодСостояния = 504 Тогда
		Возврат НСтр("ru = 'Превышено время ожидание ответа на запрос Gateway-сервера'");
	ИначеЕсли КодСостояния = 505 Тогда
		Возврат НСтр("ru = 'Версия HTTP не поддерживается сервером'");
	ИначеЕсли КодСостояния = 506 Тогда
		Возврат НСтр("ru = 'Вариантный тип содержит также вариант'");
	ИначеЕсли КодСостояния = 507 Тогда
		Возврат НСтр("ru = 'Переполнение хранилища'");
	ИначеЕсли КодСостояния = 510 Тогда
		Возврат НСтр("ru = 'Отсутствует поддержка расширений'");
	ИначеЕсли КодСостояния < 600 Тогда
		Возврат СтрШаблон(
			НСтр("ru = 'Ошибка сервера с кодом %1'"),
			Формат(КодСостояния, "ЧДЦ=; ЧН=; ЧГ="));
	ИначеЕсли КодСостояния = 999 Тогда
		Возврат НСтр("ru = 'Разрушительный сбой сервера'");
	Иначе
		Возврат СтрШаблон(
			НСтр("ru = 'Ошибка с кодом %1'"),
			Формат(КодСостояния, "ЧДЦ=; ЧН=; ЧГ="));
	КонецЕсли;

КонецФункции

Функция СтруктураШаблоновОшибокМЧДРР()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ТекстОшибкиПоУмолчанию", 			"");
	Результат.Вставить("ШаблонОшибкиИзИсключения", 			"");
	Результат.Вставить("ШаблонОшибкиДляКодаСостояния", 		"");
	Результат.Вставить("ШаблонОшибкиИзОтвета", 				"");
	Результат.Вставить("ШаблоныДляКодовОшибок", 			Новый Соответствие);
	Результат.Вставить("ШаблоныПрефиксовДляКодовОшибок", 	Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

Функция ВывестиИЗаписатьОшибкуМЧДРР(
		ОтветHTTP = Неопределено,
		СтруктураОтвета = Неопределено,
		ШаблоныОшибок = Неопределено,
		РежимВыводаИЗаписи = "",
		ПредставлениеОшибки = "")
	
	ШаблоныОшибокВызова = СтруктураШаблоновОшибокМЧДРР();
	Если ШаблоныОшибок <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ШаблоныОшибокВызова, ШаблоныОшибок);
	КонецЕсли;
	
	КодОшибки = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("type"),
		СтруктураОтвета.type, "");
	ЗаголовокОшибки = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("title"),
		СтруктураОтвета.title, "");
	ТекстОшибки = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("detail"),
		СтруктураОтвета.detail, "");
	Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("message"),
			СтруктураОтвета.message, "");
	КонецЕсли;
	
	ПредставлениеОшибкиДобавленоВТекст = Ложь;
	Если ЗначениеЗаполнено(КодОшибки) И ШаблоныОшибокВызова.ШаблоныДляКодовОшибок <> Неопределено
		И ЗначениеЗаполнено(ШаблоныОшибокВызова.ШаблоныДляКодовОшибок[КодОшибки]) Тогда
		
		ТекстОшибки = ШаблоныОшибокВызова.ШаблоныДляКодовОшибок[КодОшибки];
		
	ИначеЕсли ЗначениеЗаполнено(КодОшибки) И ШаблоныОшибокВызова.ШаблоныПрефиксовДляКодовОшибок <> Неопределено
		И ЗначениеЗаполнено(ШаблоныОшибокВызова.ШаблоныПрефиксовДляКодовОшибок[КодОшибки]) Тогда
		
		ШаблонПрефиксаДляКодаОшибки = ШаблоныОшибокВызова.ШаблоныПрефиксовДляКодовОшибок[КодОшибки];
		ТекстОшибки = ЗаголовокОшибки + ?(ЗначениеЗаполнено(ЗаголовокОшибки) И ЗначениеЗаполнено(ТекстОшибки), ": ", "")
			+ ТекстОшибки;
		ТекстОшибки = ШаблонПрефиксаДляКодаОшибки
			+ ?(ЗначениеЗаполнено(ШаблонПрефиксаДляКодаОшибки) И ЗначениеЗаполнено(ТекстОшибки),
			?(Прав(ШаблонПрефиксаДляКодаОшибки, 1) = ".", " ", ". "), "") + ТекстОшибки;
		
	ИначеЕсли ЗначениеЗаполнено(ЗаголовокОшибки) ИЛИ ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = СтрШаблон(
			ШаблоныОшибокВызова.ШаблонОшибкиИзОтвета,
			ЗаголовокОшибки + ?(ЗначениеЗаполнено(ЗаголовокОшибки) И ЗначениеЗаполнено(ТекстОшибки), ": ", "") + ТекстОшибки);
		
	Иначе
		ТекстОшибки = ?(ОтветHTTP = Неопределено, "", ОписаниеОшибкиHTTP(ОтветHTTP.КодСостояния));
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстОшибки = СтрШаблон(
				ШаблоныОшибокВызова.ШаблонОшибкиДляКодаСостояния,
				ТекстОшибки);
			
		ИначеЕсли ЗначениеЗаполнено(ШаблоныОшибокВызова.ШаблонОшибкиИзИсключения) Тогда
			ТекстОшибки = СтрШаблон(
				ШаблоныОшибокВызова.ШаблонОшибкиИзИсключения,
				ПредставлениеОшибки);
			ПредставлениеОшибкиДобавленоВТекст = Истина;
			
		Иначе
			ТекстОшибки = ШаблоныОшибокВызова.ТекстОшибкиПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимВыводаИЗаписи <> "ТолькоЗаписатьВЖурналРегистрации" Тогда
		ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(ТекстОшибки);
	КонецЕсли;
	
	Если РежимВыводаИЗаписи <> "ТолькоВывестиОшибку" Тогда
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Электронный документооборот с контролирующими органами. МЧД распределенного реестра'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки + ?(НЕ ПредставлениеОшибкиДобавленоВТекст И ЗначениеЗаполнено(ПредставлениеОшибки), "
				|" + ПредставлениеОшибки, ""));
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

Функция ПараметрыРезультатаАвторизацииНаСервереМЧДРРСКО() Экспорт
	
	ПараметрыРезультатаАвторизации = Новый Структура;
	ПараметрыРезультатаАвторизации.Вставить("ДатаСеанса", 			Неопределено);
	ПараметрыРезультатаАвторизации.Вставить("РезультатАвторизации", Неопределено);
	Возврат ПараметрыРезультатаАвторизации;
	
КонецФункции

// Используется для инициализации параметра сеанса в рамках механизмов БСП
// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
Процедура УстановитьПараметрСеансаРезультатАвторизацииНаСервереМЧДРРСКО(
		ИмяПараметра = Неопределено,
		УстановленныеПараметры = Неопределено) Экспорт
	
	Если ИмяПараметра = "РезультатАвторизацииНаСервереМЧДРРСКО" Тогда
		ПараметрыРезультатаАвторизации = ПараметрыРезультатаАвторизацииНаСервереМЧДРРСКО();
		ПараметрыСеанса.РезультатАвторизацииНаСервереМЧДРРСКО = Новый ФиксированнаяСтруктура(ПараметрыРезультатаАвторизации);
		УстановленныеПараметры.Добавить("РезультатАвторизацииАвторизацииНаСервереМЧДРРСКО");
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыРезультатовОбработкиМЧДСКО() Экспорт
	
	ПараметрыРезультатовОбработки = Новый Структура;
	ПараметрыРезультатовОбработки.Вставить("ИзменилсяСтатусМЧДФНС", Неопределено);
	Возврат ПараметрыРезультатовОбработки;
	
КонецФункции

// Используется для инициализации параметра сеанса в рамках механизмов БСП
// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
Процедура УстановитьПараметрСеансаРезультатыОбработкиМЧДСКО(
		ИмяПараметра = Неопределено,
		УстановленныеПараметры = Неопределено) Экспорт
	
	Если ИмяПараметра = "РезультатыОбработкиМЧДСКО" Тогда
		ПараметрыРезультатовОбработки = ПараметрыРезультатовОбработкиМЧДСКО();
		ПараметрыСеанса.РезультатыОбработкиМЧДСКО = Новый ФиксированнаяСтруктура(ПараметрыРезультатовОбработки);
		УстановленныеПараметры.Добавить("РезультатыОбработкиМЧДСКО");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция ТикетАутентификацииИлиДанныеПользователяНаПорталеПоддержки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МодульИнтернетПоддержкаПользователей =
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.МодульИнтернетПоддержкаПользователей();
	
	Если МодульИнтернетПоддержкаПользователей <> Неопределено
		И МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		
		ВладелецТикета = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ВладелецТикета();
		
		РезультатПолученияТикета =
			МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета);
		
		Если РезультатПолученияТикета = Неопределено ИЛИ НЕ ЗначениеЗаполнено(РезультатПолученияТикета.Тикет) Тогда
			Возврат МодульИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		
		Иначе
			Возврат РезультатПолученияТикета;
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура СброситьДанныеАутентификацииНаПорталеПоддержки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МодульИнтернетПоддержкаПользователей =
		ЭлектронныйДокументооборотСКонтролирующимиОрганами.МодульИнтернетПоддержкаПользователей();
	
	Если МодульИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки() Тогда
		МодульИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоОтправкаВСФРСервер(
		СсылкаНаОбъект = Неопределено,
		ИмяФайла = "",
		ДатаОтправки = Неопределено,
		КонтролирующийОрганИлиВидЦиклаОбмена = Неопределено,
		ТипЦиклаОбмена = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		ИмяФайлаВРег = ВРег(ИмяФайла);
		Если СтрНайти(ИмяФайлаВРег, "_ЕФС-1_") <> 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		УстановитьПривилегированныйРежим(Истина);
		ИсточникОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ИсточникОтчета");
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ИсточникОтчета = "РегламентированныйОтчет4ФСС" ИЛИ ИсточникОтчета = "РегламентированныйОтчетРСВ1"
			ИЛИ ИсточникОтчета = "РегламентированныйОтчетРСВ2" ИЛИ ИсточникОтчета = "РегламентированныйОтчетРВ3" Тогда
			
			Возврат Ложь;
			
		Иначе
			Возврат НЕ ЗначениеЗаполнено(ДатаОтправки)
				ИЛИ ДатаОтправки >= ДокументооборотСКОКлиентСервер.ДатаОбъединенияПФРиФССвСФР();
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
		УстановитьПривилегированныйРежим(Истина);
		ВидОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ВидОтчета");
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ВидОтчета = Справочники.ВидыОтправляемыхДокументов.ВедомостьУплатыАДВ11
			ИЛИ ВидОтчета = Справочники.ВидыОтправляемыхДокументов.СведенияОВзносахИСтраховомСтажеСПВ1
			ИЛИ ВидОтчета = Справочники.ВидыОтправляемыхДокументов.РСВ1
			ИЛИ ВидОтчета = Справочники.ВидыОтправляемыхДокументов.РСВ2
			ИЛИ ВидОтчета = Справочники.ВидыОтправляемыхДокументов.РВ3
			ИЛИ ВидОтчета = Справочники.ВидыОтправляемыхДокументов.Отчет4ФСС Тогда
			
			Возврат Ложь;
			
		Иначе
			Возврат НЕ ЗначениеЗаполнено(ДатаОтправки)
				ИЛИ ДатаОтправки >= ДокументооборотСКОКлиентСервер.ДатаОбъединенияПФРиФССвСФР();
		КонецЕсли;
		
	Иначе
		ИмяТипаДокументаВедомостьУплатыАДВ_11 = "ВедомостьУплатыАДВ_11";
		ИмяТипаДокументаПачкаДокументовСПВ_1 = "ПачкаДокументовСПВ_1";
		ИмяТипаСправочникаКомплектыОтчетностиПерсУчета = "КомплектыОтчетностиПерсУчета";
		
		Если Метаданные.Документы.Найти(ИмяТипаДокументаВедомостьУплатыАДВ_11) <> Неопределено
				И ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка." + ИмяТипаДокументаВедомостьУплатыАДВ_11)
			ИЛИ Метаданные.Документы.Найти(ИмяТипаДокументаПачкаДокументовСПВ_1) <> Неопределено
				И ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка." + ИмяТипаДокументаПачкаДокументовСПВ_1)
			ИЛИ Метаданные.Справочники.Найти(ИмяТипаСправочникаКомплектыОтчетностиПерсУчета) <> Неопределено
				И ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка." + ИмяТипаСправочникаКомплектыОтчетностиПерсУчета) Тогда
			
			Возврат Ложь;
		
		Иначе
			Возврат НЕ ЗначениеЗаполнено(ДатаОтправки)
				ИЛИ ДатаОтправки >= ДокументооборотСКОКлиентСервер.ДатаОбъединенияПФРиФССвСФР();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
