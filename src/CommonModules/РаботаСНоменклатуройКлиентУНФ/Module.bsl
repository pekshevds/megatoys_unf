#Область РаботаСНоменклатуройКлиентПереопределяемый

// Открытие формы выбора номенклатуры.
//
// Параметры:
//  ПараметрыФормы              - Структура                  - параметры формы.
//  Владелец                    - ФормаКлиентскогоПриложения - владелец формы.
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения         - оповещение о закрытии формы.
//  РежимОткрытияОкнаФормы      - РежимОткрытияОкнаФормы     - варианты открытия формы клиентского приложения.
//
Процедура ОткрытьФормуВыбораНоменклатуры(ПараметрыФормы, 
	Владелец = Неопределено,
	ОписаниеОповещенияОЗакрытии = Неопределено, 
	РежимОткрытияОкнаФормы = Неопределено) Экспорт
	
	КлючУникальности = Новый УникальныйИдентификатор;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Владелец, "УникальныйИдентификатор") Тогда
		КлючУникальности = Владелец.УникальныйИдентификатор;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаСписка", 
		ПараметрыФормы, Владелец, КлючУникальности,,,ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы);
	
КонецПроцедуры

// Дополняет структуру вызова формы Цены и валюта партиями номенклатуры табличной части
//
// Параметры:
//  Запасы - Табличная часть с партиями.
//  ПараметрыФормыЦеныИВалюта - Структура параметров открытия формы.
//
Процедура ДополнитьСтруктуруВызоваФормыЦеныИВалютыМассивомПартийНоменклатуры(Запасы, ПараметрыФормыЦеныИВалюта) Экспорт
	
	МассивПартий = Новый Массив;
	
	Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Партия) Тогда
			Продолжить;
		КонецЕсли;
		
		Если МассивПартий.Найти(СтрокаТабличнойЧасти.Партия) = Неопределено Тогда
			МассивПартий.Добавить(СтрокаТабличнойЧасти.Партия);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормыЦеныИВалюта.Вставить("МассивПартий", МассивПартий);
	
КонецПроцедуры

// Выводит сообщение оповещение пользователю об изменении режима налогообложения на с НДС. 
// Связан с процедурой ДополнитьСтруктуруВызоваФормыЦеныИВалютыМассивомПартийНоменклатуры
//
// Параметры:
//  ТребуетсяИзменениеНалогообложениеНаСНДС - Признак изменения режима налогообложения.
//
Процедура ВывестиОповещениеПользователяОбИзмененииРежимаНалогообложенияНаСДНС(ТребуетсяИзменениеНалогообложениеНаСНДС = Ложь) Экспорт
	
		Если ТребуетсяИзменениеНалогообложениеНаСНДС Тогда
			СтрокаСообщения = НСтр("ru = 'Налогообложение изменено на ""с НДС"".'");
			ПоказатьОповещениеПользователя(НСтр("ru = 'Налоги'"), , СтрокаСообщения);
			ТребуетсяИзменениеНалогообложениеНаСНДС = Ложь;
		КонецЕсли;
	
КонецПроцедуры

#КонецОбласти