
#Область ПрограммныйИнтерфейс

Функция НомерТелефонаВМеждународномФормате(ПредставлениеКИ) Экспорт
	
	НомерТелефона = ОставитьТолькоЦифрыВСтроке(ПредставлениеКИ);
	
	ЧастиНомераТелефона = Новый Массив;
	Если СтрНачинаетсяС(НомерТелефона, "7") Тогда
		ЧастиНомераТелефона.Добавить("+");
		ЧастиНомераТелефона.Добавить(НомерТелефона);
	ИначеЕсли СтрНачинаетсяС(НомерТелефона, "8") Тогда
		НомерТелефонаБезПервойЦифры = Прав(НомерТелефона, СтрДлина(НомерТелефона) - 1);
		ЧастиНомераТелефона.Добавить("+7");
		ЧастиНомераТелефона.Добавить(НомерТелефонаБезПервойЦифры);
	КонецЕсли;
	
	Если ЧастиНомераТелефона.Количество() <> 0 Тогда
		НомерТелефона = СтрСоединить(ЧастиНомераТелефона, "");
	КонецЕсли;
	
	Возврат НомерТелефона;
	
КонецФункции

Функция ПолучитьПредставлениеКонтактнойИнформации(Форма, ВидКИ) Экспорт
	
	Результат = Новый Массив;
	ОтборРеквизитов = Новый Структура("Вид", ВидКИ);
	ОписаниеРеквизитовКИ = Форма["КонтактнаяИнформацияОписаниеДополнительныхРеквизитов"].НайтиСтроки(ОтборРеквизитов);
	Для каждого ОписаниеРеквизита Из ОписаниеРеквизитовКИ Цикл
		Результат.Добавить(ОписаниеРеквизита.Представление);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ОписаниеРеквизитаКонтактнойИнформации(Форма, ИмяЭлемента) Экспорт
	
	Результат = Новый Массив;
	ОтборРеквизитов = Новый Структура("ИмяРеквизита", ИмяЭлемента);
	ОписаниеРеквизитовКИ = Форма["КонтактнаяИнформацияОписаниеДополнительныхРеквизитов"].НайтиСтроки(ОтборРеквизитов);
	Для каждого ОписаниеРеквизита Из ОписаниеРеквизитовКИ Цикл
		Возврат ОписаниеРеквизита;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

Функция ИмяЭлементаДляРазмещения(Форма, Элемент) Экспорт
	
	ОтборРеквизитов = Новый Структура("ИмяРеквизита", Элемент.Имя);
	ОписаниеРеквизитаКИ = Форма["КонтактнаяИнформацияОписаниеДополнительныхРеквизитов"].НайтиСтроки(ОтборРеквизитов);
	Если ОписаниеРеквизитаКИ.Количество() <> 0 Тогда
		ИмяЭлементаДляРазмещения = ОписаниеРеквизитаКИ[0].ИмяЭлементаДляРазмещения;
	Иначе
		ИмяЭлементаДляРазмещения = Неопределено;
	КонецЕсли;
	
	Возврат ИмяЭлементаДляРазмещения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОставитьТолькоЦифрыВСтроке(Знач Строка) Экспорт
	
	ЛишниеСимволы = СтрСоединить(СтрРазделить(Строка, "0123456789"), "");
	Результат     = СтрСоединить(СтрРазделить(Строка, ЛишниеСимволы), "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти