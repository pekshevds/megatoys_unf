
#Область ПрограммныйИнтерфейс

// Функция получает данные для работы формы помощника печати кассовых ссылок
// Параметры:
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектРМК - подразделение кассы ккм;
//  Организация - ОпределяемыйТип.ОрганизацияРМК - организация кассы ккм;
//  КассаККМ - ОпределяемыйТип.КассаККМРМК - настраиваемая касса
// Возвращаемое значение:
//  Структура - данные настроек кассы ккм
//
Функция ИнициализироватьНастройкиКассовыхСсылок(ТорговыйОбъект, Организация, КассаККМ) Экспорт

	НастройкиКассовыхСсылокСБП = Новый Структура;
    РеквизитыКассы			   = Новый Структура;
	
	РеквизитыКассы.Вставить("ТорговыйОбъект", 	ТорговыйОбъект);
	РеквизитыКассы.Вставить("Организация", 		Организация);
	РеквизитыКассы.Вставить("КассаККМ", 		КассаККМ);
	
	НастройкиПодключения       = РегистрыСведений.СоответствиеНастроекИнтеграцииРМК.НастройкиИнтеграции(
			РеквизитыКассы.Организация,
			РеквизитыКассы.ТорговыйОбъект);

	СписокСпособовОплаты		= Новый СписокЗначений;
	
	Если НастройкиПодключения <> Неопределено Тогда
		
		НастройкиПодключения.Колонки.Добавить("НастройкиТорговойТочки");
		
		Для Каждого НастройкаПодключения Из НастройкиПодключения Цикл   

			Если НастройкаПодключения.ПлатежнаяСистема = Перечисления.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей Тогда
				
				НастройкиТорговойТочки = ИнтеграцияСПлатежнымиСистемамиРМККлиентСервер.НовыйНастройкиТорговойТочки();
				ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьНастройкиТорговойТочки(
					НастройкиТорговойТочки, 
					НастройкаПодключения.Интеграция);
					
				НастройкаПодключения.НастройкиТорговойТочки = НастройкиТорговойТочки;
			
				Если НастройкиТорговойТочки.КассовыеСсылки Тогда
					СписокСпособовОплаты.Добавить(НастройкаПодключения.СпособОплаты);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;

		НастройкиПодключенияМассив = ОбщегоНазначения.ТаблицаЗначенийВМассив(НастройкиПодключения);
		
	КонецЕсли;			
	
	КассовыеСсылкиСБП = ПолучитьНастройкиКассовойСсылкиСБП(КассаККМ);
		
	Если СписокСпособовОплаты.НайтиПоЗначению(КассовыеСсылкиСБП.СпособОплатыДляПечати) = Неопределено Тогда
		КассовыеСсылкиСБП.СпособОплатыДляПечати = Неопределено;
		КассовыеСсылкиСБП.ИспользоватьКассовыйQRКодСБП = Ложь;
	КонецЕсли;
	
	НастройкиКассовыхСсылокСБП.Вставить("РеквизитыКассы", 		РеквизитыКассы);      
	НастройкиКассовыхСсылокСБП.Вставить("НастройкиПодключения",	НастройкиПодключенияМассив);
	НастройкиКассовыхСсылокСБП.Вставить("СписокСпособовОплаты", СписокСпособовОплаты);
	НастройкиКассовыхСсылокСБП.Вставить("КассовыеСсылки", 		КассовыеСсылкиСБП);

	Возврат НастройкиКассовыхСсылокСБП;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьНастройкиКассовойСсылкиСБП(КассаККМ) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	КассовыеСсылкиСБП = ИнтеграцияСПлатежнымиСистемамиРМККлиентСервер.КассовыеСсылкиСБП();
	
	Если ЗначениеЗаполнено(КассаККМ) Тогда
		НастройкаКассыККМ = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(КассаККМ, "КассовыеСсылкиСБП");
	КонецЕсли;
	
	Если НастройкаКассыККМ <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(КассовыеСсылкиСБП, НастройкаКассыККМ);
	КонецЕсли;

	Возврат КассовыеСсылкиСБП;
	
КонецФункции

Функция СформироватьКарточкуКассовойСсылки(ДанныеСсылки, ПараметрыПечати) Экспорт
	
	МодульИнтеграцияСПлатежнымиСистемами = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСПлатежнымиСистемами");
		
	Возврат МодульИнтеграцияСПлатежнымиСистемами.СформироватьКарточкуКассовойСсылки(
		ДанныеСсылки,
		ПараметрыПечати);
		
КонецФункции

Процедура ЗаписатьДанныеКассовойСсылки(КассаККМ, КассовыеСсылки, Отказ = Ложь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если КассовыеСсылки = Неопределено Тогда
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(КассаККМ, "КассовыеСсылкиСБП");
	Иначе
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(КассаККМ, КассовыеСсылки, "КассовыеСсылкиСБП");
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьКассовыеСсылки(СпособОплаты, НастройкиКассовыхСсылокСБП, Отказ) Экспорт

	МодульИнтеграцияСПлатежнымиСистемами = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСПлатежнымиСистемами");

	Для Каждого НастройкаПодключения Из НастройкиКассовыхСсылокСБП.НастройкиПодключения Цикл
		
		Если НастройкаПодключения.СпособОплаты = СпособОплаты Тогда
			
			РезультатСоздания = МодульИнтеграцияСПлатежнымиСистемами.КассоваяСсылка(НастройкаПодключения.Интеграция);
		
			Если ЗначениеЗаполнено(РезультатСоздания.КодОшибки) Тогда
				
				Отказ 				= Истина;
				РезультатСоздания 	= РезультатСоздания.СообщениеОбОшибке;
				
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
	
	КонецЦикла;
	
    Возврат РезультатСоздания;
	
КонецФункции

Функция ПолучитьПараметрыДокументаОплаты(ЧекККМПродажа) Экспорт
	
	ПараметрыВозврата = Новый Структура("ВидОплатыВозврата");
	
	ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьПараметрыДокументаОплаты(ПараметрыВозврата, ЧекККМПродажа);
	
	Возврат ПараметрыВозврата;
	
КонецФункции

Функция ТорговыеТочкиОперации(ДокументОперации) Экспорт
	
	ТорговыеТочки = Новый Массив;
	
	МодульИнтеграцияСПлатежнымиСистемами = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияСПлатежнымиСистемами");
	ТорговыеТочки						 = МодульИнтеграцияСПлатежнымиСистемами.ТорговыеТочкиОперации(ДокументОперации);
	
	Возврат ТорговыеТочки;
	
КонецФункции

Функция НастройкиТорговойТочки(Интеграция, КассаККМ = Неопределено
	) Экспорт
	
	НастройкиИнтеграции = ИнтеграцияСПлатежнымиСистемамиРМККлиентСервер.НовыйНастройкиТорговойТочки();
	
	ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьНастройкиТорговойТочки(НастройкиИнтеграции, Интеграция);

	Если НастройкиИнтеграции.КассовыеСсылки
		И ЗначениеЗаполнено(КассаККМ) Тогда
		НастройкиКассовойСсылки = ПолучитьНастройкиКассовойСсылкиСБП(КассаККМ);
	КонецЕсли;
	
	НастройкиИнтеграции.Вставить("НастройкиКассовойСсылки", НастройкиКассовойСсылки);
	
	Возврат НастройкиИнтеграции;	
	
КонецФункции

Процедура СформироватьДанныеQRКода(СтруктураКода, Интеграция, Идентификатор, УникальныйИдентификатор) Экспорт 
	
	СтруктураКода = Новый Структура;
	СтруктураКода.Вставить("ИдентификаторQRКода", 	Идентификатор);
	СтруктураКода.Вставить("КартинкаQRКода",		Неопределено);
	СтруктураКода.Вставить("ДанныеQRКода",			ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор));

	ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.СформироватьДанныеQRКода(
		СтруктураКода, 
		Интеграция, 
		Идентификатор, 
		УникальныйИдентификатор);
	
КонецПроцедуры

Функция ИдентификаторыОперацииОплаты(Интеграция, ДокументОплаты) Экспорт

	ИдентификаторОплаты = "";
	
	ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьИдентификаторыОперацииОплаты(
		ИдентификаторОплаты,
		Интеграция, 
		ДокументОплаты);
	
	Возврат ИдентификаторОплаты;
	
КонецФункции

Функция ПолучитьИдентификаторОплаты(ЗаявкаНаОплату, УникальныйИдентификатор) Экспорт
	
	Перем ИспользоватьКассовуюСсылку;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование идентификатора оплаты.'");
	
	Если Не ЗначениеЗаполнено(ЗаявкаНаОплату.ДокументОплаты) Тогда
		
		СтруктураНовойСсылки= Новый Структура("ЧекККМВОбработке");
		ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьСсылкуНовогоЧекаККМ(СтруктураНовойСсылки);
		
		ЗаявкаНаОплату.ДокументОплаты = СтруктураНовойСсылки.ЧекККМВОбработке;
		
	КонецЕсли;

	// проверка на кассовую ссылку
	Если ЗаявкаНаОплату.Свойство("ИспользоватьКассовуюСсылку", ИспользоватьКассовуюСсылку)
		И ИспользоватьКассовуюСсылку Тогда
		
		НастройкиКассовойСсылки = ПолучитьНастройкиКассовойСсылкиСБП(ЗаявкаНаОплату.КассаККМ);
		КассоваяСсылка			= ?(НастройкиКассовойСсылки.ИспользоватьКассовыйQRКодСБП,
			НастройкиКассовойСсылки.СпособыОплаты.Получить(ЗаявкаНаОплату.СпособОплаты),
			Неопределено);
		
	КонецЕсли;

	Если КассоваяСсылка <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ЗаявкаНаОплату, КассоваяСсылка);
	КонецЕсли;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ИдентификаторОплатыВПлатежнойСистеме",
		ЗаявкаНаОплату,
		ПараметрыВыполнения);
		
	Возврат РезультатВыполнения;
	
КонецФункции
	
Функция ОпределитьСтатусОплатыОплаты(ДокументОплаты, Интеграция, УникальныйИдентификатор) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДокументОплаты", 	ДокументОплаты);
	ПараметрыПроцедуры.Вставить("Интеграция", 		Интеграция);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка статуса оплаты.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.СтатусОплатыВПлатежнойСистеме",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция ВозвратОплаты(ЗаявкаНаВозврат, УникальныйИдентификатор) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Возврат оплаты в платежной системе.'");
	
	Если Не ЗначениеЗаполнено(ЗаявкаНаВозврат.ДокументВозврата) Тогда
		
		СтруктураНовойСсылки			 = Новый Структура("ЧекККМВОбработке");
		ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ЗаполнитьСсылкуНовогоЧекаККМ(СтруктураНовойСсылки, Истина);
		
		ЗаявкаНаВозврат.ДокументВозврата = СтруктураНовойСсылки.ЧекККМВОбработке;
		
	КонецЕсли;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ВозвратОплаты",
		ЗаявкаНаВозврат,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция ПодтвердитьВозврат(ДокументВозврата, Интеграция) Экспорт

	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("СтатусОперации");  
	РезультатОперации.Вставить("КодОшибки", "МетодНеОпределен");
	РезультатОперации.Вставить("СообщениеОбОшибке", НСтр("ru = 'Метод не определен'"));
	
	ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ПодтвердитьВозврат(РезультатОперации, ДокументВозврата, Интеграция);

	Возврат РезультатОперации;
	
КонецФункции

Функция ОпределитьСтатусВозврата(ДокументВозврата, Интеграция, УникальныйИдентификатор) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДокументВозврата", ДокументВозврата);
	ПараметрыПроцедуры.Вставить("Интеграция",  		Интеграция);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка статуса возврата.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.СтатусВозвратОплаты",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция ОтменитьОперацию(ДокументОплаты, 
							ОтменаЗаказа, 
							Интеграция, 
							ИдентификаторЗаданияФормированияQRКода, 
							ИдентификаторЗаданияПроверкиСтатуса, 
							ИдентификаторЗаданияВозврата,
							УникальныйИдентификатор) Экспорт

	ОтменитьФоновыеЗадания(ИдентификаторЗаданияФормированияQRКода, 
							ИдентификаторЗаданияПроверкиСтатуса, 
							ИдентификаторЗаданияВозврата);
							
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("СтатусОперации");  
	РезультатОперации.Вставить("КодОшибки", "МетодНеОпределен");
	РезультатОперации.Вставить("СообщениеОбОшибке", НСтр("ru = 'Метод не определен'"));

	НастройкиИнтеграции = НастройкиТорговойТочки(Интеграция);

	НастройкиИнтеграции.ОтменаЗаказаДанные 	= ОтменаЗаказа;
	НастройкиИнтеграции.ОтменаЗаказа 		= ОтменаЗаказа <> Неопределено;
	
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отмена заказа.'");

	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДокументОплаты", 							ДокументОплаты);
	ПараметрыПроцедуры.Вставить("Интеграция", 								Интеграция);
	ПараметрыПроцедуры.Вставить("НастройкиИнтеграции", 						НастройкиИнтеграции);
	ПараметрыПроцедуры.Вставить("ИдентификаторЗаданияФормированияQRКода", 	ИдентификаторЗаданияФормированияQRКода);
	ПараметрыПроцедуры.Вставить("ИдентификаторЗаданияПроверкиСтатуса", 		ИдентификаторЗаданияПроверкиСтатуса);
	ПараметрыПроцедуры.Вставить("ИдентификаторЗаданияВозврата", 			ИдентификаторЗаданияВозврата);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатежнымиСистемамиРМКПереопределяемый.ОтменитьОперациюВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
		
	Возврат РезультатВыполнения;
	
КонецФункции

Процедура ОтменитьФоновыеЗадания(ИдентификаторЗаданияФормированияQRКода, 
							ИдентификаторЗаданияПроверкиСтатуса, 
							ИдентификаторЗаданияВозврата) Экспорт
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияФормированияQRКода) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(
			ИдентификаторЗаданияФормированияQRКода);
		ИдентификаторЗаданияФормированияQRКода = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияВозврата) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(
			ИдентификаторЗаданияВозврата);
		ИдентификаторЗаданияВозврата = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияПроверкиСтатуса) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(
			ИдентификаторЗаданияПроверкиСтатуса);
		ИдентификаторЗаданияПроверкиСтатуса = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
