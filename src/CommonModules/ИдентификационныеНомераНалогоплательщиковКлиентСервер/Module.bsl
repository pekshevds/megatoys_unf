// Приказ ФНС России от 29.06.2012 N ММВ-7-6/435@
// "Об утверждении Порядка и условий присвоения, применения, а также изменения идентификационного номера налогоплательщика"

#Область ПрограммныйИнтерфейс

// Функция возвращает минимальную длину ИНН
// 
// Возвращаемое значение:
//  Число - Минимальная длина ИНН
//
Функция МинимальнаяДлинаИНН() Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - 10 цифровой код:
	// 2) для физического лица - 12 цифровой код:
	
	Возврат 10;
	
КонецФункции

// Функция проверяет, что переданный ИНН соответствует юридическому лицу
//
// Параметры:
//  ИНН	 - Строка	 - ИНН
// 
// Возвращаемое значение:
//  Булево - Переданный ИНН соответствует юридическому лицу
//
Функция ЭтоИННЮридическогоЛица(ИНН) Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - десятизначный цифровой код:
	
	Возврат СтрДлина(ИНН) = МинимальнаяДлинаИНН();
	
КонецФункции

// Функция проверяет, что переданный ИНН соответствует физическому лицу
//
// Параметры:
//  ИНН	 - Строка	 - ИНН
// 
// Возвращаемое значение:
//  Булево - Переданный ИНН соответствует физическому лицу
//
Функция ЭтоИННФизическогоЛица(ИНН) Экспорт
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 2) для физического лица - 12 цифровой код:
	
	Возврат СтрДлина(ИНН) = МаксимальнаяДлинаИНН();
	
КонецФункции

// Процедура удаляет из наименования ИНН
//
// Параметры:
//  Наименование - Строка	 - Наименование с ИНН
//  ИНН			 - Строка	 - ИНН
//
Процедура УдалитьИзНаименованияИНН(Наименование, ИНН) Экспорт
	
	ЭлементыНаименования = СтрРазделить(Наименование, " ", Ложь);
	Если ЭлементыНаименования.Количество() < 3 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементыНаименования[0] <> "ИНН" Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементыНаименования[1] <> ИНН 
		И Не КлиентБанкКлиентСервер.ТолькоНулиВСтроке(ЭлементыНаименования[1]) Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим часть наименования, включающую первое и второе слова.
	Для ИндексСлова = 0 По 1 Цикл
		Слово = ЭлементыНаименования[ИндексСлова];
		НачалоСлова  = СтрНайти(Наименование, Слово);
		КонецСлова   = НачалоСлова + СтрДлина(Слово); // Точнее, это позиция разделителя за концом слова
		Наименование = СокрЛ(Сред(Наименование, КонецСлова + 1));// Так как слов больше 2, то заведомо не выйдем за конец строки
	КонецЦикла;
	
КонецПроцедуры

// Функция проверяет соответствие требованиям ИНН
//
// Параметры:
//  ИНН			 - Строка	 - ИНН
//  ЭтоЮрЛицо	 - Булево	 - Признак юридического или физического лица
// 
// Возвращаемое значение:
//  Булево - ИНН соответствует требованиям
//
Функция ПроверитьСоответствиеТребованиямИНН(Знач ИНН, Знач ЭтоЮрЛицо) Экспорт
	Перем ТекстОшибкиФорматнаяПроверка;
	
	РезультатПроверки = Новый Структура("СоответствуетТребованиям, ЭтоЮрЛицо, ОписаниеОшибки", Истина, ЭтоЮрЛицо, "");
	
	Если ЗначениеЗаполнено(ИНН) Тогда
		
		ИНН = СокрП(ИНН);
		
		ЭтоИННФизическогоЛица  = ЭтоИННФизическогоЛица(ИНН);
		ЭтоИННЮридическогоЛица = ЭтоИННЮридическогоЛица(ИНН);
		
		Если РезультатПроверки.ЭтоЮрЛицо И НЕ ЭтоИННЮридическогоЛица Тогда
			РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'ИНН юридического лица должен состоять из 10 цифр'");
			
			Если ЭтоИННФизическогоЛица Тогда
				// Ошибка в определении вида контрагента, однако ИНН может быть все равно корректным - просто надо изменить вид.
				РезультатПроверки.ЭтоЮрЛицо = Ложь;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			КонецЕсли; 
		ИначеЕсли НЕ РезультатПроверки.ЭтоЮрЛицо И НЕ ЭтоИННФизическогоЛица Тогда
			РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'ИНН физического лица должен состоять из 12 цифр'");
			
			Если ЭтоИННЮридическогоЛица Тогда
				// Ошибка в определении вида контрагента, однако ИНН может быть все равно корректным - просто надо изменить вид.
				РезультатПроверки.ЭтоЮрЛицо = Истина;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если РезультатПроверки.СоответствуетТребованиям Тогда
			Если Лев(ИНН, 2) = "00" Тогда
				РезультатПроверки.ОписаниеОшибки = НСтр("ru = 'Первые две цифры ИНН не могут быть ""00""'");
				РезультатПроверки.СоответствуетТребованиям = Ложь;
			Иначе
				РезультатПроверки.СоответствуетТребованиям = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, РезультатПроверки.ЭтоЮрЛицо, ТекстОшибкиФорматнаяПроверка);
				
				Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
					РезультатПроверки.ОписаниеОшибки = ТекстОшибкиФорматнаяПроверка;
				КонецЕсли; 
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		РезультатПроверки.СоответствуетТребованиям = Ложь;
		// В этом случае описание ошибки не выводим,
		// так как описание ошибки из этой функции обычно используется для вывода на форме,
		// а о незаполненном ИНН на форме сообщаем другими средствами.
		
	КонецЕсли;
	
	Возврат РезультатПроверки;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МаксимальнаяДлинаИНН()
	
	// ММВ-7-6/435@
	// 1. Структура идентификационного номера налогоплательщика (далее - ИНН) представляет собой:
	// 1) для организации - десятизначный цифровой код:
	// 2) для физического лица - 12 цифровой код:
	Возврат 12;
	
КонецФункции

#КонецОбласти