#Область ПрограммныйИнтерфейс

// Функция возвращает вид платежной карты, который загружается при оплате СБП
//
// Возвращаемое значение:
//  Вид платежной карты - строка - представление вида платежной карты для оплаты СБП
//
Функция ВидПлатежнойКартыСБП() Экспорт
	
	Возврат НСтр("ru = 'Система быстрых платежей'");	
	
КонецФункции

// Процедура настраивает форму помощника кассового места
// Параметры:
//  Форма - форма нового РМК
//
Процедура НастроитьПояснениеКФормеПомощника(Форма) Экспорт

	НастройкиКассовыхСсылокСБП 	= Форма.НастройкиКассовыхСсылокСБП;
	КассовыеСсылки				= НастройкиКассовыхСсылокСБП.КассовыеСсылки;
	КассоваяСсылка				= КассовыеСсылки.СпособыОплаты.Получить(Форма.СпособОплатыКассовойСсылкиСБП);
	СписокСпособовОплаты		= НастройкиКассовыхСсылокСБП.СписокСпособовОплаты;
    НастройкиПодключения 		= НастройкиКассовыхСсылокСБП.НастройкиПодключения;

	ПодключениеКассовойСсылки = Ложь;
	
	Если НастройкиПодключения <> Неопределено Тогда
	
		Для Каждого НастройкаПодключения Из НастройкиПодключения Цикл
			
			Если НастройкаПодключения.СпособОплаты = Форма.СпособОплатыКассовойСсылкиСБП Тогда
				ПодключениеКассовойСсылки = НастройкаПодключения.НастройкиТорговойТочки.ПодключениеКассовойСсылки;
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;
	
	Элементы					= Форма.Элементы;
	
	Элементы.ГруппаИспользоватьКассовыйQRКодСБП.Видимость 	= НастройкиПодключения <> Неопределено;
	Элементы.ИспользоватьКассовыйQRКодСБП.Доступность 		= СписокСпособовОплаты.Количество() > 0;
	Элементы.СпособОплатыКассовойСсылкиСБП.Видимость 		= СписокСпособовОплаты.Количество() > 0;
	Элементы.СпособОплатыКассовойСсылкиСБП.ТолькоПросмотр	= СписокСпособовОплаты.Количество() = 1;
	Элементы.ГруппаСБП.Видимость 							= Форма.ИспользоватьКассовыйQRКодСБП И СписокСпособовОплаты.Количество() > 0;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, "Подключить") Тогда
		Элементы.Подключить.Видимость 							= ПодключениеКассовойСсылки;
	КонецЕсли;
	
	Если КассоваяСсылка <> Неопределено Тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, "КассоваяСсылкаСБП") Тогда
			Форма.КассоваяСсылкаСБП = КассоваяСсылка.КассоваяСсылка;
		КонецЕсли;
		Форма.NFCСсылкаСБП 		= ПереводыСБПc2bКлиентСервер.ФункциональнаяСсылкаNFC(КассоваяСсылка.КассоваяСсылка);
	Иначе
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, "КассоваяСсылкаСБП") Тогда
			Форма.КассоваяСсылкаСБП = "";
		КонецЕсли;
		Форма.NFCСсылкаСБП 		= "";
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти
