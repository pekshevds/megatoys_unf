#Область СлужебныйПрограммныйИнтерфейс

// Отправляет информацию о запуске приложения.
//
Процедура ОтправитьИнформациюОЗапускеПриложения() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	
	МассивТиповПО = Новый Массив();
	МассивТиповПО.Добавить("ККТ");
	МассивТиповПО.Добавить("СканерШтрихкода");
	МассивТиповПО.Добавить("СчитывательМагнитныхКарт");
	МассивТиповПО.Добавить("ЭквайринговыйТерминал");

	КоличествоККТ      = 0;
	КоличествоСканеров = 0;
	КоличествоЭТ       = 0;
	СписокОтбор = МенеджерОборудованияКлиентСервер.СписокОборудованияОтбор();
	СписокОтбор.ТипыПО = МассивТиповПО;
	СписокОборудования = МенеджерОборудованияВызовСервера.СписокОборудования(СписокОтбор);
	Для Каждого ЭлементМассива Из СписокОборудования Цикл
		Если ЭлементМассива.ТипОборудования = "ККТ" Тогда
			КоличествоККТ = КоличествоККТ + 1;
		ИначеЕсли ЭлементМассива.ТипОборудования = "СканерШтрихкода"
			Или ЭлементМассива.ТипОборудования = "СчитывательМагнитныхКарт" Тогда
			КоличествоСканеров = КоличествоСканеров + 1;
		ИначеЕсли ЭлементМассива.ТипОборудования = "ЭквайринговыйТерминал" Тогда
			КоличествоЭТ = КоличествоЭТ + 1;
		КонецЕсли;
	КонецЦикла;
	
	ДопДанные = "<data><os>{os}</os><platform>{platform}</platform><versionApp>{versionApp}</versionApp><mode>{mode}</mode><adressType>{adressType}</adressType><users>{users}</users><adress>{adress}</adress><countKKT>{countKKT}</countKKT><countBarCodeScaner>{countBarCodeScaner}</countBarCodeScaner><countET>{countET}</countET></data>";
	ДопДанные = СтрЗаменить(ДопДанные, "{os}", СисИнфо.ВерсияОС);
	ДопДанные = СтрЗаменить(ДопДанные, "{platform}", СисИнфо.ТипПлатформы);
	ДопДанные = СтрЗаменить(ДопДанные, "{versionApp}", ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ТекущаяВерсияПриложения"));
	ДопДанные = СтрЗаменить(ДопДанные, "{mode}", ?(ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("ИспользоватьСинхронизациюДанных"), "sync", "local"));
	ДопДанные = СтрЗаменить(ДопДанные, "{countKKT}", КоличествоККТ);
	ДопДанные = СтрЗаменить(ДопДанные, "{countBarCodeScaner}", КоличествоСканеров);
	ДопДанные = СтрЗаменить(ДопДанные, "{countET}", КоличествоЭТ);
	
	АдресЦентральнойБазы = ОбщегоНазначенияМПВызовСервера.ПолучитьЗначениеКонстанты("АдресЦентральнойБазы");
	Если ПустаяСтрока(АдресЦентральнойБазы) Тогда
		ТипАдреса = "";
	ИначеЕсли Найти(НРег(АдресЦентральнойБазы), "1cfresh.com") Тогда
		ТипАдреса = "isFresh";
	Иначе
		ТипАдреса = "isNoFresh";
	КонецЕсли;
	ДопДанные = СтрЗаменить(ДопДанные, "{adressType}", ТипАдреса);
	ДопДанные = СтрЗаменить(ДопДанные, "{adress}", АдресЦентральнойБазы);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ПользователиМП.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.ПользователиМП КАК ПользователиМП";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДопДанные = СтрЗаменить(ДопДанные, "{users}", Строка(Выборка.Количество));
	Иначе
		ДопДанные = СтрЗаменить(ДопДанные, "{users}", "0");
	КонецЕсли;
	
	СборСтатистикиМПСервер.ОтправитьСобытиеСДопДаннымиВФоне("Запуск приложения", ДопДанные);
	
КонецПроцедуры // ОтправитьИнформациюОЗапускеПриложения()

Процедура ОтправитьСобытиеStartВGA() Экспорт
	
	СобытиеСтатистики = "&sc=start&t=event&cd=Старт приложения&ea=Старт приложения&ec=Поведение пользователя";
	СборСтатистикиМПСервер.ОтправитьСобытиеGAВФоне(СобытиеСтатистики);
	
КонецПроцедуры

Процедура ОтправитьОткрытиеЭкранаВGA(Экран) Экспорт
	
	ПараметрыСтатистики = "&cd=" + Экран;
	ОтправитьСтатистикуВGA("screenview", ПараметрыСтатистики);
	
КонецПроцедуры

Процедура ОтправитьОшибкуВGA(ТекстОшибки) Экспорт
	
	ПараметрыСтатистики = "&exd=" + ТекстОшибки;
	ОтправитьСтатистикуВGA("exception", ПараметрыСтатистики);
	
КонецПроцедуры

Процедура ОтправитьДействиеВGA(Действие, Категория = "Поведение пользователя", ПараметрыПокупки = Неопределено) Экспорт
	
	ПараметрыСтатистики = "&cd=" + Действие + "&ea=" + Действие + "&ec=" + Категория;
	ОтправитьСтатистикуВGA("event", ПараметрыСтатистики);
	
КонецПроцедуры

Процедура ОтправитьСтатистикуВGA(ТипСобытия, ПараметрыСтатистики) 
	
	СобытиеСтатистики = "&t=" + ТипСобытия + ПараметрыСтатистики;
	СборСтатистикиМПСервер.ОтправитьСобытиеGAВФоне(СобытиеСтатистики);
	
КонецПроцедуры

#КонецОбласти
