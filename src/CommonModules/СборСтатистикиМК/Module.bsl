#Область ПрограммныйИнтерфейс

Процедура ОтправитьСобытиеВФоне(ИмяСобытия) Экспорт
	
	Параметры = Новый Массив();
	Параметры.Добавить(ИмяСобытия);
	
	ФоновыеЗадания.Выполнить(
		"СборСтатистикиМК.ОтправитьСобытие",
		Параметры,
		Новый УникальныйИдентификатор,
		"ОтправкаСобытияМК");
	
КонецПроцедуры

Процедура ОтправитьСобытие(ИмяСобытия, СообщениеОбОшибке = Неопределено) Экспорт
	
	Попытка
		HTTPСоединение = Новый HTTPСоединение(
			"unf-stat1c-gpt-msk.1c.ru",
			80);
		
		ПутьНаСервере = 
			"/unf-stat1c/hs/analytics/sendstats/"
		+ ИдентификаторМобильногоКлиента() + "/"
		+ Константы.ИдентификаторИнформационнойБазы.Получить() + "/"
		+ ИмяСобытия;
		
		HTTPЗапрос = Новый HTTPЗапрос(ПутьНаСервере);
		Результат = HTTPСоединение.Получить(HTTPЗапрос);
		КодСостояния = Результат.КодСостояния;
		Если Результат.КодСостояния <> 200 Тогда
			СообщениеОбОшибке = Результат.ПолучитьТелоКакСтроку();
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(Инфо);
	КонецПопытки;
	
КонецПроцедуры

// Отправляет событие с дополнительными данными в фоне.
//
Процедура ОтправитьСобытиеСДопДаннымиВФоне(ИмяСобытия, ДопДанные) Экспорт
	
	Параметры = Новый Массив();
	Параметры.Добавить(ИмяСобытия);
	Параметры.Добавить(ДопДанные);
	
	ФоновыеЗадания.Выполнить(
		"СборСтатистикиМК.ОтправитьСобытиеСДопДанными",
		Параметры,
		Новый УникальныйИдентификатор,
		"ОтправкаСобытияСДопДаннымиМК");
	
КонецПроцедуры // ОтправитьСобытиеСДопДаннымиВФоне()

// Отправляет событие с дополнительными данными.
//
Процедура ОтправитьСобытиеСДопДанными(ИмяСобытия, ДопДанные, СообщениеОбОшибке = Неопределено) Экспорт
	
	Попытка
		HTTPСоединение = Новый HTTPСоединение(
		"unf-stat1c-gpt-msk.1c.ru",
		80,
		,
		,
		,
		5);
		
		ПутьНаСервере = 
		"/unf-stat1c/hs/analytics/sendstats/"
		+ ИдентификаторМобильногоКлиента() + "/"
		+ Константы.ИдентификаторИнформационнойБазы.Получить() + "/"
		+ ИмяСобытия;
		
		HTTPЗапрос = Новый HTTPЗапрос(ПутьНаСервере);
		HTTPЗапрос.УстановитьТелоИзСтроки(ДопДанные);
		Результат = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КодСостояния = Результат.КодСостояния;
		Если Результат.КодСостояния <> 200 Тогда
			СообщениеОбОшибке = Результат.ПолучитьТелоКакСтроку();
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(Инфо);
	КонецПопытки;
	
КонецПроцедуры // ОтправитьСобытиеСДопДанными()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторМобильногоКлиента()
	
	Если ОбщегоНазначенияУНФ.ЭтоРозница() Тогда
		Возврат "ac12b984-f547-11ec-880f-0050569f4ccd";
	Иначе
		Возврат "a29eca3c-f547-11ec-880f-0050569f4ccd";
	КонецЕсли;
	
КонецФункции // ИдентификаторПриложения()

#КонецОбласти
