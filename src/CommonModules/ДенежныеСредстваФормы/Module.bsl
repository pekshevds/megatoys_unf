
#Область ОбработчикиСобытий

Функция НалогообложениеЗаполненоВОбработкеЗаполнения(пОбъектСсылка, пОснование) Экспорт
	
	НалогообложениеЗаполненоВОбработкеЗаполнения = пОбъектСсылка.Пустая()
		И (ТипЗнч(пОснование) = Тип("ДокументСсылка.ЗаказПокупателя")
			Или ТипЗнч(пОснование) = Тип("ДокументСсылка.РасходнаяНакладная")
			Или ТипЗнч(пОснование) = Тип("ДокументСсылка.АктВыполненныхРабот")
			Или (ТипЗнч(пОснование) = Тип("Структура")
				И пОснование.Свойство("Основание")
				И ТипЗнч(пОснование.Основание) = Тип("ДокументСсылка.ЗаказПокупателя")));
	
	Возврат НалогообложениеЗаполненоВОбработкеЗаполнения;
	
КонецФункции

Функция УчитыватьВНУПоОснованию(знач пОснование, знач пДата) Экспорт
	
	Если ТипЗнч(пОснование) = Тип("Структура") Тогда
		Если пОснование.Свойство("Основание") Тогда
			пОснование = пОснование.Основание;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОснованиеЭтоЗаказПокупателя = (ТипЗнч(пОснование) = Тип("ДокументСсылка.ЗаказПокупателя"));
	ОснованиеЭтоАктВыполненныхРабот = (ТипЗнч(пОснование) = Тип("ДокументСсылка.АктВыполненныхРабот"));
	Если ОснованиеЭтоЗаказПокупателя Тогда
		СписокРеквизитов = "УчитыватьВНУ, СпециальныйНалоговыйРежим, Организация, СостояниеЗаказа";
	ИначеЕсли ОснованиеЭтоАктВыполненныхРабот Тогда
		СписокРеквизитов = "СпециальныйНалоговыйРежим, Патент";
	Иначе
		СписокРеквизитов = "УчитыватьВНУ, СпециальныйНалоговыйРежим";
	КонецЕсли;
	
	ЗначениеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(пОснование, СписокРеквизитов, Истина);
	
	Если ОснованиеЭтоАктВыполненныхРабот Тогда
		СистемаНалогообложенияСтруктура = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(пОснование.Организация, пОснование.Дата);
		ПлательщикУСН =  СистемаНалогообложенияСтруктура.ПлательщикУСН;
		Если ПлательщикУСН 
			И ЗначениеРеквизитов.СпециальныйНалоговыйРежим <> Перечисления.СпециальныеНалоговыеРежимы.ПСН Тогда
			УчитыватьВНУ = Истина;
		Иначе
			УчитыватьВНУ = Ложь;
		КонецЕсли;
		ЗначениеРеквизитов.Вставить("УчитыватьВНУ", УчитыватьВНУ);			
	КонецЕсли;

	Если ОснованиеЭтоЗаказПокупателя Тогда
		ОснованиеЭтоЗаказНаряд = (ТипЗнч(ЗначениеРеквизитов.СостояниеЗаказа) = Тип("СправочникСсылка.СостоянияЗаказНарядов"));
		Если ОснованиеЭтоЗаказНаряд Тогда
			Возврат ЗначениеРеквизитов.УчитыватьВНУ;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СистемыНалогообложенияОрганизацийСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
			|	СистемыНалогообложенияОрганизацийСрезПоследних.ПрименяетсяПатент КАК ПрименяетсяПатент
			|ИЗ
			|	РегистрСведений.СистемыНалогообложенияОрганизаций.СрезПоследних(&пДата, Организация = &Организация) КАК СистемыНалогообложенияОрганизацийСрезПоследних
			|ГДЕ
			|	(СистемыНалогообложенияОрганизацийСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ОсобыйПорядок)
			|			ИЛИ СистемыНалогообложенияОрганизацийСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная))";
		
		Запрос.УстановитьПараметр("Организация", ЗначениеРеквизитов.Организация);
		Запрос.УстановитьПараметр("пДата", пДата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ПрименяетсяОсобыйПорядок = ВыборкаДетальныеЗаписи.Следующий();
		Если ПрименяетсяОсобыйПорядок Тогда
			Возврат Не ВыборкаДетальныеЗаписи.ПрименяетсяПатент;
		Иначе
			Возврат ЗначениеРеквизитов.УчитыватьВНУ;
		КонецЕсли;
	Иначе
		Возврат ЗначениеРеквизитов.УчитыватьВНУ;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
