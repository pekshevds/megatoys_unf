#Область ПодключаемыеКомандыИСКлиентСерверПереопределяемый

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыВозвратВОборотИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ОтчетОРозничныхПродажах", НСтр("ru = 'Отчет о розничных продажах'"), "УчетРозничныхПродаж");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПриходнаяНакладная", НСтр("ru = 'Приходную накладную'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЧекККМВозврат", НСтр("ru = 'Чек ККМ на возврат'"), "УчетРозничныхПродаж");
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыВыводаИзОборотаИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЗаказПокупателя",			НСтр("ru = 'Заказ-наряд'"), "ИспользоватьПодсистемуРаботы");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "КомплектацияЗапасов",		НСтр("ru = 'Комплектацию запасов'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "РасходнаяНакладная",			НСтр("ru = 'Расходную накладную'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ОтчетОРозничныхПродажах",	НСтр("ru = 'Отчет о розничных продажах'"), "УчетРозничныхПродаж");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЧекККМ",						НСтр("ru = 'Чек ККМ'"), "УчетРозничныхПродаж");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "СписаниеЗапасов",			НСтр("ru = 'Списание запасов'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "СборкаЗапасов",				НСтр("ru = 'Производство'"), "ИспользоватьПодсистемуПроизводство");
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыЗаказНаЭмиссиюКодовМаркировкиСУЗ(Команды) Экспорт 
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "МаркировкаТоваровИСМП",		НСтр("ru = 'Маркировка товаров ИС МП'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПеремаркировкаТоваровИСМП",	НСтр("ru = 'Перемаркировка ИС МП'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЗаказПоставщику",			НСтр("ru = 'Заказ поставщику'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЗаказНаПроизводство",		НСтр("ru = 'Заказ на производство'"));
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыМаркировкиТоваровИСМП(Команды) Экспорт 
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПриходнаяНакладная",    НСтр("ru = 'Приходную накладную'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "СборкаЗапасов",         НСтр("ru = 'Производство'"), "ИспользоватьПодсистемуПроизводство");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ИнвентаризацияЗапасов", НСтр("ru = 'Инвентаризацию запасов'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ОтчетПереработчика", НСтр("ru = 'Отчет переработчика'"), "ПередачаСырьяВПереработку");
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ОтчетОПереработке", НСтр("ru = 'Отчет о переработке'"), "ПереработкаДавальческогоСырья");
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыОтгрузкиТоваровИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "РасходнаяНакладная",	НСтр("ru = 'Расходную накладную'"));
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыПеремаркировкиТоваровИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПриходнаяНакладная",	НСтр("ru = 'Приходную накладную'"));
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЧекККМВозврат",		НСтр("ru = 'Чек ККМ на возврат'"), "УчетРозничныхПродаж");
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыПриемкиТоваровИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуОформить(Команды, "ПриходнаяНакладная", НСтр("ru = 'Приходная накладная'"));
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ПриходнаяНакладная", НСтр("ru = 'Приходная накладная'"));
	
КонецПроцедуры

// Заполняет структуру команд для динамического формирования доступных для создания документов на основании.
// 
// Параметры:
//  Команды - Структура - Исходящий, структура команд динамически формируемых для создания документов на основании.
//
Процедура КомандыСписанияКодовМаркировкиИСМП(Команды) Экспорт 
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(Команды, "ЗаказНаЭмиссиюКодовМаркировкиСУЗ", НСтр("ru = 'Заказ на эмиссию кодов маркировки';
																												|en = 'Order for marking code emission'"));
	Возврат;
	
КонецПроцедуры

Процедура КомандыУточненияСведенийОКодахМаркировкиИСМП(Команды) Экспорт
	
	ПодключаемыеКомандыИСКлиентСервер.ДобавитьКомандуВыбрать(
		Команды, "МаркировкаТоваровИСМП", НСтр("ru = 'Маркировка товаров ИС МП';
												|en = 'IS MT goods marking'"));
	Возврат;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандВозвратаВОборот(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандВыводаВОборота(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Операция = Форма.Объект.Операция;
	
	Для Каждого КлючИЗначение Из Форма.ВидимостьПодключаемыхКоманд Цикл
		
		ИмяМетаданных	= КлючИЗначение.Значение.ИмяМетаданных;
		Элемент			= Форма.Элементы[КлючИЗначение.Ключ];
		
		Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаВозвратФизическомуЛицу") Тогда
			Элемент.Видимость = ИмяМетаданных = "РасходнаяНакладная";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаБезвозмезднаяПередача") Тогда
			Элемент.Видимость = ИмяМетаданных = "СписаниеЗапасов";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаИспользованиеДляСобственныхНуждПредприятия") Тогда
			Элемент.Видимость = (ИмяМетаданных = "СборкаЗапасов"
				Или ИмяМетаданных = "СписаниеЗапасов")
				Или ИмяМетаданных = "РасходнаяНакладная"
				Или ИмяМетаданных = "КомплектацияЗапасов";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаВПроцессеРеализацииПоДоговоруРассрочки")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаКредитныйДоговор")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРеализацияКонфискованныхТоваров")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРеализацияПоДоговоруРассрочки")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортВСтраныЕАЭС")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЭкспортЗаПределыСтранЕАЭС") Тогда
			Элемент.Видимость = ИмяМетаданных = "РасходнаяНакладная";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаКонфискацияТовара")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаЛиквидацияПредприятия")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаУничтожениеТовара")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаУтратаПовреждениеТовара") Тогда
			Элемент.Видимость = ИмяМетаданных = "СписаниеЗапасов";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа") Тогда
			Элемент.Видимость = (ИмяМетаданных = "РасходнаяНакладная"
				Или ИмяМетаданных = "ОтчетОРозничныхПродажах"
				Или ИмяМетаданных = "ЧекККМ"
				Или ИмяМетаданных = "ЗаказПокупателя");
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаФасовка") Тогда
				Элемент.Видимость = ИмяМетаданных = "КомплектацияЗапасов";
		Иначе
			Элемент.Видимость = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандЗаказаНаЭмиссию(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	СпособВводаВОборот = Форма.Объект.СпособВводаВОборот;
	ВидПродукции = Форма.Объект.ВидПродукции;
	
	Для Каждого КлючИЗначение Из Форма.ВидимостьПодключаемыхКоманд Цикл
		
		ИмяМетаданных = КлючИЗначение.Значение.ИмяМетаданных;
		Элемент       = Форма.Элементы[КлючИЗначение.Ключ];
		
		Если ИмяМетаданных = "МаркировкаТоваровИСМП" Тогда
			Элемент.Видимость = Истина;
		ИначеЕсли ИмяМетаданных = "ПеремаркировкаТоваровИСМП" Тогда
			Элемент.Видимость = ИнтеграцияИСКлиентСервер.ВидПродукцииПодлежитПеремаркировке(ВидПродукции);
		ИначеЕсли СпособВводаВОборот = ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.МаркировкаОстатков") Тогда
			Элемент.Видимость = Ложь;
		ИначеЕсли СпособВводаВОборот = ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Импорт") Тогда
			Элемент.Видимость = (ИмяМетаданных = "ЗаказПоставщику");
		ИначеЕсли СпособВводаВОборот = ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.Производство") Тогда
			Элемент.Видимость = ИмяМетаданных = "ЗаказНаПроизводство";
		ИначеЕсли СпособВводаВОборот = ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля") Тогда
			Элемент.Видимость = (ИмяМетаданных = "ЗаказПоставщику");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандМаркировкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	Операция     = Форма.Объект.Операция;
	ВидПродукции = Форма.Объект.ВидПродукции;
	
	Для Каждого КлючИЗначение Из Форма.ВидимостьПодключаемыхКоманд Цикл
		
		ИмяМетаданных = КлючИЗначение.Значение.ИмяМетаданных;
		Элемент       = Форма.Элементы[КлючИЗначение.Ключ];
		Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.Агрегация") Тогда
			Элемент.Видимость = Ложь;
		ИначеЕсли ИмяМетаданных = "ПроизводственнаяОперацияВЕТИС"
			Или ИмяМетаданных = "ВходящаяТранспортнаяОперацияВЕТИС" Тогда
			Элемент.Видимость = ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС");
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПолучениеПродукцииОтФизическихЛиц")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля")
			Или Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотИмпортСФТС") Тогда
			Элемент.Видимость = ИмяМетаданных = "ПриходнаяНакладная";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФ") Тогда
			Элемент.Видимость = ИмяМетаданных = "СборкаЗапасов";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков") Тогда
			Элемент.Видимость = ИмяМетаданных = "ИнвентаризацияЗапасов";
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговоруНаСторонеЗаказчика") Тогда
			Элемент.Видимость = (ИмяМетаданных = "СборкаЗапасов"
				Или ИмяМетаданных = "ОтчетПереработчика");
		ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВводВОборотПроизводствоРФПоДоговору") Тогда
			Элемент.Видимость = (ИмяМетаданных = "СборкаЗапасов"
				Или ИмяМетаданных = "ОтчетОПереработке");
		Иначе
			Элемент.Видимость = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандОтгрузкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандПеремаркировкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандПриемкиТоваров(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандСписанияКодовМаркировки(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеВидимостьюКомандУточнениеСведенийОКодахМаркировки(Форма, Команды) Экспорт
	
	Если ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюПоУмолчанию(Форма) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияИСМПКлиентСерверПереопределяемый

Процедура ЗаполнитьСоответствиеПолейДокументовОснований(Форма, СоответствиеПолей) Экспорт
	
	Если СтрНачинаетсяС(Форма.ИмяФормы, "Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ") Тогда
		
		СоответствиеПолей.Вставить("ЗаказПоставщику", Новый Соответствие);
		СоответствиеПолей["ЗаказПоставщику"].Вставить("Организация",   "Организация");
		
		СоответствиеПолей.Вставить("ЗаказНаПроизводство", Новый Соответствие);
		СоответствиеПолей["ЗаказНаПроизводство"].Вставить("Организация",   "Организация");
		
	ИначеЕсли СтрНачинаетсяС(Форма.ИмяФормы, "Документ.МаркировкаТоваровИСМП") Тогда
		
		СоответствиеПолей.Вставить("СборкаЗапасов", Новый Соответствие);
		СоответствиеПолей["СборкаЗапасов"].Вставить("Организация", "Организация");
		СоответствиеПолей["СборкаЗапасов"].Вставить(ПредопределенноеЗначение("Перечисление.ВидыОперацийСборкаЗапасов.Сборка"), "ВидОперации");
		
		СоответствиеПолей.Вставить("ПриходнаяНакладная", Новый Соответствие);
		СоответствиеПолей["ПриходнаяНакладная"].Вставить("Организация", "Организация");
		
		СоответствиеПолей.Вставить("ОприходованиеЗапасов", Новый Соответствие);
		СоответствиеПолей["ОприходованиеЗапасов"].Вставить("Организация", "Организация");
		
	ИначеЕсли СтрНачинаетсяС(Форма.ИмяФормы, "Документ.ВыводИзОборотаИСМП") Тогда
		
		СоответствиеПолей.Вставить("ЧекККМ", Новый Соответствие);
		СоответствиеПолей["ЧекККМ"].Вставить("Организация", "Организация");
		
		СоответствиеПолей.Вставить("РасходнаяНакладная", Новый Соответствие);
		СоответствиеПолей["РасходнаяНакладная"].Вставить("Организация", "Организация");
		
		СоответствиеПолей.Вставить("ОтчетОРозничныхПродажах", Новый Соответствие);
		СоответствиеПолей["ОтчетОРозничныхПродажах"].Вставить("Организация", "Организация");
		
		СоответствиеПолей.Вставить("СписаниеЗапасов", Новый Соответствие);
		СоответствиеПолей["СписаниеЗапасов"].Вставить("Организация", "Организация");
		
		СоответствиеПолей.Вставить("СборкаЗапасов", Новый Соответствие);
		СоответствиеПолей["СборкаЗапасов"].Вставить("Организация", "Организация");
		
	ИначеЕсли СтрНачинаетсяС(Форма.ИмяФормы, "Документ.ПеремаркировкаТоваровИСМП") Тогда
		
		СоответствиеПолей.Вставить("ПриходнаяНакладная", Новый Соответствие);
		СоответствиеПолей["ПриходнаяНакладная"].Вставить("Организация", "Организация");
		СоответствиеПолей["ПриходнаяНакладная"].Вставить(ПредопределенноеЗначение("Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПокупателя"), "ВидОперации");
		
		СоответствиеПолей.Вставить("ЧекККМВозврат", Новый Соответствие);
		СоответствиеПолей["ЧекККМВозврат"].Вставить("Организация", "Организация");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Устанавливает параметры выбора номенклатуры.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой нужно установить параметры выбора,
//  ИмяПоляВвода - Строка - имя поля ввода номенклатуры.
Процедура УстановитьПараметрыВыбораНоменклатуры(Форма, ВидыПродукцииИС = Неопределено, ИмяПоляВвода = "ТоварыНоменклатура") Экспорт
	
	ПараметрыВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(ВидыПродукцииИС) Тогда
		ЗаполнитьПараметрыВыбораНоменклатурыПоВидуПродукции(ПараметрыВыбора, ВидыПродукцииИС);
	КонецЕсли;
	
	Если ПараметрыВыбора.Количество() Тогда
		
		Форма.Элементы[ИмяПоляВвода].ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет соответствие переданного документа проверяемому типу.
//
// Параметры:
//  Контекст - УправляемаяФорма, ДокументСсылка - Контекст для определения типа документа.
//  Имя      - Строка - имя объекта метаданного документа.
//
// Возвращаемое значение:
// 	Булево - это документ.
Функция ЭтоДокументПоНаименованию(Контекст, Имя) Экспорт
	
	Результат = Ложь;
	
	ТипКонтекста = ТипЗнч(Контекст);
	ТипДокумента = Тип(СтрШаблон("ДокументСсылка.%1", Имя));
	
	Если ТипКонтекста = Тип("УправляемаяФорма") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, СтрШаблон("Документ.%1", Имя)) Тогда
			Результат = Истина;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = ТипДокумента Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает параметры выбора номенклатуры.
//
// Параметры:
//  ПараметрыВыбора - Массив - параметры выбора элемента формы "Номенклатура".
//  ВидПродукцииИС - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
//
Процедура ЗаполнитьПараметрыВыбораНоменклатурыПоВидуПродукции(ПараметрыВыбора, ВидыПродукцииИС) Экспорт
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас")));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ВидПродукцииИС", ВидыПродукцииИС));
	
КонецПроцедуры

Процедура ЗаполнитьДанныеНастройкиПараметровНоменклатурыИСМП(Форма, ПользовательРазрешилРедактирование = Ложь) Экспорт
	
	ИмяЭлементаНастройки = "НастройкаПараметровНоменклатурыИСМП";
	ЭлементФормы         = Форма.Элементы.Найти(ИмяЭлементаНастройки);
	
	Если ЭлементФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПредставления = ИнтеграцияИСМПКлиентСервер.ПараметрыФормированияДанныхПредставленияНастройкиНоменклатуры();
	ПараметрыПредставления.СсылкаНаОбъект = Форма.Объект.Ссылка;
	ПараметрыПредставления.ВидПродукции = Форма.Объект.ВидПродукцииИС;
	ПараметрыПредставления.ЕстьПравоРедактирования = Форма.РазрешеноРедактированиеНоменклатуры;
	ПараметрыПредставления.РазрешеноРедактированиеРеквизитов = ПользовательРазрешилРедактирование;
	ПараметрыПредставления.ТребуетВзвешивания = Форма.Объект.Весовой;
	ПараметрыПредставления.ДопустимаНастройкаЛогистическойЕдиницы = Ложь;
	ПараметрыПредставления.ПредставлениеВНесколькоСтрок = Ложь;
	
	ДанныеПредставления = ИнтеграцияИСМПВызовСервера.ПредставлениеНастройкиПараметровНоменклатуры(ПараметрыПредставления);
	
	Если ДанныеПредставления = Неопределено Тогда
		ЭлементФормы.Видимость = Ложь;
	Иначе
		Сдвиг = 0;
		ЧислоСтрок = СтрЧислоСтрок(ДанныеПредставления);
		Для НомерСтроки = 1 По ЧислоСтрок Цикл
			Если СтрДлина(СтрПолучитьСтроку(ДанныеПредставления, НомерСтроки)) >= 80 Тогда
				Сдвиг = Сдвиг + 1;
			КонецЕсли;
		КонецЦикла;
		
		Форма[ИмяЭлементаНастройки] = ДанныеПредставления;
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.АвтоМаксимальнаяШирина = Ложь;
		ЭлементФормы.Высота = ЧислоСтрок + Сдвиг;
		ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
		ЭлементФормы.Ширина = 50;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти