#Область ПрограммныйИнтерфейс

// Отображение результата проверки контрагента в справочнике.
// Реализация тела метода является обязательной.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма справочника, в котором выполнялась проверка контрагента.
//  	Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура) формы контрагента.
//  	Структуру полей РеквизитыПроверкиКонтрагентов см. в процедуре ИнициализироватьРеквизитыФормыКонтрагент ОМ
//  	ПроверкаКонтрагентов.
//  ПредставлениеРезультатаПроверки	 - ФорматированнаяСтрока, Строка - представление результата проверки
//  					контрагента.
//
//@skip-warning
Процедура ОтобразитьРезультатПроверкиКонтрагентаВСправочнике(Форма, ПредставлениеРезультатаПроверки) Экспорт
	
	ПроверяемыеСостояния = Новый Массив;
	ПроверяемыеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП"));
	ПроверяемыеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентОтсутствуетВБазеФНС"));
	ПроверяемыеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КППНеСоответствуетДаннымБазыФНС"));
	
	Если ПроверяемыеСостояния.Найти(Форма.РеквизитыПроверкиКонтрагентов.СостояниеКонтрагента) <> Неопределено Тогда
		Форма.ПредставлениеРезультатПроверкиКонтрагента = ПредставлениеРезультатаПроверки;
	Иначе
		Форма.ПредставлениеРезультатПроверкиКонтрагента = Неопределено;
	КонецЕсли;
	
	СформироватьПредставлениеПроверкиДанных(Форма);
	
КонецПроцедуры

// Получение объекта (ДанныеФормыСтруктура) и ссылки(ДокументСсылка, СправочникСсылка) документа или
// справочника,  в котором выполняется проверка контрагента, по форме.
// Обязательна к заполнению.
//
// Параметры:
//	Форма     - ФормаКлиентскогоПриложения - Форма документа или справочника, в котором выполняется проверка контрагента.
//	Результат - Структура - Объект и Ссылка, полученные по форме документа.
//		Ключи: "Объект" (Тип ДанныеФормыСтруктура) и "Ссылка" (Тип ДокументСсылка, СправочникСсылка).
//
//@skip-warning
Процедура ПриОпределенииОбъектаИСсылкиПоФорме(Форма, Результат) Экспорт
	
	ИсточникОбъект = Форма.Объект;
	ИсточникСсылка = ИсточникОбъект.Ссылка;
	
	Результат.Вставить("Объект", ИсточникОбъект);
	Результат.Вставить("Ссылка", ИсточникСсылка);
	
КонецПроцедуры

// Возвращает представление результата проверки контрагента
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма справочника, в котором выполнялась проверка контрагента.
//
// Возвращаемое значение:
//  ПредставлениеРезультатаПроверки	 - ФорматированнаяСтрока, Строка - представление результата проверки контрагента.
//
Процедура СформироватьПредставлениеПроверкиДанных(Форма) Экспорт
	
	КомпонентыФС = Новый Массив;
	
	ПоказатьПроверкуКонтрагентаВСервисе = ПустаяСтрока(Форма.ПредставлениеПроверкиИНН) И ПустаяСтрока(Форма.ПредставлениеПроверкиКПП);
	
	Если ПоказатьПроверкуКонтрагентаВСервисе И НЕ ПустаяСтрока(Форма.ПредставлениеРезультатПроверкиКонтрагента) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеРезультатПроверкиКонтрагента);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиИНН) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеПроверкиИНН);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиКПП) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеПроверкиКПП);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиОКПО) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеПроверкиОКПО);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиОГРН) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеПроверкиОГРН);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиДублей) Тогда
		КомпонентыФС.Добавить(Форма.ПредставлениеПроверкиДублей);
		КомпонентыФС.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если КомпонентыФС.Количество() > 0 Тогда
		КомпонентыФС.Удалить(КомпонентыФС.ВГраница());
	КонецЕсли;
	
	Форма.ПредставлениеПроверкиДанных = Новый ФорматированнаяСтрока(КомпонентыФС);
	Форма.Элементы.ПредставлениеПроверкиДанных.Видимость = Не ПустаяСтрока(Форма.ПредставлениеПроверкиДанных);
	Если Не ПустаяСтрока(Форма.ПредставлениеПроверкиДанных) Тогда
		Форма.Элементы.ПредставлениеПроверкиДанных.Высота = СтрЧислоСтрок(Форма.ПредставлениеПроверкиДанных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
