#Область ПрограммныйИнтерфейс

// Обработчик события ПередНачаломРаботыСистемы
//
Процедура ПередНачаломРаботыСистемы() Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если НЕ ПараметрыРаботыКлиента.Свойство("ПараметрыЗапускаМобильногоКлиента") Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыЗапускаМобильногоКлиента = ПараметрыРаботыКлиента.ПараметрыЗапускаМобильногоКлиента;
	
	#Если МобильныйКлиент Тогда
	СисИнфо = Новый СистемнаяИнформация;
	ПоддерживаетсяОбработкаЗвонков = Ложь;
	Попытка
		ПоддерживаетсяОбработкаЗвонков = Вычислить(
			"ПоддерживаетсяФункциональностьМобильногоПриложения(ФункциональностьМобильногоПриложения.ОбработкаЗвонков)");
	Исключение
		ПоддерживаетсяОбработкаЗвонков = Ложь;
	КонецПопытки;
	Если ПоддерживаетсяОбработкаЗвонков
		И ПараметрыЗапускаМобильногоКлиента.ИспользоватьМобильнуюТелефонию
		И СредстваТелефонии.ПоддерживаетсяОбработкаЗвонков() Тогда
		СредстваТелефонии.ПодключитьОбработчикЗвонков("Подключаемый_ОбработатьЗвонок");
	КонецЕсли;
	#КонецЕсли
	
	Если ПараметрыЗапускаМобильногоКлиента.ЭтоМобильныйКлиент 
		И ПараметрыЗапускаМобильногоКлиента.НужноОткрытьФормуОценкиМобильногоКлиента Тогда
		ОткрытьФорму("ОбщаяФорма.ОценитьМобильныйКлиент", Новый Структура("ПоказатьНеСпрашивать", Истина));
	КонецЕсли; 
	
	ЗначенияСчетчикаЗапусковКлиента = ПараметрыЗапускаМобильногоКлиента.ЗначенияСчетчикаЗапусковКлиента;
	Если НЕ ЗначениеЗаполнено(ЗначенияСчетчикаЗапусковКлиента) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначенияСчетчикаЗапусковКлиента.ЗапретОткрытияФормыПереходаВМК Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначенияСчетчикаЗапусковКлиента.ЭтоМобильныйБраузер Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыРаботыКлиента.Свойство("ПоказатьОкноНачалоРаботыСПрограммой")
		И ПараметрыРаботыКлиента.ПоказатьОкноНачалоРаботыСПрограммой = Истина Тогда
		Возврат;
	КонецЕсли;
	НачалоРаботыСПрограммойВызовСервера.УстановитьИнтерфейсНачалаРаботы("ОбщаяФорма.ФормаПереходаВМобильныйКлиент");
	ОбновитьИнтерфейс();
	НачалоРаботыСПрограммойВызовСервера.УстановитьСтандартныйИнтерфейс();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура Подключаемый_ОбработатьЗвонок(НомерТелефона, Дата, ВариантСобытия, ТипЗвонка) Экспорт
	
	#Если МобильныйКлиент Тогда
		
	Пользователь = ПользователиКлиент.ТекущийПользователь();
		
	ИспользоватьМобильнуюТелефонию = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ИспользоватьМобильнуюТелефонию");
	
	Если НЕ ИспользоватьМобильнуюТелефонию Тогда
		Возврат;
	КонецЕсли;
	
	Если ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.ЗавершениеВходящего Тогда
		ВариантСобытияСтрокой = "ЗавершениеВходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.ЗавершениеИсходящего Тогда
		ВариантСобытияСтрокой = "ЗавершениеИсходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоВходящего Тогда
		ВариантСобытияСтрокой = "НачалоВходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоИсходящего Тогда
		ВариантСобытияСтрокой = "НачалоИсходящего";
	ИначеЕсли ВариантСобытия = ВариантСобытияЗвонкаСредствТелефонии.НачалоСигналаВходящего Тогда
		ВариантСобытияСтрокой = "НачалоСигналаВходящего";
	КонецЕсли;
	
	МестнаяДата = МестноеВремя(Дата);
	
	ТелефонияВызовСервера.ОбработатьЗвонок(
		НомерТелефона,
		Пользователь,
		МестнаяДата,
		ТипЗвонка,
		ВариантСобытияСтрокой);
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти 
