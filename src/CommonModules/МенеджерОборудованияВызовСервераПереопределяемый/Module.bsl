#Область ПрограммныйИнтерфейс

// Возвращает список доступных типов оборудования.
// 
// Параметры:
//   СписокТиповОборудования - Массив из ПеречислениеСсылка.ТипыПодключаемогоОборудования - 
//                             доступные типы подключаемого оборудования в конфигурации.
//   СтандартнаяОбработка - Булево - признак выполнения стандартной обработки.
// Пример:
//   СписокТиповОборудования.Добавить(Перечисления.ТипыПодключаемогоОборудования.ККТ).
//
Процедура ДоступныеТипыОборудования(СписокТиповОборудования, СтандартнаяОбработка) Экспорт

КонецПроцедуры

// Возвращает флаг возможности добавления новых драйверов оборудования в справочник.
// Параметры:
//   ДобавлениеНовыхДрайверовДоступно - Булево - Флаг возможности добавления новых компонент подключения оборудования.
//   СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки.
// Пример:
//   ДобавлениеНовыхДрайверовДоступно = Ложь;
//   СтандартнаяОбработка = Ложь. 
//
Процедура ДоступноДобавлениеНовыхДрайверов(ДобавлениеНовыхДрайверовДоступно, СтандартнаяОбработка) Экспорт

КонецПроцедуры

// Возвращает доступность сетевого оборудования.
//
// Параметры:
//  СетевоеОборудованиеДоступно - Булево - Сетевое оборудование доступно.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура ДоступноСетевоеОборудование(СетевоеОборудованиеДоступно, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Возвращает доступность распределенной фискализации.
//  
// Параметры:
//  РаспределеннаяФискализацииДоступна - Булево - Доступность распределенной фискализации.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура ДоступноРаспределеннаяФискализация(РаспределеннаяФискализацииДоступна, СтандартнаяОбработка) Экспорт
	РаспределеннаяФискализацииДоступна = Ложь;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

// Возвращает доступность инкассации в форме управление фискальным устройством.
//  
// Параметры:
//  ИнкассацияДоступна - Булево - Доступность инкассации.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура ДоступноИнкассацияВУправлениеФискальнымУстройством(ИнкассацияДоступна, СтандартнаяОбработка) Экспорт
	
	ИнкассацияДоступна = Истина;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Доступны возвраты без карты.       
//
// Параметры:
//  ВозвратыБезКарты - Структура - Доступны возвраты без карты:
//   * ВозвратПоПлатежнойКарте - Булево
//   * ВозвратЭлектроннымСертификатом - Булево
//
Процедура ДоступноВозвратыБезКарты(ВозвратыБезКарты) Экспорт
	
КонецПроцедуры  

// Переопределяемая процедура для подсистемы управление доступом СтандартныеПодсистемы
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//  Менеджер - Строка 
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Менеджер, Ограничение) Экспорт
	
КонецПроцедуры

#Область Обновление

// Переопределяемая часть процедуры обновления с БПО 3
//  
// Параметры:
//  СсылкаПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - оборудование для обновления перехода с БПО 3
//
Процедура ОбновитьСправочникПодключаемогоОборудования(СсылкаПодключаемоеОборудование) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ОборудованиеККТ

// Процедура заполняет реквизиты организации для регистрации ФН.
//  
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияБПО - организация для заполнения реквизитов.
//  ПараметрыРегистрации - Структура - параметры регистрации ФН.
//
Процедура ЗаполнитьРеквизитыОрганизацииДляРегистрацииФН(Организация, ПараметрыРегистрации) Экспорт
	
КонецПроцедуры

// Переопределяет формируемый шаблон чека.
//
// Параметры:
//  ОбщиеПараметры - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//  ДополнительныйТекст - Строка - Дополнительный текст шаблона чека.
//  СтандартнаяОбработка - Булево - Признак стандартной обработки.
//  ТипОборудования - Строка - Типы оборудования строкой.
//
// Возвращаемое Значение:
//  Булево
Функция СформироватьШаблонЧека(ОбщиеПараметры, ДополнительныйТекст, СтандартнаяОбработка, ТипОборудования = "") Экспорт
	
	Возврат МенеджерОборудованияРМК.СформироватьШаблонЧека(ОбщиеПараметры, ДополнительныйТекст, СтандартнаяОбработка,
		ТипОборудования);
		
КонецФункции

// Возвращает для каких типов идентификаторов будет заполняться тег 1162 (код товара).
//
// Параметры:
//  ТипыИдентификаторов - Массив из ПеречислениеСсылка.ТипыИдентификаторовТовараККТ
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура КодТовараЗаполняетсяДляТиповИдентификаторов(ТипыИдентификаторов, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Возвращает, для каких типов товаров будет заполняться отраслевой реквизит.
//
// Параметры:
//  ТипыТоваров - Структура:
//  * ИзделияИзНатуральногоМеха - Булево - Заполнения для изделия из натурального меха. 
//  * ОбъемноСортовойУчет - Булево - Заполнения для товаров объемно сортового учета.
//
Процедура ОтраслевойРеквизитЗаполняетсяДляТиповТоваров(ТипыТоваров) Экспорт
	
	ТипыТоваров.ИзделияИзНатуральногоМеха = ИнтеграцияИСМПКлиентСерверПовтИсп.ОбязательнаяРегистрацияОборотаМаркируемойПродукции(
		Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха, ТекущаяДатаСеанса());
	
КонецПроцедуры

 // Ведется учет ИСМП в конфигурации
// Параметры:
//  УчетПродукцииИСМП - Булево - Ведется учет ИСМП в конфигурации 
//  СтандартнаяОбработка - Булево - Выполнение стандартной обработки
//
Процедура ВедетсяУчетПродукцииИСМП(УчетПродукцииИСМП, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Ведется объемно сортовой учет в конфигурации
// Параметры:
//  ОбъемноСортовойУчет - Булево - Ведется объемно сортовой учет в конфигурации 
//  СтандартнаяОбработка - Булево - Выполнение стандартной обработки
//
Процедура ВедетсяОбъемноСортовойУчет(ОбъемноСортовойУчет, СтандартнаяОбработка) Экспорт
	

	
КонецПроцедуры

// Процедура где можно добавить любой текст при формировании чека в формате PDF.
//
// Параметры:
//   ТекстЗаголовка - Строка - Текст который будет выведен при формировании кассового чека в формате PDF
//   ТекстПодвала - Строка - Текст который будет выведен при формировании кассового чека в формате PDF
Процедура ДополнительныйТекстПриФормированииЧекаPDF(ТекстЗаголовка, ТекстПодвала) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормойЭкземпляра

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  Объект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  Параметры - Структура - Параметры операции
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ЭкземплярОборудованияПриСозданииНаСервере(Объект, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	
	Элемент = ЭтаФорма.Элементы.Добавить("ПравилоОбмена", Тип("ПолеФормы"), ЭтаФорма.Элементы.Основное);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.ПравилоОбмена";
	
	// Доступ к узлу есть только для соответствующего оборудования.
	Если Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток Тогда
		ЭтаФорма.Элементы.ПравилоОбмена.Видимость = Истина;
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипПодключаемогоОборудования", Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток);
		ПараметрыВыбораЭлемента = Новый Массив;
		ПараметрыВыбораЭлемента.Добавить(НовыйПараметр);
		
		Если ЗначениеЗаполнено(ПараметрыВыбораЭлемента) Тогда
			ЭтаФорма.Элементы.ПравилоОбмена.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
		КонецЕсли;
	Иначе
		ЭтаФорма.Элементы.ПравилоОбмена.Видимость = Ложь;
	КонецЕсли;
	
	// РаспределеннаяФискализация
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭтаФорма.Элементы,
		"АвтоматическаяФискализация",
		"ОтображениеПодсказки",
		ОтображениеПодсказки.Кнопка);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭтаФорма.Элементы,
		"АвтоматическаяФискализация",
		"Подсказка",
		НСтр("ru = 'Возможность автоматической печати чеков по оплатам, загруженным из внешних источников.
					|Обязательно подключение системы взаимодействия'"));
	// Конец РаспределеннаяФискализация
	
КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриЧтенииНаСервере".
//
// Параметры:
//  ТекущийОбъект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//
Процедура ЭкземплярОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтаФорма) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПередЗаписьюНаСервере".
//
// Параметры:
//  Отказ - Булево - Отказ операции
//  ТекущийОбъект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ПараметрыЗаписи - Структура - Параметры операции
//
Процедура ЭкземплярОборудованияПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПриЗаписиНаСервере".
//
// Параметры:
//  Отказ - Булево - Отказ операции
//  ТекущийОбъект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ПараметрыЗаписи - Структура - Параметры операции
//
Процедура ЭкземплярОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ПослеЗаписиНаСервере".
//
// Параметры:
//  ТекущийОбъект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ПараметрыЗаписи - Структура - Параметры операции
//
Процедура ЭкземплярОборудованияПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре оборудования
// при событии "ОбработкаПроверкиЗаполненияНаСервере".
//
// Параметры:
//  Объект - СправочникОбъект.ПодключаемоеОборудование - Объект подключаемого оборудования.
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  ПроверяемыеРеквизиты - Структура - Проверяемые реквизиты
//
Процедура ЭкземплярОборудованияОбработкаПроверкиЗаполненияНаСервере(Объект, ЭтаФорма, Отказ, ПроверяемыеРеквизиты) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре Фискальные операции
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  Запись - РегистрСведенийЗапись.ФискальныеОперации - Запись фискальные операции.
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  Параметры - Структура - Параметры операции
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ЭкземплярФискальныеОперацииПриСозданииНаСервере(Запись, ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Экземпляре Фискальные операции
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  Запись - РегистрСведенийЗапись.ФискальныеОперации - Запись фискальные операции.
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ЭкземплярПлатежныеОперацииПриСозданииНаСервере(Запись, ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт

КонецПроцедуры

#КонецОбласти

#Область РаботаСФормойСписка

// Дополнительные переопределяемые действия с управляемой формой в Список Фискальные операции
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  Параметры - Структура - Параметры операции
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура СписокФискальныеОперацииПриСозданииНаСервере(ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт

КонецПроцедуры

// Дополнительные переопределяемые действия с управляемой формой в Список платежные операции
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Параметры - Структура - Параметры операции
//  Отказ - Булево - Отказ создания.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура СписокПлатежныеОперацииПриСозданииНаСервере(ЭтаФорма, Отказ, Параметры, СтандартнаяОбработка) Экспорт

КонецПроцедуры

#КонецОбласти 

#Область РаботаСФормойПанельАдминистрирования

// Дополнительные переопределяемые действия с управляемой формой в Панели администрирования БПО
// при событии "ПриСозданииНаСервере".
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма настройки оборудования
//  Отказ - Булево - Отказ создания
//  Параметры - Структура - Параметры операции
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ПанельАдминистрированияПриСозданииНаСервере(Форма, Отказ, Параметры, СтандартнаяОбработка) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив();
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьОбменСПодключаемымОборудованиемOffline", Новый ОписаниеТипов("Булево")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИспользоватьОбменСКассойMinikassir", Новый ОписаниеТипов("Булево")));
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	Форма.ИспользоватьОбменСПодключаемымОборудованиемOffline = Константы.ИспользоватьОбменСПодключаемымОборудованиемOffline.Получить();
	Форма.ИспользоватьОбменСКассойMinikassir = Константы.ИспользоватьОбменСКассойMinikassir.Получить();
	
	Элементы = Форма.Элементы;
	
	Элемент = Элементы.Вставить(
		"ОфлайнОборудованиеПодключено", 
		Тип("ПолеФормы"), 
		Элементы.ГруппаУправлениеОфлайнОборудованиемПерваяСтрока,
		Элементы.ОткрытьОфлайнОборудование);
	Элемент.Вид                  = ВидПоляФормы.ПолеФлажка;
	Элемент.Заголовок            = НСтр("ru = 'Обмен с офлайн-оборудованием'");
	Элемент.ПутьКДанным          = "ИспользоватьОбменСПодключаемымОборудованиемOffline";
	Элемент.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Подсказка            = НСтр("ru = 'Использование правил обмена с оборудованием. 
							|Необходимо для использования офлайн-касс, прайс-чекеров и весов с печатью этикеток.'");
	Элемент.РасширеннаяПодсказка.Заголовок = Подсказка;
	Элемент.РасширеннаяПодсказка.Ширина = 39;
	Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииРеквизита");

	РежимРаботы = Новый ФиксированнаяСтруктура(УправлениеНебольшойФирмойПовтИсп.РежимРаботыПрограммы());
	Если РежимРаботы.МодельСервиса И Форма.ИспользоватьОбменСКассойMinikassir Тогда
		Элемент = Элементы.Вставить(
			"ИспользоватьОбменСКассойMinikassir", 
			Тип("ПолеФормы"), 
			Элементы.ГруппаУправлениеОфлайнОборудование);
		Элемент.Вид                  = ВидПоляФормы.ПолеФлажка;
		Элемент.Заголовок            = НСтр("ru = 'Обмен с кассой ШТРИХ-MPAY-Ф'");
		Элемент.ПутьКДанным          = "ИспользоватьОбменСКассойMinikassir";
		Элемент.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Право;
		Элемент.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		Подсказка            = НСтр("ru = 'Обмен с автономными кассами ШТРИХ-MPAY-Ф. Вместе с этой опцией будет включена опция Розничные продажи.'");
		Элемент.РасширеннаяПодсказка.Заголовок = Подсказка;
		Элемент.РасширеннаяПодсказка.Ширина = 39;
		Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииРеквизита");
	КонецЕсли;
	
	Элементы.ГруппаПечатьЭтикетокИЦенников.Видимость = ПолучитьФункциональнуюОпцию("ПечатьЭтикетокИЦенников") И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСтаруюОбработкуПечатиЦенников");
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не Константы.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование.Получить() Тогда
		Константы.ФункциональнаяОпцияИспользоватьПодключаемоеОборудование.Установить(Истина);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Дополнительная настройка доступности при изменении реквизита формы
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  РеквизитПутьКДанным - Строка
//
Процедура ПанельАдминистрированияУстановкаДоступности(Форма, РеквизитПутьКДанным) Экспорт
	
	Если РеквизитПутьКДанным = "ИспользоватьОбменСПодключаемымОборудованиемOffline"
		Или РеквизитПутьКДанным = "" Тогда
		ОбменДоступен = Форма.ИспользоватьОбменСПодключаемымОборудованиемOffline;
		
		Элементы = Форма.Элементы;
		Элементы.ОткрытьОфлайнОборудование.Доступность = ОбменДоступен;
		Элементы.Группа_ОфлайнДополнение.Доступность = ОбменДоступен;
	КонецЕсли;
	
КонецПроцедуры

// Позволяет заполнить пользовательские команды на Панели администрирования
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Команды - ТаблицаЗначений:
//   * Идентификатор - Строка
//   * Представление - Строка
//   * Подсказка - Строка
//   * ГруппаФормы - Неопределено, 
//                 - ГруппаФормы
//   * Важность - Строка
//   * Порядок - Число
//   * ФункциональныеОпции - Строка
//   * Менеджер - Строка
//   * Обработчик - Строка
//   * ДополнительныеПараметры - Структура
//   * ИмяФормы - Строка
//   * ПараметрыФормы - Структура
//                    - Неопределено
//   * ИмяПараметраФормы - Строка
//
Процедура ПриЗаполненииКомандПанелиАдминистрирования(Форма, Команды) Экспорт
	
	Элементы = Форма.Элементы;
	
	
	ИспользоватьНовоеРМК = ПолучитьФункциональнуюОпцию("ИспользоватьНовоеРМК");

	Группа = МенеджерОборудования.НоваяГруппаПанелиАдминистрирования(Форма, "Группа_РабочиеМестаДополнение", Элементы.ГруппаРабочиеМеста);
	Группа.ОтображатьЗаголовок = Ложь;
	
	Команда = Команды.Добавить();
	Команда.ГруппаФормы   = Группа;
	Команда.Важность      = "Важное";
	Команда.Представление = НСтр("ru = 'Настройки РМК'");
	Команда.Подсказка     = НСтр("ru = 'Настройки рабочего места кассира.'");
	Если ИспользоватьНовоеРМК Тогда
		Команда.Обработчик    = "e1cib/command/ОбщаяКоманда.НастройкиРМК";
	Иначе
		Команда.Обработчик    = "e1cib/command/Справочник.НастройкиРМК.Команда.НастройкиРМК";
	КонецЕсли;
	
	Группа = МенеджерОборудования.НоваяГруппаПанелиАдминистрирования(Форма, "Группа_ОфлайнДополнение", Элементы.ГруппаУправлениеОфлайнОборудование);
	
	Команда = Команды.Добавить();
	Команда.ГруппаФормы   = Группа;
	Команда.Представление = НСтр("ru = 'Правила обмена с оборудованием'");
	Команда.Порядок       = 50;
	Команда.Подсказка     = НСтр("ru = 'Правила для выгрузки информации о товарах в офлайн кассы и весы.'");
	Команда.ИмяФормы      = "Справочник.ПравилаОбменаСПодключаемымОборудованиемOffline.ФормаСписка";
	
	Команда = Команды.Добавить();
	Команда.ГруппаФормы   = Группа;
	Команда.Представление = НСтр("ru = 'Настройка диапазонов SKU весового товара'");
	Команда.Порядок       = 80;
	Команда.Подсказка     = НСтр("ru = 'Для весового товара необходимо назначить диапазон SKU, который будет использоваться на весах в качестве кода товара.'");
	Команда.ИмяФормы      = "ОбщаяФорма.НастройкаДиапазоновSKU";
	
	Группа = МенеджерОборудования.НоваяГруппаПанелиАдминистрирования(Форма, "Группа_ФЗ54");
	Группа.Заголовок = НСтр("ru = 'Изменения 54-ФЗ'");
	
	Команда = Команды.Добавить();
	Команда.ГруппаФормы   = Группа;
	Команда.Представление = НСтр("ru = 'Подключить кассы к оператору фискальных данных'");
	Команда.Порядок       = 50;
	Команда.Важность      = "Важное";
	Команда.Подсказка     = НСтр("ru = 'Сервис  «1С-ОФД» обеспечит удобное подключение ваших касс к оператору фискальных данных без наценок к официальной стоимости услуг ОФД.'");
	Команда.Обработчик    = "http://v8.1c.ru/cnt.jsp/:kd_roz:/https://portal.1c.ru/applications/56";
	
	Команда = Команды.Добавить();
	Команда.ГруппаФормы   = Группа;
	Команда.Представление = НСтр("ru = 'Все о 54-ФЗ и переходе на онлайн-ККТ'");
	Команда.Порядок       = 80;
	Команда.Важность      = "Важное";
	Команда.Подсказка     = НСтр("ru = 'С 1 июля 2017 года можно будет использовать только те ККТ, которые передают данные о продажах в ФНС через оператора фискальных данных (Федеральный закон 54-ФЗ).'");
	Команда.Обработчик    = "http://v8.1c.ru/cnt.jsp/:kd_roz:/http://v8.1c.ru/kkt/";
	
КонецПроцедуры

#КонецОбласти 

#Область ОчередьФискальныхЧеков

// Завершение фискализация чека в очереди
//
// Параметры:
//  ИдентификаторФискальнойЗаписи - Строка - Идентификатор фискальной записи
//  ПараметрыФискализации - Структура - Параметры операции
//  ОборудованиеККТ - СправочникСсылка.ПодключаемоеОборудование -
//  РезультатФискализации - Структура - Результат Фискализации
//  РезультатВыполненияПакетнойОперации - Структура:
//    * Результат - Булево - Результат выполнения пакета операций.
//    * ОписаниеОшибки - ИнформацияОбОшибке - Описание ошибки выполнения пакета операции.
//    * РезультатВыполненияЭквайринговойОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧека - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧекаВыдачи - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧекаКоррекции - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатАварийнойОтменыОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * ПараметрыЭквайринговойОперации - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков
//    * ПараметрыПродажаСВыдачейНаличных - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков
Процедура ФискализацияЧекаВОчереди(ИдентификаторФискальнойЗаписи, ПараметрыФискализации, ОборудованиеККТ, РезультатФискализации, РезультатВыполненияПакетнойОперации) Экспорт
	
	ОбщегоНазначенияРМКПереопределяемый.ФискализироватьЧекЗавершение(РезультатФискализации, ПараметрыФискализации,
		РезультатВыполненияПакетнойОперации);
	
КонецПроцедуры

// Завершение фискализация чека в очереди
//
// Параметры:
//  РеквизитыЧека - Структура - Данные документа основания.
//  СтатусДокументаИзменен - Булево - признак изменения статуса документа.
//
Процедура ПроверитьСтатусДокументаОснования(РеквизитыЧека, СтатусДокументаИзменен) Экспорт
	
КонецПроцедуры

#КонецОбласти

// Обработчик события заполнения персональных данных.
//
// Параметры:
//  ПерсональныеДанные - Структура - Возвращаемый параметр, персональные данные.
//  СубъектПерсональныхДанных - ОпределяемыйТип.СубъектПерсональныхДанныхБПО - субъект персональных данных. 
//  ТипПерсональныхДанных - ПеречислениеСсылка.ТипыПерсональныхДанныхККТ - Тип персональных данных
//  НаДату - Дата - Дата, на которую необходимо получать персональные данные
//
Процедура ОбработкаЗаполненияПерсональныхДанных(ПерсональныеДанные, СубъектПерсональныхДанных, ТипПерсональныхДанных, НаДату) Экспорт
	
КонецПроцедуры

#Область ПродажаСВыдачейНаличных

// Процедура где требуется проверить достаточность наличных денежных средств в кассовом аппарате
//
// Параметры:
//   ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ссылка на элемент КТТ
//   Сумма - Число - требуемая сумма для выдачи наличных
//   Отказ - Булево - если установить в Истина, тогда выдача наличных осуществляться не будет
Процедура ПроверитьДостаточностьНаличныхСредств(ИдентификаторККТ, Сумма, Отказ) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормойТоваровНСПК

// Процедура, где требуется заполнить дерево товаров по кодам ТРУ.
//
// Параметры:
//   ДеревоСертификатовЗначение - ДеревоЗначений - дерево значений для заполнения
//   КодыТоваровТРУ - ТаблицаЗначений - таблица значений с товарами и кодами ТРУ для поиска сопоставлений
Процедура ЗаполнитьДеревоТоваровПоКодамТРУ(ДеревоСертификатовЗначение, КодыТоваровТРУ) Экспорт
	ОбщегоНазначенияРМКУНФ.ЗаполнитьДеревоТоваровПоКодамТРУ(ДеревоСертификатовЗначение, КодыТоваровТРУ);
КонецПроцедуры

#КонецОбласти

#Область ЛогированиеПлатежныхОпераций

// Возвращает доступность логирования платежных операций
//  
// Параметры:
//  ЛогированиеДоступно - Булево - Доступность логирования.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура ДоступноЛогированиеПлатежныхОпераций(ЛогированиеДоступно, СтандартнаяОбработка) Экспорт
	
	ЛогированиеДоступно = Истина;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Устанавливает период хранения платежных операций в регистре сведений
//
// Параметры:
//   ПериодХранения - ПеречислениеСсылка.ПериодХраненияИсторииПлатежныхОпераций
Процедура ПериодХраненияИсторииПлатежныхОпераций(ПериодХранения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

#Область ОборудованиеККТ

// Устарела. Перенесена в модуль МенеджерОборудованияКлиент
// Переопределяет доступное ККТ для фискализации чека.
//
// Параметры:
//  РеквизитыЧека - Структура - реквизиты фискального чека
//  СписокУстройств - Массив - Список доступных ККТ для фискализации
//  ИдентификаторУстройстваККТ - СправочникСсылка.ПодключаемоеОборудование - выбранное ККТ для фискализации
//
Процедура ДоступноеККТДляФискализации(РеквизитыЧека, СписокУстройств, ИдентификаторУстройстваККТ) Экспорт

КонецПроцедуры

#КонецОбласти

#Область РаботаСЭлементомФормы

// Устарела. Больше не используется
// Дополнительные переопределяемые действия с элементом формы 
// служит для учета специфики визуального отображения в зависимости от типа клиента.
//
// Параметры:
//  ЭлементУправления - ЭлементУправленияИнтерфейсом - элемент управления.
//  СтандартнаяОбработка - Булево - Стандартная обработка.
//
Процедура ПодготовитьЭлементУправления(ЭлементУправления, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти