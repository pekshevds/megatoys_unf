Процедура ДокументыПерсУчетаПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИмяДокумента = Источник.Метаданные().Имя;
	
	Если Не Источник.ПометкаУдаления Тогда
		Документы[ИмяДокумента].ОбработкаФормированияФайла(Источник);
	КонецЕсли;
	
КонецПроцедуры

Функция ДокументыСЗВДанныеЗастрахованногоЛица(ВыборкаПоСтрокамДокумента) Экспорт
	ДанныеЗастрахованногоЛица = Новый Структура();
	ДанныеЗастрахованногоЛица.Вставить("ФизическоеЛицо", ВыборкаПоСтрокамДокумента.Сотрудник);
	ДанныеЗастрахованногоЛица.Вставить("Наименование", ВыборкаПоСтрокамДокумента.СотрудникНаименование);
	ДанныеЗастрахованногоЛица.Вставить("СтраховойНомерПФР");
	ДанныеЗастрахованногоЛица.Вставить("Фамилия");
	ДанныеЗастрахованногоЛица.Вставить("Имя");
	ДанныеЗастрахованногоЛица.Вставить("Отчество");
	ДанныеЗастрахованногоЛица.Вставить("АдресДляИнформирования");
	
	ЗаполнитьЗначенияСвойств(ДанныеЗастрахованногоЛица, ВыборкаПоСтрокамДокумента);
	
	Возврат ДанныеЗастрахованногоЛица;
КонецФункции

Функция ПолучитьДатыПериодаДокумента(ДокументСсылка)Экспорт
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если ТипДокумента = Тип("ДокументСсылка.ОписьПачекСЗВ_6")
		Или ТипДокумента = Тип("ДокументСсылка.РеестрСЗВ_6_2") 
		Или ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3")
		Или ТипДокумента = Тип("ДокументСсылка.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ")
		Или ТипДокумента = Тип("ДокументСсылка.СведенияОЗастрахованныхЛицахСЗВ_М")
		Или ТипДокумента = Тип("ДокументСсылка.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР")
		Или ТипДокумента = Тип("ДокументСсылка.ОписьОДВ_1")
		Или ТипДокумента = Тип("СправочникСсылка.КомплектыОтчетностиПерсучета")
		Или ТипДокумента = Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД") Тогда

		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ОтчетныйПериод,Организация");
		Если ТипДокумента = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3") Тогда
			
			ДатаНачала = Дата(ДанныеДокумента.ОтчетныйПериод, 1, 1);
			ДатаОкончания = Дата(ДанныеДокумента.ОтчетныйПериод, 12, 31, 23, 59, 59);
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОписьОДВ_1")
			Или ТипДокумента = Тип("ДокументСсылка.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ")
			Или ТипДокумента = Тип("ДокументСсылка.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР")
			Или ТипДокумента = Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД") Тогда
			ДатаНачала = ДанныеДокумента.ОтчетныйПериод;
			ДатаОкончания = ПерсонифицированныйУчетКлиентСерверУНФ.ОкончаниеОтчетногоПериодаСтажаПерсУчета(ДатаНачала);	
		Иначе
			ДатаНачала = ДанныеДокумента.ОтчетныйПериод;
			ДатаОкончания = ПерсонифицированныйУчетКлиентСерверУНФ.ОкончаниеОтчетногоПериодаПерсУчета(ДатаНачала);
		КонецЕсли;
		
		Возврат Новый Структура("ДатаНачала, ДатаОкончания, Организация", ДатаНачала, ДатаОкончания, ДанныеДокумента.Организация);
	Иначе
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата,Организация");
		
		Возврат Новый Структура("ДатаНачала, ДатаОкончания, Организация", ДанныеДокумента.Дата, ДанныеДокумента.Дата, ДанныеДокумента.Организация);
	КонецЕсли;	
КонецФункции	

Функция ПредставлениеПараметровТерриториальныхУсловий(ЗаписьСтажа) Экспорт
	
	ПредставлениеПараметровТУ = "";
	Ставка = ЗаписьСтажа.ПараметрТерриториальныхУсловий;
	ПервыйПараметрТУ = ЗаписьСтажа.ПервыйПараметрИсчисляемогоСтажа;
	ВторойПараметрТУ = ЗаписьСтажа.ВторойПараметрИсчисляемогоСтажа;
	
	Если Ставка <> "0.00" Тогда
		ПредставлениеПараметровТУ = Ставка;
	Иначе
		ПредставлениеПараметровТУ = ?(ПервыйПараметрТУ > 0,"" + ПервыйПараметрТУ+" м ","") + ?(ВторойПараметрТУ > 0,""+ВторойПараметрТУ+" дн ","");
	КонецЕсли;
	
	Возврат ПредставлениеПараметровТУ;
	
КонецФункции

// Формирует представление параметров исчисляемого  трудового стажа
//
// Параметры:
//		ЗаписьСтажа -	структура - запись о стаже
// Возвращаемое значение:
//		Строка
Функция ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ЗаписьСтажа) Экспорт
	
	ПредставлениеПараметровИТС = "";
	КодОснованияИТС = ЗаписьСтажа.ОснованиеИсчисляемогоСтажаКод;
	ПервыйПараметрИТС = ЗаписьСтажа.ПервыйПараметрИсчисляемогоСтажа;
	ВторойПараметрИТС = ЗаписьСтажа.ВторойПараметрИсчисляемогоСтажа;
	ТретийПараметрИТС = ЗаписьСтажа.ТретийПараметрИсчисляемогоСтажа;
	
	Если КодОснованияИТС = "ВОДОЛАЗ" Тогда
		ПредставлениеПараметровИТС = ?(ПервыйПараметрИТС > 0,""+ПервыйПараметрИТС+" ч ","")+?(ВторойПараметрИТС > 0,""+ВторойПараметрИТС+" мин","");
	ИначеЕсли КодОснованияИТС = "СЕЗОН" Тогда
		ПредставлениеПараметровИТС = "";
	Иначе
		ПредставлениеПараметровИТС = ?(ПервыйПараметрИТС > 0,"" + ПервыйПараметрИТС+" м ","") + ?(ВторойПараметрИТС > 0,""+ВторойПараметрИТС+" дн ","") + Строка(ТретийПараметрИТС);
	КонецЕсли;
	
	Возврат ПредставлениеПараметровИТС;
	
КонецФункции

// Формирует строки "дополнительных сведений" по выслуге лет в "удобочитаемом" виде для отражения в формах.
//
// Параметры:
//		ЗаписьСтажа -	структура - запись о стаже
//		СтрПервыйВторойПараметр  -	строка - возвращаемый параметр - представление первых двух параметров
//		СтрСтавки  -	строка - возвращаемый параметр - представление третьего параметра
//
Процедура ПредставлениеПараметровПенсииЗаВыслугуЛет(ЗаписьСтажа, СтрПервыйВторойПараметр, СтрСтавки) Экспорт
	
	КодОснования = ЗаписьСтажа.ОснованиеВыслугиЛетКод;
	ПервыйПараметр = ЗаписьСтажа.ПервыйПараметрВыслугиЛет;
	ВторойПараметр = ЗаписьСтажа.ВторойПараметрВыслугиЛет;
	ТретийПараметр = ЗаписьСтажа.ТретийПараметрВыслугиЛет;
	
	СтрПервыйВторойПараметр = "";
	СтрСтавки = "";
	
	Если (КодОснования="ИСПКЛС1") ИЛИ (КодОснования="САМОЛЕТ") ИЛИ (КодОснования="ИНСПЕКТ") 
		ИЛИ (КодОснования="СПЕЦАВ") ИЛИ (КодОснования="ЛЕТИСП") ИЛИ (КодОснования="ИТСИСП") 
		ИЛИ (КодОснования="ИТСМАВ") ИЛИ (КодОснования="ОПЫТИСП") Тогда
		СтрПервыйВторойПараметр = ?(ПервыйПараметр>0,""+ПервыйПараметр+" ч ","")+?(ВторойПараметр>0,""+ВторойПараметр+" мин","");
		
	ИначеЕсли (КодОснования="РЕАКТИВН") ИЛИ (КодОснования="НОРМСП") ИЛИ (КодОснования="НОРМАПР") Тогда
		СтрПервыйВторойПараметр = ?(ПервыйПараметр>0,""+ПервыйПараметр+" прыжков ","");
		
	ИначеЕсли (КодОснования="УВД") ИЛИ (КодОснования="ИТС")	ИЛИ (КодОснования="27-14") ИЛИ (КодОснования="27-15") Тогда
		СтрПервыйВторойПараметр = ?(ПервыйПараметр>0,""+ПервыйПараметр+" м ","")+?(ВторойПараметр>0,""+ВторойПараметр+" дн ","");
		
	ИначеЕсли (КодОснования="ХИРУРГД") ИЛИ (КодОснования="ХИРУРСМ") ИЛИ (КодОснования="ЗП81ГД") ИЛИ (КодОснования="ЗП81СМ") 
		ИЛИ (КодОснования="28-ГДХР") ИЛИ (КодОснования="28-СМХР") ИЛИ (КодОснования="28-ГД") ИЛИ (КодОснования="28-СМ") Тогда
		СтрСтавки = ?(ТретийПараметр>0,""+ТретийПараметр,"");
		
	ИначеЕсли (КодОснования="ЗП80РК") ИЛИ (КодОснования="ЗП80ПД") 
		ИЛИ (КодОснования="28-ПДРК") ИЛИ (КодОснования="28-ПД") Тогда
		СтрПервыйВторойПараметр = ?(ПервыйПараметр>0,""+ПервыйПараметр+" ч ","");
		СтрСтавки = ?(ТретийПараметр>0,""+ТретийПараметр,"");
		
	КонецЕсли;
	
КонецПроцедуры 

Функция ОписаниеВыгруженногоФайлаОтчетности() Экспорт
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Владелец");
	ОписаниеФайла.Вставить("ИмяФайла");
	ОписаниеФайла.Вставить("АдресВоВременномХранилище");
	ОписаниеФайла.Вставить("ПроверятьCheckXML", Ложь);
	ОписаниеФайла.Вставить("ПроверятьCheckUFA", Ложь);
	ОписаниеФайла.Вставить("ПроверятьTesterNDFL", Ложь);
	ОписаниеФайла.Вставить("ФайлВСоставеКомплекта", Ложь);
	ОписаниеФайла.Вставить("ПроверяемыйФайлКомплекта", Ложь);
	
	Возврат ОписаниеФайла;
КонецФункции

Процедура ПроверитьДанныеОрганизации(Объект, Организация, Отказ, ДатаАктуальности = Неопределено) Экспорт 
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Если ДатаАктуальности = Неопределено Тогда
			ДатаПроверкиСведений = Объект.ОкончаниеОтчетногоПериода;
		Иначе
			ДатаПроверкиСведений = ДатаАктуальности;
		КонецЕсли;	
		
		ДанныеОрганизации = СведенияОбОрганизации(Организация, ДатаПроверкиСведений);
				
		Если Не ЗначениеЗаполнено(ДанныеОрганизации.НаименованиеПолное) Тогда
			ТекстСообщения = НСтр("ru = 'У организации не заполнено полное наименование.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Организация, "НаименованиеПолное", , Отказ);
		КонецЕсли;
		
		
		Если Не ЗначениеЗаполнено(ДанныеОрганизации.РегистрационныйНомерПФР) Тогда
			ТекстСообщения = НСтр("ru = 'У организации не заполнен регистрационный номер ПФР (за сотрудников). Проверьте значение поля ""Есть сотрудники"" в реквизитах компании.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Организация, "РегистрационныйНомерПФР", , Отказ);
		ИначеЕсли Не ПроверитьРегистрационныйНомерПФР(ДанныеОрганизации.РегистрационныйНомерПФР) Тогда
			ТекстСообщения = НСтр("ru = 'У организации неверно заполнен регистрационный номер ПФР (за сотрудников).'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Организация, "РегистрационныйНомерПФР", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеОрганизации.КодОрганаПФР) Тогда
			ТекстСообщения = НСтр("ru = 'У организации не заполнен код органа ПФР.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Организация, "КодОрганаПФР", , Отказ);
		КонецЕсли;
		
		Если ДанныеОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			Если Не ЗначениеЗаполнено(ДанныеОрганизации.РегистрацияВНалоговомОргане) Тогда
				ШаблонСообщения = НСтр("ru = 'Для организации не указана регистрация в ИФНС, действующая на %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									ШаблонСообщения,
									Формат(ДатаПроверкиСведений, "ДЛФ=DD"));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Организация, , ,Отказ);			
			ИначеЕсли Не ЗначениеЗаполнено(ДанныеОрганизации.КПП) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У организации не заполнен код по КПП'"), Организация, "КПП", ,Отказ);
			ИначеЕсли Не РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(ДанныеОрганизации.КПП, "") Или СтрЗаменить(ДанныеОрганизации.КПП, "0", "") = "" Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У организации неверно указан код по КПП'"), Организация, "КПП", ,Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеОрганизации.ИНН) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У организации не заполнен ИНН'"), Организация, "ИНН", ,Отказ);
		ИначеЕсли Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ДанныеОрганизации.ИНН, РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация), "") Или СтрЗаменить(ДанныеОрганизации.ИНН, "0", "") = "" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У организации неверно указан ИНН'"), Организация, "ИНН", ,Отказ);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Функция ПроверитьРегистрационныйНомерПФР(РегистрационныйНомерПФР, Отказ = Ложь) Экспорт 
	
	Если Не ЗначениеЗаполнено(РегистрационныйНомерПФР) Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Если СтрДлина(СокрЛП(РегистрационныйНомерПФР)) <> 14 Тогда
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(СокрЛП(РегистрационныйНомерПФР), 1, 3))
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(СокрЛП(РегистрационныйНомерПФР), 5, 3))
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Сред(СокрЛП(РегистрационныйНомерПФР), 9)) Тогда 
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


Функция ПредставлениеДатыВФорматеdateTimeСЧасовымПоясом(Дата) Экспорт

	СмещениеВСекундах = СмещениеСтандартногоВремени();
	ЗнакЧасовогоПояса = ?(СмещениеВСекундах < 0, "-", "+");
	ПоложительноеСмещениеВСекундах = ?(СмещениеВСекундах < 0, -СмещениеВСекундах, СмещениеВСекундах);
	
	Возврат Формат(Дата, "ДФ=yyyy-MM-ddTЧЧ:мм:сс; ДП=0001-01-01T00:00:00") + ?(ЗначениеЗаполнено(ПоложительноеСмещениеВСекундах), ЗнакЧасовогоПояса + Формат('00010101' + ПоложительноеСмещениеВСекундах, "ДФ=чч:мм"), "Z")
	
КонецФункции

Процедура ПроверитьДанныеЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилаПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь) Экспорт
	ПроверитьОбязательныеДанныеЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилаПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ);	
	 
	Для Каждого Правило Из ПравилаПроверки Цикл
		Если Правило.ПроверяемыйЭлемент = "СтраховойНомерПФР" Тогда
			ПроверитьСНИЛСЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, Правило, Ошибки, ИсточникаДанныхЗЛ, Отказ);
		ИначеЕсли Правило.ПроверяемыйЭлемент = "МестоРождения" Тогда 	
			ПроверитьМестоРожденияЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, Правило, Ошибки, ИсточникаДанныхЗЛ, Отказ);
		ИначеЕсли Правило.ПроверяемыйЭлемент = "ВидДокумента" Тогда
			ПроверитьУдостоверениеЛичностиЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилаПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ);
		ИначеЕсли Правило.ПроверяемыйЭлемент = "Адрес" Тогда
			ПроверитьАдресЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, Правило, Ошибки, ИсточникаДанныхЗЛ, Отказ);
		КонецЕсли;	
	КонецЦикла; 
КонецПроцедуры	

Процедура ПроверитьОбязательныеДанныеЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилаПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь)
	Для Каждого Правило Из ПравилаПроверки Цикл
		Если Правило.ОбязательноКЗаполнению И 
			Не ЗначениеЗаполнено(ДанныеЗЛ[Правило.ПутьКДанным]) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1: не заполнено поле %2'"), 
																			ДанныеЗЛ.Наименование, 
																			Правило.ПредставлениеПроверяемогоЭлемента);
																			
			ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(Ошибки, ИсточникаДанныхЗЛ, ДанныеЗЛ.ФизическоеЛицо, НомерВПачке, ТекстОшибки, Правило.ПутьКДанным, Отказ);																
		КонецЕсли;			
	КонецЦикла;		
КонецПроцедуры	

Процедура ПроверитьСНИЛСЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилоПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь)
			
	ТекстСообщения = "";
	Если ЗначениеЗаполнено(ДанныеЗЛ[ПравилоПроверки.ПутьКДанным]) И 
		Не РегламентированныеДанныеКлиентСервер.СтраховойНомерПФРСоответствуетТребованиям(ДанныеЗЛ[ПравилоПроверки.ПутьКДанным], ТекстСообщения) Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1: %2'"), ДанныеЗЛ.Наименование, ТекстСообщения);		
		
		ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(Ошибки, ИсточникаДанныхЗЛ, ДанныеЗЛ.ФизическоеЛицо, НомерВПачке, ТекстОшибки, ПравилоПроверки.ПутьКДанным, Отказ);						
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПроверитьМестоРожденияЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилоПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь)
	Если ЗначениеЗаполнено(ДанныеЗЛ[ПравилоПроверки.ПутьКДанным])  
		И (СокрЛП(СтрЗаменить(ДанныеЗЛ[ПравилоПроверки.ПутьКДанным],",","")) = "" 
			Или СокрЛП(СтрЗаменить(ДанныеЗЛ[ПравилоПроверки.ПутьКДанным],",","")) = "0") Тогда
			
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1: не заполнено поле %2'"), 
																			ДанныеЗЛ.Наименование,
																			ПравилоПроверки.ПредставлениеПроверяемогоЭлемента);
																			
		ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(Ошибки, ИсточникаДанныхЗЛ, ДанныеЗЛ.ФизическоеЛицо,  НомерВПачке, ТекстОшибки, ПравилоПроверки.ПутьКДанным, Отказ);																		 
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПроверитьУдостоверениеЛичностиЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилаПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь)
	ПравилоПроверкиВидДокумента        = Неопределено;
	ПравилоПроверкиСерияДокумента      = Неопределено;
	ПравилоПроверкиНомерДокумента      = Неопределено;
	
	Для Каждого Правило Из ПравилаПроверки Цикл
		Если Правило.ПроверяемыйЭлемент = "ВидДокумента" Тогда
			ПравилоПроверкиВидДокумента = Правило;
			ВидДокумента = ДанныеЗЛ[ПравилоПроверкиВидДокумента.ПутьКДанным]
		ИначеЕсли Правило.ПроверяемыйЭлемент = "СерияДокумента" Тогда
			ПравилоПроверкиСерияДокумента = Правило;
		ИначеЕсли  Правило.ПроверяемыйЭлемент = "НомерДокумента" Тогда
			ПравилоПроверкиНомерДокумента = Правило;
	   	КонецЕсли;	
	КонецЦикла;	
	
	Если ПравилоПроверкиВидДокумента <> Неопределено 
		И ЗначениеЗаполнено(ВидДокумента) Тогда
		 
		ТекстОшибкиСерия = "";
		Если ПравилоПроверкиСерияДокумента <> Неопределено
		 	И Не ДокументыФизическихЛицКлиентСервер.СерияДокументаУказанаПравильно(ВидДокумента, ДанныеЗЛ[ПравилоПроверкиСерияДокумента.ПутьКДанным], ТекстОшибкиСерия) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1: %2.'"), ДанныеЗЛ.Наименование, ТекстОшибкиСерия);
					
			ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(Ошибки, ИсточникаДанныхЗЛ,  ДанныеЗЛ.ФизическоеЛицо, НомерВПачке, ТекстОшибки, ПравилоПроверкиСерияДокумента.ПутьКДанным, Отказ);	
		КонецЕсли;	
	КонецЕсли;	
	
	Если ПравилоПроверкиВидДокумента <> Неопределено 
		И ЗначениеЗаполнено(ВидДокумента) Тогда
		 
		ТекстОшибкиНомер = "";
		Если ПравилоПроверкиНомерДокумента <> Неопределено 
		 	И Не ДокументыФизическихЛицКлиентСервер.НомерДокументаУказанПравильно(ВидДокумента, ДанныеЗЛ[ПравилоПроверкиНомерДокумента.ПутьКДанным], ТекстОшибкиНомер) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1: %2.'"), ДанныеЗЛ.Наименование, ТекстОшибкиНомер);
					
			ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(Ошибки, ИсточникаДанныхЗЛ, ДанныеЗЛ.ФизическоеЛицо, НомерВПачке, ТекстОшибки, ПравилоПроверкиНомерДокумента.ПутьКДанным, Отказ);	
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры	

Процедура ПроверитьАдресЗастрахованногоЛица(ДанныеЗЛ, НомерВПачке, ПравилоПроверки, Ошибки, ИсточникаДанныхЗЛ, Отказ = Ложь)
	
	Адрес = ДанныеЗЛ[ПравилоПроверки.ПутьКДанным];
	
	Если ЗначениеЗаполнено(Адрес) Тогда
		
		СообщенияПроверки = "";
		РезультатПроверки = РаботаСАдресами.ПроверитьАдрес(Адрес);
		
		Если РезультатПроверки.СписокОшибок.Количество() <> 0 Тогда
			
			Для каждого СтруктураОшибки Из РезультатПроверки.СписокОшибок Цикл
				СообщенияПроверки = СообщенияПроверки + СтруктураОшибки.Представление + Символы.ПС;
			КонецЦикла;
			СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СообщенияПроверки, 1);
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Сотрудник %1: - поле ""%2"": %3'"),
					ДанныеЗЛ.Наименование,
					ПравилоПроверки.ПредставлениеПроверяемогоЭлемента,
					СообщенияПроверки);
			
			ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуЗаполненияДанныхЗЛ(
					Ошибки,
					ИсточникаДанныхЗЛ,
					ДанныеЗЛ.ФизическоеЛицо,
					НомерВПачке,
					ТекстОшибки,
					ПравилоПроверки.ПутьКДанным,
					Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВыгрузитьДокументы(ДокументСсылка, УникальныйИдентификатор)Экспорт
	
	Если ТипЗнч(ДокументСсылка) = Тип("СправочникСсылка.КомплектыОтчетностиПерсучета") Тогда
		МассивОписанийФайловВыгрузки = Новый Массив;

		КомплектОбъект = ДокументСсылка.ПолучитьОбъект();
		КомплектОбъект.ПоместитьОписанияФайловКомплектаВМассив(МассивОписанийФайловВыгрузки, УникальныйИдентификатор);
		
		Возврат МассивОписанийФайловВыгрузки;		
	Иначе	
		СоответсвиеТиповСсылокТипамФайловВыгрузки = СоответсвиеТиповСсылокТипамФайловВыгрузки();
		Кодировка =  КодировкаТекста.ANSI;
		ИмяФайла =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ИмяФайлаДляПФР");
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОписьПачекСЗВ_6") Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.СохранитьФайлВыгрузкиАДВ62(ДокументСсылка);
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			СсылкаНаДвоичныеДанныеФайла = "";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеестрСЗВ_6_2") Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.СохранитьФайлВыгрузкиСЗВ62(ДокументСсылка);
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3") Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.СохранитьФайлВыгрузкиСЗВ63(ДокументСсылка);
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4") Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.СохранитьФайлВыгрузкиСЗВ64(ДокументСсылка);
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СведенияОЗастрахованныхЛицахСЗВ_М") Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.СохранитьФайлВыгрузкиСЗВМ(ДокументСсылка);
			Кодировка = КодировкаТекста.UTF8;
			ИмяФайла =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ИмяФайлаДляПФР") +".xml";
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ") 
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР") 
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОписьОДВ_1")
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД")	Тогда
			СсылкаНаДвоичныеДанныеФайла = УчетСтраховыхВзносовУНФ.ПолучитьДанныеФайла(ДокументСсылка, Новый УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
			Кодировка = КодировкаТекста.UTF8;
			ИмяФайла =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ИмяФайлаДляПФР") +".xml";
		КонецЕсли;
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("Ссылка",  ДокументСсылка);
		ОписаниеФайла.Вставить("АдресФайлаВыгрузки", СсылкаНаДвоичныеДанныеФайла);
		ОписаниеФайла.Вставить("ИмяФайлаВыгрузки", ИмяФайла);
		ТипФайлаВыгрузки = СоответсвиеТиповСсылокТипамФайловВыгрузки.Получить(ТипЗнч(ДокументСсылка));
		ОписаниеФайла.Вставить("ТипФайлаВыгрузки",  ?(ТипФайлаВыгрузки = Неопределено, "", ТипФайлаВыгрузки));
		ОписаниеФайла.Вставить("КодировкаФайлаВыгрузки", Кодировка);

		Возврат ОписаниеФайла;
	КонецЕсли;
	
КонецФункции

Функция СписокНестрахуемыхВидовДохода() Экспорт
	МассивВидовДохода = Новый Массив;
	
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносамУНФ.ДенежноеДовольствиеВоеннослужащих);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносамУНФ.ДенежноеСодержаниеПрокуроров);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносамУНФ.ДоходыСтудентовЗаРаботуВСтудотрядеПоТрудовомуДоговору);
	МассивВидовДохода.Добавить(Справочники.ВидыДоходовПоСтраховымВзносамУНФ.ДоходыСтудентовЗаРаботуВСтудотрядеПоГражданскоПравовомуДоговору);

	Возврат МассивВидовДохода;	
КонецФункции	

Функция СоответсвиеТиповСсылокТипамФайловВыгрузки()Экспорт
	СоответствиеТиповСсылокТипамФайловВыгрузки = Новый Соответствие;
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ОписьПачекСЗВ_6"), "ПачкаАДВ");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.РегламентированныйОтчет"), "ПачкаРСВ");	
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.РеестрСЗВ_6_2"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_3"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ПачкаДокументовСЗВ_6_4"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ПачкаРазделов6РасчетаРСВ_1"), "пачкаСВВ");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.СведенияОЗастрахованныхЛицахСЗВ_М"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.ОписьОДВ_1"), "ПачкаИС");
	СоответствиеТиповСсылокТипамФайловВыгрузки.Вставить(Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД"), "ПачкаИС");
	
	Возврат СоответствиеТиповСсылокТипамФайловВыгрузки;
	
КонецФункции

Функция ПараметрыФормированияДокументовКвартальнойОтчетности() Экспорт
	ВариантыИсточниковДанныхСтажа = ВариантыИсточниковДанныхСтажа();
	ВариантыИсточниковДанныхНачисленныхВзносов = ВариантыИсточниковДанныхНачисленныхВзносов();
	ВариантыИсточниковДанныхУплаченныхВзносов = ВариантыИсточниковДанныхУплаченныхВзносов();
	ВариантыИсточниковДанныхСведенийОЗаработке = ВариантыИсточниковДанныхСведенийОЗаработке();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИсточникДанныхИсчисленныхВзносов", ВариантыИсточниковДанныхНачисленныхВзносов.ДанныеУчета);
	СтруктураПараметров.Вставить("ИсточникДанныхУплаченныхВзносов", ВариантыИсточниковДанныхУплаченныхВзносов.ДанныеУчета);
	СтруктураПараметров.Вставить("ИсточникДанныхСведенийОЗаработке", ВариантыИсточниковДанныхСведенийОЗаработке.ДанныеУчета);
    СтруктураПараметров.Вставить("ИсточникДанныхОСтаже", ВариантыИсточниковДанныхСтажа.ДанныеУчета);
	СтруктураПараметров.Вставить("ТипСведенийСЗВ", Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ);
	СтруктураПараметров.Вставить("КорректируемыйПериод");
	СтруктураПараметров.Вставить("УчитываемыеДокументы");
	СтруктураПараметров.Вставить("ДатаАктуальности", ТекущаяДатаСеанса());
	СтруктураПараметров.Вставить("НулевыеСведения", Ложь);
	СтруктураПараметров.Вставить("КорректируемыеДокументы", Новый Массив);
	СтруктураПараметров.Вставить("ОбновляемыеДокументы");
	СтруктураПараметров.Вставить("ЗамещаемыеДокументы");
	СтруктураПараметров.Вставить("СпециальныйДокумент2015года", Ложь);

	Возврат СтруктураПараметров;

КонецФункции

Функция ПараметрыДляСоздатьВТСведенияОВзносахЗаработкеСтажеЗЛ() Экспорт
	СтруктураПараметров = Новый Структура;
	
	ВариантыИсточниковДанныхСтажа = ВариантыИсточниковДанныхСтажа();
	ВариантыИсточниковДанныхНачисленныхВзносов = ВариантыИсточниковДанныхНачисленныхВзносов();
	ВариантыИсточниковДанныхУплаченныхВзносов = ВариантыИсточниковДанныхУплаченныхВзносов();
	ВариантыИсточниковДанныхСведенийОЗаработке = ВариантыИсточниковДанныхСведенийОЗаработке();
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИсточникДанныхИсчисленныхВзносов", ВариантыИсточниковДанныхНачисленныхВзносов.ДанныеУчета);
	СтруктураПараметров.Вставить("ИсточникДанныхУплаченныхВзносов", ВариантыИсточниковДанныхУплаченныхВзносов.ДанныеУчета);
	СтруктураПараметров.Вставить("ИсточникДанныхСведенийОЗаработке", ВариантыИсточниковДанныхСведенийОЗаработке.ДанныеУчета);
    СтруктураПараметров.Вставить("ИсточникДанныхОСтаже", ВариантыИсточниковДанныхСтажа.ДанныеУчета);
	СтруктураПараметров.Вставить("ТипСведенийСЗВ", Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ);
	СтруктураПараметров.Вставить("НулевыеСведения", Ложь);
	СтруктураПараметров.Вставить("КорректируемыеДокументы", Новый Массив);
	СтруктураПараметров.Вставить("УчитываемыеДокументы");
	СтруктураПараметров.Вставить("ОбновляемыеДокументы");
	СтруктураПараметров.Вставить("ОтчетныйПериодИсходныхСведений");
	СтруктураПараметров.Вставить("ДатаАктуальности", ТекущаяДатаСеанса());     
	СтруктураПараметров.Вставить("СпециальныйДокумент2015года", Ложь);
	
	Возврат СтруктураПараметров;
КонецФункции

Функция ПараметрыДляСоздатьВТСведенияОЗаработке() Экспорт
	СтруктураПараметров = Новый Структура;
	
	ВариантыИсточниковДанныхСведенийОЗаработке = ВариантыИсточниковДанныхСведенийОЗаработке();
	
	СтруктураПараметров.Вставить("ИсточникДанныхСведенийОЗаработке", ВариантыИсточниковДанныхСведенийОЗаработке.ДанныеУчета);
	СтруктураПараметров.Вставить("НулевыеСведения", Ложь);
	СтруктураПараметров.Вставить("УчитываемыеДокументы");
	СтруктураПараметров.Вставить("ТипСведенийСЗВ", Перечисления.ТипыСведенийСЗВ.ИСХОДНАЯ);
	СтруктураПараметров.Вставить("КорректируемыеДокументы", Новый Массив);
	СтруктураПараметров.Вставить("СпециальныйДокумент2015года", Ложь);

	
	Возврат СтруктураПараметров;
КонецФункции

Функция СтруктураОтбораДанныхДляКвартальнойОтчетности() Экспорт
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("КатегорияЗастрахованныхЛиц");
	СтруктураПараметров.Вставить("ТипДоговора");
	СтруктураПараметров.Вставить("СписокФизическихЛиц");
	СтруктураПараметров.Вставить("СписокФизическихЛицДляАвтоКорректировки", Новый Массив);
	
	Возврат СтруктураПараметров; 
КонецФункции	

// Процедура возвращает структуру с табличными документами разделов 2.5 и 6 формы РСВ-1.
//
// Параметры:
//  СсылкаНаСохрРеглОтчет - ссылка на сохраненный документ "РегламентированныйОтчет".
//  СтруктураРазделов     - Структура - структура с полями:
//    * Раздел25 - Массив - массив структур см. РегламентированнаяОтчетность.ПустаяСтруктураДляРазделовФормыРСВ_1;
//    * Раздел6  - Массив - массив структур см. РегламентированнаяОтчетность.ПустаяСтруктураДляРазделовФормыРСВ_1
//
Процедура ЗаполнитьПечатныеФормыРазделовРСВ1(СсылкаНаСохрРеглОтчет, СтруктураРазделов, НумероватьСтраницы, НомерПервойстраницыРаздела25, НомерПоследнейстраницыРаздела25, НомерПервойстраницыРаздела6, ОбщееКоличествоСтраницРаздела6) Экспорт
	Ссылка = Справочники.КомплектыОтчетностиПерсучета.КомплектСодержащийРСВ_1(СсылкаНаСохрРеглОтчет);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПараметрыФормирования = Справочники.КомплектыОтчетностиПерсучета.ПараметрыФормированияПечатныхФормКомплекта();
		
		Если СтруктураРазделов.Свойство("Раздел25") Тогда
			ПараметрыФормирования.ФормироватьПечатныеФормыРаздел25 = СтруктураРазделов.Раздел25.ПолучатьТабличныеДокументыРаздела;
			ПараметрыФормирования.ФормироватьСтруктуруРаздел25 = СтруктураРазделов.Раздел25.ПолучатьСтруктуруРаздела;
		КонецЕсли;	
		
		Если СтруктураРазделов.Свойство("Раздел6") Тогда
			Если СтруктураРазделов.Раздел6.ПолучатьСтруктуруРаздела Тогда
				ПараметрыФормирования.ФормироватьСтруктуруРаздел6 = Истина;
			Иначе
				
				ПараметрыФормирования.ФормироватьСтруктуруРаздел6 = Ложь;
				ПечатаемыеДокументы = Новый Массив;
				
				Для Каждого ОписаниеСтраницыРаздела6 Из СтруктураРазделов.Раздел6.СтраницыРаздела Цикл
					ПечатаемыеДокументы.Добавить(ОписаниеСтраницыРаздела6.Идентификатор);	
				КонецЦикла;	
				
				ПараметрыФормирования.ПечатаемыеДокументы = ПечатаемыеДокументы;
			КонецЕсли;
			
			ПараметрыФормирования.ФормироватьПечатныеФормыРаздел6 = СтруктураРазделов.Раздел6.ПолучатьТабличныеДокументыРаздела;	
		КонецЕсли;	
			
		
		ПечатныеФормыКомплекта2014Года = Справочники.КомплектыОтчетностиПерсучета.ПечатныеФормыКомплекта2014Года(
												Ссылка, 
												ПараметрыФормирования,
												НумероватьСтраницы,
												НомерПервойстраницыРаздела25,
												НомерПоследнейстраницыРаздела25,
												НомерПервойстраницыРаздела6,
												ОбщееКоличествоСтраницРаздела6);
												
		Если СтруктураРазделов.Свойство("Раздел25") Тогда 
			СтруктураРазделов.Раздел25.СтраницыРаздела = ПечатныеФормыКомплекта2014Года.Раздел25;
		КонецЕсли;	
							
		Если СтруктураРазделов.Свойство("Раздел6") Тогда
			СтруктураРазделов.Раздел6.СтраницыРаздела =  ПечатныеФормыКомплекта2014Года.Раздел6 ;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(СсылкаНаСохрРеглОтчет) Тогда
		ДанныеРСВ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаСохрРеглОтчет, "ДатаНачала, Организация, ДатаПодписи");
		ОписаниеРаздела25 = РегламентированнаяОтчетностьПереопределяемый.ПустаяСтруктураДляРазделовФормыРСВ_1();
		ОписаниеРаздела25.Описание = "Раздел 2.5";
		ОписаниеРаздела25.ТабличныйДокумент = Справочники.КомплектыОтчетностиПерсучета.ПустойРаздел25РасчетаРСВ1(
												ДанныеРСВ.Организация, 
												ДанныеРСВ.ДатаПодписи,
												НумероватьСтраницы, 
												НомерПервойстраницыРаздела25,
												НачалоКвартала(ДанныеРСВ.ДатаНачала));
												
												
												
		СтруктураРазделов.Раздел25.СтраницыРаздела.Добавить(ОписаниеРаздела25);	
	КонецЕсли;		

КонецПроцедуры

Процедура ВывестиДанныеПоБуквенно(Данные, Раздел, ИмяПоказателя, КоличествоЯчеек, НачальнаяЯчейка = 1) Экспорт

	Для НомЯчейки = НачальнаяЯчейка По НачальнаяЯчейка + КоличествоЯчеек - 1 Цикл
		Раздел.Области[ИмяПоказателя + Формат(НомЯчейки,"ЧГ=0")].Текст = Сред(Данные, НомЯчейки - НачальнаяЯчейка + 1, 1);
	КонецЦикла;

КонецПроцедуры

Процедура ПроставитьНомерСтраницыВЯчейках(НумероватьСтраницы, НомерСтраницы, ОбластьКолонтитула) Экспорт

	Если НумероватьСтраницы Тогда
		ВывестиДанныеПоБуквенно(СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Формат(НомерСтраницы,"ЧГ=0"), 6, "0", "Слева"), ОбластьКолонтитула, "НомСтр", 6);
	КонецЕсли;

КонецПроцедуры

Функция ОписаниеИсточникаДанныхДляСоздатьВТСведенияОбОрганизациях() Экспорт
	Описание = Новый Структура;
	Описание.Вставить("ИмяТаблицы");
	Описание.Вставить("ИмяПоляОрганизация");
	Описание.Вставить("ИмяПоляПериод");
	Описание.Вставить("СписокСсылок");
	
	Возврат Описание;
КонецФункции	

Функция ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию() Экспорт

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕКРЕТ);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЛОТПУСК);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ВРНЕТРУД);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ВАХТА);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕДНЕТРУД);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕСЯЦ);
	МассивПараметров.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
	
	Возврат МассивПараметров;
КонецФункции 

Процедура УдалитьПробелыРядомСДефисом(РедактируемаяСтрока) Экспорт 
	
	Пока Найти(РедактируемаяСтрока, " -") <> 0 Цикл 
		РедактируемаяСтрока = СтрЗаменить(РедактируемаяСтрока, " -", "-");
	КонецЦикла;
	
	Пока Найти(РедактируемаяСтрока, "- ") <> 0 Цикл 
		РедактируемаяСтрока = СтрЗаменить(РедактируемаяСтрока, "- ", "-");
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьВТСведенияОбОрганизацияхПоОписаниюДокументаИсточникаДанных(МенеджерВременныхТаблиц, ОписаниеИсточникаДанных) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокСсылок", ОписаниеИсточникаДанных.СписокСсылок);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсточникДанных.Организация КАК Организация,
	|	ИсточникДанных.Период КАК Период
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	#ИсточникДанных КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка В(&СписокСсылок)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Организация", "." + ОписаниеИсточникаДанных.ИмяПоляОрганизация); 
	Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Период", "." + ОписаниеИсточникаДанных.ИмяПоляПериод); 
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИсточникДанных", ОписаниеИсточникаДанных.ИмяТаблицы); 

	Запрос.Выполнить();
	
	СоздатьВТСведенияОбОрганизацияхПоВременнойТаблице(МенеджерВременныхТаблиц);	

	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВТОрганизации";
	
	Запрос.Выполнить();
	
КонецПроцедуры	

Процедура СоздатьВТСведенияОбОрганизацияхПоВременнойТаблице(МенеджерВременныхТаблиц) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Организация,
	|	Организации.Период,
	|	Организации.Организация КАК СтруктурнаяЕдиница
	|ПОМЕСТИТЬ ВТОрганизацииДляСреза
	|ИЗ
	|	ВТОрганизации КАК Организации";
	
	
	Запрос.Выполнить();
	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Организация,
	|	Организации.Период,
	|	ДанныеОрганизации.ИНН,
	|	ДанныеОрганизации.КодОрганаПФР,
	|	""""  КАК КодОрганаФСГС,
	|	"""" КАК КодПодчиненностиФСС,
	|	ДанныеОрганизации.КодПоОКПО,
	|	ДанныеОрганизации.НаименованиеПолное,
	|	ДанныеОрганизации.Наименование КАК НаименованиеСокращенное,
	|	ДанныеОрганизации.Наименование,
	|	ДанныеОрганизации.ОГРН,
	|	ДанныеОрганизации.КодОКВЭД,
	|	ДанныеОрганизации.НаименованиеОКВЭД,
	|	ДанныеОрганизации.КодОКОПФ,
	|	ДанныеОрганизации.КодОрганаФСС,
	|	"""" КАК НаименованиеОКОПФ,
	|	ДанныеОрганизации.КодОКФС,
	|	"""" КАК НаименованиеОКФС,
	|	ДанныеОрганизации.КПП КАК КПП,
	|	ЕСТЬNULL(ДанныеОрганизации.РегистрацияВНалоговомОргане, Значение(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)) КАК РегистрацияВНалоговомОргане,
	|	ЕСТЬNULL(ДанныеОрганизации.РегистрацияВНалоговомОргане.НаименованиеИФНС, """") КАК НаименованиеИФНС,
	|	ЕСТЬNULL(ДанныеОрганизации.РегистрацияВНалоговомОргане.КодПоОКАТО, """") КАК ОКАТО,
	|	"""" КАК ТерриториальныеУсловияПФР,
	|	ЕСТЬNULL(ДанныеОрганизации.РегистрационныйНомерПФР, """") КАК РегистрационныйНомерПФР,
	|	ДанныеОрганизации.ЮридическоеФизическоеЛицо,
	|	ДанныеОрганизации.ЮридическоеФизическоеЛицо,
	|	"""" КАК РайонныйКоэффициент,
	|	Организации.Организация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТСведенияОбОрганизациях
	|ИЗ
	|	ВТОрганизации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОрганизации
	|		ПО Организации.Организация = ДанныеОрганизации.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОрганизацииДляСреза";
		
	Запрос.Выполнить();	
	
КонецПроцедуры

Функция УзелФайлаПФР(ДеревоВыгрузки) Экспорт
	Атрибуты = Новый Соответствие;
	Атрибуты.Вставить("xmlns", "http://schema.pfr.ru");
	УзелПФР = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(ДеревоВыгрузки, "ФайлПФР", "", Атрибуты);	
	
	Возврат УзелПФР;
КонецФункции

Процедура ПроверитьЗаписиОСтаже(МенеджерВременныхТаблиц, Ссылка, ОтчетныйПериод, Отказ, РасширеннаяПроверка = Истина, ИспользоватьНовыйКлассификаторПараметровСтажа = Ложь, ВыделятьСтажДоговорников = Ложь) Экспорт
	Ошибки = Новый Массив;
	
	ДоговорыГПХ = Новый Массив;
	Если ВыделятьСтажДоговорников Тогда
		ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДОГОВОР);
		ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛАВТ);
		ДоговорыГПХ.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.НЕОПЛДОГ);
	КонецЕсли;	

	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДоговорыГПХ", ДоговорыГПХ);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаписиОСтаже.НомерСтроки КАК НомерСтрокиСтаж,
	|	ЗаписиОСтаже.НомерСтрокиСотрудник КАК НомерСтрокиСотрудник,
	|	ЗаписиОСтаже.Сотрудник КАК Сотрудник,
	|	ФизическиеЛица.Наименование КАК СотрудникНаименование,
	|	ЗаписиОСтаже.НомерОсновнойЗаписи,
	|	ЗаписиОСтаже.НомерДополнительнойЗаписи,
	|	ЗаписиОСтаже.ДатаНачалаПериода,
	|	ЗаписиОСтаже.ДатаОкончанияПериода,
	|	ТерриториальныеУсловияПФР.Код КАК ТерриториальныеУсловияКод,
	|	ЗаписиОСтаже.ТерриториальныеУсловия,
	|	ЗаписиОСтаже.ПараметрТерриториальныхУсловий КАК ТерриториальныеУсловияСтавка,
	|	ЗаписиОСтаже.ОсобыеУсловияТруда,
	|	ОсобыеУсловияТрудаПФР.Код КАК ОсобыеУсловияТрудаКод,
	|	ЗаписиОСтаже.КодПозицииСписка,
	|	СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспеченияУНФ.Код КАК КодПозицииСпискаКод,
	|	ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа,
	|	ОснованияИсчисляемогоСтраховогоСтажа.Код КАК ОснованиеИсчисляемогоСтажаКод,
	|	ЗаписиОСтаже.ПервыйПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ВторойПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа,
	|	ЗаписиОСтаже.ОснованиеВыслугиЛет,
	|	ОснованияДосрочногоНазначенияПенсии.Код КАК ОснованиеВыслугиЛетКод,
	|	ЗаписиОСтаже.ПервыйПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ВторойПараметрВыслугиЛет,
	|	ЗаписиОСтаже.ТретийПараметрВыслугиЛет,
	|	ПараметрыИсчисляемогоСтраховогоСтажа.Код КАК ТретийПараметрИсчисляемогоСтажаКод,
	|	ВЫБОР
	|		КОГДА ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа В (&ДоговорыГПХ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорГПХ
	|ИЗ
	|	ВТЗаписиОСтаже КАК ЗаписиОСтаже
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТерриториальныеУсловияПФР КАК ТерриториальныеУсловияПФР
	|		ПО ЗаписиОСтаже.ТерриториальныеУсловия = ТерриториальныеУсловияПФР.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТрудаПФР
	|		ПО ЗаписиОСтаже.ОсобыеУсловияТруда = ОсобыеУсловияТрудаПФР.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспеченияУНФ КАК СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспеченияУНФ
	|		ПО ЗаписиОСтаже.КодПозицииСписка = СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспеченияУНФ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОснованияИсчисляемогоСтраховогоСтажа КАК ОснованияИсчисляемогоСтраховогоСтажа
	|		ПО ЗаписиОСтаже.ОснованиеИсчисляемогоСтажа = ОснованияИсчисляемогоСтраховогоСтажа.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОснованияДосрочногоНазначенияПенсии КАК ОснованияДосрочногоНазначенияПенсии
	|		ПО ЗаписиОСтаже.ОснованиеВыслугиЛет = ОснованияДосрочногоНазначенияПенсии.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ЗаписиОСтаже.Сотрудник = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыИсчисляемогоСтраховогоСтажа КАК ПараметрыИсчисляемогоСтраховогоСтажа
	|		ПО ЗаписиОСтаже.ТретийПараметрИсчисляемогоСтажа = ПараметрыИсчисляемогоСтраховогоСтажа.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорГПХ,
	|	НомерСтрокиСотрудник,
	|	НомерСтрокиСтаж";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ДоговорГПХ") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтрокиСотрудник") Цикл
			ПроверитьЗаписиОСтажеПоСотруднику(Ссылка, ОтчетныйПериод, Выборка, РасширеннаяПроверка, Ошибки, Отказ, ИспользоватьНовыйКлассификаторПараметровСтажа, ВыделятьСтажДоговорников)		
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПроверитьЗаписиОСтажеПоСотруднику(Ссылка, ОтчетныйПериод, ВыборкаПоСтрокамДокумента, РасширеннаяПроверкаСтажа, Ошибки, Отказ, ИспользоватьНовыйКлассификаторПараметровСтажа = Ложь, ВыделятьСтажДоговорников = Ложь) Экспорт	
	ПараметрыИсчисляемогоСтраховогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа;
	
	ОкончаниеПериода = ПерсонифицированныйУчетКлиентСерверУНФ.ОкончаниеОтчетногоПериодаСтажаПерсУчета(ОтчетныйПериод);
	
	ПараметрыСтажаСПравомНаДосрочнуюПенсию = ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию();
	
	ТекущаяОсновнаяЗапись = Новый Структура("Сотрудник, ДатаНачалаПериода, ДатаОкончанияПериода, ОсобыеУсловияТруда, ОснованиеВыслугиЛет, ТерриториальныеУсловия, ОснованиеИсчисляемогоСтажа, ОснованиеВыслугиЛетКод, НомерОсновнойЗаписи, НомерДополнительнойЗаписи");
	СписокТерриториальныеУсловияЗаписи = Новый СписокЗначений;
	СписокУсловияТрудаЗаписи = Новый СписокЗначений;
	СписокВыслугаЛетЗаписи = Новый СписокЗначений;
	
	ПредыдущийНомерОсновнойЗаписи = 0;
	ПредыдущийНомерДополнительнойЗаписи = 0;
	
	НомерСтроки = 0;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.НомерСтрокиСтаж) Тогда
					
		Пока ВыборкаПоСтрокамДокумента.СледующийПоЗначениюПоля("НомерСтрокиСтаж") Цикл
			
			НомерСтроки = НомерСтроки + 1;
									
			// ПРОВЕРКА КОРРЕКТНОГО ЗАПОЛНЕНИЯ ДАТ НАЧАЛА И ОКОНЧАНИЯ ПЕРИОДА
			
			// По умолчанию считаем, что запись основная
			ЭтоОсновнаяЗапись = Истина;
			
			// Проверим, возможно ли появление в текущей позиции дополнительной записи о стаже
			ДополнительнаяЗаписьВозможна = ПредыдущийНомерОсновнойЗаписи <> 0;
			
			// Проверка заполнения реквизитов "ДатаНачалаПериода" и "ДатаОкончанияПериода". 
			Если Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачалаПериода) И Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончанияПериода) Тогда
				Если ДополнительнаяЗаписьВозможна Тогда
					ЭтоОсновнаяЗапись = Ложь;
				Иначе
					ТекстОшибки = НСтр("ru = 'не указана дата начала периода.'");     
					ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаНачалаПериода");
					
					ТекстОшибки = НСтр("ru = 'не указана дата окончания периода.'");     
					ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаОкончанияПериода");					
				КонецЕсли;	
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачалаПериода) Тогда
				ТекстОшибки = НСтр("ru = 'при указании даты окончания периода необходимо указывать дату начала периода.'");     
				ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаНачалаПериода");
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаОкончанияПериода) Тогда
				ТекстОшибки = НСтр("ru = 'при указании даты начала периода необходимо указывать дату окончания периода.'");     
				ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаОкончанияПериода");
				
			Иначе//Обе даты заполнены
				
				// Дата начала периода не должна быть ранее начала периода
				Если ВыборкаПоСтрокамДокумента.ДатаНачалаПериода < ОтчетныйПериод Тогда
					ТекстОшибки = НСтр("ru = 'дата начала периода должна быть не ранее начала периода.'");     
					ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаНачалаПериода");
					
				КонецЕсли;	 
				
				// Дата окончания периода не должна быть позднее окончания периода
				Если ВыборкаПоСтрокамДокумента.ДатаОкончанияПериода > ОкончаниеПериода Тогда
					ТекстОшибки = НСтр("ru = 'дата окончания периода должна быть не позднее окончания периода.'");     
					ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаОкончанияПериода");
				КонецЕсли;	 
				
				// Начало периода не должно быть позже окончания периода 
				Если ВыборкаПоСтрокамДокумента.ДатаНачалаПериода > ВыборкаПоСтрокамДокумента.ДатаОкончанияПериода Тогда
					ТекстОшибки = НСтр("ru = 'дата начала периода не должна превышать дату окончания периода.'");     
					ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаНачалаПериода");
				КонецЕсли;
				
			КонецЕсли;	 
			
			Если РасширеннаяПроверкаСтажа Тогда
				
				Если ЭтоОсновнаяЗапись Тогда
					
					// Проверка на хронологический порядок записей
					Если ТекущаяОсновнаяЗапись.ДатаОкончанияПериода <> Неопределено Тогда
						Если ВыборкаПоСтрокамДокумента.ДатаНачалаПериода <= ТекущаяОсновнаяЗапись.ДатаОкончанияПериода Тогда
							ТекстОшибки = НСтр("ru = 'дата начала периода должна быть больше даты окончания предыдущего периода'");     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ДатаНачалаПериода");
						КонецЕсли;	 
					КонецЕсли;
					
					// Переустановка текущей основной записи
					ТекущаяОсновнаяЗапись.Сотрудник = ВыборкаПоСтрокамДокумента.Сотрудник;
					ТекущаяОсновнаяЗапись.ДатаНачалаПериода = ВыборкаПоСтрокамДокумента.ДатаНачалаПериода;
					ТекущаяОсновнаяЗапись.ДатаОкончанияПериода = ВыборкаПоСтрокамДокумента.ДатаОкончанияПериода;
					ТекущаяОсновнаяЗапись.ОсобыеУсловияТруда = ВыборкаПоСтрокамДокумента.ОсобыеУсловияТруда;
					ТекущаяОсновнаяЗапись.ОснованиеВыслугиЛет = ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет;
					ТекущаяОсновнаяЗапись.ОснованиеВыслугиЛетКод = ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛетКод;
					ТекущаяОсновнаяЗапись.ТерриториальныеУсловия = ВыборкаПоСтрокамДокумента.ТерриториальныеУсловия;
					ТекущаяОсновнаяЗапись.ОснованиеИсчисляемогоСтажа = ВыборкаПоСтрокамДокумента.ОснованиеИсчисляемогоСтажа;
					ТекущаяОсновнаяЗапись.НомерОсновнойЗаписи = ВыборкаПоСтрокамДокумента.НомерОсновнойЗаписи;
					ТекущаяОсновнаяЗапись.НомерДополнительнойЗаписи = 0;
					
					СписокТерриториальныеУсловияЗаписи.Очистить();
					СписокУсловияТрудаЗаписи.Очистить();
					СписокВыслугаЛетЗаписи.Очистить();
				КонецЕсли;	 
				
				// ПРОВЕРКА ПРАВИЛЬНОСТИ НУМЕРАЦИИ ЗАПИСЕЙ
				
				// Проверка нумерации основных и дополнительных записей
				Если (ЭтоОсновнаяЗапись) Тогда
					Если Не ВыделятьСтажДоговорников Тогда  
						ОжидаемыйНомерОсновнойЗаписи = ПредыдущийНомерОсновнойЗаписи +1;
						Если (ВыборкаПоСтрокамДокумента.НомерОсновнойЗаписи <> ОжидаемыйНомерОсновнойЗаписи)  Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'неверный номер основной записи. Ожидается номер: %1'"), ОжидаемыйНомерОсновнойЗаписи);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "НомерОсновнойЗаписи");
						КонецЕсли;
					КонецЕсли;	
				Иначе
					ОжидаемыйНомерДополнительнойЗаписи = ПредыдущийНомерДополнительнойЗаписи +1;
					Если (ВыборкаПоСтрокамДокумента.НомерДополнительнойЗаписи <> ОжидаемыйНомерДополнительнойЗаписи)  Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'неверный номер дополнительной записи. Ожидается номер: %1'"), ОжидаемыйНомерДополнительнойЗаписи);     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "НомерДополнительнойЗаписи");
					КонецЕсли;
				КонецЕсли;
				
				//  ОПРЕДЕЛИМ, ЗАДАНЫ ЛИ РЕКВИЗИТЫ ОТДЕЛЬНЫХ ГРУПП
				
				// Признаки заполнения группы реквизитов "ТерриториальныеУсловия"
				УказаныТерриториальныеУсловия = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ТерриториальныеУсловия);
				
				// Признаки заполнения реквизитов группы "Особые условия труда"
				УказанКодПозицииСписка = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодПозицииСписка);
				УказаныОсобыеУсловияТруда = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОсобыеУсловияТруда);
				ЗаданаГруппаУсловияТруда = УказаныОсобыеУсловияТруда;
				
				// Признаки заполнения реквизитов группы "Исчисляемый стаж"
				УказаноОснованиеИсчисляемогоСтажа = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОснованиеИсчисляемогоСтажа);
				УказанТретийПараметрИсчисляемогоСтажа = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажа);
				ЗаданаГруппаИсчисляемыйСтаж = УказаноОснованиеИсчисляемогоСтажа ИЛИ УказанТретийПараметрИсчисляемогоСтажа;
				
				// Признаки заполнения реквизитов группы "ФАКТИЧЕСКИ-ОТРАБОТАННОЕ ВРЕМЯ"
				УказанПараметрИсчисляемогоСтажа1 = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПервыйПараметрИсчисляемогоСтажа); 
				УказанПараметрИсчисляемогоСтажа2 = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВторойПараметрИсчисляемогоСтажа); 
				ЗаданаГруппаФОВ = УказанПараметрИсчисляемогоСтажа1 ИЛИ УказанПараметрИсчисляемогоСтажа2;
				
				// Признаки заполнения реквизитов группы "ВыслугаЛет"
				УказаноОснованиеВыслугиЛет = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет);
				ЗаданаГруппаВыслугаЛет = УказаноОснованиеВыслугиЛет;
				
				//  ПРОВЕРКА НА ДОПУСТИМЫЕ СОЧЕТАНИЯ МЕЖДУ ГРУППАМИ СТАЖА ОСНОВНОЙ И ДОПОЛНИТЕЛЬНОЙ ЗАПИСЕЙ 
				
				Если НЕ ЭтоОсновнаяЗапись Тогда
					ДокументыСЗВПроверитьДополнительнойЗаписиСтажа(Ошибки, Ссылка, ВыборкаПоСтрокамДокумента, ТекущаяОсновнаяЗапись, ВыборкаПоСтрокамДокумента.НомерСтрокиСотрудник, НомерСтроки, Отказ);
				КонецЕсли;	 
				
				// ПРОВЕРКА ОСНОВНЫХ И ДОПОЛНИТЕЛЬНЫХ ЗАПИСЕЙ НА ДУБЛИ ЗНАЧЕНИЙ РЕКВИЗИТОВ
				
				Если УказаныТерриториальныеУсловия Тогда
					ПредставлениеЗначения = ""+ СОКРЛП(ВыборкаПоСтрокамДокумента.ТерриториальныеУсловияКод);
					Если СписокТерриториальныеУсловияЗаписи.НайтиПоЗначению(ПредставлениеЗначения) <> Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'указанные в дополнительной записи территориальные условия %1 не должны повторяться в рамках одной основной записи.'"), ПредставлениеЗначения);     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТерриториальныеУсловия");
					Иначе	
						СписокТерриториальныеУсловияЗаписи.Добавить(ПредставлениеЗначения);
					КонецЕсли;	 
				КонецЕсли;	 
				
				Если УказаныОсобыеУсловияТруда Тогда
					ПредставлениеЗначения = СОКРЛП(ВыборкаПоСтрокамДокумента.ОсобыеУсловияТрудаКод);
					Если СписокУсловияТрудаЗаписи.НайтиПоЗначению(ПредставлениеЗначения) <> Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'указанные в дополнительной записи условия труда %1 не должны повторяться в рамках одной основной записи.'"), ПредставлениеЗначения);     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ОсобыеУсловияТруда");
					Иначе	
						СписокУсловияТрудаЗаписи.Добавить(ПредставлениеЗначения);
					КонецЕсли;	 
				КонецЕсли;	 
				
				Если УказаноОснованиеВыслугиЛет Тогда
					ПредставлениеЗначения = СОКРЛП(ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛетКод);
					Если СписокВыслугаЛетЗаписи.НайтиПоЗначению(ПредставлениеЗначения) <> Неопределено Тогда
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'указанный в дополнительной записи код выслуги лет %1 не должны повторяться в рамках одной основной записи.'"), ПредставлениеЗначения);     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ОснованиеВыслугиЛет");
					Иначе	
						СписокВыслугаЛетЗаписи.Добавить(ПредставлениеЗначения);
					КонецЕсли;	 
				КонецЕсли;	
				
				//  ПРОВЕРКА ЗНАЧЕНИЙ РЕКВИЗИТОВ ГРУППЫ "Особые условия труда"
				
				КПС = СокрЛП(ВыборкаПоСтрокамДокумента.КодПозицииСпискаКод);
				
				НазваниеСписка ="";
				Если УказанКодПозицииСписка Тогда 
					
					Если НЕ УказаныОсобыеУсловияТруда Тогда
						ТекстОшибки = НСтр("ru = 'код позиции списка не может указываться без указания кода особых условий труда.'");     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
					КонецЕсли;	 
					
					ДлинаКПС = СтрДлина(КПС);
					
					Если (ДлинаКПС=8)ИЛИ(ДлинаКПС=14) Тогда
						
						Первые2СимволаКПС = Лев(КПС,2);
						Если (Первые2СимволаКПС = "10")ИЛИ(Первые2СимволаКПС = "11")ИЛИ(Первые2СимволаКПС = "12") Тогда
							НазваниеСписка = "Список№1";
						ИначеЕсли (Первые2СимволаКПС >= "20")И(Первые2СимволаКПС <= "23") Тогда
							НазваниеСписка = "Список№2";
						Иначе	
							ТекстОшибки = НСтр("ru = 'неверный код позиции списка вредных профессий.'");     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
						КонецЕсли;	
						
					ИначеЕсли (ДлинаКПС=5) Тогда
						НазваниеСписка = "ПрочиеСписки";
					Иначе	
						ТекстОшибки = НСтр("ru = 'длина кода позиции списка должна быть равна либо 5, либо 8, либо 14 символам.'");     
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
					КонецЕсли;	 
				КонецЕсли;
				
				Если УказаныОсобыеУсловияТруда Тогда
					
					// Проверки на возможные коды списков для конкретных условий труда
					
					КодОсобыхУсловийТруда = СОКРЛП(ВыборкаПоСтрокамДокумента.ОсобыеУсловияТрудаКод);
					Первые8СимволовКПС = ВРЕГ(Лев(КПС,8));
					
					Если ВыборкаПоСтрокамДокумента.ОсобыеУсловияТруда = Справочники.ОсобыеУсловияТрудаПФР.Ст27_1 Тогда
						Если (НазваниеСписка <> "Список№1") Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для особых условий труда с кодом %1 должен быть указан код позиции Списка №1.'"), КодОсобыхУсловийТруда);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
						КонецЕсли;	 
						
					ИначеЕсли ВыборкаПоСтрокамДокумента.ОсобыеУсловияТруда = Справочники.ОсобыеУсловияТрудаПФР.Ст27_2 Тогда
						
						Если (НЕ УказанКодПозицииСписка) Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для особых условий труда с кодом %1 должен быть указан код позиции Списка №1 или Списка №2.'"), КодОсобыхУсловийТруда);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
						КонецЕсли;	 
						
						Если (НазваниеСписка = "Список№1") Тогда
							Если (Первые8СимволовКПС <> "1010300Б")И(Первые8СимволовКПС <> "1010100Г")И(Первые8СимволовКПС <> "1010100Д")  Тогда
								ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для особых условий труда с кодом %1  указан неверный код позиции списка.'"), КодОсобыхУсловийТруда);     
								ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
							КонецЕсли;	
						ИначеЕсли (НазваниеСписка <> "Список№2") Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для особых условий труда с кодом %1 указан неверный код позиции списка.'"), КодОсобыхУсловийТруда);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
						КонецЕсли;	 
						
					Иначе
						// Для остальных кодов условий труда КПС указываться не должен
						Если УказанКодПозицииСписка Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для особых условий труда с кодом %1 код позиции списка указываться не должен.'"), КодОсобыхУсловийТруда);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "КодПозицииСписка");
						КонецЕсли;	 
					КонецЕсли;	 
				КонецЕсли;	
				//  ПРОВЕРКА ПРАВИЛЬНОСТИ ЗАПОЛНЕНИЯ РЕКВИЗИТОВ ГРУППЫ "Выслуга лет"
				
				УказанПараметр1 = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПервыйПараметрВыслугиЛет);
				УказанПараметр2 = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВторойПараметрВыслугиЛет);
				УказанПараметр3 = ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ТретийПараметрВыслугиЛет);
				ПараметрыУказаны = (УказанПараметр1) ИЛИ (УказанПараметр2) ИЛИ (УказанПараметр3);
				
				Если УказаноОснованиеВыслугиЛет Тогда
					
					КодОснования = СокрЛП(ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛетКод);
					
					УказанТолькоТретийПараметр = (НЕ УказанПараметр1) И (НЕ УказанПараметр2) И (УказанПараметр3);
					
					// Медицинские работники
					Если (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_СМ) 
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_ГД)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_СМХР)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_ГДХР) Тогда
						
						Если ДоляСтавкиУказанаНеверно(ВыборкаПоСтрокамДокумента.ТретийПараметрВыслугиЛет) Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 третий параметр должен содержать долю ставки (число от 0.1 до 2)'"), КодОснования);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТретийПараметрВыслугиЛет");
						Иначе
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 должен указываться только третий параметр'"), КодОснования);  
							Если УказанПараметр1 Тогда    
								ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрВыслугиЛет");
							КонецЕсли;	
							Если УказанПараметр2 Тогда
								ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");	
							КонецЕсли;	
						КонецЕсли;	
						
						// Педагогические работники	
					ИначеЕсли ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_ПД Тогда
						
						Если УказанПараметр2 Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 второй параметр указываться не должен.'"), КодОснования);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка,  ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");
						ИначеЕсли ДоляСтавкиУказанаНеверно(ВыборкаПоСтрокамДокумента.ТретийПараметрВыслугиЛет) Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 третий параметр должен содержать долю ставки (число от 0.1 до 2).'"), КодОснования);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТретийПараметрВыслугиЛет");
						КонецЕсли;	
						
					ИначеЕсли ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_ПДРК Тогда
						
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 второй параметр указываться не должен.'"), КодОснования);
						Если УказанПараметр2 Тогда     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");	
						ИначеЕсли УказанПараметр3 
							И ВыборкаПоСтрокамДокумента.ТретийПараметрВыслугиЛет <> 1 Тогда
							
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 третий параметр может содержать только ставку равную 0 или 1.'"), КодОснования);
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТретийПараметрВыслугиЛет");
						КонецЕсли;	
						
						//Летчики
					ИначеЕсли (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.САМОЛЕТ) 
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.СПЕЦАВ)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ИТСИСП)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ЛЕТИСП) 
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ИНСПЕКТ)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ОПЫТИСП)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ИСПКЛС1)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.ИТСМАВ)Тогда
						
						Если  УказанПараметр3 Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 параметр 3 указываться не должен.'"), КодОснования);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТретийПараметрВыслугиЛет");
						КонецЕсли;	 
						
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 параметры 1 и 2 должны содержать отработанное время (часы и минуты).'"), КодОснования);  
						Если  (НЕ УказанПараметр1) И (НЕ УказанПараметр2) Тогда   
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрВыслугиЛет");
						КонецЕсли;		
						//Парашютисты	
					ИначеЕсли (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.РЕАКТИВН)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.НОРМСП)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.НОРМАПР) Тогда	
						
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 параметры 2 и 3 указываться не должны.'"), КодОснования);
						Если УказанПараметр2 Тогда     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");
						КонецЕсли;	
						Если УказанПараметр3 Тогда     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа,  "ТретийПараметрВыслугиЛет");
						КонецЕсли;	
						
						// Управление воздушным движением и  инженерно-технический состав 	
					ИначеЕсли (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_14)
						ИЛИ (ВыборкаПоСтрокамДокумента.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_15) Тогда
						
						Если  УказанПараметр3 Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 параметр 3 указываться не должен.'"), КодОснования);     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ТретийПараметрВыслугиЛет");
						КонецЕсли;	 
						
						// Прочие коды выслуги лет	
					Иначе
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'для основания выслуги лет %1 параметры выслуги лет 1,2,3 указываться не должны.'"), КодОснования); 
						Если УказанПараметр1 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрВыслугиЛет");
						КонецЕсли;	
						Если УказанПараметр2 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");
						КонецЕсли;	
						Если УказанПараметр3 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа,  "ТретийПараметрВыслугиЛет");
						КонецЕсли;	
					КонецЕсли;	 
				Иначе
					Если ПараметрыУказаны Тогда
						ТекстОшибки = НСтр("ru = 'параметры выслуги лет 1,2,3 без указания основания выслуги лет указываться не должны.'");     
						Если УказанПараметр1 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрВыслугиЛет");
						КонецЕсли;	
						Если УказанПараметр2 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрВыслугиЛет");
						КонецЕсли;	
						Если УказанПараметр3 Тогда    
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа,  "ТретийПараметрВыслугиЛет");
						КонецЕсли;	
					КонецЕсли;	 
				КонецЕсли;	 
				
				
				
				//  ПРОВЕРКА ПРАВИЛЬНОСТИ ЗАПОЛНЕНИЯ РЕКВИЗИТОВ ГРУППЫ "Исчисляемый стаж"  (Основание + Третий параметр).
				Если УказанТретийПараметрИсчисляемогоСтажа Тогда
					
					ЭтоПараметрСПравомНаДосрочнуюПенсию = ПараметрыСтажаСПравомНаДосрочнуюПенсию.Найти(ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажа) <> Неопределено;
					Если Не ЭтоПараметрСПравомНаДосрочнуюПенсию И (УказаноОснованиеИсчисляемогоСтажа Или ЗаданаГруппаФОВ Или УказаноОснованиеВыслугиЛет Или ЗаданаГруппаУсловияТруда) Тогда
						ШаблонТекстаОшибки = НСтр("ru = 'при указании третьего параметра исчисляемого стажа ""%1"" группы реквизитов должны быть пустыми: Исчисляемый стаж: основание, Фактически отработанное время, Выслуга лет'");  
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												ШаблонТекстаОшибки,
												ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажаКод);
	
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа);
					КонецЕсли;
					Если ЭтоПараметрСПравомНаДосрочнуюПенсию И ЗаданаГруппаФОВ Тогда
						ШаблонТекстаОшибки = НСтр("ru = 'при указании третьего параметра исчисляемого стажа ""%1"" группа реквизитов фактически отработанного времени указываться не должна.'");  
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												ШаблонТекстаОшибки,
												ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажаКод);

						
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа);
					КонецЕсли;
					
					Если (ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕСЯЦ Или ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕДНЕТРУД)
						И Не УказаноОснованиеИсчисляемогоСтажа И Не УказаноОснованиеВыслугиЛет И Не ЗаданаГруппаУсловияТруда И Не УказаныТерриториальныеУсловия Тогда
				
						ШаблонТекстаОшибки = НСтр("ru = 'при указании третьего параметра исчисляемого стажа ""%1"" обязательно заполнение хотя бы одной из групп реквизитов ""Условия труда"", ""Исчисление стажа: код"", ""Досрочное назначение пенсии"".'");  
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												ШаблонТекстаОшибки,
												ВыборкаПоСтрокамДокумента.ТретийПараметрИсчисляемогоСтажаКод);
	
						ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа);
					КонецЕсли;
					
				КонецЕсли;	
						 				
				//  ПРОВЕРКА ПРАВИЛЬНОСТИ ЗАПОЛНЕНИЯ РЕКВИЗИТОВ ГРУППЫ "ФАКТИЧЕСКИ-ОТРАБОТАННОЕ ВРЕМЯ" (ПАРАМЕТРЫ 1,2 ИСЧИСЛЯЕМОГО СТАЖА)   
				
				Если ЗаданаГруппаФОВ Тогда
					Если (НЕ УказаныОсобыеУсловияТруда) И (НЕ УказаноОснованиеИсчисляемогоСтажа)
						И (НЕ УказаныТерриториальныеУсловия)  Тогда
						
						ТекстОшибки = НСтр("ru = 'параметры 1,2 исчисляемого стажа должны указываться только при заполнении других групп реквизитов (Территориальные условия, Условия труда, Исчисляемый стаж: основание).'");
						
						Если УказанПараметрИсчисляемогоСтажа1 Тогда
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрИсчисляемогоСтажа");
						КонецЕсли;
						Если УказанПараметрИсчисляемогоСтажа2 Тогда
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрИсчисляемогоСтажа");
						КонецЕсли;
					КонецЕсли;	
					
					Если ВыборкаПоСтрокамДокумента.ОснованиеИсчисляемогоСтажа	<>	Справочники.ОснованияИсчисляемогоСтраховогоСтажа.ВОДОЛАЗ Тогда
						
						Если ВыборкаПоСтрокамДокумента.ПервыйПараметрИсчисляемогоСтажа > 11 Тогда
							ТекстОшибки = НСтр("ru = 'первый параметр исчисляемого стажа (Месяцы) не должен быть больше 11.'");     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрИсчисляемогоСтажа");							
						КонецЕсли;	 	
						Если ВыборкаПоСтрокамДокумента.ВторойПараметрИсчисляемогоСтажа > 30 Тогда
							ТекстОшибки = НСтр("ru = 'второй параметр исчисляемого стажа (Дни) не должен быть больше 30.'");     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ВторойПараметрИсчисляемогоСтажа");
						КонецЕсли;	 	
						
						Лет = 0;
						Месяцев = 0;
						Дней = 0;
						ПолучитьСтажЗаПериод(ТекущаяОсновнаяЗапись.ДатаОкончанияПериода,	ТекущаяОсновнаяЗапись.ДатаНачалаПериода, Лет,	Месяцев, Дней);			
						ПолучитьПриведенныйСтаж(Лет,Месяцев,Дней);
						КалендарноеВремя = Лет*12*30+Месяцев*30+Дней;
						ФОВДляСравнения = ВыборкаПоСтрокамДокумента.ПервыйПараметрИсчисляемогоСтажа *30 + ВыборкаПоСтрокамДокумента.ВторойПараметрИсчисляемогоСтажа; 
						
						Если (ФОВДляСравнения > КалендарноеВремя) Тогда
							ТекстОшибки = НСтр("ru = 'указанное фактически отработанное время превышает календарное время.'");     
							ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаПоСтрокамДокумента, Отказ, РасширеннаяПроверкаСтажа, "ПервыйПараметрИсчисляемогоСтажа");
						КонецЕсли;	
					КонецЕсли;	 
					
				КонецЕсли;
				
			КонецЕсли;
			
			//  ПОДГОТОВКА К СЛЕДУЮЩЕЙ ИТЕРАЦИИ  
			
			// Запомним для следующей итерации
			ПредыдущийНомерОсновнойЗаписи = ВыборкаПоСтрокамДокумента.НомерОсновнойЗаписи;
			ПредыдущийНомерДополнительнойЗаписи = ВыборкаПоСтрокамДокумента.НомерДополнительнойЗаписи;
			
		КонецЦикла;//Цикл по записям стажа сотрудника	
	КонецЕсли;						
КонецПроцедуры	

// Выполняет проверку дополнительной записи стажа на допустимость значений реквизитов
Процедура ДокументыСЗВПроверитьДополнительнойЗаписиСтажа(Ошибки, Ссылка, Запись, ОсновнаяЗапись, НомерСтрокиСотрудник, НомерСтроки, Отказ) Экспорт
	СотрудникНаименование = Запись.СотрудникНаименование; 	
	
	Если НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ОсобыеУсловияТруда) 
		И НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ОснованиеВыслугиЛет)
		И НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ТерриториальныеУсловия)
		И НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ОснованиеИсчисляемогоСтажа) Тогда
		
		ТекстОшибки = НСтр("ru = 'дополнительная запись может вводиться только при заполнении хотя бы одного из реквизитов основной записи: Особые условия труда , Выслуга лет, Территориальные условия, Основание исчисляемого стажа.'");  
		ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина);
	КонецЕсли;	 
	
	Если ЗначениеЗаполнено(Запись.ОснованиеИсчисляемогоСтажа) Тогда
		ТекстОшибки =("ru = 'в дополнительной записи о стаже не может указываться основание исчисляемого стажа.'");     
		ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОснованиеИсчисляемогоСтажа");
	КонецЕсли;	
	
	// Проверка на обязательный ввод одной из льгот
	УказаныТерриториальныеУсловия = ЗначениеЗаполнено(Запись.ТерриториальныеУсловия);
	
	УказаныОсобыеУсловияТруда = ЗначениеЗаполнено(Запись.ОсобыеУсловияТруда);
	УказаноОснованиеВыслугиЛет = ЗначениеЗаполнено(Запись.ОснованиеВыслугиЛет);
	
	Если (НЕ УказаныТерриториальныеУсловия) И (НЕ УказаныОсобыеУсловияТруда) И (НЕ УказаноОснованиеВыслугиЛет) Тогда
		ТекстОшибки =НСтр("ru = 'в дополнительной записи должна быть задана либо территориальная льгота, либо особые условия труда, либо выслуга лет.'");     
		ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина);
	КонецЕсли;	 
	
	// Проверка на ввод территориальной льготы
	Если УказаныТерриториальныеУсловия Тогда
		Если  НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ТерриториальныеУсловия) Тогда
			ТекстОшибки = НСтр("ru = 'в дополнительной записи территориальная льгота может быть указана только при указании территориальной льготы в основной записи.'");     
			ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ТерриториальныеУсловия");
		КонецЕсли;	 
	КонецЕсли;	
	
	// Проверка на ввод условий труда
	Если УказаныОсобыеУсловияТруда Тогда
		УсловияТрудаЗаписи = Запись.ОсобыеУсловияТруда;
		Если (УсловияТрудаЗаписи <> Справочники.ОсобыеУсловияТрудаПФР.Ст27_1)
			И (УсловияТрудаЗаписи <> Справочники.ОсобыеУсловияТрудаПФР.Ст27_2)
			И (УсловияТрудаЗаписи <> Справочники.ОсобыеУсловияТрудаПФР.Ст27_7) Тогда
			ТекстОшибки = НСтр("ru = 'в дополнительной записи код условий труда должен принимать значение 27-1, 27-2 или 27-7.'");     
			ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОсобыеУсловияТруда");
		КонецЕсли;
		
		Если (УсловияТрудаЗаписи = Справочники.ОсобыеУсловияТрудаПФР.Ст27_7) 
			И (ОсновнаяЗапись.ОснованиеИсчисляемогоСтажа <> Справочники.ОснованияИсчисляемогоСтраховогоСтажа.УИК104) Тогда
			ТекстОшибки = НСтр("ru = 'в дополнительной записи код условий труда 27-7 может быть указан только в случае указания в основной записи кода исчисляемого стажа УИК104.'");     
			ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОсобыеУсловияТруда");
		КонецЕсли;	 
	КонецЕсли;
	
	// Проверка на ввод основания выслуги лет
	КодВыслугиЛетЗаписи = СОКРЛП(Запись.ОснованиеВыслугиЛетКод);
	
	Если НЕ ЗначениеЗаполнено(ОсновнаяЗапись.ОснованиеВыслугиЛет) Тогда
		Если СтрНайти(",,27-СМ,27-ГД,27-СМХР,27-ГДХР,",","+КодВыслугиЛетЗаписи+",") = 0 Тогда
			ТекстОшибки = НСтр("ru = 'если в основной записи не указан код выслуги лет, то в дополнительной записи основание выслуги лет должно быть пустым или указаны коды 28-СМ,28-ГД,28-СМХР или 28-ГДХР.'");     
			ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОснованиеВыслугиЛет");
		КонецЕсли;	
	Иначе//В основной записи код выслуги указан
		
		МассивПравилНаКодыВыслугиДопЗаписей =  Новый Массив;
		МассивПравилНаКодыВыслугиДопЗаписей.Добавить(",ЛЕТИСП,САМОЛЕТ,СПЕЦАВ,ОПЫТИСП,ИСПКЛС1,ИТСМАВ,");
		МассивПравилНаКодыВыслугиДопЗаписей.Добавить("НОРМАПР,РЕАКТИВН,НОРМСП,");
		МассивПравилНаКодыВыслугиДопЗаписей.Добавить(",,27-СМ,27-ГД,27-СМХР,27-ГДХР,");//пустой код допустим
		
		КодВыслугиЛетОсновнойЗаписи = СОКРЛП(ОсновнаяЗапись.ОснованиеВыслугиЛетКод);
		
		ПроверочнаяСтрокаКодов = "";
		Для Каждого СтрокаКодов Из МассивПравилНаКодыВыслугиДопЗаписей Цикл 
			Если СтрНайти(СтрокаКодов,","+КодВыслугиЛетОсновнойЗаписи+",") Тогда
				ПроверочнаяСтрокаКодов = СтрокаКодов;
				Прервать;
			КонецЕсли;	 
		КонецЦикла;	
		
		Если ПроверочнаяСтрокаКодов = "" Тогда
			ШаблонТекстаОшибки = НСтр("ru = 'при указании в основной записи кода выслуги лет %3 в дополнительной записи код выслуги указываться не может.'");  
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки, КодВыслугиЛетЗаписи);
			ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОснованиеВыслугиЛет");
 		Иначе
			// Код выслуги основной записи исключим из списка возможных значений
			ПроверочнаяСтрокаКодов = СтрЗаменить(ПроверочнаяСтрокаКодов,","+КодВыслугиЛетОсновнойЗаписи,"");
			
			Если НЕ СтрНайти(ПроверочнаяСтрокаКодов,","+КодВыслугиЛетЗаписи+",") Тогда
				ШаблонТекстаОшибки = НСтр("ru = 'при указании в основной записи кода выслуги лет %1 в дополнительной записи могут быть указаны только следующие коды выслуги: %2.'");
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки, КодВыслугиЛетОсновнойЗаписи, Сред(ПроверочнаяСтрокаКодов,2));     
				ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, Запись, Отказ, Истина, "ОснованиеВыслугиЛет");
 			КонецЕсли;	 
		КонецЕсли;	 
	КонецЕсли;	 
	
КонецПроцедуры	 

Процедура ДокументыСЗВДобавитьОшибкуСтажа(Ошибки, ТекстОшибки, Ссылка, ВыборкаЗаписейСтажа, Отказ, РасширеннаяПроверка = Истина, Поле = "") Экспорт
	Если РасширеннаяПроверка Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1, записи о стаже, № основной записи %2 %3: %4'"), ВыборкаЗаписейСтажа.СотрудникНаименование, ВыборкаЗаписейСтажа.НомерОсновнойЗаписи, ?(ЗначениеЗаполнено(ВыборкаЗаписейСтажа.НомерДополнительнойЗаписи), СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '№ дополнительной записи %1'"), ВыборкаЗаписейСтажа.НомерДополнительнойЗаписи), ""), ТекстОшибки);
	Иначе 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сотрудник %1, записи о стаже: %2'"), ВыборкаЗаписейСтажа.СотрудникНаименование, ТекстОшибки);
	КонецЕсли;	
	
	Если РасширеннаяПроверка Тогда
		НомерОсновнойЗаписи = ВыборкаЗаписейСтажа.НомерОсновнойЗаписи;
		НомерДополнительнойЗаписи = ВыборкаЗаписейСтажа.НомерДополнительнойЗаписи;
	Иначе	
		НомерОсновнойЗаписи = 0;
		НомерДополнительнойЗаписи = 0;
	КонецЕсли;	
		
	ПерсонифицированныйУчетКлиентСерверУНФ.ДобавитьОшибкуДанныхОСтаже(Ошибки, 
			Ссылка, 
			ВыборкаЗаписейСтажа.НомерСтрокиСотрудник,
			ВыборкаЗаписейСтажа.НомерСтрокиСтаж, 
			ТекстСообщения, 
			Поле, 
			Отказ);
	
КонецПроцедуры

Функция ПечатнаяФормаОДВ_1(МассивОбъектов, ОбъектыПечати, ИмяДокумента) Экспорт 
	
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечатиОДВ_1(МассивОбъектов, ИмяДокумента).Выбрать();
	ВыборкаПоПоДаннымДосрочногоНазначенияПенсии = ЗапросПоДаннымДосрочногоНазначенияПенсииОДВ_1(МассивОбъектов, ИмяДокумента).Выбрать();

	ЭтоПервыйДокументКоллекции = Истина;
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет = ?(ВыборкаПоШапкеДокумента.Дата < '20220819',
		УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ФормаОДВ_1_2019"),
		УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ФормаОДВ_1_2022"));
		
		ДокументРезультат = Новый ТабличныйДокумент;
		ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОДВ_1";
		ДокументРезультат.ПолеСлева = 0;
		ДокументРезультат.ПолеСправа = 0;
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьСтрокаПозиции = Макет.ПолучитьОбласть("СтрокаПозиции");
		ОбластьУплатаВзносовСтрока = Макет.ПолучитьОбласть("УплатаВзносовСтрока");
		ОбластьШапкаРаздела5 = Макет.ПолучитьОбласть("ШапкаРаздела5");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		Если ЭтоПервыйДокументКоллекции Тогда
			ЭтоПервыйДокументКоллекции = Ложь;	
		Иначе
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;	
		
		ТекущийЛист = Новый ТабличныйДокумент;
		ТекущийЛист.ПолеСлева = 0;
		ТекущийЛист.ПолеСправа = 0;
		ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
		
		РегистрационныйНомерПФР = СтрЗаменить(ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР," ","");
		РегистрационныйНомерПФР = СтрЗаменить(РегистрационныйНомерПФР,"-","");
		ВывестиДанныеПоБуквенно(РегистрационныйНомерПФР, ОбластьШапка, "НомРег", 12);
		
		ИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ВыборкаПоШапкеДокумента.ИНН, 12, "-", "Справа");
		ВывестиДанныеПоБуквенно(ИНН, ОбластьШапка, "ИНН", 12);
		
		ВывестиДанныеПоБуквенно(Формат(ВыборкаПоШапкеДокумента.КПП,"ЧГ=0"), ОбластьШапка, "КПП", 9);
		ВывестиДанныеПоБуквенно(Формат(Год(ВыборкаПоШапкеДокумента.ОтчетныйПериод),"ЧГ=0"), ОбластьШапка, "Год", 4);
		
		ОбластьШапка.Параметры.НаименованиеОрганизации = ВыборкаПоШапкеДокумента.НаименованиеОрганизации;
		ОбластьШапка.Параметры.Организация = ВыборкаПоШапкеДокумента.Организация;
		ОбластьШапка.Параметры.КодОтчетногоПериода = ПерсонифицированныйУчетКлиентСерверУНФ.КодОтчетногоПериода(ВыборкаПоШапкеДокумента.ОтчетныйПериод);
		
		Если ИмяДокумента = "ОписьОДВ_1" Тогда 
			Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ Тогда 
				ОбластьШапка.Параметры.Корректирующая = Истина;
			Иначе 
				ОбластьШапка.Параметры.Отменяющая = Истина;
			КонецЕсли;
		Иначе 
			ОбластьШапка.Параметры.Исходная = Истина;
		КонецЕсли;
		
		Если ИмяДокумента = "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ" Тогда 
			ОбластьШапка.Параметры.КоличествоЗастрахованныхЛицСЗВ_Стаж = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;
		ИначеЕсли ИмяДокумента = "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ" Тогда
			ОбластьШапка.Параметры.КоличествоЗастрахованныхЛицСЗВ_ИСХ = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;
		ИначеЕсли ИмяДокумента = "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР" Тогда
			ОбластьШапка.Параметры.КоличествоЗастрахованныхЛицСЗВ_КОРР = ВыборкаПоШапкеДокумента.КоличествоЗастрахованныхЛиц;
		КонецЕсли;
		
		ЕстьДанныеНачисленныхУплаченныхВзносов = ИмяДокумента = "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ" ; 
		
		Если ЕстьДанныеНачисленныхУплаченныхВзносов Тогда 
			СтруктураПоиска = Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка);
		КонецЕсли;
		ТекущийЛист.Вывести(ОбластьШапка);
		
		ТекущийЛист.Вывести(ОбластьУплатаВзносовСтрока);
		ТекущийЛист.Вывести(ОбластьУплатаВзносовСтрока);
		
		ТекущийЛист.Вывести(ОбластьШапкаРаздела5);
		Если ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.Количество() > 0 Тогда 

			СтруктураПоиска = Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка);
			Если ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.НайтиСледующий(СтруктураПоиска) Тогда
				КоличествоРабочихМест = 0;
				КоличествоРаботающих = 0;
				ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.СледующийПоЗначениюПоля("Ссылка");
				Пока ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.Следующий() Цикл 
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаПозиции.Параметры, ВыборкаПоПоДаннымДосрочногоНазначенияПенсии);
					Если ЗначениеЗаполнено(ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОсобыеУсловияТруда) Тогда 
						ОбластьСтрокаПозиции.Параметры.ОснованиеВыслугиЛетКод = ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОсобыеУсловияТрудаКод;
					ИначеЕсли ЗначениеЗаполнено(ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОснованиеДосрочногоНазначенияПенсии) Тогда 
						ОбластьСтрокаПозиции.Параметры.ОснованиеВыслугиЛетКод = ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОснованиеДосрочногоНазначенияПенсииКод;
					КонецЕсли;
					ВывестиОбластьОДВ_1(ОбластьСтрокаПозиции, ДокументРезультат, ТекущийЛист);
					// Вывод дополнительного основания выслуги лет
					Если ЗначениеЗаполнено(ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОсобыеУсловияТруда)
						И ЗначениеЗаполнено(ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОснованиеДосрочногоНазначенияПенсии) Тогда
						ОчиститьОбласть(ОбластьСтрокаПозиции);
						ОбластьСтрокаПозиции.Параметры.ОснованиеВыслугиЛетКод = ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.ОснованиеДосрочногоНазначенияПенсииКод;
						ВывестиОбластьОДВ_1(ОбластьСтрокаПозиции, ДокументРезультат, ТекущийЛист);
					КонецЕсли;
					КоличествоРабочихМест = КоличествоРабочихМест + ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.КоличествоРабочихМест;
					КоличествоРаботающих = КоличествоРаботающих + ВыборкаПоПоДаннымДосрочногоНазначенияПенсии.КоличествоРаботающих;
				КонецЦикла;
				ОбластьПодвал.Параметры.КоличествоРабочихМест = КоличествоРабочихМест;
				ОбластьПодвал.Параметры.КоличествоРаботающих = КоличествоРаботающих;
			КонецЕсли;
		Иначе 
			ТекущийЛист.Вывести(ОбластьСтрокаПозиции);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры, ВыборкаПоШапкеДокумента);
		ВывестиОбластьОДВ_1(ОбластьПодвал, ДокументРезультат, ТекущийЛист);
		
		ДокументРезультат.Вывести(ТекущийЛист);
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиОбластьОДВ_1(ВыводимаяОбласть, ДокументРезультат, ТекущийЛист)
	
	ВыводимыеОбласти = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыводимаяОбласть);
	Если ТекущийЛист.ПроверитьВывод(ВыводимыеОбласти) Тогда 
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	Иначе 
		ДокументРезультат.Вывести(ТекущийЛист);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ТекущийЛист = Новый ТабличныйДокумент;
		ТекущийЛист.ПолеСлева = 0;
		ТекущийЛист.ПолеСправа = 0;
		ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	КонецЕсли;	
	
КонецПроцедуры 

Процедура ОчиститьОбласть(ОбластьМакета)
	
	Для Счетчик = 0 По ОбластьМакета.Параметры.Количество() - 1 Цикл
		ОбластьМакета.Параметры[Счетчик] = ""
	КонецЦикла;
	
КонецПроцедуры

Функция ЗапросПоШапкеДляПечатиОДВ_1(МассивСсылок, ИмяДокумента) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ОписаниеИсточникаДанных = ОписаниеИсточникаДанныхДляСоздатьВТСведенияОбОрганизациях();
	ОписаниеИсточникаДанных.ИмяТаблицы = "Документ" + "." + ИмяДокумента;
	ОписаниеИсточникаДанных.ИмяПоляОрганизация = "Организация";
	ОписаниеИсточникаДанных.ИмяПоляПериод = "ОкончаниеОтчетногоПериода";
	ОписаниеИсточникаДанных.СписокСсылок = МассивСсылок;

	СоздатьВТСведенияОбОрганизацияхПоОписаниюДокументаИсточникаДанных(Запрос.МенеджерВременныхТаблиц, ОписаниеИсточникаДанных);
	
	Если ИмяДокумента = "ОписьОДВ_1" Тогда 
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК Количество,
		|	ЗНАЧЕНИЕ(Документ.ОписьОДВ_1.ПустаяСсылка) КАК Ссылка
		|ПОМЕСТИТЬ ВТИтоги";
		Запрос.Выполнить();
	Иначе 
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(Сотрудники.НомерСтроки) КАК Количество,
		|	Сотрудники.Ссылка
		|ПОМЕСТИТЬ ВТИтоги
		|ИЗ
		|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	Сотрудники.Ссылка";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ", ИмяДокумента);
		Запрос.Выполнить();
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация,
	|	СведенияОбОрганизациях.НаименованиеСокращенное КАК НаименованиеОрганизации,
	|	СведенияОбОрганизациях.ИНН КАК ИНН,
	|	СведенияОбОрганизациях.КПП КАК КПП,
	|	СведенияОбОрганизациях.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо,
	|	ГОД(ДанныеДокумента.ОтчетныйПериод) КАК Год,
	|	ДанныеДокумента.ОтчетныйПериод,
	|	ДанныеДокумента.ОкончаниеОтчетногоПериода,
	|	ДанныеДокумента.КорректируемыйПериод КАК КорректируемыйПериод,
	|	ДанныеДокумента.ТипСведений КАК ТипСведений,
	|	ДанныеДокумента.Руководитель КАК Руководитель,
	|	ДанныеДокумента.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ДанныеДокумента.Дата,
	|	СведенияОбОрганизациях.КодПоОКПО КАК КодПоОКПО,
	|	СведенияОбОрганизациях.ОГРН КАК ОГРН,
	|	СведенияОбОрганизациях.НаименованиеПолное КАК НаименованиеПолное,
	|	СведенияОбОрганизациях.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	СведенияОбОрганизациях.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР,
	|	Итоги.Количество КАК КоличествоЗастрахованныхЛиц,
	|	ДанныеДокумента.НачисленыВзносыНаОПС КАК НачисленыВзносыНаОПС,
	|	ДанныеДокумента.НачисленыВзносыПоДТ КАК НачисленыВзносыПоДТ,
	|	ДанныеДокумента.ИмяФайлаДляПФР
	|ПОМЕСТИТЬ ВТДанныеДокументов
	|ИЗ
	|	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОбОрганизациях КАК СведенияОбОрганизациях
	|		ПО ДанныеДокумента.Организация = СведенияОбОрганизациях.Организация
	|			И ДанныеДокумента.ОкончаниеОтчетногоПериода = СведенияОбОрганизациях.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтоги КАК ИТОГИ
	|		ПО ДанныеДокумента.Ссылка = Итоги.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивСсылок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Руководитель";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ", ИмяДокумента);
	
	Если ИмяДокумента <> "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.НачисленыВзносыНаОПС", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.НачисленыВзносыПоДТ", "ЛОЖЬ");
	КонецЕсли;
	
	Если ИмяДокумента <> "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.КорректируемыйПериод", "ДАТАВРЕМЯ(1, 1, 1)");
	КонецЕсли;
	
	Если ИмяДокумента = "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.ТипСведений", "NULL");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОбОрганизациях.ИНН", "ДанныеДокумента.ИНН");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОбОрганизациях.КПП", "ДанныеДокумента.КПП");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОбОрганизациях.РегистрационныйНомерПФР", "ДанныеДокумента.РегистрационныйНомерПФР");
	КонецЕсли;	
	
	Если ИмяДокумента = "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.ВсегоРаботающихДНП", "0");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеДокумента.ВсегоРабочихМестДНП", "0");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	
	УчетСтраховыхВзносовУНФ.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.РегистрационныйНомерПФР КАК РегистрационныйНомерПФР,
	|	ДанныеДокументов.НаименованиеОрганизации КАК НаименованиеОрганизации,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо,
	|	ДанныеДокументов.ИНН КАК ИНН,
	|	ДанныеДокументов.КПП КАК КПП,
	|	ДанныеДокументов.ОГРН КАК ОГРН,
	|	ДанныеДокументов.КодПоОКПО КАК ОКПО,
	|	ДанныеДокументов.НаименованиеПолное КАК НаименованиеПолное,
	|	ДанныеДокументов.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	ДанныеДокументов.Год КАК Год,
	|	ДанныеДокументов.ОтчетныйПериод КАК ОтчетныйПериод,
	|	ДанныеДокументов.ОкончаниеОтчетногоПериода КАК ОкончаниеОтчетногоПериода,
	|	ДанныеДокументов.КорректируемыйПериод КАК КорректируемыйПериод,
	|	ДанныеДокументов.ТипСведений КАК ТипСведений,
	|	ДанныеДокументов.Руководитель КАК Руководитель,
	|	ЕСТЬNULL(ВТФИОПоследние.Фамилия, """") КАК ФамилияРуководителя,
	|	ЕСТЬNULL(ВТФИОПоследние.Имя, """") КАК ИмяРуководителя,
	|	ЕСТЬNULL(ВТФИОПоследние.Отчество, """") КАК ОтчествоРуководителя,
	|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ДанныеДокументов.Дата КАК Дата,
	|	ДанныеДокументов.КоличествоЗастрахованныхЛиц КАК КоличествоЗастрахованныхЛиц,
	|	ДанныеДокументов.НачисленыВзносыНаОПС КАК НачисленыВзносыНаОПС,
	|	ДанныеДокументов.НачисленыВзносыПоДТ КАК НачисленыВзносыПоДТ,
	|	ДанныеДокументов.ИмяФайлаДляПФР КАК ИмяФайлаДляПФР
	|ИЗ
	|	ВТДанныеДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ВТФИОПоследние
	|		ПО ДанныеДокументов.Ссылка = ВТФИОПоследние.Ссылка
	|			И ДанныеДокументов.Руководитель = ВТФИОПоследние.ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипСведений,
	|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоДаннымДосрочногоНазначенияПенсииОДВ_1(МассивСсылок, ИмяДокумента) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДосрочноеНазначениеПенсии.Ссылка КАК Ссылка,
	               |	ДосрочноеНазначениеПенсии.НомерСтроки КАК НомерСтроки,
	               |	ДосрочноеНазначениеПенсии.Подразделение КАК Подразделение,
	               |	ДосрочноеНазначениеПенсии.Подразделение.Наименование КАК НаименованиеПодразделения,
	               |	ДосрочноеНазначениеПенсии.Должность КАК Должность,
	               |	ДосрочноеНазначениеПенсии.Должность.Наименование КАК НаименованиеДолжности,
	               |	ДосрочноеНазначениеПенсии.КоличествоРабочихМест КАК КоличествоРабочихМест,
	               |	ДосрочноеНазначениеПенсии.КоличествоРаботающих КАК КоличествоРаботающих,
	               |	ДосрочноеНазначениеПенсии.ХарактерВыполняемыхРабот КАК ХарактерВыполняемыхРабот,
	               |	ДосрочноеНазначениеПенсии.ПервичныеДокументы КАК ПервичныеДокументы,
	               |	ДосрочноеНазначениеПенсии.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
	               |	ДосрочноеНазначениеПенсии.ОсобыеУсловияТруда.Код КАК ОсобыеУсловияТрудаКод,
	               |	ДосрочноеНазначениеПенсии.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеДосрочногоНазначенияПенсии,
	               |	ДосрочноеНазначениеПенсии.ОснованиеДосрочногоНазначенияПенсии.Код КАК ОснованиеДосрочногоНазначенияПенсииКод,
	               |	ДосрочноеНазначениеПенсии.КодПозицииСписка КАК КодПозицииСписка,
	               |	ДосрочноеНазначениеПенсии.КодПозицииСписка.Код КАК КодПозицииСпискаКод
	               |ИЗ
	               |	Документ.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ.ДосрочноеНазначениеПенсии КАК ДосрочноеНазначениеПенсии
	               |ГДЕ
	               |	ДосрочноеНазначениеПенсии.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	НомерСтроки";
	
	Если ИмяДокумента <> "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ", ИмяДокумента);
	КонецЕсли;
	
	Возврат Запрос.Выполнить();			   
	
КонецФункции

Функция ЗапросПоДаннымНачисленныхУплаченныхВзносовОДВ_1(МассивСсылок, ИмяДокумента) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеОКорректировкеСведений.Ссылка
	               |ПОМЕСТИТЬ ВТСписокДокументов
	               |ИЗ
	               |	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР КАК ДанныеОКорректировкеСведений
	               |ГДЕ
	               |	ДанныеОКорректировкеСведений.Ссылка В(&МассивСсылок)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеНачисленныхУплаченныхВзносов.Ссылка,
	               |	ДанныеНачисленныхУплаченныхВзносов.ВидСтраховыхВзносов,
	               |	ДанныеНачисленныхУплаченныхВзносов.ЗадолженностьНаНачалоПериода,
	               |	ДанныеНачисленныхУплаченныхВзносов.Начислено,
	               |	ДанныеНачисленныхУплаченныхВзносов.Уплачено,
	               |	ДанныеНачисленныхУплаченныхВзносов.ЗадолженностьНаКонецПериода
	               |ПОМЕСТИТЬ ВТДанныеОВзносах
	               |ИЗ
	               |	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.НачисленныеУплаченныеВзносы КАК ДанныеНачисленныхУплаченныхВзносов
	               |ГДЕ
	               |	ДанныеНачисленныхУплаченныхВзносов.Ссылка В(&МассивСсылок)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокДокументов.Ссылка КАК Ссылка,
	               |	ВЫБОР
	               |		КОГДА Страховая.Ссылка ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ЕстьСтраховая,
	               |	ЕСТЬNULL(Страховая.ЗадолженностьНаНачалоПериода, 0) КАК ЗадолженностьНаНачалоСтраховая,
	               |	ЕСТЬNULL(Страховая.Начислено, 0) КАК НачисленоСтраховая,
	               |	ЕСТЬNULL(Страховая.Уплачено, 0) КАК УплаченоСтраховая,
	               |	ЕСТЬNULL(Страховая.ЗадолженностьНаКонецПериода, 0) КАК ЗадолженностьНаКонецСтраховая,
	               |	ВЫБОР
	               |		КОГДА Накопительная.Ссылка ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ЕстьНакопительная,
	               |	ЕСТЬNULL(Накопительная.ЗадолженностьНаНачалоПериода, 0) КАК ЗадолженностьНаНачалоНакопительная,
	               |	ЕСТЬNULL(Накопительная.Начислено, 0) КАК НачисленоНакопительная,
	               |	ЕСТЬNULL(Накопительная.Уплачено, 0) КАК УплаченоНакопительная,
	               |	ЕСТЬNULL(Накопительная.ЗадолженностьНаКонецПериода, 0) КАК ЗадолженностьНаКонецНакопительная,
	               |	ВЫБОР
	               |		КОГДА ТарифСВ.Ссылка ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ЕстьТарифСВ,
	               |	ЕСТЬNULL(ТарифСВ.ЗадолженностьНаНачалоПериода, 0) КАК ЗадолженностьНаНачалоТарифСВ,
	               |	ЕСТЬNULL(ТарифСВ.Начислено, 0) КАК НачисленоТарифСВ,
	               |	ЕСТЬNULL(ТарифСВ.Уплачено, 0) КАК УплаченоТарифСВ,
	               |	ЕСТЬNULL(ТарифСВ.ЗадолженностьНаКонецПериода, 0) КАК ЗадолженностьНаКонецТарифСВ
	               |ИЗ
	               |	ВТСписокДокументов КАК СписокДокументов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОВзносах КАК Страховая
	               |		ПО СписокДокументов.Ссылка = Страховая.Ссылка
	               |			И (Страховая.ВидСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ВидыСтраховыхВзносовПФР.НаСтраховуюПенсию))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОВзносах КАК Накопительная
	               |		ПО СписокДокументов.Ссылка = Накопительная.Ссылка
	               |			И (Накопительная.ВидСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ВидыСтраховыхВзносовПФР.НаНакопительнуюПенсию))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОВзносах КАК ТарифСВ
	               |		ПО СписокДокументов.Ссылка = ТарифСВ.Ссылка
	               |			И (ТарифСВ.ВидСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ВидыСтраховыхВзносовПФР.ПоТарифуСтраховыхВзносов))
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка";
	
	Если ИмяДокумента = "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР", "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ");
	КонецЕсли;
				   
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;			   
	
КонецФункции

Функция ЗапросПоДаннымРасшифровкиУплаченныхВзносовОДВ_1(МассивСсылок, ИмяДокумента) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УплаченныеВзносыРасшифровка.Ссылка КАК Ссылка,
	               |	УплаченныеВзносыРасшифровка.Год КАК Год,
	               |	УплаченныеВзносыРасшифровка.НаСтраховуюПенсию,
	               |	УплаченныеВзносыРасшифровка.НаНакопительнуюПенсию,
	               |	УплаченныеВзносыРасшифровка.ПоТарифуСтраховыхВзносов
	               |ИЗ
	               |	Документ.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР.УплаченныеВзносыРасшифровка КАК УплаченныеВзносыРасшифровка
	               |ГДЕ
	               |	УплаченныеВзносыРасшифровка.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	Год";
	
	Если ИмяДокумента = "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР", "СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ");
	КонецЕсли;
				   
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;			   
	
КонецФункции

// Формирует временную таблицу периодов с заданной периодичностью, формируемой, с помощью,
// запроса, возвращаемого методом ЗапросВТПериоды.
// Создает в МенеджерВременныхТаблиц временную таблицу с именем <ИмяВТ> 
// и периодичностью <Периодичность>	
// за интервал с <НачалоИнтервала> по <ОкончаниеИнтервала>.
//
Процедура СоздатьВТПериоды(МенеджерВременныхТаблиц, НачалоИнтервала, ОкончаниеИнтервала, Периодичность = "МЕСЯЦ", ИмяПоляПериод = "Период", ИмяВТ = "ВТПериоды", ИспользоватьКонецПериода = Ложь) Экспорт
	
	Если НачалоИнтервала > ОкончаниеИнтервала Тогда
		ВызватьИсключение НСтр("ru='Дата окончания не может быть меньше даты начала.'")
	КонецЕсли;
	
	Запрос = ЗапросВТПериоды(НачалоИнтервала, ОкончаниеИнтервала, Периодичность, ИмяПоляПериод, ИмяВТ, ИспользоватьКонецПериода);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗапросВТПериоды(Знач ОписаниеНачалаИнтервала, Знач ОписаниеОкончанияИнтервала, Периодичность = "МЕСЯЦ", ИмяПоляПериод = "Период", ИмяВТ = "ВТПериоды", ИспользоватьКонецПериода = Ложь) Экспорт
	
	Если ТипЗнч(ОписаниеНачалаИнтервала) = Тип("Дата")
		И ТипЗнч(ОписаниеОкончанияИнтервала) = Тип("Дата") Тогда
		
		Если ОписаниеОкончанияИнтервала < ОписаниеНачалаИнтервала Тогда
			
			ТекстИсключения = НСтр("ru='Дата окончания периода не может быть меньше даты начала'");
			ВызватьИсключение ТекстИсключения;
			
		Иначе
			
			ЛетВИнтервале = Год(ОписаниеОкончанияИнтервала) - Год(ОписаниеНачалаИнтервала);
			Если ЛетВИнтервале > 100 Тогда
				
				ТекстИсключения = НСтр("ru='Попытка получить данные за слишком большой интервал времени'")
					+ " (%1 " + НСтр("ru='лет'") + ": " + НСтр("ru='с'")+ " %2 " + НСтр("ru='по'") + " %3)";
				
				ТекстИсключения = СтрШаблон(ТекстИсключения, Формат(ЛетВИнтервале, "ЧГ="), Формат(ОписаниеНачалаИнтервала, "ДЛФ=D"), Формат(ОписаниеОкончанияИнтервала, "ДЛФ=D"));
				
				ВызватьИсключение ТекстИсключения;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	0 КАК Цифра
		|ПОМЕСТИТЬ ВТЦифры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ЧАС, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ЧАС) КАК ИмяПоляПериод
		|ПОМЕСТИТЬ ИмяВТ
		|ИЗ
		|	ВТЦифры КАК Цифры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦифры КАК Цифры10
		|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ЧАС) > 9)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦифры КАК Цифры100
		|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ЧАС) > 99)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦифры КАК Цифры1000
		|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ЧАС) > 999)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦифры КАК Цифры10000
		|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ЧАС) > 9999)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦифры КАК Цифры100000
		|		ПО (РАЗНОСТЬДАТ(&НачалоПериодаИнтервала, &ОкончаниеПериодаИнтервала, ЧАС) > 99999)
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаИнтервала, ЧАС, Цифры.Цифра + ЕСТЬNULL(Цифры10.Цифра, 0) * 10 + ЕСТЬNULL(Цифры100.Цифра, 0) * 100 + ЕСТЬNULL(Цифры1000.Цифра, 0) * 1000 + ЕСТЬNULL(Цифры10000.Цифра, 0) * 10000 + ЕСТЬNULL(Цифры100000.Цифра, 0) * 100000), ЧАС) МЕЖДУ &НачалоИнтервала И &ОкончаниеИнтервала";
	
	Если ВРег(Периодичность) <> "ГОД"
		И ВРег(Периодичность) <> "КВАРТАЛ"
		И ВРег(Периодичность) <> "МЕСЯЦ"
		И ВРег(Периодичность) <> "ДЕНЬ" Тогда
		
		ВызватьИсключение НСтр("ru='Невозможно сформировать запрос с переданными параметрами.'");
		
	КонецЕсли;
	
	Если ИспользоватьКонецПериода Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НАЧАЛОПЕРИОДА", "КОНЕЦПЕРИОДА");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЧАС", Периодичность);
	ЗаменитьИмяСоздаваемойВременнойТаблицы(ТекстЗапроса, "ИмяВТ", ИмяВТ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК ИмяПоляПериод", "КАК " + ИмяПоляПериод);
	
	Если ИспользоватьКонецПериода Тогда
		НачалоПериодаИнтервалаПредставление = "КОНЕЦПЕРИОДА(&НачалоИнтервала, " + ВРег(Периодичность) + ")";
		ОкончаниеПериодаИнтервалаПредставление = "КОНЕЦПЕРИОДА(&ОкончаниеИнтервала, " + ВРег(Периодичность) + ")";
	Иначе
		НачалоПериодаИнтервалаПредставление = "НАЧАЛОПЕРИОДА(&НачалоИнтервала, " + ВРег(Периодичность) + ")";
		ОкончаниеПериодаИнтервалаПредставление = "НАЧАЛОПЕРИОДА(&ОкончаниеИнтервала, " + ВРег(Периодичность) + ")";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериодаИнтервала", НачалоПериодаИнтервалаПредставление);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОкончаниеПериодаИнтервала", ОкончаниеПериодаИнтервалаПредставление);
	
	Если ТипЗнч(ОписаниеНачалаИнтервала) = Тип("Строка") Тогда
		НачалоИнтервалаПредставление = ОписаниеНачалаИнтервала;
	Иначе
		НачалоИнтервалаПредставление = "ДАТАВРЕМЯ(" + Формат(ОписаниеНачалаИнтервала, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
	КонецЕсли;
	
	Если ТипЗнч(ОписаниеОкончанияИнтервала) = Тип("Строка") Тогда
		ОкончаниеИнтервалаПредставление = ОписаниеОкончанияИнтервала;
	Иначе
		ОкончаниеИнтервалаПредставление = "ДАТАВРЕМЯ(" + Формат(ОписаниеОкончанияИнтервала, "ДФ='гггг, М, д, Ч, м, с'; ДП=") + ")";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоИнтервала",
		"ВЫБОР КОГДА " + НачалоИнтервалаПредставление + " = ДАТАВРЕМЯ(1, 1, 1) ТОГДА ДАТАВРЕМЯ(1980, 1, 1) ИНАЧЕ " + НачалоИнтервалаПредставление + " КОНЕЦ");
	
	ИмяОкончаниеИнтервала = "ОкончаниеИнтервала" + СтрЗаменить(ИмяВТ, "_", "");
	Запрос.УстановитьПараметр(ИмяОкончаниеИнтервала, ДобавитьМесяц(ТекущаяДатаСеанса(), 60));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОкончаниеИнтервала",
		"ВЫБОР КОГДА " + ОкончаниеИнтервалаПредставление+ " = КОНЕЦПЕРИОДА(ДАТАВРЕМЯ(1, 1, 1), " + ВРег(Периодичность) + ") ТОГДА &" + ИмяОкончаниеИнтервала + " ИНАЧЕ " + ОкончаниеИнтервалаПредставление+ " КОНЕЦ");
	
	ДобавитьЗапросУничтоженияВременнойТаблицы(ТекстЗапроса, "ВТЦифры");
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
	
КонецФункции

// Осуществляет замену имени временной таблицы. Если не указано новое имя 
// временной таблицы, производится удаление из текста запроса строки, осуществляющей
// создание временной таблицы. Если новое имя временной таблицы передано, то производится
// замена всех фрагментов текста, содержащих старое имя временной таблицы на новое имя.
//
// Параметры:
//	ТекстЗапроса
//	ТекущееИмяТаблицы
//	НовоеИмяТаблицы
//
Процедура ЗаменитьИмяСоздаваемойВременнойТаблицы(ТекстЗапроса, ТекущееИмяТаблицы, НовоеИмяТаблицы = "") Экспорт
	
	Если ПустаяСтрока(НовоеИмяТаблицы) Тогда
		ЗамещаемыйТекст = "";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ " + ТекущееИмяТаблицы, "");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекущееИмяТаблицы, НовоеИмяТаблицы);
	КонецЕсли;
	
КонецПроцедуры

Функция СведенияОбОрганизации(Организация, Период) Экспорт
	ТаблицаОрганизации = Новый ТаблицаЗначений;
	ТаблицаОрганизации.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОрганизации.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	СтрокаТаблицы = ТаблицаОрганизации.Добавить();
	СтрокаТаблицы.Организация = Организация;
	СтрокаТаблицы.Период = Период;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОрганизации", ТаблицаОрганизации);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОрганизации.Организация,
	|	ТаблицаОрганизации.Период
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	&ТаблицаОрганизации КАК ТаблицаОрганизации";
	
	Запрос.Выполнить();
	
	СоздатьВТСведенияОбОрганизацияхПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОбОрганизациях.Организация,
	|	СведенияОбОрганизациях.Период,
	|	СведенияОбОрганизациях.ИНН,
	|	СведенияОбОрганизациях.КодОрганаПФР,
	|	СведенияОбОрганизациях.КодОрганаФСГС,
	|	СведенияОбОрганизациях.КодОрганаФСС,
	|	СведенияОбОрганизациях.КодПоОКПО,
	|	СведенияОбОрганизациях.НаименованиеПолное,
	|	СведенияОбОрганизациях.Наименование,
	|	СведенияОбОрганизациях.ОГРН,
	|	СведенияОбОрганизациях.КодОКОПФ,
	|	СведенияОбОрганизациях.КодОКФС,
	|	СведенияОбОрганизациях.РегистрацияВНалоговомОргане,
	|	СведенияОбОрганизациях.КПП,
	|	СведенияОбОрганизациях.ОКАТО,
	|	СведенияОбОрганизациях.РегистрационныйНомерПФР,
	|	СведенияОбОрганизациях.ЮридическоеФизическоеЛицо,
	|	СведенияОбОрганизациях.ГоловнаяОрганизация
	|ИЗ
	|	ВТСведенияОбОрганизациях КАК СведенияОбОрганизациях";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка;
КонецФункции

Функция ДоляСтавкиУказанаНеверно(ДоляСтавки) Экспорт

	Возврат	ДоляСтавки < 0.1 Или ДоляСтавки > 2

КонецФункции

// Вычисляет разность между двумя датами: в годах, месяцах, днях
Процедура ПолучитьСтажЗаПериод(Знач ДатаОкончанияПериода, Знач ДатаНачалаПериода, Лет=0, Месяцев=0, Дней=0) Экспорт 
	
	Если ДатаОкончанияПериода >= ДатаНачалаПериода Тогда
		
		Лет1		=	Год(ДатаОкончанияПериода);
		Месяцев1	=	Месяц(ДатаОкончанияПериода);
		
		Если НачалоДня(ДатаОкончанияПериода) <> НачалоДня(КонецМесяца(ДатаОкончанияПериода)) Тогда
			Дней1		=	День(ДатаОкончанияПериода);
		Иначе
			Дней1		=	30;//В каждом месяце ровно 30 дней
		КонецЕсли;	 
		
		Лет2		=	Год(ДатаНачалаПериода);
		Месяцев2	=	Месяц(ДатаНачалаПериода);
		Если НачалоДня(ДатаНачалаПериода) <> НачалоДня(КонецМесяца(ДатаНачалаПериода)) Тогда
			Дней2		=	День(ДатаНачалаПериода);
		Иначе
			Дней2		=	30;//В каждом месяце ровно 30 дней
		КонецЕсли;	 
		
		Лет		=	Лет1		-	Лет2;
		Месяцев	=	Месяцев1	-	Месяцев2;
		Дней	=	Дней1		-	Дней2 + 1;//1 день на увольнение
	Иначе
		Лет = 0; 
		Месяцев = 0;
		Дней = 0;
	КонецЕсли;	
КонецПроцедуры 

// Приводит стаж к нормальному виду, когда месяцев не более 12, дней не более 30
Процедура ПолучитьПриведенныйСтаж(Лет, Месяцев, Дней) Экспорт	
	ДнейВГоду 		= 12 * 30;
	ВсегоДней 		= Лет*ДнейВГоду + Месяцев*30 + Дней;
	Лет 			= Цел(ВсегоДней / ДнейВГоду);
	ОстатокДней 	= ВсегоДней - Лет * ДнейВГоду;
	Месяцев			= Цел(ОстатокДней / 30);
	Дней			= ОстатокДней % 30;
КонецПроцедуры 


Функция ВариантыИсточниковДанныхСтажа() Экспорт 
	Варианты = Новый Структура;
	Варианты.Вставить("ДанныеУчета", "ДанныеУчета");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеДокументов", "ДанныеУчетаИФиксированныеДанныеДокументов");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеКорректируемыхДокументов", "ДанныеУчетаИФиксированныеДанныеКорректируемыхДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументов", "ДанныеКорректируемыхДокументов");

	Возврат Варианты;
	
КонецФункции

Функция ВариантыИсточниковДанныхНачисленныхВзносов() Экспорт 
	Варианты = Новый Структура;
	Варианты.Вставить("ДанныеУчета", "ДанныеУчета");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеДокументов", "ДанныеУчетаИФиксированныеДанныеДокументов");
	Варианты.Вставить("ДанныеДокументов", "ДанныеДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументов", "ДанныеКорректируемыхДокументов");

	Возврат Варианты;
	
КонецФункции

Функция ВариантыИсточниковДанныхУплаченныхВзносов() Экспорт 
	Варианты = Новый Структура;
	Варианты.Вставить("ДанныеУчета", "ДанныеУчета");
	Варианты.Вставить("ДанныеДокументов", "ДанныеДокументов");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеДокументов", "ДанныеУчетаИФиксированныеДанныеДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументов", "ДанныеКорректируемыхДокументов");

	Возврат Варианты;
	
КонецФункции

Функция ВариантыИсточниковДанныхСведенийОЗаработке() Экспорт 
	Варианты = Новый Структура;
	Варианты.Вставить("ДанныеУчета", "ДанныеУчета");
	Варианты.Вставить("ДанныеУчетаИФиксированныеДанныеДокументов", "ДанныеУчетаИФиксированныеДанныеДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументов", "ДанныеКорректируемыхДокументов");
	Варианты.Вставить("ДанныеКорректируемыхДокументовОтменяемые", "ДанныеКорректируемыхДокументовОтменяемые");
	Варианты.Вставить("ДанныеУчетаОтменяемые", "ДанныеУчетаОтменяемые");

	Возврат Варианты;
	
КонецФункции

Функция ДобавитьОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы(КоллекцияОписанийЭлементов, ОписаниеЭлемента) Экспорт
	КоллекцияОписанийЭлементов.Добавить(ОписаниеЭлемента);
КонецФункции

Функция ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы() Экспорт
	ОписаниеЭлемента = ОписаниеЭлементаСИндикациейОшибок();	
	
	ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы";
	
	Возврат ОписаниеЭлемента;
КонецФункции

Функция ДобавитьОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки(КоллекцияОписанийЭлементов, ОписаниеЭлемента)Экспорт
	КоллекцияОписанийЭлементов.Добавить(ОписаниеЭлемента);
КонецФункции	

Функция ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки() Экспорт
	ОписаниеЭлемента = ОписаниеЭлементаСИндикациейОшибок();	
	
	ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки";
	
	Возврат ОписаниеЭлемента;		
КонецФункции	

// Добавляет к тексту запроса, переданному в параметре ТекстЗапроса, текст запроса
// уничтожения временной таблицы
//
// Параметры:
//		ТекстЗапроса - Строка
//		ИмяВременнойТаблицы - Строка
//
Процедура ДобавитьЗапросУничтоженияВременнойТаблицы(ТекстЗапроса, ИмяВременнойТаблицы) Экспорт
	
	Если Не ПустаяСтрока(ИмяВременнойТаблицы) Тогда
		ТекстЗапроса = ?(ПустаяСтрока(ТекстЗапроса), "", ТекстЗапроса + 
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|") + "УНИЧТОЖИТЬ " + ИмяВременнойТаблицы;
	КонецЕсли;
	
КонецПроцедуры

Функция СообщенияОбОшибкахИзДанныхФормы(Форма, КлючДанных = Неопределено) Экспорт
	Возврат ОшибкиИзДанныхФормы(Форма, КлючДанных, Истина);		
КонецФункции

Процедура ДобавитьОписаниеСвязиДанныхСтрокТаблиц(ОписаниеСвязиДанных, ПутьКТаблицеВедущихДанных, ИмяРеквизитаТаблицыВедущихДанных, ПутьКТаблицеСвязанныхДанных, ИмяРеквизитаТаблицыСвязанныхДанных, КлючСвязи = Неопределено) Экспорт
	
	ЭлементОписанияСвязиДанных = ОписаниеСвязиДанных.Добавить();
	
	ЭлементОписанияСвязиДанных.ТипСвязиДанных = "СвязьДанныхСтрокТаблиц";
	ЭлементОписанияСвязиДанных.ПутьКРеквизитуВедущихДанных = ПутьКТаблицеВедущихДанных;
	ЭлементОписанияСвязиДанных.ИмяРеквизитаТаблицыВедущихДанных = ИмяРеквизитаТаблицыВедущихДанных;
	ЭлементОписанияСвязиДанных.ПутьКРеквизитуСвязанныхДанных = ПутьКТаблицеСвязанныхДанных;
	ЭлементОписанияСвязиДанных.ИмяРеквизитаТаблицыСвязанныхДанных = ИмяРеквизитаТаблицыСвязанныхДанных;	
	ЭлементОписанияСвязиДанных.ПолныйПутьКОсновнымДанным = ПутьКТаблицеВедущихДанных + ?(ИмяРеквизитаТаблицыВедущихДанных = "", "", "." + ИмяРеквизитаТаблицыВедущихДанных);
	ЭлементОписанияСвязиДанных.ПолныйПутьКПодчиненнымДанным = ПутьКТаблицеСвязанныхДанных + ?(ИмяРеквизитаТаблицыСвязанныхДанных = "", "", "." + ИмяРеквизитаТаблицыСвязанныхДанных);	
	ЭлементОписанияСвязиДанных.ТипСвязиДанныхОбратный  = "СвязьДанныхСтрокТаблиц";

	Если КлючСвязи = Неопределено Тогда
		КлючСвязи = КлючСвязиДанныхСтрокТаблиц();
		ДобавитьЭлементКлючаСвязиПоИндексуСтрокТаблиц(КлючСвязи);
	КонецЕсли;	
	
	ЭлементОписанияСвязиДанных.КлючСвязи = КлючСвязи;
		
КонецПроцедуры

Процедура ДобавитьОписаниеСвязиРеквизитов(ОписаниеСвязиДанных, ПутьКРеквизитуВедущихДанных, ПутьКРеквизитуСвязанныхДанных)Экспорт
	
	ЭлементОписанияСвязиДанных = ОписаниеСвязиДанных.Добавить();
	
	ЭлементОписанияСвязиДанных.ТипСвязиДанных = "СвязьРеквизитовОбъектов";
	ЭлементОписанияСвязиДанных.ПутьКРеквизитуВедущихДанных = ПутьКРеквизитуВедущихДанных;
	ЭлементОписанияСвязиДанных.ПутьКРеквизитуСвязанныхДанных = ПутьКРеквизитуСвязанныхДанных;
	ЭлементОписанияСвязиДанных.ПолныйПутьКОсновнымДанным = ПутьКРеквизитуВедущихДанных;
	ЭлементОписанияСвязиДанных.ПолныйПутьКПодчиненнымДанным = ПутьКРеквизитуСвязанныхДанных;
	ЭлементОписанияСвязиДанных.ТипСвязиДанныхОбратный  = "СвязьРеквизитовОбъектов";
	
КонецПроцедуры

Функция ОписаниеСвязиДанных() Экспорт
	ОписаниеСвязиДанных = Новый ТаблицаЗначений;	
	ОписаниеСвязиДанных.Колонки.Добавить("ТипСвязиДанных");
	ОписаниеСвязиДанных.Колонки.Добавить("ПутьКРеквизитуВедущихДанных");
	ОписаниеСвязиДанных.Колонки.Добавить("ИмяРеквизитаТаблицыВедущихДанных");
	ОписаниеСвязиДанных.Колонки.Добавить("ПутьКРеквизитуСвязанныхДанных");
	ОписаниеСвязиДанных.Колонки.Добавить("ИмяРеквизитаТаблицыСвязанныхДанных");
	ОписаниеСвязиДанных.Колонки.Добавить("ПолныйПутьКОсновнымДанным");
	ОписаниеСвязиДанных.Колонки.Добавить("ПолныйПутьКПодчиненнымДанным");
	ОписаниеСвязиДанных.Колонки.Добавить("ТипСвязиДанныхОбратный");
	
	ОписаниеСвязиДанных.Колонки.Добавить("КлючСвязи");
		
	Возврат ОписаниеСвязиДанных;
КонецФункции

Процедура ПреобразоватьПутиКДаннымВСообщенияхПользователю(ДанныеОбъекта, ПодчиненныеДанные, СообщенияПользователю, ОписаниеСвязиДанных, ПутьКДаннымОбъектаВФорме = "") Экспорт
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		ПутьКДаннымОбъекта = Сообщение.Поле;		
		
		Если ТипЗнч(ДанныеОбъекта) <> Тип("ФормаКлиентскогоПриложения") Тогда
			Если ВРег(Сред(ПутьКДаннымОбъекта, 1, 7)) = "ОБЪЕКТ." Тогда
				ПутьКДаннымОбъекта = Сред(ПутьКДаннымОбъекта, 8);
			Иначе
				ПутьКДаннымОбъекта = Сообщение.Поле;
			КонецЕсли;
		КонецЕсли;	
	
		ПутьКДаннымФормы = ПутьКДаннымПодчиненногоОбъекта(ДанныеОбъекта, ПодчиненныеДанные, ПутьКДаннымОбъекта, ОписаниеСвязиДанных);
		Если ПутьКДаннымФормы <> Неопределено Тогда
			Сообщение.Поле = ПутьКДаннымФормы;
			Сообщение.ПутьКДанным = "";
		Иначе			
			Если ПустаяСтрока(ПутьКДаннымОбъектаВФорме) Тогда
				Сообщение.Поле = ПутьКДаннымОбъекта;
			ИначеЕсли Не ПустаяСтрока(Сообщение.Поле) Тогда
				Сообщение.Поле = ПутьКДаннымОбъектаВФорме + "." + ПутьКДаннымОбъекта;
			КонецЕсли;				
		КонецЕсли;	
	КонецЦикла;	

КонецПроцедуры

Процедура ПоместитьСообщенияОбОшибкахВДанныеФормы(Форма, СообщенияПользователю, КлючДанных = Неопределено) Экспорт
	ПутиКДаннымФормы = ПутиКДаннымФормы(Форма);
	
	Для Каждого Ошибка Из СообщенияПользователю Цикл
		ОписаниеПутиКДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(Ошибка.Поле);
		
		Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда
			ПутьКДанным = ОписаниеПутиКДанным.ПутьКДаннымТаблицы;
		Иначе
			ПутьКДанным = ОписаниеПутиКДанным.ПутьКДанным;
		КонецЕсли;	
		
		Если ПутиКДаннымФормы.Найти(ПутьКДанным) = Неопределено Тогда
			ХранилищеОшибок = Форма.ТаблицаХраненияОшибок;
			СтрокаХранилищаОшибок = ХранилищеОшибок.Добавить();
			СтрокаХранилищаОшибок.Текст = Ошибка.Текст;
			СтрокаХранилищаОшибок.КлючДанных = КлючДанных;
			СтрокаХранилищаОшибок.НеПривязана = Истина;
		Иначе	
			ОписаниеПутиКДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(Ошибка.Поле);
			
			Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымРеквизита" Тогда
				ХранилищеОшибок = Форма.ТаблицаХраненияОшибок;
				СтрокаХранилищаОшибок = ХранилищеОшибок.Добавить();
				СтрокаХранилищаОшибок.Текст = Ошибка.Текст;
				СтрокаХранилищаОшибок.Поле = Ошибка.Поле;
				СтрокаХранилищаОшибок.КлючДанных = КлючДанных;
			Иначе	
				Таблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПутиКДанным.ПутьКДаннымТаблицы);
				ХранилищеОшибок = Таблица[ОписаниеПутиКДанным.ИндексСтроки].ТаблицаХраненияОшибок;
				СтрокаХранилищаОшибок = ХранилищеОшибок.Добавить();
				СтрокаХранилищаОшибок.Текст = Ошибка.Текст;
				СтрокаХранилищаОшибок.Поле = ОписаниеПутиКДанным.ИмяРеквизитаСтроки;
				СтрокаХранилищаОшибок.КлючДанных = КлючДанных;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

Процедура УстановитьФлагиНаличияОшибокПоСообщениямОбОшибках(Форма, Ошибки, ОписаниеПодчиненностиДанных = Неопределено, УстанавливатьФлагНаличияНепривязанныхОшибок = Истина) Экспорт
	СчетчикиОшибокВПодчиненныхЭлементах = Новый Соответствие;
	
	ПутиКДанным = Новый Массив;
	
	Для Каждого Ошибка Из Ошибки Цикл
		ОписаниеПутиКДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(Ошибка.Поле);
		ПутиКДанным.Добавить(Ошибка.Поле);					
	КонецЦикла;	
		
	УстановитьФлагиНаличияДляПутиКДанным(Форма, ПутиКДанным, Истина, ОписаниеПодчиненностиДанных, Ложь);
	
КонецПроцедуры

Функция ОписаниеЭлементаСИндикациейОшибок()
	ОписаниеЭлемента = Новый Структура;
	
	ОписаниеЭлемента.Вставить("Тип");
	ОписаниеЭлемента.Вставить("ИмяЭлемента", "");
	ОписаниеЭлемента.Вставить("ИмяЭлементаТаблица", "");
	ОписаниеЭлемента.Вставить("ОтображатьГиперссылку", Ложь);
	ОписаниеЭлемента.Вставить("ПутьКДаннымФормыСодержащимОшибку", "");
	ОписаниеЭлемента.Вставить("ИмяРеквизитаТаблицы", "");
	ОписаниеЭлемента.Вставить("ОтображатьНепривязанныеОшибки", Ложь);
	ОписаниеЭлемента.Вставить("ОформлениеДляДанныхСОшибкой", "");
	ОписаниеЭлемента.Вставить("ОформлениеДляНеЗаполненныхДанныхДанных", "");

	Возврат ОписаниеЭлемента;		
КонецФункции

Функция КлючСвязиДанныхСтрокТаблиц() Экспорт
	Возврат Новый Массив;	
КонецФункции	

Процедура ДобавитьЭлементКлючаСвязиПоИндексуСтрокТаблиц(КлючСвязи) Экспорт
	ЭлементКлюча = ЭлементКлючаСвязиСтрокТаблиц();
	ЭлементКлюча.Тип = "СвязьПоИндексуСтрокТаблиц";
	
	КлючСвязи.Добавить(ЭлементКлюча);
КонецПроцедуры	

Функция ПутиКДаннымФормы(Форма, ЗаполнятьПутиКРеквизитамШапки = Истина)
	ПутиКДаннымФормы = Новый Массив;	
	
	ЗаполнитьПутиКДаннымФормы(ПутиКДаннымФормы, Форма, , , ЗаполнятьПутиКРеквизитамШапки);
	
	Возврат ПутиКДаннымФормы;
	
КонецФункции	

Процедура ЗаполнитьПутиКДаннымФормы(ПутиКДаннымФормы, Форма, ОписаниеТекущегоРеквизитаФормы = Неопределено, УровеньВложенности = 1, ЗаполнятьПутиКРеквизитамШапки = Истина)
	Если УровеньВложенности > 2 Тогда
		Возврат;
	КонецЕсли;	
	
	Если ОписаниеТекущегоРеквизитаФормы = Неопределено Тогда
		ПутьКТекущемуРеквизитуФормы = "";
		ОписаниеТипаТекущегоРеквизита = Неопределено;
	Иначе
		ПутьКТекущемуРеквизитуФормы = ОписаниеТекущегоРеквизитаФормы.Путь 
										+ ?(ОписаниеТекущегоРеквизитаФормы.Путь = "", ОписаниеТекущегоРеквизитаФормы.Имя, "." + ОписаниеТекущегоРеквизитаФормы.Имя);
										
		ОписаниеТипаТекущегоРеквизита = ОписаниеТекущегоРеквизитаФормы.ТипЗначения;									
	КонецЕсли;	
	
	Если ОписаниеТипаТекущегоРеквизита = Неопределено Тогда
		РеквизитыФормы = Форма.ПолучитьРеквизиты("");
	Иначе
		РеквизитыФормы = Форма.ПолучитьРеквизиты(ПутьКТекущемуРеквизитуФормы);
	КонецЕсли;			
	
	Для Каждого ОписаниеРеквизита Из РеквизитыФормы Цикл
		Если ОписаниеРеквизита.ТипЗначения.СодержитТип(Тип("КоллекцияДвижений")) Тогда
			Продолжить;
		КонецЕсли;	
		
		ПутьКРеквизиту = ОписаниеРеквизита.Путь + ?(ОписаниеРеквизита.Путь = "", ОписаниеРеквизита.Имя, "." + ОписаниеРеквизита.Имя);

		РеквизитФормы = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКРеквизиту);
		
		Если ТипЗнч(РеквизитФормы) = Тип("ДанныеФормыКоллекция") Тогда
			ПутиКДаннымФормы.Добавить(ПутьКРеквизиту);	
		ИначеЕсли ТипЗнч(РеквизитФормы) = Тип("ДанныеФормыСтруктура")
			Или ТипЗнч(РеквизитФормы) = Тип("ДанныеФормыСтруктураСКоллекцией") Тогда
			
			Если ЗаполнятьПутиКРеквизитамШапки Тогда 
				ПутиКДаннымФормы.Добавить(ПутьКРеквизиту);
			КонецЕсли;	
			
			ЗаполнитьПутиКДаннымФормы(ПутиКДаннымФормы, Форма, ОписаниеРеквизита, УровеньВложенности + 1, ЗаполнятьПутиКРеквизитамШапки);
		ИначеЕсли ЗаполнятьПутиКРеквизитамШапки Тогда 
			ПутиКДаннымФормы.Добавить(ПутьКРеквизиту);	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьФлагиНаличияДляПутиКДанным(Форма, ПутиКДанным, ЗначениеФлага, ОписаниеПодчиненностиДанных = Неопределено, СбрасыватьСчетчикОшибокВПодчиненныхЭлементах = Ложь)
	ПутиКУзламДанныхСОшибками = Новый Массив;
	
	ОтработанныеПутиКУзламДаннымСОшибками = Новый Соответствие;
	
	Для Каждого ПутьКДанным Из ПутиКДанным Цикл 
		ОписаниеПутиКДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКДанным);
		
		Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымСтрокиТаблицы" И
			Не ПустаяСтрока(ОписаниеПутиКДанным.ИмяРеквизитаСтроки) Тогда
			
			ОписаниеПутиКДанным.ИмяРеквизитаСтроки = "";
			
			Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымРеквизита" Тогда
				ПутьКДанным =  ОписаниеПутиКДанным.ПутьКДанным;
			Иначе
				ПутьКДанным =  ОписаниеПутиКДанным.ПутьКДаннымТаблицы + "[" + ИндексСтрокой(ОписаниеПутиКДанным.ИндексСтроки) + "]" + ?(ОписаниеПутиКДанным.ИмяРеквизитаСтроки = "", "", ".") + ОписаниеПутиКДанным.ИмяРеквизитаСтроки;
			КонецЕсли;	
		КонецЕсли;
		
		Если ОтработанныеПутиКУзламДаннымСОшибками.Получить(ПутиКДанным) = Неопределено Тогда
			ПутиКУзламДанныхСОшибками.Добавить(ПутьКДанным);
			ОтработанныеПутиКУзламДаннымСОшибками.Вставить(ПутьКДанным, Истина);
		КонецЕсли;	
	КонецЦикла;	
		
	ДеревоПодчиненностиДанных = ДеревоПодчиненностиДанныхПоПутямКПодчиненнымДанным(Форма, ПутиКУзламДанныхСОшибками, ОписаниеПодчиненностиДанных);
	
	ПутиКДаннымФормы = ПутиКДаннымФормы(Форма, Истина);
	
	СчетчикФлаговНаСтраницах = Новый Структура(Форма.СчетчикФлаговНаСтраницах);
	
	Для Каждого ТекущийУзел Из ДеревоПодчиненностиДанных.Строки Цикл
		УстановитьФлагНаличияОшибокДляУЗлаДанных(Форма, ТекущийУзел, ЗначениеФлага, ОтработанныеПутиКУзламДаннымСОшибками, ПутиКДаннымФормы, СчетчикФлаговНаСтраницах, СбрасыватьСчетчикОшибокВПодчиненныхЭлементах);
	КонецЦикла;	
	
	Форма.СчетчикФлаговНаСтраницах = Новый ФиксированнаяСтруктура(СчетчикФлаговНаСтраницах);
	
	УстановитьКартинкиДляЗаголовковСтраницФормы(Форма)
КонецПроцедуры	

Функция ПутьКДаннымПодчиненногоОбъекта(ДанныеВедущегоОбъекта, ДанныеПодчиненногоОбъекта, ПутьКДаннымВедущегоОбъекта, ОписаниеСвязиДанных) Экспорт 
	ПутьКДаннымПодчиненногоОбъекта = Неопределено;
	
	Если ТипЗнч(ПутьКДаннымВедущегоОбъекта) = Тип("Структура") Тогда
		ОписаниеПутиКДаннымВедущегоОбъекта = ПутьКДаннымВедущегоОбъекта;
	Иначе
		ОписаниеПутиКДаннымВедущегоОбъекта = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКДаннымВедущегоОбъекта);
	КонецЕсли;
	
	ПравилаСвязиСДаннымиОбъекта = ПравилаСвязиПоПутиКДаннымВедущегоОбъекта(ОписаниеПутиКДаннымВедущегоОбъекта, ОписаниеСвязиДанных, Ложь);
	
	ОписаниеПутиКСвязаннымДанным = ОписаниеПутейКСвязаннымДаннымПоПравиламСвязиДанных(
										ДанныеВедущегоОбъекта, 
										ДанныеПодчиненногоОбъекта,
										ОписаниеПутиКДаннымВедущегоОбъекта,
										ПравилаСвязиСДаннымиОбъекта);
										
	Если ОписаниеПутиКСвязаннымДанным.Количество() > 0 Тогда									
		ПутьКДаннымПодчиненногоОбъекта = ПутьКДаннымСтрокойПоОписаниюПути(ОписаниеПутиКСвязаннымДанным[0]);
	КонецЕсли;
	
	Возврат ПутьКДаннымПодчиненногоОбъекта;
КонецФункции


Функция ПравилаСвязиПоПутиКДаннымВедущегоОбъекта(ПутьКВедущимДанным, ОписаниеСвязиДанных, ОбратныйПоиск = Ложь)
	ПравилаСвязи = Новый Массив;
	
	Если ТипЗнч(ПутьКВедущимДанным) = Тип("Строка") Тогда
		ОписаниеПутиКВедущимДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКВедущимДанным);
	Иначе
		ОписаниеПутиКВедущимДанным = ПутьКВедущимДанным;
	КонецЕсли;	
	
	ДопустимыеТипыСвязиДанных = ДопустимыеТипыСвязиДанныхПоОписаниюПутиКВедущимДанным(ОписаниеПутиКВедущимДанным);
		
	КлючиПоискаПравилСвязи = КлючиПоискаПравилСвязиПоОписаниюПутиКДанным(ОписаниеПутиКВедущимДанным, Ложь);
	
	Возврат ПравилаСвязиДанныхПоКлючамПоиска(ОписаниеСвязиДанных, КлючиПоискаПравилСвязи, ДопустимыеТипыСвязиДанных, ОбратныйПоиск);
	
КонецФункции

Процедура ЗаполнитьПутиКВедущимДанным(ПутиКВедущимДанным, Данные, ПутьКПодчиненнымДанным, ОписаниеПодчиненностиДанных, РекурсивныйПоиск = Истина)
	ОписаниеПутиКПодчиненнымДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКПодчиненнымДанным);
	
	Если ОписаниеПутиКПодчиненнымДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда 
		ОписаниеПутиКПодчиненнымДанным.ИмяРеквизитаСтроки = "";
	КонецЕсли;	
	
	КлючиПоискаПравилСвязи = КлючиПоискаПравилСвязиПоОписаниюПутиКДанным(ОписаниеПутиКПодчиненнымДанным, Истина);	
		
	ДопустимыеТипыСвязиДанных = ДопустимыеТипыСвязиДанныхПоОписаниюПутиКВедущимДанным(ОписаниеПутиКПодчиненнымДанным);
	
	ПравилаСвязиДанных = ПравилаСвязиДанныхПоКлючамПоиска(ОписаниеПодчиненностиДанных, КлючиПоискаПравилСвязи, ДопустимыеТипыСвязиДанных, Истина);
	
	ОписаниеПутейКВедущимДанным = ОписаниеПутейКСвязаннымДаннымПоПравиламСвязиДанных(Данные, Данные, ОписаниеПутиКПодчиненнымДанным, ПравилаСвязиДанных);	
	
	Для Каждого ОписаниеПутиКВедущимДанным Из ОписаниеПутейКВедущимДанным Цикл
		ПутьКВедущимДанным = ПутьКДаннымСтрокойПоОписаниюПути(ОписаниеПутиКВедущимДанным);
		
		Если ПутьКВедущимДанным <> ПутьКПодчиненнымДанным 	
			И ПутиКВедущимДанным.Найти(ПутьКВедущимДанным) = Неопределено Тогда
			
			ПутиКВедущимДанным.Добавить(ПутьКВедущимДанным);
			
			Если РекурсивныйПоиск Тогда
				ЗаполнитьПутиКВедущимДанным(ПутиКВедущимДанным, Данные, ПутьКВедущимДанным, ОписаниеПодчиненностиДанных);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

Функция ПутиКВедущимДанным(Данные, ПутьКПодчиненнымДанным, ОписаниеПодчиненностиДанных, РекурсивныйПоиск = Истина) Экспорт
	ПутиКВедущимДанным = Новый Массив;
	
	ЗаполнитьПутиКВедущимДанным(ПутиКВедущимДанным, Данные, ПутьКПодчиненнымДанным, ОписаниеПодчиненностиДанных, РекурсивныйПоиск);

	Возврат ПутиКВедущимДанным;
		
КонецФункции

Функция ПравилаСвязиДанныхПоКлючамПоиска(ОписаниеСвязиДанных, КлючиПоискаПравилСвязи, ДопустимыеТипыСвязиДанных, ОбратныйПоиск = Ложь)
	ПравилаСвязи = Новый Массив;
	
	Для Каждого КлючПоиска Из КлючиПоискаПравилСвязи Цикл
		
		Если ОбратныйПоиск Тогда
			СтруктураПоиска = Новый Структура("ПолныйПутьКПодчиненнымДанным", КлючПоиска);
		Иначе
			СтруктураПоиска = Новый Структура("ПолныйПутьКОсновнымДанным", КлючПоиска);
		КонецЕсли;	

		СтрокиСоответствияПутей = ОписаниеСвязиДанных.НайтиСтроки(СтруктураПоиска); 
		
		Если ОбратныйПоиск Тогда 
			ИмяПоляТипСвязиДанных = "ТипСвязиДанныхОбратный";
		Иначе
			ИмяПоляТипСвязиДанных = "ТипСвязиДанных";	
		КонецЕсли;	
		
		Для Каждого ПравилоСвязиДанных Из СтрокиСоответствияПутей Цикл
			Если ОбратныйПоиск Тогда
				ПравилоСвязиДанных = ПравилоОбратнойСвязиДанных(ПравилоСвязиДанных);
			КонецЕсли;	
			
			Если ДопустимыеТипыСвязиДанных.Найти(ПравилоСвязиДанных.ТипСвязиДанных) <> Неопределено Тогда			
				ПравилаСвязи.Добавить(ПравилоСвязиДанных);	
			КонецЕсли;
		КонецЦикла;	
		
		Если ПравилаСвязи.Количество() > 0 Тогда 
			Прервать;
		КонецЕсли;		
	КонецЦикла;	

	Возврат ПравилаСвязи;	
КонецФункции	

Функция ОшибкиИзДанныхФормы(Форма, КлючДанных = Неопределено, ФормироватьСообщенияПользователю = Ложь)
	Ошибки = Новый Массив;
	
	ПутиКТаблицамФормы = ПутиКДаннымФормы(Форма, Ложь);
	
	ОшибкиИзДанныхШапкиФормыВМассив(Ошибки, Форма, "*", КлючДанных, Ложь, ФормироватьСообщенияПользователю);
		
	Для Каждого ПутьКДаннымТаблицы Из ПутиКТаблицамФормы Цикл 
		ДанныеТаблицы = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДаннымТаблицы);
		
		Для Каждого СтрокаТаблицы Из ДанныеТаблицы Цикл
			ИндексСтроки = ДанныеТаблицы.Индекс(СтрокаТаблицы);
			
			ОшибкиИзСтрокиТаблицыФормыВМассив(
				Ошибки, 
				СтрокаТаблицы, 
				ПутьКДаннымТаблицы, 
				ИндексСтроки,
				"*",
				КлючДанных,
				ФормироватьСообщенияПользователю);	
		КонецЦикла;			
	КонецЦикла;	
	
	Возврат Ошибки;
КонецФункции

#Область ОписаниеСвязанныхДанных

Функция ПутьКДаннымСтрокойПоОписаниюПути(ОписаниеПути) Экспорт
	Если ОписаниеПути.Тип = "ПутьКДаннымРеквизита" Тогда
		Возврат ОписаниеПути.ПутьКДанным;
	Иначе
		Возврат ОписаниеПути.ПутьКДаннымТаблицы + "[" + ИндексСтрокой(ОписаниеПути.ИндексСтроки) + "]" + ?(ОписаниеПути.ИмяРеквизитаСтроки = "", "", ".") + ОписаниеПути.ИмяРеквизитаСтроки;
	КонецЕсли;	
КонецФункции

Функция ОписаниеПутейКСвязаннымДаннымПоПравиламСвязиДанных(ВедущиеДанные, СвязанныеДанные, ОписаниеПутиКВедущимДанным, ПравилаСвязиСДаннымиОбъекта)	
	Если ОписаниеПутиКВедущимДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда
		ОписаниеПутейКСвязаннымДанным = ОписаниеПутейКДаннымСвязаннымСДаннымиСтрокиТаблицы(
											ВедущиеДанные, 
											СвязанныеДанные, 
											ОписаниеПутиКВедущимДанным, 
											ПравилаСвязиСДаннымиОбъекта);
	Иначе
		ОписаниеПутейКСвязаннымДанным = ОписаниеПутейКДаннымСвязаннымСДаннымиРеквизита(
											ВедущиеДанные, 
											СвязанныеДанные, 
											ОписаниеПутиКВедущимДанным, 
											ПравилаСвязиСДаннымиОбъекта);
	КонецЕсли;	
	
	Возврат ОписаниеПутейКСвязаннымДанным;
КонецФункции	

Функция ОписаниеПутейКДаннымСвязаннымСДаннымиСтрокиТаблицы(ВедущиеДанные, СвязанныеДанные, ОписаниеПутиКВедущимДанным, ПравилаСвязиСДаннымиСтрокиТаблицы)		
	ОписаниеПутейКСвязаннымДанным = Новый Массив;
		
	ТаблицаВедущихДанных = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(ВедущиеДанные, ОписаниеПутиКВедущимДанным.ПутьКДаннымТаблицы);
			
	Для Каждого ПравилоСвязи Из ПравилаСвязиСДаннымиСтрокиТаблицы Цикл
		ОписаниеПутейКСвязаннымДаннымПоПравилуСвязи = ОписаниеПутейКДаннымСвязаннымСДаннымиСтрокиТаблицыПоПравилуСвязи(ТаблицаВедущихДанных, ОписаниеПутиКВедущимДанным.ИндексСтроки, СвязанныеДанные, ПравилоСвязи);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОписаниеПутейКСвязаннымДанным, ОписаниеПутейКСвязаннымДаннымПоПравилуСвязи);
	КонецЦикла;	
		
	Возврат ОписаниеПутейКСвязаннымДанным;	
КонецФункции	

Функция ОписаниеПутейКДаннымСвязаннымСДаннымиСтрокиТаблицыПоПравилуСвязи(ТаблицаВедущихДанных, ИндексСтроки, СвязанныеДанные, ПравилоСвязи)
	ОписаниеПутейКСвязаннымДанным = Новый Массив;
	
	СтрокаТаблицыВедущихДанных = ТаблицаВедущихДанных[ИндексСтроки];
		
	Если Не СтрокаТаблицыСоответствуетФиксированнымЗначениямКлючаСвязи(СтрокаТаблицыВедущихДанных, ПравилоСвязи.КлючСвязи) Тогда
		Возврат ОписаниеПутейКСвязаннымДанным;
	КонецЕсли;	
	
	Если ПравилоСвязи.ТипСвязиДанных = "СвязьДанныхСтрокТаблиц" Тогда
		СвязываемаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(СвязанныеДанные, ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных);
		ИндексыСвязываемыхСтрок = ИндексыСтрокСвязываемыхСоСтрокойВедущихДанных(СтрокаТаблицыВедущихДанных, ИндексСтроки, СвязываемаяТаблица, ПравилоСвязи.КлючСвязи);		
		
		Для Каждого ИндексСвязываемойСтроки Из ИндексыСвязываемыхСтрок Цикл 
			ОписаниеПутиКСвязываемымДанным = ОписаниеПутиКДаннымСтрокиТаблицы();
			
			ОписаниеПутиКСвязываемымДанным.ПутьКДаннымТаблицы = ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных;
			ОписаниеПутиКСвязываемымДанным.ИндексСтроки = ИндексСвязываемойСтроки;
			ОписаниеПутиКСвязываемымДанным.ИмяРеквизитаСтроки = ПравилоСвязи.ИмяРеквизитаТаблицыСвязанныхДанных;
			
			ОписаниеПутейКСвязаннымДанным.Добавить(ОписаниеПутиКСвязываемымДанным);	
		КонецЦикла;		
		
	ИначеЕсли ПравилоСвязи.ТипСвязиДанных = "СвязьДанныхСтрокиТаблицыСРеквизитом" Тогда	
		ОписаниеПутиКСвязываемымДанным = ОписаниеПутиКДаннымРеквизитаОбъекта();

		ОписаниеПутиКСвязываемымДанным.ПутьКДанным = ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных; 	
		
		ОписаниеПутейКСвязаннымДанным.Добавить(ОписаниеПутиКСвязываемымДанным);	
	КонецЕсли;	
	
	Возврат ОписаниеПутейКСвязаннымДанным;	
КонецФункции	

Функция ИндексыСтрокСвязываемыхСоСтрокойВедущихДанных(СтрокаТаблицыВедущихДанных, ИндексСтрокиТаблицыВедущихДанных, СвязываемаяТаблица, КлючСвязи)
	ИндексыСвязанныхСтрок = Новый Массив;
	
	СтруктураПоиска = Новый Структура;
	
	ИндексСтрокиТаблицыСвязываемыхДанных = Неопределено;
	
	Для Каждого ЭлементКлючаСвязи Из КлючСвязи Цикл
		Если ЭлементКлючаСвязи.Тип = "СвязьПоИндексуСтрокТаблиц" Тогда
			ИндексСтрокиТаблицыСвязываемыхДанных = ИндексСтрокиТаблицыВедущихДанных;	
		ИначеЕсли ЭлементКлючаСвязи.Тип = "СвязьПоПоляСтрокТаблиц" Тогда
			
			ЗначениеПоиска = СтрокаТаблицыВедущихДанных[ЭлементКлючаСвязи.ЛевоеЗначение];
			
			СтруктураПоиска.Вставить(ЭлементКлючаСвязи.ПравоеЗначение, ЗначениеПоиска);
			
		ИначеЕсли ЭлементКлючаСвязи.Тип = "СвязьПоФиксированномуЗначениюПоляПодчиненнойТаблицы" Тогда	
			 
			 СтруктураПоиска.Вставить(ЭлементКлючаСвязи.ПравоеЗначение, ЭлементКлючаСвязи.ЛевоеЗначение);
		КонецЕсли;		
	КонецЦикла;	
	
	Если ИндексСтрокиТаблицыСвязываемыхДанных <> Неопределено Тогда
		Если СтруктураПоиска.Количество() > 0 Тогда
			СтрокаТаблицыСвязываемыхДанных = СвязываемаяТаблица[ИндексСтрокиТаблицыСвязываемыхДанных];
			
			Для Каждого КлючПоискаДанныхВСтроке Из СтруктураПоиска Цикл
				Если СтрокаТаблицыСвязываемыхДанных[КлючПоискаДанныхВСтроке.Ключ] <> КлючПоискаДанныхВСтроке.Значение Тогда
					Возврат ИндексыСвязанныхСтрок;				
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;	
		
		ИндексыСвязанныхСтрок.Добавить(ИндексСтрокиТаблицыСвязываемыхДанных);	
	Иначе
		Если СтруктураПоиска.Количество() = 0 Тогда
			Для Каждого СтрокаТаблицыСвязываемыхДанных Из СвязываемаяТаблица Цикл
				ИндексыСвязанныхСтрок.Добавить(СвязываемаяТаблица.Индекс(СтрокаТаблицыСвязываемыхДанных));		
			КонецЦикла;				
		Иначе	
			НайденныеСтроки = СвязываемаяТаблица.НайтиСтроки(СтруктураПоиска);
		
			Для Каждого СвязаннаяСтрока Из НайденныеСтроки Цикл		
				ИндексыСвязанныхСтрок.Добавить(СвязываемаяТаблица.Индекс(СвязаннаяСтрока));
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ИндексыСвязанныхСтрок;
КонецФункции

Функция СтрокаТаблицыСоответствуетФиксированнымЗначениямКлючаСвязи(СтрокаТаблицы, КлючСвязи)
	Если КлючСвязи = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого ЭлементКлючаСвязи Из КлючСвязи Цикл
		Если ЭлементКлючаСвязи.Тип = "СвязьПоФиксированномуЗначениюПоляВедущейТаблицы"
			И СтрокаТаблицы[ЭлементКлючаСвязи.ЛевоеЗначение] <> ЭлементКлючаСвязи.ПравоеЗначение Тогда
			
			Возврат Ложь;
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат Истина;
	
КонецФункции

Функция ОписаниеПутейКДаннымСвязаннымСДаннымиРеквизита(ВедущиеДанные, СвязанныеДанные, ОписаниеПутиКВедущимДанным, ПравилаСвязиСДаннымиРеквизитаОбъекта)
	ОписаниеПутейКСвязаннымДанным = Новый Массив;
		
	Для Каждого ПравилоСвязи Из ПравилаСвязиСДаннымиРеквизитаОбъекта Цикл
		ОписаниеПутейКСвязаннымДаннымПоПравилуСвязи = ОписаниеПутейКДаннымСвязаннымСРеквизитомПоПравилуСвязи(СвязанныеДанные, ПравилоСвязи);	
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОписаниеПутейКСвязаннымДанным, ОписаниеПутейКСвязаннымДаннымПоПравилуСвязи);	
	КонецЦикла;	
			
	Возврат ОписаниеПутейКСвязаннымДанным;	
КонецФункции

Функция ОписаниеПутейКДаннымСвязаннымСРеквизитомПоПравилуСвязи(СвязанныеДанные, ПравилоСвязи)
	ОписаниеПутей = Новый Массив;
	
	Если ПравилоСвязи.ТипСвязиДанных = "СвязьРеквизитовОбъектов" Тогда
		ОписаниеПути = ОписаниеПутиКДаннымРеквизитаОбъекта();
		
		ОписаниеПути.ПутьКДанным = ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных;
		
		ОписаниеПутей.Добавить(ОписаниеПути);
	ИначеЕсли ПравилоСвязи.ТипСвязиДанных = "СвязьРеквизитаСДаннымиСтрокиТаблицы" Тогда
		СвязываемаяТаблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(СвязанныеДанные, ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных);
		ИндексыСвязываемыхСтрок = ИндексыСтрокСвязываемыхСРеквизитомВедущихДанных(СвязываемаяТаблица, ПравилоСвязи.КлючСвязи);	
		
		Для Каждого ИндексСвязываемойСтроки Из ИндексыСвязываемыхСтрок Цикл 
			ОписаниеПути = ОписаниеПутиКДаннымСтрокиТаблицы();	
			
			ОписаниеПути.ПутьКДаннымТаблицы = ПравилоСвязи.ПутьКРеквизитуСвязанныхДанных;
			ОписаниеПути.ИндексСтроки = ИндексСвязываемойСтроки;
			ОписаниеПути.ИмяРеквизитаСтроки = ПравилоСвязи.ИмяРеквизитаТаблицыСвязанныхДанных;	
			
			ОписаниеПутей.Добавить(ОписаниеПути);
		КонецЦикла;	
	КонецЕсли;		
	
	Возврат ОписаниеПутей;
КонецФункции	

Функция ИндексыСтрокСвязываемыхСРеквизитомВедущихДанных(СвязываемаяТаблица, КлючСвязи)
	ИндексыСвязываемыхСтрок = Новый Массив;
	
	СтруктураПоиска = Новый Структура;
	
	Для Каждого ЭлементКлючаСвязи Из КлючСвязи Цикл
		Если ЭлементКлючаСвязи.Тип = "СвязьПоФиксированномуЗначениюПоляПодчиненнойТаблицы" Тогда	
			 
			 СтруктураПоиска.Вставить(ЭлементКлючаСвязи.ПравоеЗначение, ЭлементКлючаСвязи.ЛевоеЗначение);
		КонецЕсли;		
	КонецЦикла;	
	
	Если СтруктураПоиска.Количество() = 0 Тогда
		СтрокиТаблицы = СвязываемаяТаблица;	
	Иначе	
		СтрокиТаблицы = СвязываемаяТаблица.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл	
		ИндексыСвязываемыхСтрок.Добавить(СвязываемаяТаблица.Индекс(СтрокаТаблицы));		
	КонецЦикла;	

	Возврат ИндексыСвязываемыхСтрок;
	
КонецФункции

Функция ПравилоОбратнойСвязиДанных(ПравилоПрямойСвязи)
	ПравилоОбратнойСвязиДанных = Новый Структура;
	
	ПравилоОбратнойСвязиДанных.Вставить("ТипСвязиДанных", ПравилоПрямойСвязи.ТипСвязиДанныхОбратный);
	ПравилоОбратнойСвязиДанных.Вставить("ПутьКРеквизитуВедущихДанных", ПравилоПрямойСвязи.ПутьКРеквизитуСвязанныхДанных);
	ПравилоОбратнойСвязиДанных.Вставить("ИмяРеквизитаТаблицыВедущихДанных", ПравилоПрямойСвязи.ИмяРеквизитаТаблицыСвязанныхДанных);
	ПравилоОбратнойСвязиДанных.Вставить("ПутьКРеквизитуСвязанныхДанных", ПравилоПрямойСвязи.ПутьКРеквизитуВедущихДанных);
	ПравилоОбратнойСвязиДанных.Вставить("ИмяРеквизитаТаблицыСвязанныхДанных", ПравилоПрямойСвязи.ИмяРеквизитаТаблицыВедущихДанных);
	ПравилоОбратнойСвязиДанных.Вставить("ПолныйПутьКОсновнымДанным", ПравилоПрямойСвязи.ПолныйПутьКПодчиненнымДанным);
	ПравилоОбратнойСвязиДанных.Вставить("КлючСвязи", ПравилоПрямойСвязи.КлючСвязи);
	
	Возврат ПравилоОбратнойСвязиДанных;
КонецФункции	

Функция ДопустимыеТипыСвязиДанныхПоОписаниюПутиКВедущимДанным(ОписаниеПутиКВедущимДанным)
	ДопустимыеТипыСвязиДанных = Новый Массив;
	
	Если ОписаниеПутиКВедущимДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда
		ДопустимыеТипыСвязиДанных.Добавить("СвязьДанныхСтрокиТаблицыСРеквизитом");
		ДопустимыеТипыСвязиДанных.Добавить("СвязьДанныхСтрокТаблиц");	
	Иначе
		ДопустимыеТипыСвязиДанных.Добавить("СвязьРеквизитовОбъектов");
		ДопустимыеТипыСвязиДанных.Добавить("СвязьРеквизитаСДаннымиСтрокиТаблицы");
	КонецЕсли;
	
	Возврат ДопустимыеТипыСвязиДанных;
КонецФункции	

Функция КлючиПоискаПравилСвязиПоОписаниюПутиКДанным(ОписаниеПутиКДанным, СтрогоеСоответствие = Ложь)
	КлючиПоискаПравилСвязи = Новый Массив;
	
	Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда
		ПутьКВедущимДаннымБезИндекса = ОписаниеПутиКДанным.ПутьКДаннымТаблицы + ?(ОписаниеПутиКДанным.ИмяРеквизитаСтроки = "", "", ".") + ОписаниеПутиКДанным.ИмяРеквизитаСтроки;
				
		КлючиПоискаПравилСвязи.Добавить(ПутьКВедущимДаннымБезИндекса);
		Если Не СтрогоеСоответствие Тогда
			КлючиПоискаПравилСвязи.Добавить(ОписаниеПутиКДанным.ПутьКДаннымТаблицы + ".*");
		КонецЕсли;	
	Иначе
		КлючиПоискаПравилСвязи.Добавить(ОписаниеПутиКДанным.ПутьКДанным);
		
		Если Не СтрогоеСоответствие Тогда
			ЭлементыПути = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ОписаниеПутиКДанным.ПутьКДанным, ".");

			Если ЭлементыПути.Количество() <= 1 Тогда
				КлючиПоискаПравилСвязи.Добавить("*");
			Иначе
				Путь = "";
				Для Сч = 0 По ЭлементыПути.Количество() - 2 Цикл
					Путь = Путь + "." + ЭлементыПути[Сч];
				КонецЦикла;
				КлючиПоискаПравилСвязи.Добавить(Сред(Путь, 2) + ".*");
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
		
	Возврат КлючиПоискаПравилСвязи;
КонецФункции

Функция ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКДаннымСтрокой)
	Если НЕ (СтрНайти(ПутьКДаннымСтрокой, "[") = 0) Тогда
		ОписаниеПути = ОписаниеПутиКДаннымСтрокиТаблицыПоПутиКДаннымСтрокой(ПутьКДаннымСтрокой);
	Иначе
		ОписаниеПути = ОписаниеПутиКДаннымРеквизитаОбъекта();
		ОписаниеПути.ПутьКДанным = ПутьКДаннымСтрокой;
	КонецЕсли;	
	
	Возврат ОписаниеПути;
КонецФункции	

Функция ОписаниеПутиКДаннымСтрокиТаблицыПоПутиКДаннымСтрокой(ПутьКДаннымСтрокой)
	ОписаниеПути = ОписаниеПутиКДаннымСтрокиТаблицы();
	
	ОписаниеПути.ИндексСтроки = ИндексСтрокиТаблицыИзПутиКДанным(ПутьКДаннымСтрокой);
	
	НомерПоследнегоСимволаИмениТаблицы = СтрНайти(ПутьКДаннымСтрокой, "[" + ИндексСтрокой(ОписаниеПути.ИндексСтроки) + "]");
	ОписаниеПути.ПутьКДаннымТаблицы = Лев(ПутьКДаннымСтрокой, НомерПоследнегоСимволаИмениТаблицы - 1);
	
	НомерСимволаЗакрывающейСкобки = СтрНайти(ПутьКДаннымСтрокой, "]");
	
	ОписаниеПути.ИмяРеквизитаСтроки = Сред(ПутьКДаннымСтрокой, НомерСимволаЗакрывающейСкобки + 2);
	
	Возврат ОписаниеПути;
КонецФункции

Функция ОписаниеПутиКДаннымСтрокиТаблицы()
	ОписаниеПути = Новый Структура("Тип, ПутьКДаннымТаблицы, ИндексСтроки, ИмяРеквизитаСтроки");	
	ОписаниеПути.Тип = "ПутьКДаннымСтрокиТаблицы";
	ОписаниеПути.ПутьКДаннымТаблицы = "";
	ОписаниеПути.ИмяРеквизитаСтроки = "";
	
	Возврат ОписаниеПути;
КонецФункции

Функция ОписаниеПутиКДаннымРеквизитаОбъекта()
	ОписаниеПути = Новый Структура("Тип, ПутьКДанным");	
	ОписаниеПути.Тип = "ПутьКДаннымРеквизита";
	ОписаниеПути.ПутьКДанным = "";
	
	Возврат ОписаниеПути;
КонецФункции

Функция ЭлементКлючаСвязиСтрокТаблиц()
	ЭлементКлюча = Новый Структура;
	ЭлементКлюча.Вставить("ЛевоеЗначение");
	ЭлементКлюча.Вставить("ПравоеЗначение");
	ЭлементКлюча.Вставить("Тип");
	
	Возврат ЭлементКлюча;
КонецФункции	

Процедура ИндикацияОшибокДополнитьФорму(Форма, ОписаниеЭлементовСИндикациейОшибок, ОписаниеПодчиненностиДанных = Неопределено) Экспорт
	ДобавляемыеРеквизиты = Новый Массив;
	ОписаниеСозданныхРеквизитов = Новый Соответствие;
	ОписаниеСвязиЭлементовИндикацииСРеквизитами = Новый Соответствие;
		
	Для Каждого ОписаниеЭлемента Из ОписаниеЭлементовСИндикациейОшибок Цикл
		ОписаниеРеквизитаИндикации = ОписаниеРеквизитаИндикацииОшибкиИзОписанияЭлементаИндикации(ОписаниеЭлемента);
		
		ОписаниеСозданногоРеквизита = СоздатьРеквизитПризнакНаличияОшибки(ДобавляемыеРеквизиты, ОписаниеРеквизитаИндикации, ОписаниеСозданныхРеквизитов);
		
		Если ОписаниеЭлемента.ОтображатьНепривязанныеОшибки Тогда
			ОписаниеФлагаСИндикациейНепривязанныхОшибок = ОписаниеСозданногоРеквизита;
		КонецЕсли;	
		
		Если Не ПустаяСтрока(ОписаниеЭлемента.ИмяЭлемента) Тогда
			ОписаниеСозданногоРеквизита.Вставить("ОтображатьНепривязанныеОшибки", ОписаниеЭлемента.ОтображатьНепривязанныеОшибки);
			ОписаниеСвязиЭлементовИндикацииСРеквизитами.Вставить(ОписаниеЭлемента.ИмяЭлемента, ОписаниеСозданногоРеквизита);	
		КонецЕсли;	
	КонецЦикла;		
	
	Если ОписаниеПодчиненностиДанных <> Неопределено Тогда
		ОписаниеДобавляемыхРеквизитовИндикации = ОписаниеДобавляемыхРеквизитовИндикацииОшибокИзОписанияПодчиненности(ОписаниеПодчиненностиДанных);
		
		Для Каждого ОписаниеРеквизитаИндикации Из ОписаниеДобавляемыхРеквизитовИндикации Цикл
			СоздатьРеквизитПризнакНаличияОшибки(ДобавляемыеРеквизиты, ОписаниеРеквизитаИндикации, ОписаниеСозданныхРеквизитов);	
		КонецЦикла;	
		
		ОписаниеДобавляемыхРеквизитовСчетчиков = ОписаниеДобавляемыхРеквизитовСчетчиковОшибокИзОписанияПодчиненности(ОписаниеПодчиненностиДанных);
		
		Для Каждого ОписаниеРеквизитаСчетчика Из ОписаниеДобавляемыхРеквизитовСчетчиков Цикл
			СоздатьРеквизитСчетчикНаличияОшибкиВПодчиненныхДанных(ДобавляемыеРеквизиты, ОписаниеРеквизитаСчетчика, ОписаниеСозданныхРеквизитов);	
		КонецЦикла;	
	КонецЕсли;	
	
	РеквизитОписаниеФлагов = Новый РеквизитФормы("ОписаниеРеквизитовПризнаковНаличияОшибок", Новый ОписаниеТипов("Неопределено"));
	РеквизитОписаниеЭлементовСИндикацией = Новый РеквизитФормы("ОписаниеЭлементовСИндикациейОшибок", Новый ОписаниеТипов("Неопределено"));
	РеквизитОписаниеИндикатораНепривязанныхОшибок = Новый РеквизитФормы("ОписаниеФлагаСИндикациейНепривязанныхОшибок", Новый ОписаниеТипов("Неопределено"));
	РеквизитСчетчикФлаговНаСтраницах = Новый РеквизитФормы("СчетчикФлаговНаСтраницах", Новый ОписаниеТипов("Неопределено"));
	РеквизитЦветаЗаголовковСворачиваемыхГрупп = Новый РеквизитФормы("ЦветаЗаголовковСворачиваемыхГрупп", Новый ОписаниеТипов("Неопределено"));
	
	ДобавляемыеРеквизиты.Добавить(РеквизитОписаниеФлагов);
	ДобавляемыеРеквизиты.Добавить(РеквизитОписаниеЭлементовСИндикацией);
	ДобавляемыеРеквизиты.Добавить(РеквизитОписаниеИндикатораНепривязанныхОшибок);
	ДобавляемыеРеквизиты.Добавить(РеквизитСчетчикФлаговНаСтраницах);
	ДобавляемыеРеквизиты.Добавить(РеквизитЦветаЗаголовковСворачиваемыхГрупп);
		
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Форма.ОписаниеРеквизитовПризнаковНаличияОшибок = Новый ФиксированноеСоответствие(ОписаниеСозданныхРеквизитов);
		
	ОписаниеСозданныхЭлементов = Новый Соответствие;
	
	ЦветаЗаголовковСворачиваемыхГрупп = Новый Структура;
	
	СчетчикФлаговНаСтраницах = Новый Структура;
	
	Для Каждого ОписаниеЭлемента Из ОписаниеЭлементовСИндикациейОшибок Цикл
		Если ЗначениеЗаполнено(ОписаниеЭлемента.ИмяЭлемента) Тогда
			ЭлементИндикации = СоздатьЭлементИндикацииОшибки(Форма, ОписаниеЭлемента, ОписаниеСвязиЭлементовИндикацииСРеквизитами);		
			
			Если ЭлементИндикации <> Неопределено Тогда
				СтраницыВладельцы =  Новый Массив;
				ЗаполнитьСтраницыВладельцыЭлемента(ЭлементИндикации, СтраницыВладельцы);

				ОписаниеЭлемента.Вставить("СтраницыВладельцы", СтраницыВладельцы);
				
				ОписаниеСозданныхЭлементов.Вставить(ЭлементИндикации.Имя, ОписаниеЭлемента);
				
				ОписаниеРеквизитовИндикации = ОписаниеСвязиЭлементовИндикацииСРеквизитами.Получить(ОписаниеЭлемента.ИмяЭлемента);
				
				Если ОписаниеРеквизитовИндикации <> Неопределено Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОписаниеРеквизитовИндикации.СтраницыВладельцы, СтраницыВладельцы);
				КонецЕсли;	
				
				Для Каждого ИмяСтраницы Из СтраницыВладельцы Цикл
					СчетчикФлаговНаСтраницах.Вставить(ИмяСтраницы, 0);		
					Элемент = Форма.Элементы[ИмяСтраницы];
					
					Если Элемент.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда
						ЦветаЗаголовковСворачиваемыхГрупп.Вставить(Элемент.Имя, Элемент.ЦветТекстаЗаголовка);
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
	Если ОписаниеФлагаСИндикациейНепривязанныхОшибок <> Неопределено Тогда
		Форма.ОписаниеФлагаСИндикациейНепривязанныхОшибок = Новый ФиксированнаяСтруктура(ОписаниеФлагаСИндикациейНепривязанныхОшибок);
	КонецЕсли;
	
	Форма.ОписаниеЭлементовСИндикациейОшибок = Новый ФиксированноеСоответствие(ОписаниеСозданныхЭлементов);
	Форма.СчетчикФлаговНаСтраницах = Новый ФиксированнаяСтруктура(СчетчикФлаговНаСтраницах);	
	Форма.ЦветаЗаголовковСворачиваемыхГрупп = Новый ФиксированнаяСтруктура(ЦветаЗаголовковСворачиваемыхГрупп);	
КонецПроцедуры

Функция СоздатьПолеКартинкиДляИндикацииОшибок(ЭлементыФормы, ЭлементОтображенияДанныхСОшибкой, Родитель, ПутьКДанным)
	ИмяЭлемента = ЭлементОтображенияДанныхСОшибкой.Имя + "ЕстьОшибки";
	
	ИндексЭлемента = ЭлементОтображенияДанныхСОшибкой.Родитель.ПодчиненныеЭлементы.Индекс(ЭлементОтображенияДанныхСОшибкой); 
	
	Если ТипЗнч(Родитель) = Тип("ТаблицаФормы") 
		Или ИндексЭлемента = ЭлементОтображенияДанныхСОшибкой.Родитель.ПодчиненныеЭлементы.Количество() - 1 Тогда
		
		ЭлементИндикации = ЭлементыФормы.Вставить(ИмяЭлемента, Тип("ПолеФормы"), Родитель);	
	Иначе
		СледующийЭлемент = ЭлементОтображенияДанныхСОшибкой.Родитель.ПодчиненныеЭлементы[ИндексЭлемента + 1];
		ЭлементИндикации = ЭлементыФормы.Вставить(ИмяЭлемента, Тип("ПолеФормы"), Родитель, СледующийЭлемент);	
	КонецЕсли;	
	
	ЭлементИндикации.ПутьКДанным  = ПутьКДанным;
	ЭлементИндикации.Вид = ВидПоляФормы.ПолеКартинки;
	ЭлементИндикации.АктивизироватьПоУмолчанию = Ложь;
	ЭлементИндикации.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементИндикации.ПропускатьПриВводе = Истина;
	ЭлементИндикации.КартинкаЗначений  = БиблиотекаКартинок.Предупреждение;
	ЭлементИндикации.Видимость = ЭлементОтображенияДанныхСОшибкой.Видимость;
	ЭлементИндикации.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ЭлементИндикации.Высота = 1;
	ЭлементИндикации.Ширина = 2;
	ЭлементИндикации.РастягиватьПоВертикали = Ложь;
	ЭлементИндикации.РастягиватьПоГоризонтали = Ложь;
	ЭлементИндикации.Гиперссылка = Истина;
	
	Возврат ЭлементИндикации;
КонецФункции	

Процедура ЗаполнитьСтраницыВладельцыЭлемента(Элемент, Страницы)
	Если ТипЗнч(Элемент) = Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат;
	ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаФормы")
		И ((Элемент.Вид = ВидГруппыФормы.Страница
			И Элемент.Родитель.ОтображениеСтраниц <> ОтображениеСтраницФормы.Нет)
		ИЛИ (Элемент.Вид = ВидГруппыФормы.ОбычнаяГруппа
			И Элемент.Поведение  = ПоведениеОбычнойГруппы.Свертываемая))Тогда 
		
		Страницы.Добавить(Элемент.Имя);
	КонецЕсли;	
	
	ЗаполнитьСтраницыВладельцыЭлемента(Элемент.Родитель, Страницы)	
КонецПроцедуры	

Функция ОписаниеРеквизитаИндикацииОшибкиИзОписанияЭлементаИндикации(ОписаниеЭлемента)
	Если ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы"
		Или ОписаниеЭлемента.Тип = "ОписаниеЭлементаШапкиСИндикациейОшибокВСтрокеТаблицы" Тогда
		
		ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВСтрокеТаблицы();
		ЗаполнитьЗначенияСвойств(ОписаниеРеквизита, ОписаниеЭлемента,, "Тип");
	Иначе
		ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВШапке();
		ЗаполнитьЗначенияСвойств(ОписаниеРеквизита, ОписаниеЭлемента,, "Тип");	
	КонецЕсли;	

	Возврат ОписаниеРеквизита;
КонецФункции

Функция СоздатьРеквизитПризнакНаличияОшибки(ДобавляемыеРеквизиты, СвойстваСоздаваемогоРеквизита, ОписаниеСозданныхРеквизитов)
	
	Если СвойстваСоздаваемогоРеквизита.Тип = "РеквизитСтрокиТаблицы" Тогда
		
		ИмяРеквизитаФлаг = "ЕстьОшибки"; 
		
		Путь = СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку;
	Иначе
		ЭлементыПути = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку, ".");
		
		Путь  = "";
		Если ЭлементыПути.Количество() = 0 Тогда
			ИмяРеквизитаФлаг = "ЕстьОшибки";
		Иначе
			ИмяРеквизитаФлаг = ЭлементыПути[ЭлементыПути.Количество() - 1] + "ЕстьОшибки";
		КонецЕсли;		
	КонецЕсли;	
	
	ПолныйПуть = Путь + ?(Путь = "", "", ".") + ИмяРеквизитаФлаг;
	
	ОписаниеСозданногоРеквизита =  ОписаниеСозданныхРеквизитов[СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку];
	
	Если ОписаниеСозданногоРеквизита = Неопределено Тогда 
		ОписаниеСозданногоРеквизита = ОписаниеРеквизитовИндикацииСвязанныхСДаннымиФормы();
		ОписаниеСозданногоРеквизита.ПутьКДанным = Путь;
		ОписаниеСозданногоРеквизита.ИмяРеквизитаФлаг = ИмяРеквизитаФлаг;
		ОписаниеСозданногоРеквизита.ПутьКДаннымФормыСодержащимОшибку = СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку; 
		
		ОписаниеСозданныхРеквизитов.Вставить(СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку, ОписаниеСозданногоРеквизита);

		РеквизитФлаг = Новый РеквизитФормы(ИмяРеквизитаФлаг, Новый ОписаниеТипов("Булево"), Путь);
		ДобавляемыеРеквизиты.Добавить(РеквизитФлаг);
	КонецЕсли;	
		
	Возврат ОписаниеСозданногоРеквизита;
КонецФункции

Функция ОписаниеРеквизитовИндикацииСвязанныхСДаннымиФормы()
	Описание = Новый Структура;
	Описание.Вставить("ПутьКДанным");
	Описание.Вставить("ИмяРеквизитаФлаг");
	Описание.Вставить("ИмяРеквизитаСчетчик");
	Описание.Вставить("ПутьКДаннымФормыСодержащимОшибку");
	Описание.Вставить("СтраницыВладельцы", Новый Массив);

	Возврат Описание;	
КонецФункции

Функция ОписаниеДобавляемыхРеквизитовСчетчиковОшибокИзОписанияПодчиненности(ОписаниеПодчиненностиДанных)
	ОписаниеРеквизитовСчетчиков = Новый Массив;
	
	Для Каждого ЭлементОписанияПодчиненности Из ОписаниеПодчиненностиДанных Цикл
		Если ЭлементОписанияПодчиненности.ТипСвязиДанных = "СвязьРеквизитовОбъектов"
			Или ЭлементОписанияПодчиненности.ТипСвязиДанных = "СвязьРеквизитаСДаннымиСтрокиТаблицы" Тогда 
			
			ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВШапке();
			ОписаниеРеквизита.ПутьКДаннымФормыСодержащимОшибку = ЭлементОписанияПодчиненности.ПутьКРеквизитуВедущихДанных;
			
		Иначе
			ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВСтрокеТаблицы();
			ОписаниеРеквизита.ПутьКДаннымФормыСодержащимОшибку = ЭлементОписанияПодчиненности.ПутьКРеквизитуВедущихДанных;	
			ОписаниеРеквизита.ИмяРеквизитаТаблицы = ЭлементОписанияПодчиненности.ИмяРеквизитаТаблицыВедущихДанных;
		КонецЕсли;	
		
		ОписаниеРеквизитовСчетчиков.Добавить(ОписаниеРеквизита);
	КонецЦикла;	
	
	Возврат ОписаниеРеквизитовСчетчиков;
КонецФункции

Функция ОписаниеРеквизитаИндикацииОшибкиВСтрокеТаблицы()
	Возврат Новый Структура("Тип, ПутьКДаннымФормыСодержащимОшибку, ИмяРеквизитаТаблицы", "РеквизитСтрокиТаблицы");	
КонецФункции

Функция ОписаниеРеквизитаИндикацииОшибкиВШапке()
	Возврат Новый Структура("Тип, ПутьКДаннымФормыСодержащимОшибку", "РеквизитШапки");	
КонецФункции

Процедура ХраненияОшибокВДанныхФормыДополнитьФорму(Форма) Экспорт
	ПутиКРеквизитамФормы = ПутиКДаннымФормы(Форма, Ложь);
	ПутиКРеквизитамФормы.Добавить("");
		
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого ПутьКРеквизиту Из ПутиКРеквизитамФормы Цикл
		РеквизитТаблицаХраненияОшибок = Новый РеквизитФормы("ТаблицаХраненияОшибок", Новый ОписаниеТипов("ТаблицаЗначений"), ПутьКРеквизиту);
		ДобавляемыеРеквизиты.Добавить(РеквизитТаблицаХраненияОшибок);
		
		ПутьКТаблице = ПутьКРеквизиту + ?(ПутьКРеквизиту = "", "ТаблицаХраненияОшибок", "." + "ТаблицаХраненияОшибок");
		
		РеквизитТекстОшибки = Новый РеквизитФормы("Текст", Новый ОписаниеТипов("Строка"), ПутьКТаблице);
		ДобавляемыеРеквизиты.Добавить(РеквизитТекстОшибки);
		
		РеквизитПолеОшибки = Новый РеквизитФормы("Поле", Новый ОписаниеТипов("Строка"), ПутьКТаблице);
		ДобавляемыеРеквизиты.Добавить(РеквизитПолеОшибки);	
		
		РеквизитКлючДанныхОшибки = Новый РеквизитФормы("КлючДанных", Новый ОписаниеТипов("Неопределено"), ПутьКТаблице);
		ДобавляемыеРеквизиты.Добавить(РеквизитКлючДанныхОшибки);
		
		РеквизитКлючДанныхОшибки = Новый РеквизитФормы("НеПривязана", Новый ОписаниеТипов("Булево"), ПутьКТаблице);
		ДобавляемыеРеквизиты.Добавить(РеквизитКлючДанныхОшибки);
	КонецЦикла;	
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
КонецПроцедуры

Функция СоздатьЭлементИндикацииОшибки(Форма, ОписаниеЭлемента, ОписаниеЭлементовСРеквизитамиИндикации)
	ЭлементИндикации = Неопределено;
	
	Элементы = Форма.Элементы;
	
	ЭлементОтображенияДанныхСОшибкой = Элементы[ОписаниеЭлемента.ИмяЭлемента];
	ОписаниеРеквизитаИндикации = ОписаниеЭлементовСРеквизитамиИндикации[ОписаниеЭлемента.ИмяЭлемента];
	ПутьКДанным = ОписаниеРеквизитаИндикации.ПутьКДанным + ?(ОписаниеРеквизитаИндикации.ПутьКДанным = "", "", ".") + ОписаниеРеквизитаИндикации.ИмяРеквизитаФлаг;
	
	ПутьКДаннымСчетчика = "";
	Если ЗначениеЗаполнено(ОписаниеРеквизитаИндикации.ИмяРеквизитаСчетчик) Тогда
		ПутьКДаннымСчетчика = ОписаниеРеквизитаИндикации.ПутьКДанным + ?(ОписаниеРеквизитаИндикации.ПутьКДанным = "", "", ".") + ОписаниеРеквизитаИндикации.ИмяРеквизитаСчетчик;
	КонецЕсли;	
	
	Если ОписаниеЭлемента.ОтображатьГиперссылку Тогда
		Если ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы" Тогда
			ЭлементИндикации = СоздатьЭлементИндикацииВТаблицеФормы(Форма, ЭлементОтображенияДанныхСОшибкой, ПутьКДанным);
		ИначеЕсли ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки" 
			Или ОписаниеЭлемента.Тип = "ОписаниеЭлементаШапкиСИндикациейОшибокВСтрокеТаблицы" Тогда
			
			ЭлементИндикации = СоздатьЭлементИндикацииОшибкиВШапкеФормы(Форма, ЭлементОтображенияДанныхСОшибкой, ПутьКДанным);	
		КонецЕсли;	
	КонецЕсли;
	
	УстановитьУсловноеОформлениеДляЭлементаСодержащегоОшибку(Форма, ОписаниеЭлемента, ОписаниеРеквизитаИндикации);
	
	Возврат ЭлементИндикации;
КонецФункции	

Функция СоздатьЭлементИндикацииВТаблицеФормы(Форма, ЭлементОтображенияДанныхСОшибкой, ПутьКДанным)
	Элементы = Форма.Элементы;
	
	Если ТипЗнч(ЭлементОтображенияДанныхСОшибкой) = Тип("ТаблицаФормы") Тогда
		Родитель = ЭлементОтображенияДанныхСОшибкой;
	Иначе
		Родитель = ЭлементОтображенияДанныхСОшибкой.Родитель;
	КонецЕсли;		
	
	ЭлементИндикации = СоздатьПолеКартинкиДляИндикацииОшибок(Элементы, ЭлементОтображенияДанныхСОшибкой, Родитель, ПутьКДанным);	
	ЭлементИндикации.ГиперссылкаЯчейки = Истина;
	ЭлементИндикации.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	ЭлементИндикации.ТолькоПросмотр = Истина;
	
	ЭлементИндикации.РастягиватьПоГоризонтали = Ложь;
		
	Если ЭлементИндикации.Родитель.ПодчиненныеЭлементы.Количество() > 0 Тогда 
		Элементы.Переместить(ЭлементИндикации, ЭлементИндикации.Родитель, ЭлементИндикации.Родитель.ПодчиненныеЭлементы[0]);			
	КонецЕсли;	
	
	ЭлементИндикации.Ширина = 1;
	
	Родитель.УстановитьДействие("ПриАктивизацииЯчейки", "Подключаемый_ЭлементИндикацииПриАктивизацииЯчейки");
	
	ЯчейкаАктивируемаяПоУмолчанию = АктивныйЭлементВТаблицеСодержащейГиперссылку(Родитель, ЭлементИндикации);
	
	Если ЯчейкаАктивируемаяПоУмолчанию <> Неопределено Тогда
		ЯчейкаАктивируемаяПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;	
	
	ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
			
	Отбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	Отбор.ПравоеЗначение = Ложь;
	Отбор.Использование = Истина;

	ОформляемоеПоле =  ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ЭлементИндикации.Имя); 
	ОформляемоеПоле.Использование = Истина;

	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);	
	
	Возврат ЭлементИндикации;
		
КонецФункции	

Функция УстановитьУсловноеОформлениеДляЭлементаСодержащегоОшибку(Форма, ОписаниеЭлемента, ОписаниеРеквизитаИндикации)
	ПутьКДаннымФлага = ОписаниеРеквизитаИндикации.ПутьКДанным + ?(ОписаниеРеквизитаИндикации.ПутьКДанным = "", "", ".") + ОписаниеРеквизитаИндикации.ИмяРеквизитаФлаг;
	ПутьКДаннымСчетчика = ОписаниеРеквизитаИндикации.ПутьКДанным + ?(ОписаниеРеквизитаИндикации.ПутьКДанным = "", "", ".") + ОписаниеРеквизитаИндикации.ИмяРеквизитаСчетчик;
	
	Если ОписаниеЭлемента.Тип = "ОписаниеЭлементаСИндикациейОшибокВРеквизитеШапки" Тогда
		
		ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
		
		ГруппаОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

		Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеРеквизитаИндикации.ПутьКДаннымФормыСодержащимОшибку);
				
		Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДаннымФлага);
		Отбор.ПравоеЗначение = Истина;
		Отбор.Использование = Истина;
		
		ОформляемоеПоле =  ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОписаниеЭлемента.ИмяЭлемента); 
		ОформляемоеПоле.Использование = Истина;

		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);	
	КонецЕсли;	
	
	ЭлементОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы  = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДаннымФлага);
	Отбор.ПравоеЗначение = Истина;
	Отбор.Использование = Истина;

	Если Не ПустаяСтрока(ОписаниеРеквизитаИндикации.ИмяРеквизитаСчетчик) Тогда
		Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДаннымСчетчика);
		Отбор.ПравоеЗначение = 0;
		Отбор.Использование = Истина;
	КонецЕсли;	

	ОформляемоеПоле =  ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОписаниеЭлемента.ИмяЭлемента); 
	ОформляемоеПоле.Использование = Истина;
	
	Если ОписаниеЭлемента.Тип = "ОписаниеЭлементаШапкиСИндикациейОшибокВСтрокеТаблицы" Тогда
		ОформляемоеПоле =  ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОписаниеЭлемента.ИмяЭлементаТаблица); 
		ОформляемоеПоле.Использование = Истина;
	КонецЕсли;	

	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
		
КонецФункции	

Функция СоздатьЭлементИндикацииОшибкиВШапкеФормы(Форма, ЭлементОтображенияДанныхСОшибкой, ПутьКДанным)
	Элементы = Форма.Элементы;
	
	Если ТипЗнч(ЭлементОтображенияДанныхСОшибкой.Родитель) =  Тип("ГруппаФормы")
		И ЭлементОтображенияДанныхСОшибкой.Родитель.Вид  = ВидГруппыФормы.ОбычнаяГруппа
		И ЭлементОтображенияДанныхСОшибкой.Родитель.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная Тогда
		
		Родитель = ЭлементОтображенияДанныхСОшибкой.Родитель;
	Иначе	
	                                                
		ИмяГруппы = ЭлементОтображенияДанныхСОшибкой.Имя + "ГруппаИндикацииОшибки";
				
		Родитель = Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), ЭлементОтображенияДанныхСОшибкой.Родитель, ЭлементОтображенияДанныхСОшибкой);
		Родитель.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Родитель.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		Родитель.ОтображатьЗаголовок  = Ложь;
		Родитель.Отображение = ОтображениеОбычнойГруппы.Нет;
	
		Элементы.Переместить(ЭлементОтображенияДанныхСОшибкой, Родитель);
	КонецЕсли;	
		
	ЭлементИндикации = СоздатьПолеКартинкиДляИндикацииОшибок(Элементы, ЭлементОтображенияДанныхСОшибкой, Родитель, ПутьКДанным);	
	
	ЭлементИндикации.УстановитьДействие("Нажатие", "Подключаемый_ЭлементИндикацииОшибкиНажатие");
		
	Возврат ЭлементИндикации;
КонецФункции

Функция АктивныйЭлементВТаблицеСодержащейГиперссылку(Таблица, ЯчейкаГиперссылка)
	КоличествоПодчиненныхЭлементов = Таблица.ПодчиненныеЭлементы.Количество();
	
	ИндексЯчейкиГиперссылки = Таблица.ПодчиненныеЭлементы.Индекс(ЯчейкаГиперссылка);
	
	АктивируемыйЭлемент = Неопределено;
	
	Если КоличествоПодчиненныхЭлементов > 1 Тогда
		ИндексТекущейЯчейки = ИндексЯчейкиГиперссылки;			
		Пока ИндексТекущейЯчейки < КоличествоПодчиненныхЭлементов - 1 Цикл 
			ИндексТекущейЯчейки = ИндексТекущейЯчейки + 1;
			
			ТекущийПодчиненныйЭлемент = Таблица.ПодчиненныеЭлементы[ИндексТекущейЯчейки];
			
			Если ТипЗнч(ТекущийПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда
				АктивируемыйЭлемент = ТекущийПодчиненныйЭлемент;
				Прервать;
			Иначе
				АктивируемыйЭлемент = АктивныйЭлементВТаблицеСодержащейГиперссылку(ТекущийПодчиненныйЭлемент, ТекущийПодчиненныйЭлемент);
				Если ТипЗнч(АктивируемыйЭлемент) <> Тип("ПолеФормы") Тогда
					АктивируемыйЭлемент = ТекущийПодчиненныйЭлемент;
				Иначе	
					Прервать;
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла;
		
		Если АктивируемыйЭлемент = Неопределено Тогда
			ИндексТекущейЯчейки = ИндексЯчейкиГиперссылки;

			Пока ИндексТекущейЯчейки >  0 Цикл 
				ИндексТекущейЯчейки = ИндексТекущейЯчейки - 1;
				
				ТекущийПодчиненныйЭлемент = Таблица.ПодчиненныеЭлементы[ИндексТекущейЯчейки];
				
				Если ТипЗнч(ТекущийПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда
					АктивируемыйЭлемент = ТекущийПодчиненныйЭлемент;
					Прервать;
				Иначе
					АктивируемыйЭлемент = АктивныйЭлементВТаблицеСодержащейГиперссылку(ТекущийПодчиненныйЭлемент, ТекущийПодчиненныйЭлемент);
					Если ТипЗнч(АктивируемыйЭлемент) <> Тип("ПолеФормы") Тогда
						АктивируемыйЭлемент = Неопределено;
					Иначе	
						Прервать;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;			
	КонецЕсли;	
	
	Возврат АктивируемыйЭлемент;
КонецФункции

Функция УстановитьФлагНаличияОшибки(Форма, ПутьКДанным, ЗначениеФлага, КоличествоПодчиненныхЭлементовСОшибками, СбрасыватьСчетчикОшибокВПодчиненныхДанныхДляТекущегоУзла, СчетчикФлаговНаСтраницах, ПутиКДаннымФормы = Неопределено) Экспорт
	УстановленыНовыеСвойстваИндикации = Ложь;
	
	ОписаниеПутиКДанным = ОписаниеПутиКДаннымОбъектаПоПутиКДаннымСтрокой(ПутьКДанным);
	
	НепривязанныеДанные = Ложь;
	Если ОписаниеПутиКДанным.Тип = "ПутьКДаннымСтрокиТаблицы" Тогда
		Таблица = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПутиКДанным.ПутьКДаннымТаблицы);
		ДанныеРеквизитовИндикации = Таблица[ОписаниеПутиКДанным.ИндексСтроки];
		ОписаниеРеквизитовИндикации = Форма.ОписаниеРеквизитовПризнаковНаличияОшибок.Получить(ОписаниеПутиКДанным.ПутьКДаннымТаблицы);
		
		Если ПутиКДаннымФормы <> Неопределено 
			И ПутиКДаннымФормы.Найти(ОписаниеПутиКДанным.ПутьКДаннымТаблицы) = Неопределено Тогда
			
			НепривязанныеДанные = Истина;		
		КонецЕсли;	
	Иначе
		ОписаниеРеквизитовИндикации = Форма.ОписаниеРеквизитовПризнаковНаличияОшибок.Получить(ОписаниеПутиКДанным.ПутьКДанным);
		ДанныеРеквизитовИндикации = Форма;
		
		Если ПутиКДаннымФормы <> Неопределено 
			И ПутиКДаннымФормы.Найти(ОписаниеПутиКДанным.ПутьКДанным) = Неопределено Тогда
			
			НепривязанныеДанные = Истина;		
		КонецЕсли;
	КонецЕсли;	
		
	Если ОписаниеРеквизитовИндикации = Неопределено Тогда	
		Если НепривязанныеДанные
			И Форма.ОписаниеФлагаСИндикациейНепривязанныхОшибок <> Неопределено Тогда
			
			Если Форма[Форма.ОписаниеФлагаСИндикациейНепривязанныхОшибок.ИмяРеквизитаФлаг] = Ложь
				И ЗначениеФлага = Истина Тогда
				
				Форма[Форма.ОписаниеФлагаСИндикациейНепривязанныхОшибок.ИмяРеквизитаФлаг] = Истина;
				
				УстановленыНовыеСвойстваИндикации = Истина;
			КонецЕсли;	
		КонецЕсли;	
		Возврат УстановленыНовыеСвойстваИндикации;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ОписаниеРеквизитовИндикации.ИмяРеквизитаФлаг) Тогда
		Если ЗначениеФлага <> Неопределено
			И ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаФлаг] <> ЗначениеФлага Тогда
			
			ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаФлаг] = ЗначениеФлага;
			УстановленыНовыеСвойстваИндикации = Истина;
			
			ПриращениеСчетчика = ?(ЗначениеФлага, 1, -1);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик) Тогда
		ТекущиеКоличествоПодчиненныхЭлементовСОшибками = ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик];
		
		Если СбрасыватьСчетчикОшибокВПодчиненныхДанныхДляТекущегоУзла Тогда
			ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик] = 0;
		Иначе	
			ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик] = ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик] + КоличествоПодчиненныхЭлементовСОшибками;
		КонецЕсли;	
		
		Если (ТекущиеКоличествоПодчиненныхЭлементовСОшибками = 0 
			И ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик] > 0) Тогда
			
			УстановленыНовыеСвойстваИндикации = Истина;
			ПриращениеСчетчика = 1;
		ИначеЕсли (ТекущиеКоличествоПодчиненныхЭлементовСОшибками > 0 
			И ДанныеРеквизитовИндикации[ОписаниеРеквизитовИндикации.ИмяРеквизитаСчетчик] = 0) Тогда
			
			УстановленыНовыеСвойстваИндикации = Истина;
			ПриращениеСчетчика = - 1;
		КонецЕсли;			
	КонецЕсли;	
	
	Если УстановленыНовыеСвойстваИндикации Тогда
		Для Каждого ИмяСтраницы Из ОписаниеРеквизитовИндикации.СтраницыВладельцы Цикл
			СчетчикФлаговНаСтраницах[ИмяСтраницы] = СчетчикФлаговНаСтраницах[ИмяСтраницы] + ПриращениеСчетчика;	
		КонецЦикла;			
	КонецЕсли;	
		 	
	Возврат УстановленыНовыеСвойстваИндикации;
КонецФункции

Функция СтрокиТаблицыОшибокДанныхШапкиФормы(Форма, ПутьКДанным = "", КлючДанных = Неопределено, ПолучатьТолькоНепривязанныеОшибки = Ложь) Экспорт
	СтруктураПоиска = Новый Структура;
	
	Если ПутьКДанным <> "*" Тогда
		
		СтруктураПоиска.Вставить("Поле", ПутьКДанным);
	КонецЕсли;
	
	Если КлючДанных <> Неопределено Тогда
		СтруктураПоиска.Вставить("КлючДанных", КлючДанных);	
	КонецЕсли;	
	
	Если ПолучатьТолькоНепривязанныеОшибки Тогда
		СтруктураПоиска.Вставить("НеПривязана", Истина);	
	КонецЕсли;	
	
	Если СтруктураПоиска.Количество() = 0 Тогда
		ОшибкиШапкиФормы = Форма.ТаблицаХраненияОшибок;
	Иначе
		ОшибкиШапкиФормы = Форма.ТаблицаХраненияОшибок.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;	
	
	Возврат ОшибкиШапкиФормы;
КонецФункции	

Функция ДеревоПодчиненностиДанныхПоПутямКПодчиненнымДанным(Данные, ПутиКПодчиненнымДанным, ОписаниеПодчиненностиДанных = Неопределено)
	ОтработанныеУзлыВедущихДанных = Новый Соответствие;	
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ПутьКДанным", Новый ОписаниеТипов("Строка"));
	
	Для Каждого ПутьКПодчиненнымДанным Из ПутиКПодчиненнымДанным Цикл
		ДобавитьУзлыДереваПодчиненностиДанныхПоПутиКПодчиненнымДанным(Данные, Дерево, ПутьКПодчиненнымДанным, ОписаниеПодчиненностиДанных, ОтработанныеУзлыВедущихДанных);
	КонецЦикла;
	
	Возврат Дерево;
КонецФункции	

Функция ДобавитьУзлыДереваПодчиненностиДанныхПоПутиКПодчиненнымДанным(Данные, ДеревоПодчиненностиДанных, ПутьКПодчиненнымДанным, ОписаниеПодчиненности, ОтработанныеУзлыВедущихДанных)
	УзлыПодчиненныхДанных = ОтработанныеУзлыВедущихДанных.Получить(ПутьКПодчиненнымДанным);
	Если УзлыПодчиненныхДанных <> Неопределено Тогда
		Возврат УзлыПодчиненныхДанных;
	КонецЕсли;
	
	Если ОписаниеПодчиненности <> Неопределено Тогда
		ПутиКВедущимДанным = ПутиКВедущимДанным(Данные, ПутьКПодчиненнымДанным, ОписаниеПодчиненности, Ложь);
	Иначе
		ПутиКВедущимДанным = Новый Массив;
	КонецЕсли;	
	
	Если ПутиКВедущимДанным.Количество() = 0 Тогда
		Узел = ДеревоПодчиненностиДанных.Строки.Добавить();
		Узел.ПутьКДанным = ПутьКПодчиненнымДанным;
		
		УзлыПодчиненныхДанных = Новый Массив;
		УзлыПодчиненныхДанных.Добавить(Узел);
		
		ОтработанныеУзлыВедущихДанных.Вставить(ПутьКПодчиненнымДанным, УзлыПодчиненныхДанных);	
				
		Возврат УзлыПодчиненныхДанных;
	КонецЕсли;	
		
	УзлыПодчиненныхДанных = Новый Массив;
	
	Для Каждого ПутьКВедущимДанным Из ПутиКВедущимДанным Цикл
		УзлыВедущихДанных = ДобавитьУзлыДереваПодчиненностиДанныхПоПутиКПодчиненнымДанным(Данные, ДеревоПодчиненностиДанных, ПутьКВедущимДанным, ОписаниеПодчиненности, ОтработанныеУзлыВедущихДанных);
				
		Для Каждого УзелВедущихДанных Из УзлыВедущихДанных Цикл
			УзелПодчиненныхДанных = УзелВедущихДанных.Строки.Добавить();
			УзелПодчиненныхДанных.ПутьКДанным = ПутьКПодчиненнымДанным;
			
			УзлыПодчиненныхДанных.Добавить(УзелПодчиненныхДанных);
		КонецЦикла;		
	КонецЦикла;
		
	ОтработанныеУзлыВедущихДанных.Вставить(ПутьКПодчиненнымДанным, УзлыПодчиненныхДанных);
	
	Возврат УзлыПодчиненныхДанных;	
КонецФункции	

Функция УстановитьФлагНаличияОшибокДляУЗлаДанных(Форма, ТекущийУзел, ЗначениеФлага, УзлыДанныхСИзменяемымЗначениемФлага, ПутиКДаннымФормы, СчетчикФлаговНаСтраницах, СбрасыватьСчетчикОшибокВПодчиненныхЭлементах = Ложь)
	КоличествоПодчиненныхУзловСОшибками = 0;
		
	Для Каждого ПодчиненныйУзел Из ТекущийУзел.Строки Цикл
		Если УстановитьФлагНаличияОшибокДляУЗлаДанных(Форма, ПодчиненныйУзел, ЗначениеФлага, УзлыДанныхСИзменяемымЗначениемФлага, ПутиКДаннымФормы, СчетчикФлаговНаСтраницах, СбрасыватьСчетчикОшибокВПодчиненныхЭлементах) Тогда
			Если ЗначениеФлага Тогда
				КоличествоПодчиненныхУзловСОшибками = КоличествоПодчиненныхУзловСОшибками + 1;
			Иначе 
				КоличествоПодчиненныхУзловСОшибками = КоличествоПодчиненныхУзловСОшибками - 1;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
	СбрасыватьСчетчикОшибокВПодчиненныхДанныхДляТекущегоУзла = Ложь;
	ЕстьОшибкиВСобственныхДанных = Неопределено;
	Если УзлыДанныхСИзменяемымЗначениемФлага.Получить(ТекущийУзел.ПутьКДанным) <> Неопределено Тогда 
		ЕстьОшибкиВСобственныхДанных = ЗначениеФлага;
		СбрасыватьСчетчикОшибокВПодчиненныхДанныхДляТекущегоУзла = СбрасыватьСчетчикОшибокВПодчиненныхЭлементах
	КонецЕсли;	
		
	Возврат УстановитьФлагНаличияОшибки(
				Форма, 
				ТекущийУзел.ПутьКДанным, 
				ЕстьОшибкиВСобственныхДанных, 
				КоличествоПодчиненныхУзловСОшибками,
				СбрасыватьСчетчикОшибокВПодчиненныхДанныхДляТекущегоУзла, 
				СчетчикФлаговНаСтраницах,
				ПутиКДаннымФормы);
	
КонецФункции

Функция ОписаниеДобавляемыхРеквизитовИндикацииОшибокИзОписанияПодчиненности(ОписаниеПодчиненностиДанных)
	ОписаниеРеквизитовИндикации = Новый Массив;
	
	Для Каждого ЭлементОписанияПодчиненности Из ОписаниеПодчиненностиДанных Цикл
		Если ЭлементОписанияПодчиненности.ТипСвязиДанных = "СвязьРеквизитовОбъектов"  Тогда 
			
			ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВШапке();
			ОписаниеРеквизита.ПутьКДаннымФормыСодержащимОшибку = ЭлементОписанияПодчиненности.ПутьКРеквизитуСвязанныхДанных;
			
		Иначе
			ОписаниеРеквизита = ОписаниеРеквизитаИндикацииОшибкиВСтрокеТаблицы();
			ОписаниеРеквизита.ПутьКДаннымФормыСодержащимОшибку = ЭлементОписанияПодчиненности.ПутьКРеквизитуСвязанныхДанных;	
			ОписаниеРеквизита.ИмяРеквизитаТаблицы = ЭлементОписанияПодчиненности.ИмяРеквизитаТаблицыСвязанныхДанных;
		КонецЕсли;	
		
		ОписаниеРеквизитовИндикации.Добавить(ОписаниеРеквизита);
	КонецЦикла;	
	
	Возврат ОписаниеРеквизитовИндикации;	
КонецФункции

Функция СоздатьРеквизитСчетчикНаличияОшибкиВПодчиненныхДанных(ДобавляемыеРеквизиты, СвойстваСоздаваемогоРеквизита, ОписаниеСозданныхРеквизитов)
	
	Если СвойстваСоздаваемогоРеквизита.Тип = "РеквизитСтрокиТаблицы" Тогда
		
		ИмяРеквизитаСчетчик = "КоличествоПодчиненныхЭлементовСОшибками"; 
		
		Путь = СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку;
	Иначе
		ЭлементыПути = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку, ".");
		
		Путь  = "";
		Если ЭлементыПути.Количество() = 0 Тогда
			ИмяРеквизитаСчетчик = "КоличествоПодчиненныхЭлементовСОшибками";
		Иначе
			ИмяРеквизитаСчетчик = ЭлементыПути[ЭлементыПути.Количество() - 1] + "КоличествоПодчиненныхЭлементовСОшибками";
		КонецЕсли;		
	КонецЕсли;	
	
	ПолныйПуть = Путь + ?(Путь = "", "", ".") + ИмяРеквизитаСчетчик;
	
	ОписаниеСозданногоРеквизита =  ОписаниеСозданныхРеквизитов[СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку];
	
	Если ОписаниеСозданногоРеквизита = Неопределено Тогда 
		ОписаниеСозданногоРеквизита = ОписаниеРеквизитовИндикацииСвязанныхСДаннымиФормы();
		ОписаниеСозданногоРеквизита.ПутьКДанным = Путь;
		ОписаниеСозданногоРеквизита.ИмяРеквизитаСчетчик = ИмяРеквизитаСчетчик;
		ОписаниеСозданногоРеквизита.ПутьКДаннымФормыСодержащимОшибку = СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку;
		
		ОписаниеСозданныхРеквизитов.Вставить(СвойстваСоздаваемогоРеквизита.ПутьКДаннымФормыСодержащимОшибку, ОписаниеСозданногоРеквизита);

		РеквизитСчетчик = Новый РеквизитФормы(ИмяРеквизитаСчетчик, Новый ОписаниеТипов("Число"), Путь);
		ДобавляемыеРеквизиты.Добавить(РеквизитСчетчик);
	Иначе
		Если Не ЗначениеЗаполнено(ОписаниеСозданногоРеквизита.ИмяРеквизитаСчетчик) Тогда
			ОписаниеСозданногоРеквизита.ИмяРеквизитаСчетчик = ИмяРеквизитаСчетчик;
			РеквизитСчетчик = Новый РеквизитФормы(ИмяРеквизитаСчетчик, Новый ОписаниеТипов("Число"), Путь);
			ДобавляемыеРеквизиты.Добавить(РеквизитСчетчик);
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ОписаниеСозданногоРеквизита;
КонецФункции	

#КонецОбласти

Функция ИндексСтрокой(ИндексСтроки)
	Если ИндексСтроки = 0 Тогда 
		Возврат "0";
	ИначеЕсли ИндексСтроки < 1000 Тогда
		Возврат Строка(ИндексСтроки);
	Иначе 	
		ИндексСтрокой = Строка(ИндексСтроки);
		Возврат СтрЗаменить(ИндексСтрокой, Символы.НПП, "");
	КонецЕсли;	
КонецФункции

Функция ИндексСтрокиТаблицыИзПутиКДанным(ПутьКДанным)
	НомерСимволаОткрывающейСкобки = СтрНайти(ПутьКДанным, "[");
	
	Если НомерСимволаОткрывающейСкобки = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НомерСимволаЗакрывающейСкобки = СтрНайти(Сред(ПутьКДанным, НомерСимволаОткрывающейСкобки + 1), "]") + НомерСимволаОткрывающейСкобки;
	
	Если НомерСимволаЗакрывающейСкобки = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИндексСтрокой = Сред(ПутьКДанным, НомерСимволаОткрывающейСкобки + 1, НомерСимволаЗакрывающейСкобки - НомерСимволаОткрывающейСкобки - 1);
	
	Если ПустаяСтрока(ИндексСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Число(ИндексСтрокой);	
КонецФункции

Процедура ОшибкиИзСтрокиТаблицыФормыВМассив(Ошибки, СтрокаТаблицы, ПутьКДаннымТаблицы, ИндексСтроки, ИмяРеквизитаСтроки = "", КлючДанных = Неопределено, ФормироватьСообщенияПользователю = Ложь)
	ОшибкиСтрокиТаблицы = СтрокиТаблицыОшибокСтрокиТаблицыДанныхФормы(СтрокаТаблицы, ИмяРеквизитаСтроки, КлючДанных);
	
	Для Каждого ОшибкаДанныхСтрокиТаблицы Из ОшибкиСтрокиТаблицы Цикл
		ОписаниеОшибки = ОписаниеОшибкиИзОшибкиДанныхСтрокиТаблицы(ОшибкаДанныхСтрокиТаблицы, ПутьКДаннымТаблицы, ИндексСтроки);
		
		Если ФормироватьСообщенияПользователю Тогда
			СообщениеОбОшибке = СообщениеПользователюИзОписанияОшибкиВДанных(ОписаниеОшибки);
			Ошибки.Добавить(СообщениеОбОшибке);
		Иначе
			Ошибки.Добавить(ОписаниеОшибки);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Функция СообщениеПользователюИзОписанияОшибкиВДанных(ОписаниеОшибкиДанных)
	СообщениеОбОшибке = Новый СообщениеПользователю;
	
	Если ЗначениеЗаполнено(ОписаниеОшибкиДанных.КлючДанных) Тогда 
		СообщениеОбОшибке.УстановитьДанные(ОписаниеОшибкиДанных.КлючДанных);
	КонецЕсли;
	
	СообщениеОбОшибке.Текст = ОписаниеОшибкиДанных.Текст;
	СообщениеОбОшибке.Поле = ПутьКДаннымСтрокойПоОписаниюПути(ОписаниеОшибкиДанных.ПутьКДанным);
	
	Возврат СообщениеОбОшибке;
	
КонецФункции	

Функция ОписаниеОшибкиИзОшибкиДанныхСтрокиТаблицы(ОшибкаДанныхСтрокиТаблицы, ПутьКДаннымТаблицы, ИндексСтроки)
	ОписаниеОшибки = ОписаниеОшибкиДанных();
		
	ОписаниеОшибки.Текст = ОшибкаДанныхСтрокиТаблицы.Текст;
	ОписаниеОшибки.КлючДанных = ОшибкаДанныхСтрокиТаблицы.КлючДанных;
	
	ПутьКДанным = ОписаниеПутиКДаннымСтрокиТаблицы();
	ПутьКДанным.ПутьКДаннымТаблицы = ПутьКДаннымТаблицы;
	ПутьКДанным.ИндексСтроки = ИндексСтроки;
	ПутьКДанным.ИмяРеквизитаСтроки = ОшибкаДанныхСтрокиТаблицы.Поле;
	
	ОписаниеОшибки.ПутьКДанным = ПутьКДанным;
	
	Возврат ОписаниеОшибки;	
КонецФункции

Функция ОписаниеОшибкиДанных()
	ОписаниеОшибкиВДанных = Новый Структура;
	
	ОписаниеОшибкиВДанных.Вставить("Текст", "");
	ОписаниеОшибкиВДанных.Вставить("КлючДанных", "");
	ОписаниеОшибкиВДанных.Вставить("ПутьКДанным");
	
	Возврат ОписаниеОшибкиВДанных;
КонецФункции	

Процедура ОшибкиИзДанныхШапкиФормыВМассив(Ошибки, Форма, ПутьКДанным = "", КлючДанных = Неопределено, ПолучатьТолькоНепривязанныеОшибки = Ложь, ФормироватьСообщенияПользователю = Ложь)
	ОшибкиШапкиФормы = СтрокиТаблицыОшибокДанныхШапкиФормы(Форма, ПутьКДанным, КлючДанных, ПолучатьТолькоНепривязанныеОшибки);
		
	Для Каждого ОшибкаДанныхШапки Из ОшибкиШапкиФормы Цикл
		ОписаниеОшибки = ОписаниеОшибкиИзОшибкиДанныхШапки(ОшибкаДанныхШапки);
		
		Если ФормироватьСообщенияПользователю Тогда
			СообщениеОбОшибке = СообщениеПользователюИзОписанияОшибкиВДанных(ОписаниеОшибки);
			Ошибки.Добавить(СообщениеОбОшибке);
		Иначе
			Ошибки.Добавить(ОписаниеОшибки);
		КонецЕсли;	
	КонецЦикла;	

КонецПроцедуры

Функция ОписаниеОшибкиИзОшибкиДанныхШапки(ОшибкаДанныхШапки)
	ОписаниеОшибки = ОписаниеОшибкиДанных();
		
	ОписаниеОшибки.Текст = ОшибкаДанныхШапки.Текст;
	ОписаниеОшибки.КлючДанных = ОшибкаДанныхШапки.КлючДанных;
	
	ПутьКДанным = ОписаниеПутиКДаннымРеквизитаОбъекта();
	ПутьКДанным.ПутьКДанным = ОшибкаДанныхШапки.Поле;
	
	ОписаниеОшибки.ПутьКДанным = ПутьКДанным;
		
	Возврат ОписаниеОшибки;	

КонецФункции

Процедура УстановитьЗаголовкиВТаблице(Форма, ДанныеТаблицы, ОписаниеКолонокЗаголовков) Экспорт
	ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков);
	
	Для Каждого СтрокТаблицы Из ДанныеТаблицы Цикл
		УстановитьЗаголовкиВСтрокеТаблицы(Форма, СтрокТаблицы, ОписаниеКолонокЗаголовков, Ложь);			
	КонецЦикла;	
КонецПроцедуры

Функция УстановитьЗаголовкиВСтрокеТаблицы(Форма, СтрокаТаблицы, ОписаниеКолонокЗаголовков, ЗаполнятьЗаголовкиПоУмолчанию = Истина)
	Если ЗаполнятьЗаголовкиПоУмолчанию Тогда
		ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков);
	КонецЕсли;	

	Для Каждого ОписаниеЗаголовка Из ОписаниеКолонокЗаголовков Цикл
		СтрокаТаблицы[ОписаниеЗаголовка.ПолеТаблицы + "Заголовок"] = ОписаниеЗаголовка.Заголовок + ":";			
	КонецЦикла;
КонецФункции

Процедура ЗаполнитьТекстыЗаголовковКолонокПоУмолчанию(Форма, ОписаниеКолонокЗаголовков)
	Если ОписаниеКолонокЗаголовков <> Неопределено Тогда
		Для Каждого ОписаниеКолонки Из ОписаниеКолонокЗаголовков Цикл
			Если Не ЗначениеЗаполнено(ОписаниеКолонки.Заголовок) Тогда
				ПолеТаблицы = Форма.Элементы.Найти(ОписаниеКолонки.ПолеТаблицы);
				
				Если ПолеТаблицы <> Неопределено Тогда
					ОписаниеКолонки.Заголовок = ПолеТаблицы.Заголовок;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция СтрокиТаблицыОшибокСтрокиТаблицыДанныхФормы(СтрокаТаблицы, ИмяРеквизитаСтроки = "", КлючДанных = Неопределено)
	ОшибкиСтрокиТаблицы = Новый Массив;
	
	Если Не СтрокаТаблицы.Свойство("ТаблицаХраненияОшибок") Тогда
		Возврат ОшибкиСтрокиТаблицы;
	КонецЕсли;	
	
	СтруктураПоиска = Новый Структура;
	
	Если ИмяРеквизитаСтроки <> "*" Тогда
		
		СтруктураПоиска.Вставить("Поле", ИмяРеквизитаСтроки);
	КонецЕсли;
	
	Если КлючДанных <> Неопределено Тогда
		СтруктураПоиска.Вставить("КлючДанных", КлючДанных);	
	КонецЕсли;	
		
	Если СтруктураПоиска.Количество() = 0 Тогда	
		Для Каждого СтрокаОшибки Из СтрокаТаблицы.ТаблицаХраненияОшибок Цикл 
			ОшибкиСтрокиТаблицы.Добавить(СтрокаОшибки);
		КонецЦикла;	
	Иначе
		ОшибкиСтрокиТаблицы = СтрокаТаблицы.ТаблицаХраненияОшибок.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;	
	
	Возврат ОшибкиСтрокиТаблицы;	
КонецФункции	

Процедура УстановитьКартинкиДляЗаголовковСтраницФормы(Форма) 
	ИзмененыСвойстваЭлементовФормы = Ложь;
	
	ЦветЗаголовкаГруппыСОшибкой = Новый Цвет(255, 0, 0);
	Для Каждого СчетчикФлагов Из Форма.СчетчикФлаговНаСтраницах Цикл
		Элемент =  Форма.Элементы.Найти(СчетчикФлагов.Ключ);
		
		Если Элемент = Неопределено Тогда
			Возврат;
		КонецЕсли;	
		
		Если СчетчикФлагов.Значение = 0 Тогда
			Если Элемент.Вид = ВидГруппыФормы.Страница Тогда
				Если Элемент.Картинка.Вид <> ВидКартинки.Пустая Тогда
					Элемент.Картинка = Новый Картинка;
					ИзмененыСвойстваЭлементовФормы = Истина;
				КонецЕсли;
			Иначе
				Если Элемент.ЦветТекстаЗаголовка = ЦветЗаголовкаГруппыСОшибкой Тогда
					Цвет = Форма.ЦветаЗаголовковСворачиваемыхГрупп[Элемент.Имя];
					
					Элемент.ЦветТекстаЗаголовка = Цвет;
					ИзмененыСвойстваЭлементовФормы = Истина;
				КонецЕсли;				
			КонецЕсли;	
		Иначе
			Если Элемент.Вид = ВидГруппыФормы.Страница Тогда
				Если Элемент.Картинка.Вид = ВидКартинки.Пустая Тогда
					Элемент.Картинка = БиблиотекаКартинок.Предупреждение;
					ИзмененыСвойстваЭлементовФормы = Истина;
				КонецЕсли;	
			Иначе
				Если Элемент.ЦветТекстаЗаголовка <> ЦветЗаголовкаГруппыСОшибкой Тогда
					Элемент.ЦветТекстаЗаголовка = ЦветЗаголовкаГруппыСОшибкой;
					ИзмененыСвойстваЭлементовФормы = Истина;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого ОписаниеЭлемента Из Форма.ОписаниеЭлементовСИндикациейОшибок Цикл
		Если ОписаниеЭлемента.Значение.Тип = "ОписаниеЭлементаСИндикациейОшибокВСтрокеТаблицы" Тогда
			Таблица = Форма.Элементы.Найти(ОписаниеЭлемента.Значение.ИмяЭлементаТаблица);
			ГиперссылкаТаблицы = Форма.Элементы.Найти(ОписаниеЭлемента.Ключ);
			
			Если Элемент <> Неопределено
				И ГиперссылкаТаблицы <> Неопределено Тогда
				
				ТекущаяЯчейка = АктивныйЭлементВТаблицеСодержащейГиперссылку(Элемент, ГиперссылкаТаблицы);  	
			КонецЕсли;				
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	

Процедура УстановитьВыводВФайлСвойстваЗначенияЭлемента(Элемент, ИмяСвойства) Экспорт
	
	СтрокаПоля = Элемент.Поля.Найти(ИмяСвойства, "ИмяПоля");
	Если СтрокаПоля <> Неопределено Тогда
		СтрокаПоля.НеВыводитьВФайл = Не ЗначениеЗаполнено(Элемент.Значение[ИмяСвойства]);
	КонецЕсли;		
	
КонецПроцедуры

Процедура УстановитьВыводВФайл(Элемент, ПроверяемоеСвойство) Экспорт 
	
	МассивИмен = СтрРазделить(ПроверяемоеСвойство, ".");
	Свойство = Элемент;
	
	Для Сч = 0 По МассивИмен.Количество()-1 Цикл
		Свойство = Свойство[МассивИмен[Сч]]
	КонецЦикла;
	
	Элемент.НеВыводитьВФайл = Не ЗначениеЗаполнено(Свойство);
	
КонецПроцедуры

Функция ДанныеСтрокСовпадают(ПерваяСтрока, ВтораяСтрока, ПроверяемыеРеквизиты) Экспорт 
	
	Для Каждого ИмяРеквизита Из ПроверяемыеРеквизиты Цикл 
		Если Не ЗначениеЗаполнено(ПерваяСтрока[ИмяРеквизита]) И Не ЗначениеЗаполнено(ВтораяСтрока[ИмяРеквизита]) Тогда 
			Продолжить;
		КонецЕсли;
		Если ПерваяСтрока[ИмяРеквизита] <> ВтораяСтрока[ИмяРеквизита] Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ВписатьСтавкуВНаборДанныхИсчисляемогоСтажа(Ставка, НаборДанных) Экспорт

	ИмяПоля = "ИС5__ДоляСтавки";
	Если ЗначениеЗаполнено(Ставка) Тогда
		НаборДанных[ИмяПоля].Значение = Ставка
	Иначе
		НаборДанных.Удалить(ИмяПоля)
	КонецЕсли;	

КонецПроцедуры

Процедура ВписатьСтавкуВНаборДанныхТеррУсловий(ТерриториальныеУсловия, Ставка, НаборДанных) Экспорт

	ИмяПоля = "ИС5__Коэффициент";
	Если (ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКС
		Или ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКС
		Или ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.РКСМ 
		Или ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.МКСР) И ЗначениеЗаполнено(Ставка) Тогда
		
		НаборДанных.Значение[ИмяПоля] = Ставка;
	Иначе
		ОписаниеПолей = НаборДанных.Поля;
		СтрокаКУдалению = ОписаниеПолей.Найти(ИмяПоля,"ИмяПоля");
		ОписаниеПолей.Удалить(СтрокаКУдалению);
	КонецЕсли;

КонецПроцедуры 

Процедура ВывестиДанныеОУТ(Выборка, УзелОснование, ФорматОУТ)Экспорт 
	
	Если ЗначениеЗаполнено(Выборка.ОсобыеУсловияТруда) Тогда 
		НаборЗаписейОУТ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОУТ);
		НаборЗаписейОУТ.Значение.Код = ВРег(СокрЛП(Выборка.ОсобыеУсловияТрудаКод));
		НаборЗаписейОУТ.Значение.ПозицияСписка = ВРег(СокрЛП(Выборка.КодПозицииСпискаКод));
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелОснование, Новый Структура("ОУТ", НаборЗаписейОУТ));
	ИначеЕсли ЗначениеЗаполнено(Выборка.ОснованиеДосрочногоНазначенияПенсии) Тогда
		НаборЗаписейОУТ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОУТ);
		НаборЗаписейОУТ.Значение.Код = ВРег(СокрЛП(Выборка.ОснованиеДосрочногоНазначенияПенсииКод));
		НаборЗаписейОУТ.Значение.ПозицияСписка = ВРег(СокрЛП(Выборка.КодПозицииСпискаКод));
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелОснование, Новый Структура("ОУТ", НаборЗаписейОУТ));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ОсобыеУсловияТруда) 
		И ЗначениеЗаполнено(Выборка.ОснованиеДосрочногоНазначенияПенсии) Тогда
		НаборЗаписейОУТ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОУТ);
		НаборЗаписейОУТ.Значение.Код = ВРег(СокрЛП(Выборка.ОснованиеДосрочногоНазначенияПенсииКод));
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелОснование, Новый Структура("ОУТ", НаборЗаписейОУТ));
	КонецЕсли;
	
КонецПроцедуры 

Функция СписокОтсутствийИсключающихКалендарнуюВыработку() Экспорт
	
	СписокОтсутствий = Новый Массив;
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ВАХТА);
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ВРНЕТРУД);
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕКРЕТ);
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЛОТПУСК);
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕДНЕТРУД);
	СписокОтсутствий.Добавить(Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.МЕСЯЦ);
	
	Возврат СписокОтсутствий;
	
КонецФункции
 ////////////////////////////////////////////////////////////////////////////////
// Формирование файла ЕФС-1 

Функция ИмяФайлаЕФС1(Организация, Дата, УИД = Неопределено) Экспорт 
	
	Если УИД = Неопределено Тогда
		УИД = Новый УникальныйИдентификатор;
	КонецЕсли;	
	
	Параметры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрационныйНомерПФР, КодОрганаПФР");
	
	Возврат "ПФР"
			+ "_" + ?(ЗначениеЗаполнено(Параметры.РегистрационныйНомерПФР), Параметры.РегистрационныйНомерПФР, "")
			+ "_ЕФС-1"
			+ "_" + Формат(Дата, "ДФ=ггггММдд")
			+ "_" + УИД;
			
КонецФункции

Функция СписокРазделовЕФС1() Экспорт
	
	РазделыЕФС1 = Новый Структура;
	РазделыЕФС1.Вставить("СЗВТД", Ложь);
	РазделыЕФС1.Вставить("СЗВСТАЖ", Ложь);
	РазделыЕФС1.Вставить("ДНП", Ложь);
	РазделыЕФС1.Вставить("ДСВ", Ложь);
	
	Возврат РазделыЕФС1;
	
КонецФункции

Функция СформироватьВыходнойФайлЕФС1(РазделыЕФС1, ВыборкаПоШапкеДокумента, ВыборкаТД = Неопределено, ВыборкаСТАЖ = Неопределено, ВыборкаДНП = Неопределено, ВыборкаДСВ = Неопределено) Экспорт 
	
	// Загружаем формат файла сведений
	ДеревоФорматаXML = ПолучитьОбщийМакет("ФорматЕФС1_2022XML");
	ТекстФорматаXML = ДеревоФорматаXML.ПолучитьТекст();
	
	ДеревоФормата = СправкиПоНДФЛ.ЗагрузитьXMLВДокументDOM(ТекстФорматаXML);
	
	ФорматЕФС_1 = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "ЕФС_1");
	ФорматЕФС_1.СтраховательЗаКоторого.НеВыводитьВФайл = Истина;
	
	НаборЗаписейДатаЗаполнения = Новый Структура("ДатаЗаполнения", ОбщегоНазначения.СкопироватьРекурсивно(ФорматЕФС_1.ДатаЗаполнения));
	ФорматЕФС_1.Удалить("ДатаЗаполнения");
	
	НаборЗаписейРуководитель = Новый Структура("Руководитель", ОбщегоНазначения.СкопироватьРекурсивно(ФорматЕФС_1.Руководитель));
	ФорматЕФС_1.Удалить("Руководитель");
	
	ФорматСЗВ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЕФС_1.СЗВ.Значение);
	ФорматЕФС_1.Удалить("СЗВ");
	
	// Создаем начальное дерево
	ДеревоВыгрузки = СправкиПоНДФЛ.СоздатьДеревоXML();
	
	Атрибуты = Новый Соответствие;
	Атрибуты.Вставить("xmlns", "http://пф.рф/ЕФС-1/2022-09-22");
	Атрибуты.Вставить("xmlns:ВС2", "http://пф.рф/ВС/типы/2017-10-23");
	Атрибуты.Вставить("xmlns:АФ5", "http://пф.рф/АФ/2018-12-07");
	Атрибуты.Вставить("xmlns:УТ2", "http://пф.рф/УТ/2017-08-21");
	Атрибуты.Вставить("xmlns:ЕФС", "http://пф.рф/ВС/ЕФС/2022-09-22");
	
	УзелПФР = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(ДеревоВыгрузки, "ЭДПФР", "", Атрибуты);
	
	// Данные формы и страхователя
	НаборЗаписейСтрахователь = ФорматЕФС_1.Страхователь.Значение;
	НаборЗаписейСтрахователь.ЕФС__РегНомер = ВыборкаПоШапкеДокумента.РегистрационныйНомерПФР;
	НаименованиеКраткое = СокрЛП(ВРег(ВыборкаПоШапкеДокумента.НаименованиеСокращенное));
	УдалитьПробелыРядомСДефисом(НаименованиеКраткое);
	НаборЗаписейСтрахователь.ЕФС__Наименование = НаименованиеКраткое;
	НаборЗаписейСтрахователь.УТ2__ИНН = ВыборкаПоШапкеДокумента.ИНН;
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КПП) Тогда
		НаборЗаписейСтрахователь.УТ2__КПП = ВыборкаПоШапкеДокумента.КПП;
	Иначе
		НаборЗаписейСтрахователь.Удалить("УТ2__КПП");
		СтрокаПоляКПП = ФорматЕФС_1.Страхователь.Поля.Найти("УТ2__КПП", "ИмяПоля");
		Если СтрокаПоляКПП <> Неопределено Тогда
			ФорматЕФС_1.Страхователь.Поля.Удалить(СтрокаПоляКПП);
		КонецЕсли;		
	КонецЕсли;
	
	НаборЗаписейСтрахователь.УТ2__КодПоОКВЭД = ВыборкаПоШапкеДокумента.КодОКВЭД;
	
	Если ВыборкаПоШапкеДокумента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда 
		НаборЗаписейСтрахователь.ЕФС__ОГРНИП = ВыборкаПоШапкеДокумента.ОГРН;
	Иначе 
		НаборЗаписейСтрахователь.ЕФС__ОГРН = ВыборкаПоШапкеДокумента.ОГРН;
	КонецЕсли;
	
	ВидыКонтактнойИнформации = Новый Массив;
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.EmailОрганизации);
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ВыборкаПоШапкеДокумента.Организация, , ВидыКонтактнойИнформации, ВыборкаПоШапкеДокумента.Дата);
		
	ТелефонОрганизации = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации, "Вид");
	Если ТелефонОрганизации <> Неопределено Тогда 
		НаборЗаписейСтрахователь.УТ2__Телефон = ПредставлениеТелефонаЕФС1(ТелефонОрганизации.Представление);
	КонецЕсли;
	
	АдресЭлПочтыОрганизации = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.EmailОрганизации, "Вид");
	Если АдресЭлПочтыОрганизации <> Неопределено Тогда 
		НаборЗаписейСтрахователь.УТ2__АдресЭлПочты = АдресЭлПочтыОрганизации.Представление;
	КонецЕсли;
	
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "ЕФС__ОКФС");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "ЕФС__ОКОГУ");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "ЕФС__ОКПО");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "УТ2__КодПоОКВЭД");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "ЕФС__ОГРН");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "ЕФС__ОГРНИП");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "УТ2__Телефон");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.Страхователь, "УТ2__АдресЭлПочты");
	
	// Данные прежнего страхователя
	Если РазделыЕФС1.СЗВТД И ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПрежнийСтраховательНаименованиеПолное) Тогда
		ФорматЕФС_1.СтраховательЗаКоторого.НеВыводитьВФайл = Ложь;
		НаборЗаписейРаботодатель = ФорматЕФС_1.СтраховательЗаКоторого.Значение;
		ПрежнийСтраховательНаименованиеПолное = СокрЛП(ВРег(ВыборкаПоШапкеДокумента.ПрежнийСтраховательНаименованиеПолное));
		УдалитьПробелыРядомСДефисом(ПрежнийСтраховательНаименованиеПолное);
		НаборЗаписейРаботодатель.ЕФС__Наименование = ПрежнийСтраховательНаименованиеПолное;
		НаборЗаписейРаботодатель.ЕФС__РегНомер = СокрЛП(ВыборкаПоШапкеДокумента.ПрежнийСтраховательРегистрационныйНомерПФР);
		НаборЗаписейРаботодатель.УТ2__ИНН = СокрЛП(ВыборкаПоШапкеДокумента.ПрежнийСтраховательИНН);
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПрежнийСтраховательКПП) Тогда
			НаборЗаписейРаботодатель.УТ2__КПП = СокрЛП(ВыборкаПоШапкеДокумента.ПрежнийСтраховательКПП);
		Иначе
			НаборЗаписейРаботодатель.Удалить("УТ2__КПП");
			СтрокаПоляКПП = ФорматЕФС_1.СтраховательЗаКоторого.Поля.Найти("УТ2__КПП", "ИмяПоля");
			Если СтрокаПоляКПП <> Неопределено Тогда
				ФорматЕФС_1.СтраховательЗаКоторого.Поля.Удалить(СтрокаПоляКПП);
			КонецЕсли;
		КонецЕсли;
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "ЕФС__ОКФС");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "ЕФС__ОКОГУ");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "ЕФС__ОКПО");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "УТ2__КодПоОКВЭД");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "ЕФС__ОГРН");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "ЕФС__ОГРНИП");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "УТ2__Телефон");
		УстановитьВыводВФайлСвойстваЗначенияЭлемента(ФорматЕФС_1.СтраховательЗаКоторого, "УТ2__АдресЭлПочты");
	КонецЕсли;
	
	УзелЕФС1 = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "ЕФС-1", "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЕФС1, ФорматЕФС_1);

	УзелСЗВ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелЕФС1, "СЗВ", "");
	
	Если РазделыЕФС1.СЗВТД Тогда 
		
		ФорматЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ.ЗЛ.Значение);
		
		ФорматСЗВ_ТД = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СЗВ_ТД.Значение);
		ФорматЗЛ.Удалить("СЗВ_ТД");
		
		ФорматСЗВ_СТАЖ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СЗВ_СТАЖ.Значение);
		ФорматЗЛ.Удалить("СЗВ_СТАЖ");
		
		НаборЗаписейЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ);
		
		ФорматТрудоваяДеятельность = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_ТД.ТД.Значение);
		ФорматСЗВ_ТД.Удалить("ТД");
		
		ФорматМероприятие = ОбщегоНазначения.СкопироватьРекурсивно(ФорматТрудоваяДеятельность.Мероприятие.Значение);
		ФорматТрудоваяДеятельность.Удалить("Мероприятие");
		
		ФорматМероприятиеОтменяемое = ОбщегоНазначения.СкопироватьРекурсивно(ФорматТрудоваяДеятельность.МероприятиеОтменяемое.Значение);
		ФорматТрудоваяДеятельность.Удалить("МероприятиеОтменяемое");
		
		Пока ВыборкаТД.СледующийПоЗначениюПоля("НомерСтрокиСотрудника") Цикл
			
			// Данные по физлицу
			Фамилия = СокрЛП(ВыборкаТД.Фамилия);
			Имя = СокрЛП(ВыборкаТД.Имя);
			Отчество = СокрЛП(ВыборкаТД.Отчество);
			
			НаборЗаписейЗЛ.УТ2__СНИЛС.Значение = ВыборкаТД.СтраховойНомерПФР;
			НаборЗаписейЗЛ.УТ2__ДатаРождения.Значение = Формат(ВыборкаТД.ДатаРождения, "ДФ=yyyy-MM-dd");
			НаборЗаписейЗЛ.ИНН.Значение = ВыборкаТД.ИННРаботника;
			НаборЗаписейЗЛ.СтатусЗЛ.Значение = Строка(ВыборкаТД.КодКатегорииЗЛ);
			НаборЗаписейЗЛ.Гражданство.Значение = ВыборкаТД.Гражданство;
			
			НаборЗаписейФИО = НаборЗаписейЗЛ.УТ2__ФИО.Значение;
			НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
			НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
			НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
			
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Фамилия");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Имя");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Отчество");
			
			УстановитьВыводВФайл(НаборЗаписейЗЛ.ИНН, "Значение");
			
			УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ, "ЗЛ", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
			
			НаборЗаписейСЗВ_ТД = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_ТД);
			
			Если ЗначениеЗаполнено(ВыборкаТД.ЗаявлениеОПредоставленииДата) Тогда
				ЗаявлениеОПредоставлении = НаборЗаписейСЗВ_ТД.Заявления.Значение.ЗаявлениеОПредоставлении.Значение;
				ЗаявлениеОПредоставлении.Дата.Значение = Формат(ВыборкаТД.ЗаявлениеОПредоставленииДата, "ДФ=yyyy-MM-dd");
				ЗаявлениеОПредоставлении.СтатусЗаявления.Значение = ?(ЗначениеЗаполнено(ВыборкаТД.ОтменаЗаявленияОПредоставлении), 0, 1);
			Иначе
				НаборЗаписейСЗВ_ТД.Заявления.Значение.ЗаявлениеОПредоставлении.НеВыводитьВФайл = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаТД.ЗаявлениеОПродолженииДата) Тогда
				ЗаявлениеОПродолжении = НаборЗаписейСЗВ_ТД.Заявления.Значение.ЗаявлениеОПродолжении.Значение;
				ЗаявлениеОПродолжении.Дата.Значение = Формат(ВыборкаТД.ЗаявлениеОПродолженииДата, "ДФ=yyyy-MM-dd");
				ЗаявлениеОПродолжении.СтатусЗаявления.Значение = ?(ЗначениеЗаполнено(ВыборкаТД.ОтменаЗаявленияОПродолжении), 0, 1);
			Иначе
				НаборЗаписейСЗВ_ТД.Заявления.Значение.ЗаявлениеОПродолжении.НеВыводитьВФайл = Истина;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ВыборкаТД.ЗаявлениеОПредоставленииДата) 
				И Не ЗначениеЗаполнено(ВыборкаТД.ЗаявлениеОПродолжении) Тогда
				НаборЗаписейСЗВ_ТД.Заявления.НеВыводитьВФайл = Истина;
			КонецЕсли;
			
			УзелСЗВ_ТД = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСотрудника, "СЗВ-ТД", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСЗВ_ТД, НаборЗаписейСЗВ_ТД);
			
			УзелТрудоваяДеятельность = Неопределено;
			
			Пока ВыборкаТД.Следующий() Цикл
				
				Если Не ЗначениеЗаполнено(ВыборкаТД.ВидМероприятия) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаТД.ДатаОтмены) Тогда
					
					НаборЗаписейМероприятиеОтменяемое = ОбщегоНазначения.СкопироватьРекурсивно(ФорматМероприятиеОтменяемое);
					НаборЗаписейМероприятиеОтменяемое.ЕФС__UUID.Значение = Строка(ВыборкаТД.ИдМероприятия);
					НаборЗаписейМероприятиеОтменяемое.ЕФС__ДатаМероприятия.Значение = Формат(ВыборкаТД.ДатаМероприятия, "ДФ=yyyy-MM-dd");
					НаборЗаписейМероприятиеОтменяемое.ЕФС__ДатаОтмены.Значение = Формат(ВыборкаТД.ДатаОтмены, "ДФ=yyyy-MM-dd");
					НаборЗаписейМероприятиеОтменяемое.ЕФС__Вид.Значение = Перечисления.ВидыМероприятийТрудовойДеятельности.Код(ВыборкаТД.ВидМероприятия);
					НаборЗаписейМероприятиеОтменяемое.ЕФС__ЯвляетсяСовместителем.Значение = ?(ВыборкаТД.ЯвляетсяСовместителем, 1, 0);
					
					Если ЗначениеЗаполнено(ВыборкаТД.ТерриториальныеУсловия) Тогда
						НаборЗаписейМероприятиеОтменяемое.ЕФС__РаботаКС.Значение = СокрЛП(Строка(ВыборкаТД.ТерриториальныеУсловия));
					Иначе
						НаборЗаписейМероприятиеОтменяемое.ЕФС__РаботаКС.НеВыводитьВФайл = Истина;
					КонецЕсли;
					
					УзелМероприятие = ВывестиВФайлМероприятиеЕФС1(УзелСЗВ_ТД, УзелТрудоваяДеятельность, "МероприятиеОтменяемое", НаборЗаписейМероприятиеОтменяемое);
					
				Иначе
					
					НаборЗаписейМероприятие = ОбщегоНазначения.СкопироватьРекурсивно(ФорматМероприятие);
					НаборЗаписейМероприятие.ЕФС__UUID.Значение = Строка(ВыборкаТД.ИдМероприятия);
					НаборЗаписейМероприятие.ЕФС__Дата.Значение = Формат(ВыборкаТД.ДатаМероприятия, "ДФ=yyyy-MM-dd");
					НаборЗаписейМероприятие.ЕФС__Вид.Значение = Перечисления.ВидыМероприятийТрудовойДеятельности.Код(ВыборкаТД.ВидМероприятия);
					
					Если ЗначениеЗаполнено(ВыборкаТД.ТерриториальныеУсловия) Тогда
						НаборЗаписейМероприятие.ЕФС__РаботаКС.Значение = СокрЛП(Строка(ВыборкаТД.ТерриториальныеУсловия));
					Иначе
						НаборЗаписейМероприятие.ЕФС__РаботаКС.НеВыводитьВФайл = Истина;
					КонецЕсли;
					
					НаборЗаписейМероприятие.ЕФС__Сведения.Значение = СокрЛП(ВыборкаТД.Сведения);
					
					Если ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.УстановлениеВторойПрофессии
						И ЗначениеЗаполнено(ВыборкаТД.ОписаниеДолжности) Тогда
						
						НаборЗаписейМероприятие.ЕФС__Должность.Значение = СокрЛП(ВыборкаТД.ОписаниеДолжности);
						
					Иначе
						
						НаборЗаписейМероприятие.ЕФС__Должность.Значение = СокрЛП(ВыборкаТД.Должность) 
							+ ?(ЗначениеЗаполнено(ВыборкаТД.КодПоРееструДолжностей), ", " + СокрЛП(ВыборкаТД.КодПоРееструДолжностей), "")
							+ ?(ЗначениеЗаполнено(ВыборкаТД.РазрядКатегория), ", " + СокрЛП(ВыборкаТД.РазрядКатегория), "");
						
					КонецЕсли;
					
					НаборЗаписейМероприятие.ЕФС__ЯвляетсяСовместителем.Значение = ?(ВыборкаТД.ЯвляетсяСовместителем, "1", "0");
					
					НаборЗаписейМероприятие.ЕФС__СтруктурноеПодразделение.Значение = СокрЛП(ВыборкаТД.Подразделение);
					
					НаборЗаписейМероприятие.ЕФС__ВидПР.Значение = СокрЛП(ВыборкаТД.ВидПР);
					
					НаборЗаписейМероприятие.ЕФС__КодВФпоОКЗ.Значение = СокрЛП(ЭлектронныеТрудовыеКнижкиУНФ.ПредставлениеКодаОКЗ(ВыборкаТД.КодПоОКЗ));
					Если ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.НачалоДоговораГПХ
						Или ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.ОкончаниеДоговораГПХ Тогда 
						НаборЗаписейМероприятие.ЕФС__КодВФ.Значение = СокрЛП(ВыборкаТД.ТрудоваяФункцияКод);
					КонецЕсли;
					
					НаборЗаписейМероприятие.ЕФС__ДатаС.Значение = Формат(ВыборкаТД.ДатаС, "ДФ=yyyy-MM-dd");
					НаборЗаписейМероприятие.ЕФС__ДатаПо.Значение = Формат(ВыборкаТД.ДатаПо, "ДФ=yyyy-MM-dd");
					
					Если ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.Увольнение Тогда
						
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__КодПричиныУвольнения = Строка(ВыборкаТД.ПричинаУвольненияПФР);
						
						НаборЗаписейМероприятие.ЕФС__Причина.Значение = СокрЛП(ВыборкаТД.ОснованиеУвольненияТекстОснования);
						
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__НормативныйДокумент = НормативныйДокументОснованияУвольнения(ВыборкаТД.ОснованиеУвольнения);
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__Статья = ВыборкаТД.ОснованиеУвольнения.Статья;
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__Часть = ВыборкаТД.ОснованиеУвольнения.Пункт;
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__Пункт = ВыборкаТД.ОснованиеУвольненияПункт;
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__Подпункт = ВыборкаТД.ОснованиеУвольнения.Подпункт;
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.Значение.ЕФС__Абзац = ВыборкаТД.ОснованиеУвольнения.Абзац;
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__НормативныйДокумент");
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__Статья");
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__Часть");
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__Пункт");
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__Подпункт");
						УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения, "ЕФС__Абзац");
					Иначе 
						НаборЗаписейМероприятие.ЕФС__ОснованиеУвольнения.НеВыводитьВФайл = Истина;
					КонецЕсли;
					
					НаборЗаписейОснование = НаборЗаписейМероприятие.ЕФС__Основание.Значение;
					НаборЗаписейОснование.ЕФС__Наименование.Значение = СокрЛП(ВыборкаТД.НаименованиеДокументаОснования);
					НаборЗаписейОснование.ЕФС__Дата.Значение = Формат(ВыборкаТД.ДатаДокументаОснования, "ДФ=yyyy-MM-dd");;
					НаборЗаписейОснование.ЕФС__Номер.Значение = СокрЛП(ВыборкаТД.НомерДокументаОснования);
					НаборЗаписейОснование.ЕФС__Серия.Значение = СокрЛП(ВыборкаТД.СерияДокументаОснования);
					
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__Сведения, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__Должность, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__СтруктурноеПодразделение, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__ВидПР, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__КодВФ, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__КодВФпоОКЗ, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__Причина, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__ДатаС, "Значение");
					УстановитьВыводВФайл(НаборЗаписейМероприятие.ЕФС__ДатаПо, "Значение");
					УстановитьВыводВФайл(НаборЗаписейОснование.ЕФС__Серия, "Значение");
					
					УзелМероприятие = ВывестиВФайлМероприятиеЕФС1(УзелСЗВ_ТД, УзелТрудоваяДеятельность, "Мероприятие", НаборЗаписейМероприятие);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если РазделыЕФС1.СЗВСТАЖ Тогда 
		
		ФорматЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ.ЗЛ.Значение);
		
		ФорматСЗВ_ТД = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СЗВ_ТД.Значение);
		ФорматЗЛ.Удалить("СЗВ_ТД");
		
		ФорматСЗВ_СТАЖ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ.СЗВ_СТАЖ.Значение);
		ФорматЗЛ.Удалить("СЗВ_СТАЖ");
		
		ФорматСтажевыйПериод = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ_СТАЖ.СтажевыйПериод.Значение);
		ФорматСЗВ_СТАЖ.Удалить("СтажевыйПериод");
		
		НаборЗаписейЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматЗЛ);
		
		Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			
			// Данные по физлицу
			Фамилия = СокрЛП(ВыборкаСТАЖ.Фамилия);
			Имя = СокрЛП(ВыборкаСТАЖ.Имя);
			Отчество = СокрЛП(ВыборкаСТАЖ.Отчество);
			
			НаборЗаписейЗЛ.УТ2__СНИЛС.Значение = ВыборкаСТАЖ.СтраховойНомерПФР;
			НаборЗаписейЗЛ.УТ2__ДатаРождения.Значение = Формат(ВыборкаСТАЖ.ДатаРождения, "ДФ=yyyy-MM-dd");
			НаборЗаписейЗЛ.ИНН.Значение = ВыборкаСТАЖ.ИНН;
			НаборЗаписейЗЛ.СтатусЗЛ.Значение = Строка(ВыборкаСТАЖ.КодКатегорииЗЛ);
			НаборЗаписейЗЛ.Гражданство.Значение = ВыборкаСТАЖ.Гражданство;
			
			НаборЗаписейФИО = НаборЗаписейЗЛ.УТ2__ФИО.Значение;
			НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
			НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
			НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
			
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Фамилия");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Имя");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Отчество");
			
			УстановитьВыводВФайл(НаборЗаписейЗЛ.ИНН, "Значение");
			
			УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ, "ЗЛ", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
			
			Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда
				ФорматСЗВ_СТАЖ.Тип.Значение = 1;	
			ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
				ФорматСЗВ_СТАЖ.Тип.Значение = 2;	
			ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая Тогда
				ФорматСЗВ_СТАЖ.Тип.Значение = 3;	
			ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
				ФорматСЗВ_СТАЖ.Тип.Значение = 4;	
			КонецЕсли;	
			
			ФорматСЗВ_СТАЖ.ОтчетныйПериод.Значение = ВыборкаПоШапкеДокумента.Год;
			
			Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.КорректируемыйПериод) Тогда 
				ФорматСЗВ_СТАЖ.КоррОтмПериод.Значение = Год(ВыборкаПоШапкеДокумента.КорректируемыйПериод);
			КонецЕсли;
			
			УстановитьВыводВФайл(ФорматСЗВ_СТАЖ.КоррОтмПериод, "Значение");
			
			УзелСЗВ_СТАЖ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСотрудника, "СЗВ-СТАЖ", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСЗВ_СТАЖ, ФорматСЗВ_СТАЖ);
			
			ВписатьЗаписиОСтажеВНаборДанныхЕФС1(УзелСЗВ_СТАЖ, ФорматСтажевыйПериод, ВыборкаСТАЖ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если РазделыЕФС1.ДНП Тогда
		
		УзелДНП = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ, "ДНП", "");
		
		ФорматДНП = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ.ДНП.Значение);
		
		ФорматОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматДНП.ОснованиеДНП.Значение);
		ФорматДНП.Удалить("ОснованиеДНП");
		
		Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ Тогда 
			ФорматДНП.Тип.Значение = 2;
		ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.ОТМЕНЯЮЩАЯ Тогда 
			ФорматДНП.Тип.Значение = 3;
		Иначе 
			ФорматДНП.Тип.Значение = 1;
		КонецЕсли;
		
		ФорматДНП.ОтчетныйПериод.Значение = ВыборкаПоШапкеДокумента.Год;
		
		Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ
			Или ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.ОТМЕНЯЮЩАЯ Тогда 
			ФорматДНП.КоррОтмПериод.Значение = ВыборкаПоШапкеДокумента.Год;
		КонецЕсли;
		
		УстановитьВыводВФайл(ФорматДНП.КоррОтмПериод, "Значение");
		
		ФорматВсегоШтатФакт = ОбщегоНазначения.СкопироватьРекурсивно(ФорматДНП);
		ФорматВсегоШтатФакт.Удалить("Тип");
		ФорматВсегоШтатФакт.Удалить("ОтчетныйПериод");
		ФорматВсегоШтатФакт.Удалить("КоррОтмПериод");
		
		ФорматДНП.Удалить("ЕФС__ВсегоШтат");
		ФорматДНП.Удалить("ЕФС__ВсегоФакт");
		
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелДНП, ФорматДНП);
		
		ФорматОУТ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснование.ЕФС__ОУТ);
		ФорматОснование.Удалить("ЕФС__ОУТ");
		
		Пока ВыборкаДНП.Следующий() Цикл 
			
			НаборЗаписейОснование = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОснование);
			НаборЗаписейОснование.ЕФС__Подразделение.Значение =ВРег(ВыборкаДНП.НаименованиеПодразделения);
			НаборЗаписейОснование.ЕФС__Профессия.Значение = ВРег(ВыборкаДНП.НаименованиеДолжности);
			НаборЗаписейОснование.ЕФС__КоличествоШтат.Значение = ВыборкаДНП.КоличествоРабочихМест;
			НаборЗаписейОснование.ЕФС__КоличествоФакт.Значение = ВыборкаДНП.КоличествоРаботающих;
			НаборЗаписейОснование.ЕФС__Описание.Значение = ВРег(ВыборкаДНП.ХарактерВыполняемыхРабот);
			НаборЗаписейОснование.ЕФС__Документы.Значение = ВРег(ВыборкаДНП.ПервичныеДокументы);
			УзелОснование = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелДНП, "ОснованиеДНП", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелОснование, НаборЗаписейОснование);
			
			Если ЗначениеЗаполнено(ВыборкаДНП.ОсобыеУсловияТруда) Тогда 
				НаборЗаписейОУТ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматОУТ);
				НаборЗаписейОУТ.Значение.ЕФС__КодОУТ = СокрЛП(ВыборкаДНП.ОсобыеУсловияТрудаКод);
				НаборЗаписейОУТ.Значение.ЕФС__ПозицияСписка = СокрЛП(ВыборкаДНП.КодПозицииСпискаКод);
				СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелОснование, Новый Структура("ЕФС__ОУТ", НаборЗаписейОУТ));
			КонецЕсли;
			
		КонецЦикла;
		
		ФорматВсегоШтатФакт.ЕФС__ВсегоШтат.Значение = ВыборкаПоШапкеДокумента.ВсегоРабочихМестДНП;
		ФорматВсегоШтатФакт.ЕФС__ВсегоФакт.Значение = ВыборкаПоШапкеДокумента.ВсегоРаботающихДНП;
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелДНП, ФорматВсегоШтатФакт);
		
	КонецЕсли;
	
	Если РазделыЕФС1.ДСВ Тогда
		
		Если ВыборкаПоШапкеДокумента.СуммаДСВРаботника > 0 Тогда 
			
			УзелДСВ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ, "ДСВ", "");
			
			ФорматДСВ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ.ДСВ.Значение);
			
			ФорматРеестрДСВ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматДСВ.РеестрДСВ.Значение);
			ФорматДСВ.Удалить("РеестрДСВ");
			
			ФорматПлатежноеПоручение = ФорматДСВ.ПлатежноеПоручение.Значение;
			ФорматПлатежноеПоручение.Реквизиты.Значение.Дата = Формат(ВыборкаПоШапкеДокумента.ДатаПоручения, "ДФ=yyyy-MM-dd");
			ФорматПлатежноеПоручение.Реквизиты.Значение.Номер = ВыборкаПоШапкеДокумента.НомерПоручения;
			ФорматПлатежноеПоручение.ДатаИсполнения.Значение = Формат(ВыборкаПоШапкеДокумента.ДатаИсполненияПоручения, "ДФ=yyyy-MM-dd");
			ФорматПлатежноеПоручение.СуммаИтогоПП.Значение = ВыборкаПоШапкеДокумента.СуммаДСВОбщая;
			
			ФорматДСВ.Период.Значение = ВыборкаПоШапкеДокумента.Год;
			ФорматДСВ.НомерРеестра.Значение = ВыборкаПоШапкеДокумента.НомерПачки;
			ФорматДСВ.СуммаВсегоДСВРаботника.Значение = ВыборкаПоШапкеДокумента.СуммаДСВРаботника;
			ФорматДСВ.КоличествоЗЛвРеестре.Значение = ВыборкаПоШапкеДокумента.ВзносыРаботникаКоличество;
			
			УстановитьВыводВФайл(ФорматДСВ.СуммаВсегоДСВРаботника, "Значение");
			УстановитьВыводВФайл(ФорматДСВ.СуммаВсегоВзносовРаботодателя, "Значение");
			
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелДСВ, ФорматДСВ);
			
			УзелРеестрДСВ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелДСВ, "РеестрДСВ", "");
			
			ВыборкаДСВ.Сбросить();
			
			Пока ВыборкаДСВ.Следующий() Цикл
				
				Если ВыборкаДСВ.ВзносыРаботника = 0 Тогда 
					Продолжить;
				КонецЕсли;
				
				// Данные по физлицу
				Фамилия = СокрЛП(ВыборкаДСВ.Фамилия);
				Имя = СокрЛП(ВыборкаДСВ.Имя);
				Отчество = СокрЛП(ВыборкаДСВ.Отчество);
				
				НаборЗаписейЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматРеестрДСВ.ЗЛ.Значение);
				
				НаборЗаписейЗЛ.УТ2__СНИЛС.Значение = ВыборкаДСВ.СтраховойНомерПФР;
				НаборЗаписейЗЛ.СуммаДСВРаботника.Значение = ВыборкаДСВ.ВзносыРаботника;
				
				НаборЗаписейФИО = НаборЗаписейЗЛ.УТ2__ФИО.Значение;
				НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
				НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
				НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
				
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Фамилия");
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Имя");
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Отчество");
				
				УстановитьВыводВФайл(НаборЗаписейЗЛ.СуммаДСВРаботника, "Значение");
				УстановитьВыводВФайл(НаборЗаписейЗЛ.СуммаВзносовРаботодателя, "Значение");
				
				УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелРеестрДСВ, "ЗЛ", "");
				СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ВыборкаПоШапкеДокумента.СуммаДСВРаботодателя > 0 Тогда 
			
			УзелДСВ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ, "ДСВ", "");
			
			ФорматДСВ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСЗВ.ДСВ.Значение);
			
			ФорматРеестрДСВ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматДСВ.РеестрДСВ.Значение);
			ФорматДСВ.Удалить("РеестрДСВ");
			
			ФорматПлатежноеПоручение = ФорматДСВ.ПлатежноеПоручение.Значение;
			ФорматПлатежноеПоручение.Реквизиты.Значение.Дата = Формат(ВыборкаПоШапкеДокумента.ДатаПоручения, "ДФ=yyyy-MM-dd");
			ФорматПлатежноеПоручение.Реквизиты.Значение.Номер = ВыборкаПоШапкеДокумента.НомерПоручения;
			ФорматПлатежноеПоручение.ДатаИсполнения.Значение = Формат(ВыборкаПоШапкеДокумента.ДатаИсполненияПоручения, "ДФ=yyyy-MM-dd");
			ФорматПлатежноеПоручение.СуммаИтогоПП.Значение = ВыборкаПоШапкеДокумента.СуммаДСВОбщая;
			
			ФорматДСВ.Период.Значение = ВыборкаПоШапкеДокумента.Год;
			ФорматДСВ.НомерРеестра.Значение = ВыборкаПоШапкеДокумента.НомерПачки;
			ФорматДСВ.СуммаВсегоВзносовРаботодателя.Значение = ВыборкаПоШапкеДокумента.СуммаДСВРаботодателя;
			ФорматДСВ.КоличествоЗЛвРеестре.Значение = ВыборкаПоШапкеДокумента.ВзносыРаботодателяКоличество;
			
			УстановитьВыводВФайл(ФорматДСВ.СуммаВсегоДСВРаботника, "Значение");
			УстановитьВыводВФайл(ФорматДСВ.СуммаВсегоВзносовРаботодателя, "Значение");
			
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелДСВ, ФорматДСВ);
			
			УзелРеестрДСВ = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелДСВ, "РеестрДСВ", "");
			
			ВыборкаДСВ.Сбросить();
			
			Пока ВыборкаДСВ.Следующий() Цикл
				
				Если ВыборкаДСВ.ВзносыРаботодателя = 0 Тогда 
					Продолжить;
				КонецЕсли;
				
				// Данные по физлицу
				Фамилия = СокрЛП(ВыборкаДСВ.Фамилия);
				Имя = СокрЛП(ВыборкаДСВ.Имя);
				Отчество = СокрЛП(ВыборкаДСВ.Отчество);
				
				НаборЗаписейЗЛ = ОбщегоНазначения.СкопироватьРекурсивно(ФорматРеестрДСВ.ЗЛ.Значение);
				
				НаборЗаписейЗЛ.УТ2__СНИЛС.Значение = ВыборкаДСВ.СтраховойНомерПФР;
				НаборЗаписейЗЛ.СуммаВзносовРаботодателя.Значение = ВыборкаДСВ.ВзносыРаботодателя;
				
				НаборЗаписейФИО = НаборЗаписейЗЛ.УТ2__ФИО.Значение;
				НаборЗаписейФИО.УТ2__Фамилия = ВРег(Фамилия);
				НаборЗаписейФИО.УТ2__Имя = ВРег(Имя);
				НаборЗаписейФИО.УТ2__Отчество = ВРег(Отчество);
				
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Фамилия");
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Имя");
				УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЗЛ.УТ2__ФИО, "УТ2__Отчество");
				
				УстановитьВыводВФайл(НаборЗаписейЗЛ.СуммаДСВРаботника, "Значение");
				УстановитьВыводВФайл(НаборЗаписейЗЛ.СуммаВзносовРаботодателя, "Значение");
				
				УзелСотрудника = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелРеестрДСВ, "ЗЛ", "");
				СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСотрудника, НаборЗаписейЗЛ);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НаборЗаписейРуководитель.Руководитель.Значение.УТ2__Должность.Значение = ВРег(ВыборкаПоШапкеДокумента.ДолжностьРуководителя);
	НаборЗаписейФИО = НаборЗаписейРуководитель.Руководитель.Значение.УТ2__ФИО.Значение;  
	ФИООтдельно = СтрРазделить(ВыборкаПоШапкеДокумента.Руководитель, " ");
	Если ФИООтдельно.Количество() > 0 Тогда
		НаборЗаписейФИО.УТ2__Фамилия = ВРег(ФИООтдельно[0]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 1 Тогда
		НаборЗаписейФИО.УТ2__Имя = ВРег(ФИООтдельно[1]);
	КонецЕсли;
	Если ФИООтдельно.Количество() > 2 Тогда
		НаборЗаписейФИО.УТ2__Отчество = ВРег(ФИООтдельно[2]);
	КонецЕсли;
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейРуководитель.Руководитель.Значение.УТ2__ФИО, "УТ2__Фамилия");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейРуководитель.Руководитель.Значение.УТ2__ФИО, "УТ2__Имя");
	УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейРуководитель.Руководитель.Значение.УТ2__ФИО, "УТ2__Отчество");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЕФС1, НаборЗаписейРуководитель);
	
	НаборЗаписейДатаЗаполнения.ДатаЗаполнения.Значение = Формат(ВыборкаПоШапкеДокумента.Дата, "ДФ=yyyy-MM-dd");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЕФС1, НаборЗаписейДатаЗаполнения);
	
	// Служебная информация о файле
	НаборЗаписейСлужебнаяИнформация = СправкиПоНДФЛ.ЗагрузитьФорматНабораЗаписей(ДеревоФормата, "СлужебнаяИнформация");
	Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР) Тогда
		ЧастиИмениФайла = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ВыборкаПоШапкеДокумента.ИмяФайлаДляПФР),"_");
		НаборЗаписейСлужебнаяИнформация.АФ5__GUID.Значение = ЧастиИмениФайла[ЧастиИмениФайла.ВГраница()];
	КонецЕсли;
	НаборЗаписейСлужебнаяИнформация.АФ5__ДатаВремя.Значение = ПредставлениеДатыВФорматеdateTimeСЧасовымПоясом(ВыборкаПоШапкеДокумента.Дата);
	НаборЗаписейСлужебнаяИнформация.АФ5__ПрограммаПодготовки.Значение = ВРег(РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелПФР, "СлужебнаяИнформация",""), НаборЗаписейСлужебнаяИнформация);
	
	// Преобразуем дерево в строковое описание XML
	ПотокВыгрузкиXML = СправкиПоНДФЛ.СоздатьПотокXML(, "UTF-8");
	СправкиПоНДФЛ.ЗаписатьУзелДереваXMLВXML(ДеревоВыгрузки, ПотокВыгрузкиXML, "xsi", "http://www.w3.org/2001/XMLSchema", Ложь);
	ТекстФайла = СправкиПоНДФЛ.ЗаписатьПотокXML(ПотокВыгрузкиXML);
	
	Возврат СтрЗаменить(ТекстФайла, "__", ":");
	
КонецФункции

Функция НормативныйДокументОснованияУвольненияВРодительномПадеже(ОснованиеУвольнения) Экспорт
	
	Если Не ЗначениеЗаполнено(ОснованиеУвольнения) Тогда
		Возврат "";
	КонецЕсли;
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеУвольнения, "ДокументОснование");
	Возврат Перечисления.НормативныеДокументыОснованийКадровыхПриказов.НормативныйДокументВРодительномПадеже(ДокументОснование);
	
КонецФункции

Функция НормативныйДокументОснованияУвольнения(ОснованиеУвольнения) Экспорт
	
	Если Не ЗначениеЗаполнено(ОснованиеУвольнения) Тогда
		Возврат "";
	КонецЕсли;
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеУвольнения, "ДокументОснование");
	Возврат Перечисления.НормативныеДокументыОснованийКадровыхПриказов.НормативныйДокумент(ДокументОснование);
	
КонецФункции


Функция ВывестиВФайлМероприятиеЕФС1(УзелСЗВ_ТД, УзелТрудоваяДеятельность, ИмяУзла, НаборЗаписейМероприятие)
	
	Если УзелТрудоваяДеятельность = Неопределено Тогда
		УзелТрудоваяДеятельность = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ_ТД, "ТД", "");
	КонецЕсли;
	
	УзелМероприятие = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелТрудоваяДеятельность, ИмяУзла, "");
	СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелМероприятие, НаборЗаписейМероприятие);
	
	Возврат УзелМероприятие;
	
КонецФункции

Процедура ВписатьЗаписиОСтажеВНаборДанныхЕФС1(УзелСЗВ_СТАЖ, ФорматСтажевыйПериод, ВыборкаСТАЖ) Экспорт	
	
	ФорматСтажевыйПериод.ЛьготныйСтаж.НеВыводитьВФайл = Истина;
	ПараметрыСтажаСПравомНаДосрочнуюПенсию = ПараметрыИсчисляемогоСтажаПредусматривающиеПравоНаДосрочнуюПенсию();
			
	Если ЗначениеЗаполнено(ВыборкаСТАЖ.НомерОсновнойЗаписи) Тогда
		
		Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерОсновнойЗаписи")	Цикл	
			
			ФорматСтажевыйПериод.ЛьготныйСтаж.НеВыводитьВФайл = Истина;

			УзелСтажевыйПериод = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСЗВ_СТАЖ, "СтажевыйПериод", "");
			СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелСтажевыйПериод, ФорматСтажевыйПериод);
			
			УзелСтажевыйПериод.Строки.Найти("Период").Строки.Найти("УТ2__С").Значение = Формат(ВыборкаСТАЖ.ДатаНачалаПериода, "ДФ=yyyy-MM-dd");
			УзелСтажевыйПериод.Строки.Найти("Период").Строки.Найти("УТ2__По").Значение = Формат(ВыборкаСТАЖ.ДатаОкончанияПериода, "ДФ=yyyy-MM-dd");
			
			ФорматЛьготныйСтаж = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСтажевыйПериод.ЛьготныйСтаж.Значение);
			ВывестиЛьготныйСтажЕФС1(ВыборкаСТАЖ, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			
			Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
				// В дополнительной записи с номером 0 находится информация об основной записи
				Если ВыборкаСТАЖ.НомерДополнительнойЗаписи = 0 Тогда
					Продолжить;
				КонецЕсли;
				ФорматЛьготныйСтаж = ОбщегоНазначения.СкопироватьРекурсивно(ФорматСтажевыйПериод.ЛьготныйСтаж.Значение);
				ВывестиЛьготныйСтажЕФС1(ВыборкаСТАЖ, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры	

Процедура ВывестиЛьготныйСтажЕФС1(ВыборкаСТАЖ, УзелСтажевыйПериод, ФорматЛьготныйСтаж, ПараметрыСтажаСПравомНаДосрочнуюПенсию)
	
	ВыводитьЛьготныйСтаж = Ложь;
	
	НаборЗаписейЛьготныйСтаж = ФорматЛьготныйСтаж;
	НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ЕФС__ИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ЕФС__ДопСведенияИС.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ЕФС__ОУТ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ЕФС__ВЛ.НеВыводитьВФайл = Ложь;
	НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ.НеВыводитьВФайл = Ложь;
	
	// ДопСведенияИС
	Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) Тогда
		НаборЗаписейЛьготныйСтаж.ЕФС__ДопСведенияИС.Значение = ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажаКод;
		ВыводитьЛьготныйСтаж = Истина;
	ИначеЕсли ЗначениеЗаполнено(ВыборкаСТАЖ.ЗамещениеГосударственныхМуниципальныхДолжностей) Тогда
		НаборЗаписейЛьготныйСтаж.ЕФС__ДопСведенияИС.Значение = ВыборкаСТАЖ.ЗамещениеГосударственныхМуниципальныхДолжностей;
		ВыводитьЛьготныйСтаж = Истина;
	Иначе
		НаборЗаписейЛьготныйСтаж.ЕФС__ДопСведенияИС.НеВыводитьВФайл = Истина;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа)
		И ПараметрыСтажаСПравомНаДосрочнуюПенсию.Найти(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) = Неопределено Тогда 
		
		НаборЗаписейЛьготныйСтаж.ЕФС__ВЛ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ЕФС__ИС.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ЕФС__ОУТ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.НеВыводитьВФайл = Истина;
		НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ.НеВыводитьВФайл = Истина;
		
		Если ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕТИ 
			И ВыборкаСТАЖ.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловияПФР.СЕЛО Тогда
			НаборЗаписейТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.Значение;
			НаборЗаписейТерриториальныеУсловия.ЕФС__Основание = СокрЛП(ВыборкаСТАЖ.ТерриториальныеУсловияКод);
			НаборЗаписейТерриториальныеУсловия.ЕФС__Коэффициент = ВыборкаСТАЖ.ТерриториальныеУсловияСтавка;
			НаборЗаписейТерриториальныеУсловия.ЕФС__ДоляСтавки = ВыборкаСТАЖ.ТретийПараметрВыслугиЛет;
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__ТУ, "ЕФС__Коэффициент");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__ТУ, "ЕФС__ДоляСтавки");
			НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.НеВыводитьВФайл = Ложь;	
		КонецЕсли;	
		
	Иначе
		НаборЗаписейТерриториальныеУсловия = НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.Значение;
		// Территориальные условия
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТерриториальныеУсловияКод) Тогда
			НаборЗаписейТерриториальныеУсловия.ЕФС__Основание = СокрЛП(ВыборкаСТАЖ.ТерриториальныеУсловияКод);
			НаборЗаписейТерриториальныеУсловия.ЕФС__Коэффициент = ВыборкаСТАЖ.РайонныйКоэффициент;
			НаборЗаписейТерриториальныеУсловия.ЕФС__ДоляСтавки = ВыборкаСТАЖ.ТерриториальныеУсловияСтавка;
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__ТУ, "ЕФС__Коэффициент");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__ТУ, "ЕФС__ДоляСтавки");
		Иначе
			НаборЗаписейЛьготныйСтаж.ЕФС__ТУ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейТерриториальныеУсловия.ЕФС__Основание);
		
		// Особые условия труда
		НаборЗаписейОсобыеУсловияТруда = НаборЗаписейЛьготныйСтаж.ЕФС__ОУТ.Значение;
		
		НаборЗаписейЛьготныйСтаж.ЕФС__ОУТ.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.ОсобыеУсловияТрудаКод) Тогда
			НаборЗаписейОсобыеУсловияТруда.ЕФС__Код = СокрЛП(ВыборкаСТАЖ.ОсобыеУсловияТрудаКод);
			НаборЗаписейОсобыеУсловияТруда.ЕФС__ПозицияСписка = СокрЛП(ВыборкаСТАЖ.КодПозицииСпискаКод);
			ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейОсобыеУсловияТруда.ЕФС__Код);
		Иначе
			НаборЗаписейЛьготныйСтаж.ЕФС__ОУТ.НеВыводитьВФайл = Истина;
		КонецЕсли;	
		
		// Исчисляемый стаж
		НаборЗаписейИсчисляемыйСтаж = НаборЗаписейЛьготныйСтаж.ЕФС__ИС.Значение;
		НаборЗаписейИсчисляемыйСтаж.ЕФС__Основание.НеВыводитьВФайл = Ложь;
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажаКод) Тогда
			НаборЗаписейИсчисляемыйСтаж.ЕФС__Основание.Значение = СокрЛП(ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажаКод);
		Иначе
			НаборЗаписейИсчисляемыйСтаж.ЕФС__Основание.НеВыводитьВФайл = Истина;
		КонецЕсли;
		
		НаборЗаписейЛьготныйСтаж.ЕФС__ИС.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если Не ЗначениеЗаполнено(ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажа) И ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) Тогда
			НаборЗаписейЛьготныйСтаж.ЕФС__ИС.НеВыводитьВФайл = Истина;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаСТАЖ.ПервыйПараметрИсчисляемогоСтажа) Или ЗначениеЗаполнено(ВыборкаСТАЖ.ВторойПараметрИсчисляемогоСтажа) Тогда		
			Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				СписокОтсутствий = СписокОтсутствийИсключающихКалендарнуюВыработку();
				Если СписокОтсутствий.Найти(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) <> Неопределено Тогда
					НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
				КонецЕсли;
			ИначеЕсли ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоСтраховогоСтажа.ВОДОЛАЗ Тогда
				НаборЗаписейВыработкаВЧасах = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаСТАЖ.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаСТАЖ.ВторойПараметрИсчисляемогоСтажа;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаСТАЖ.ПервыйПараметрИсчисляемогоСтажа > 0 Или ВыборкаСТАЖ.ВторойПараметрИсчисляемогоСтажа > 0 Тогда
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаСТАЖ.ПервыйПараметрИсчисляемогоСтажа;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаСТАЖ.ВторойПараметрИсчисляемогоСтажа;
			Иначе
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейИсчисляемыйСтаж.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ЕФС__ИС.НеВыводитьВФайл = Истина;		
		КонецЕсли;	
		
		ВыводитьЛьготныйСтаж = ВыводитьЛьготныйСтаж Или ЗначениеЗаполнено(НаборЗаписейИсчисляемыйСтаж.ЕФС__Основание.Значение);
		
		// Выслуга лет
		НаборЗаписейВыслугаЛет = НаборЗаписейЛьготныйСтаж.ЕФС__ВЛ.Значение;
		НаборЗаписейЛьготныйСтаж.ЕФС__ВЛ.НеВыводитьВФайл = Ложь;	
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Ложь;
		НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.ОснованиеВыслугиЛетКод) Тогда
			
			НаборЗаписейВыслугаЛет.ЕФС__Основание.Значение = ВРег(СокрЛП(ВыборкаСТАЖ.ОснованиеВыслугиЛетКод));
			НаборЗаписейВыслугаЛет.ЕФС__ДоляСтавки.Значение = ВыборкаСТАЖ.ТретийПараметрВыслугиЛет;
			УстановитьВыводВФайл(НаборЗаписейВыслугаЛет.ЕФС__ДоляСтавки, "Значение");
			Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) Тогда	
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаСТАЖ.ПервыйПараметрВыслугиЛет) И Не ЗначениеЗаполнено(ВыборкаСТАЖ.ВторойПараметрВыслугиЛет) Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
			ИначеЕсли ВыборкаСТАЖ.ОснованиеВыслугиЛет = Справочники.ОснованияДосрочногоНазначенияПенсии.Ст27_15 Тогда
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаКалендарная = НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.Значение;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеМесяцы = ВыборкаСТАЖ.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаКалендарная.ВС2__ВсеДни = ВыборкаСТАЖ.ВторойПараметрВыслугиЛет;
			Иначе
				НаборЗаписейВыслугаЛет.ВС2__ВыработкаКалендарная.НеВыводитьВФайл = Истина;
				НаборЗаписейВыработкаВЧасах = НаборЗаписейВыслугаЛет.ВС2__ВыработкаВЧасах.Значение;
				НаборЗаписейВыработкаВЧасах.ВС2__Часы = ВыборкаСТАЖ.ПервыйПараметрВыслугиЛет;
				НаборЗаписейВыработкаВЧасах.ВС2__Минуты = ВыборкаСТАЖ.ВторойПараметрВыслугиЛет;
			КонецЕсли;
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ЕФС__ВЛ.НеВыводитьВФайл = Истина;	
		КонецЕсли;	
		
		// Сведения о специальной оценке условий труда
		НаборЗаписейСпецоценка = НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ.Значение;
		НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ.НеВыводитьВФайл = Ложь;
		
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.НомерРабочегоМеста)
			Или ЗначениеЗаполнено(ВыборкаСТАЖ.КлассПодклассУсловийТруда) Тогда
			НаборЗаписейСпецоценка.ЕФС__Номер = СокрЛП(ВыборкаСТАЖ.НомерРабочегоМеста);
			НаборЗаписейСпецоценка.ЕФС__Код = КодКлассаУсловийТрудаЕФС1(ВыборкаСТАЖ.КлассПодклассУсловийТруда);
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ, "ЕФС__Номер");
			УстановитьВыводВФайлСвойстваЗначенияЭлемента(НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ, "ЕФС__Код");
			ВыводитьЛьготныйСтаж = Истина;
		Иначе
			НаборЗаписейЛьготныйСтаж.ЕФС__СОУТ.НеВыводитьВФайл = Истина;
		КонецЕсли;	
		
	КонецЕсли;
	
	Если ВыводитьЛьготныйСтаж Тогда
		УзелЛьготныйСтаж = СправкиПоНДФЛ.ДобавитьУзелВДеревоXML(УзелСтажевыйПериод, "ЛьготныйСтаж","");
		СправкиПоНДФЛ.ДобавитьИнформациюВДерево(УзелЛьготныйСтаж, НаборЗаписейЛьготныйСтаж);
	КонецЕсли;
	
КонецПроцедуры

Функция КодКлассаУсловийТрудаЕФС1(КлассУсловийТруда) Экспорт
	
	Код = "";
	Если КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Оптимальный Тогда 
		Код = "1";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Допустимый Тогда 
		Код = "2";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный1 Тогда 
		Код = "3.1";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный2 Тогда 
		Код = "3.2";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный3 Тогда 
		Код = "3.3";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Вредный4 Тогда 
		Код = "3.4";
	ИначеЕсли КлассУсловийТруда = Перечисления.КлассыУсловийТрудаПоРезультатамСпециальнойОценки.Опасный Тогда 
		Код = "4";
	КонецЕсли;
	
	Возврат Код;
	
КонецФункции


Функция ПредставлениеТелефонаЕФС1(Телефон) Экспорт 
	
	Представление = "";
	КоличествоСимволов = СтрДлина(Телефон);
	Для Сч = 1 По КоличествоСимволов Цикл 
		ТекущийСимвол = Сред(Телефон, Сч, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекущийСимвол) Тогда 
			Представление = Представление + ТекущийСимвол;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Представление;
	
КонецФункции

Функция ПечатнаяФормаЕФС_1(ОбъектыПечати, РазделыЕФС1, ВыборкаПоШапкеДокумента, ВыборкаТД = Неопределено, ВыборкаСТАЖ = Неопределено, ВыборкаДНП = Неопределено, ВыборкаДСВ = Неопределено) Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЕФС_1";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ЭтоПервыйДокументКоллекции = Истина;
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ФормаЕФС_1");
		
		НачалоФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		ОбластьТитульныйЛист = Макет.ПолучитьОбласть("ТитульныйЛист");
		ОбластьПодраздел2 = Макет.ПолучитьОбласть("Подраздел2");
		ОбластьШапкаПодраздела2 = Макет.ПолучитьОбласть("ШапкаПодраздела2");
		ОбластьСтрокаПодраздела2 = Макет.ПолучитьОбласть("СтрокаПодраздела2");
		ОбластьПодвалПодраздела2 = Макет.ПолучитьОбласть("ПодвалПодраздела2");
		ОбластьПодраздел3 = Макет.ПолучитьОбласть("Подраздел3");
		ОбластьШапкаПодраздела3 = Макет.ПолучитьОбласть("ШапкаПодраздела3");
		ОбластьСтрокаПодраздела3 = Макет.ПолучитьОбласть("СтрокаПодраздела3");
		ОбластьПодвалПодраздела3 = Макет.ПолучитьОбласть("ПодвалПодраздела3");
		
		Если ЭтоПервыйДокументКоллекции Тогда
			ЭтоПервыйДокументКоллекции = Ложь;	
		Иначе
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;	
		
		ИНН = СокрЛП(ВыборкаПоШапкеДокумента.ИНН);
		ВывестиДанныеПоБуквенно(ИНН, ОбластьТитульныйЛист, "ИНН", Мин(СтрДлина(ИНН), 12));
		
		КПП = СокрЛП(ВыборкаПоШапкеДокумента.КПП);
		ВывестиДанныеПоБуквенно(КПП, ОбластьТитульныйЛист, "КПП", Мин(СтрДлина(КПП), 9));
		
		ОКВЭД = СтрЗаменить(СокрЛП(ВыборкаПоШапкеДокумента.КодОКВЭД), ".", "");
		ВывестиДанныеПоБуквенно(ОКВЭД, ОбластьТитульныйЛист, "ОКВЭД", Мин(СтрДлина(ОКВЭД), 6));
		
		ОГРН = СокрЛП(ВыборкаПоШапкеДокумента.ОГРН);
		ВывестиДанныеПоБуквенно(ОГРН, ОбластьТитульныйЛист, "ОГРН", Мин(СтрДлина(ОГРН), 15));
		
		Если РазделыЕФС1.СЗВТД Тогда 
			
			ПрежнийСтраховательИНН = СокрЛП(ВыборкаПоШапкеДокумента.ПрежнийСтраховательИНН);
			ВывестиДанныеПоБуквенно(ПрежнийСтраховательИНН, ОбластьТитульныйЛист, "ПрежнийСтраховательИНН", Мин(СтрДлина(ПрежнийСтраховательИНН), 12));
			
			ПрежнийСтраховательКПП = СокрЛП(ВыборкаПоШапкеДокумента.ПрежнийСтраховательКПП);
			ВывестиДанныеПоБуквенно(ПрежнийСтраховательКПП, ОбластьТитульныйЛист, "ПрежнийСтраховательКПП", Мин(СтрДлина(ПрежнийСтраховательКПП), 9));
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОбластьТитульныйЛист.Параметры, ВыборкаПоШапкеДокумента);
		
		ВидыКонтактнойИнформации = Новый Массив;
		ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
		ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.EmailОрганизации);
		
		КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ВыборкаПоШапкеДокумента.Организация, , ВидыКонтактнойИнформации, ВыборкаПоШапкеДокумента.Дата);
			
		ТелефонОрганизации = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации, "Вид");
		Если ТелефонОрганизации <> Неопределено Тогда 
			Телефон = ПредставлениеТелефонаЕФС1(ТелефонОрганизации.Представление);
			ВывестиДанныеПоБуквенно(Телефон, ОбластьТитульныйЛист, "Телефон", Мин(СтрДлина(Телефон), 15));
		КонецЕсли;
		
		АдресЭлПочтыОрганизации = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.EmailОрганизации, "Вид");
		Если АдресЭлПочтыОрганизации <> Неопределено Тогда 
			ОбластьТитульныйЛист.Параметры.АдресЭлПочты = АдресЭлПочтыОрганизации.Представление;
		КонецЕсли;
		
		ОбластьТитульныйЛист.Параметры.ДатаЗаполнения = СправкиПоНДФЛ.ФорматДатыЧислоВКавычкахМесяцПрописью(ВыборкаПоШапкеДокумента.Дата, Истина);
		
		ТекущийЛист = Новый ТабличныйДокумент;
		ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
		
		ТекущийЛист.Вывести(ОбластьТитульныйЛист);
		
		ДокументРезультат.Вывести(ТекущийЛист);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		
		Если РазделыЕФС1.СЗВТД И ВыборкаТД.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаТД.СледующийПоЗначениюПоля("Ссылка");
			
			Пока ВыборкаТД.СледующийПоЗначениюПоля("НомерСтрокиСотрудника") Цикл
			
				ОбластьПодраздел1 = Макет.ПолучитьОбласть("Подраздел1");
				ОбластьПодраздел11 = Макет.ПолучитьОбласть("Подраздел11");
				ОбластьШапкаПодраздела11 = Макет.ПолучитьОбласть("ШапкаПодраздела11");
				
				ЗаполнитьЗначенияСвойств(ОбластьПодраздел1.Параметры, ВыборкаТД);
				ОбластьПодраздел1.Параметры.ДатаРождения = СправкиПоНДФЛ.ФорматДатыЧислоВКавычкахМесяцПрописью(ВыборкаТД.ДатаРождения, Истина);
				
				ИННРаботника = СокрЛП(ВыборкаТД.ИННРаботника);
				ВывестиДанныеПоБуквенно(ИННРаботника, ОбластьПодраздел1, "ИННРаботника", Мин(СтрДлина(ИННРаботника), 12));
				
				КодКатегорииЗЛ = СокрЛП(ВыборкаТД.КодКатегорииЗЛ);
				ВывестиДанныеПоБуквенно(КодКатегорииЗЛ, ОбластьПодраздел1, "КодКатегорииЗЛ", Мин(СтрДлина(КодКатегорииЗЛ), 4));
				
				Гражданство = СокрЛП(ВыборкаТД.Гражданство);
				ВывестиДанныеПоБуквенно(Гражданство, ОбластьПодраздел1, "Гражданство", Мин(СтрДлина(Гражданство), 3));
				
				ЗаполнитьЗначенияСвойств(ОбластьПодраздел11.Параметры, ВыборкаТД);
				
				ТекущийЛист = Новый ТабличныйДокумент;
				ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
				
				ТекущийЛист.Вывести(ОбластьПодраздел1);
				ТекущийЛист.Вывести(ОбластьПодраздел11); 
				ТекущийЛист.Вывести(ОбластьШапкаПодраздела11);
				
				ОбластиСтрок = Новый Массив;
				НомерСтрокиФормы = 1;
				Пока ВыборкаТД.Следующий() Цикл
					
					Если Не ЗначениеЗаполнено(ВыборкаТД.ВидМероприятия) Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаПодраздела11 = Макет.ПолучитьОбласть("СтрокаПодраздела11");
					
					СтрокаПодраздела11.Параметры.Заполнить(ВыборкаТД);
					СтрокаПодраздела11.Параметры.НомерСтрокиФормы = НомерСтрокиФормы;
					
					СтрокаПодраздела11.Параметры.ТрудоваяФункция = ЭлектронныеТрудовыеКнижкиУНФ.ПредставлениеВыполняемойФункции(ВыборкаТД);
					
					СтрокаПодраздела11.Параметры.ВидМероприятия = ЭлектронныеТрудовыеКнижкиУНФ.ПредставлениеВидаМероприятия(ВыборкаТД);
					
					Если ЗначениеЗаполнено(ВыборкаТД.КодПоОКЗ) Тогда
						СтруктураЗаполнения = Новый Структура;
						СтруктураЗаполнения.Вставить("КодПоОКЗ",
							ЭлектронныеТрудовыеКнижкиУНФ.ПредставлениеКодаОКЗ(ВыборкаТД.КодПоОКЗ));
						СтрокаПодраздела11.Параметры.Заполнить(СтруктураЗаполнения);
						Если ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.НачалоДоговораГПХ
							Или ВыборкаТД.ВидМероприятия = Перечисления.ВидыМероприятийТрудовойДеятельности.ОкончаниеДоговораГПХ Тогда 
							СтрокаПодраздела11.Параметры.КодПоОКЗ = СтрокаПодраздела11.Параметры.КодПоОКЗ + " " + ВыборкаТД.ТрудоваяФункцияКод;
						КонецЕсли;
					КонецЕсли;
					
					ЭлектронныеТрудовыеКнижкиУНФ.УстановитьПараметрыДокументовОснований(СтрокаПодраздела11, ВыборкаТД);
					
					Если ЗначениеЗаполнено(СтрокаПодраздела11.Параметры.ОснованиеУвольнения) Тогда
						СтрокаПодраздела11.Параметры.ОснованиеУвольнения = ЭлектронныеТрудовыеКнижкиУНФ.ПредставлениеПричиныУвольнения(ВыборкаТД, , Истина);
					КонецЕсли;
					
					ОбластиСтрок.Добавить(СтрокаПодраздела11);
					НомерСтрокиФормы = НомерСтрокиФормы + 1;
					
				КонецЦикла;
				
				Для Каждого Строка Из ОбластиСтрок Цикл
					
					МассивОбластей = Новый Массив;
					МассивОбластей.Добавить(Строка);
					
					Если Не ТекущийЛист.ПроверитьВывод(МассивОбластей) Тогда
						ДокументРезультат.Вывести(ТекущийЛист);
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
						ТекущийЛист = Новый ТабличныйДокумент;
						ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
						ТекущийЛист.Вывести(ОбластьШапкаПодраздела11);
					КонецЕсли;
					
					ТекущийЛист.Вывести(Строка);
					
				КонецЦикла;
				
				Если ОбластиСтрок.Количество() = 0 Тогда 
					СтрокаПодраздела11 = Макет.ПолучитьОбласть("СтрокаПодраздела11");
					ТекущийЛист.Вывести(СтрокаПодраздела11);
				КонецЕсли;
				
				Если Не РазделыЕФС1.СЗВСТАЖ Тогда 
					
					ОбластьПодраздел12 = Макет.ПолучитьОбласть("Подраздел12");
					ОбластьШапкаПодраздела12 = Макет.ПолучитьОбласть("ШапкаПодраздела12");
					СтрокаПодраздела12 = Макет.ПолучитьОбласть("СтрокаПодраздела12");
					
					ОбластиСтрок.Очистить();
					ОбластиСтрок.Добавить(ОбластьПодраздел12);
					ОбластиСтрок.Добавить(ОбластьШапкаПодраздела12);
					ОбластиСтрок.Добавить(СтрокаПодраздела12);
					
					Если Не ТекущийЛист.ПроверитьВывод(ОбластиСтрок) Тогда
						ДокументРезультат.Вывести(ТекущийЛист);
						ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
						ТекущийЛист = Новый ТабличныйДокумент;
						ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
					КонецЕсли;
					
					ТекущийЛист.Вывести(ОбластьПодраздел12);
					ТекущийЛист.Вывести(ОбластьШапкаПодраздела12);
					ТекущийЛист.Вывести(СтрокаПодраздела12);
					
				КонецЕсли;
				
				ДокументРезультат.Вывести(ТекущийЛист);
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если РазделыЕФС1.СЗВСТАЖ И ВыборкаСТАЖ.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаСТАЖ.СледующийПоЗначениюПоля("Ссылка");
			
			Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерСтроки") Цикл
				
				ОбластьПодраздел1 = Макет.ПолучитьОбласть("Подраздел1");
				ОбластьПодраздел12 = Макет.ПолучитьОбласть("Подраздел12");
				ОбластьШапкаПодраздела12 = Макет.ПолучитьОбласть("ШапкаПодраздела12");
				СтрокаПодраздела12 = Макет.ПолучитьОбласть("СтрокаПодраздела12");
				ОбластьСтрокаКодПозицииСписка = Макет.ПолучитьОбласть("СтрокаКодПозицииСписка");
				
				ЗаполнитьЗначенияСвойств(ОбластьПодраздел1.Параметры, ВыборкаСТАЖ);
				ОбластьПодраздел1.Параметры.ДатаРождения = СправкиПоНДФЛ.ФорматДатыЧислоВКавычкахМесяцПрописью(ВыборкаСТАЖ.ДатаРождения, Истина);
				
				ИННРаботника = СокрЛП(ВыборкаСТАЖ.ИНН);
				ВывестиДанныеПоБуквенно(ИННРаботника, ОбластьПодраздел1, "ИННРаботника", Мин(СтрДлина(ИННРаботника), 12));
				
				КодКатегорииЗЛ = СокрЛП(ВыборкаСТАЖ.КодКатегорииЗЛ);
				ВывестиДанныеПоБуквенно(КодКатегорииЗЛ, ОбластьПодраздел1, "КодКатегорииЗЛ", Мин(СтрДлина(КодКатегорииЗЛ), 4));
				
				Гражданство = СокрЛП(ВыборкаСТАЖ.Гражданство);
				ВывестиДанныеПоБуквенно(Гражданство, ОбластьПодраздел1, "Гражданство", Мин(СтрДлина(Гражданство), 3));
				
				ЗаполнитьЗначенияСвойств(ОбластьПодраздел12.Параметры, ВыборкаСТАЖ);
				
				ПериодСТАЖ = СтрЗаменить(ВыборкаПоШапкеДокумента.Год, Символы.НПП, "");
				ВывестиДанныеПоБуквенно(ПериодСТАЖ, ОбластьПодраздел12, "ПериодСТАЖ", Мин(СтрДлина(ПериодСТАЖ), 4));
				
				Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.Исходная Тогда
					ОбластьПодраздел12.Параметры.Исходная = "Х";	
				ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_СТАЖ.НазначениеПенсии Тогда
					ОбластьПодраздел12.Параметры.НазначениеПенсии = "Х";	
				ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая Тогда
					ОбластьПодраздел12.Параметры.Корректирующая = "Х";	
				ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
					ОбластьПодраздел12.Параметры.Отменяющая = "Х";	
				КонецЕсли;	
				
				Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Корректирующая 
					Или ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ_КОРР.Отменяющая Тогда
					КорректируемыйПериодСТАЖ = СтрЗаменить(Год(ВыборкаПоШапкеДокумента.КорректируемыйПериод), Символы.НПП, "");
					ВывестиДанныеПоБуквенно(КорректируемыйПериодСТАЖ, ОбластьПодраздел12, "КорректируемыйПериодСТАЖ", Мин(СтрДлина(КорректируемыйПериодСТАЖ), 4));
				КонецЕсли;
				
				ТекущийЛист = Новый ТабличныйДокумент;
				ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
				
				ТекущийЛист.Вывести(ОбластьПодраздел1);
				
				ОбластьПодраздел11 = Макет.ПолучитьОбласть("Подраздел11");
				ОбластьШапкаПодраздела11 = Макет.ПолучитьОбласть("ШапкаПодраздела11");
				ОбластьСтрокаПодраздела11 = Макет.ПолучитьОбласть("СтрокаПодраздела11");
				ТекущийЛист.Вывести(ОбластьПодраздел11); 
				ТекущийЛист.Вывести(ОбластьШапкаПодраздела11);
				ТекущийЛист.Вывести(ОбластьСтрокаПодраздела11);
				
				ТекущийЛист.Вывести(ОбластьПодраздел12); 
				ТекущийЛист.Вывести(ОбластьШапкаПодраздела12);
				
				ОчиститьОбласть(СтрокаПодраздела12);
				
				НомерСтроки = 0;
				
				Если ВыборкаСТАЖ.ЗаполненСтаж Тогда
					Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерОсновнойЗаписи") Цикл
						НомерСтроки = НомерСтроки + 1;
						СтрокаПодраздела12.Параметры.НомерПериода = СтрЗаменить(НомерСтроки, Символы.НПП, "");
						Если ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа = Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕТИПРЛ Тогда 
							СтрокаПодраздела12.Параметры.ТерриториальныеУсловияКод = "";
						КонецЕсли;
						ВывестиСтрокуСтажаЕФС1(ВыборкаСТАЖ, СтрокаПодраздела12, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, ОбластьШапкаПодраздела12);
						Если ЗначениеЗаполнено(ВыборкаСТАЖ.КодПозицииСпискаКод) Тогда 
							ЗаполнитьЗначенияСвойств(ОбластьСтрокаКодПозицииСписка.Параметры, ВыборкаСТАЖ);
							ВывестиОбластьЕФС1(ОбластьСтрокаКодПозицииСписка, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, ОбластьШапкаПодраздела12);
						КонецЕсли;
						СтрокаПодраздела12.Параметры.НомерПериода = "";
						Пока ВыборкаСТАЖ.СледующийПоЗначениюПоля("НомерДополнительнойЗаписи") Цикл
							Если ВыборкаСТАЖ.НомерДополнительнойЗаписи = 0 Тогда
								Продолжить;
							КонецЕсли;
							ВывестиСтрокуСтажаЕФС1(ВыборкаСТАЖ, СтрокаПодраздела12, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, ОбластьШапкаПодраздела12);
						КонецЦикла;	
					КонецЦикла;
				Иначе
					ВывестиОбластьЕФС1(СтрокаПодраздела12, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист);
				КонецЕсли;
				
				ДокументРезультат.Вывести(ТекущийЛист);
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если РазделыЕФС1.ДНП И ВыборкаДНП.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаДНП.СледующийПоЗначениюПоля("Ссылка");
			
			ОчиститьОбласть(ОбластьПодраздел2);
			ЗаполнитьЗначенияСвойств(ОбластьПодраздел2.Параметры, ВыборкаДНП);
			
			ПериодДНП = СтрЗаменить(ВыборкаПоШапкеДокумента.Год, Символы.НПП, "");
			Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ
				Или ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.ОТМЕНЯЮЩАЯ Тогда
				ПериодДНП = СтрЗаменить(Год(ВыборкаПоШапкеДокумента.Дата), Символы.НПП, "");
			КонецЕсли;
			ВывестиДанныеПоБуквенно(ПериодДНП, ОбластьПодраздел2, "ПериодДНП", Мин(СтрДлина(ПериодДНП), 4));
			
			Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ Тогда 
				ОбластьПодраздел2.Параметры.Корректирующая = "Х";
			ИначеЕсли ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.ОТМЕНЯЮЩАЯ Тогда 
				ОбластьПодраздел2.Параметры.Отменяющая = "Х";
			Иначе 
				ОбластьПодраздел2.Параметры.Исходная = "Х";
			КонецЕсли;
			
			Если ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.КОРРЕКТИРУЮЩАЯ 
				Или ВыборкаПоШапкеДокумента.ТипСведений = Перечисления.ТипыСведенийСЗВ.ОТМЕНЯЮЩАЯ Тогда
				КорректируемыйПериодДНП = СтрЗаменить(ВыборкаПоШапкеДокумента.Год, Символы.НПП, "");
				ВывестиДанныеПоБуквенно(КорректируемыйПериодДНП, ОбластьПодраздел2, "КорректируемыйПериодДНП", Мин(СтрДлина(КорректируемыйПериодДНП), 4));
			КонецЕсли;
			
			ТекущийЛист = Новый ТабличныйДокумент;
			ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
			
			ТекущийЛист.Вывести(ОбластьПодраздел2);
			ТекущийЛист.Вывести(ОбластьШапкаПодраздела2);
			
			Пока ВыборкаДНП.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаПодраздела2.Параметры, ВыборкаДНП);
				ВывестиОбластьЕФС1(ОбластьСтрокаПодраздела2, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, ОбластьШапкаПодраздела2);
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(ОбластьПодвалПодраздела2.Параметры, ВыборкаПоШапкеДокумента);
			ВывестиОбластьЕФС1(ОбластьПодвалПодраздела2, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист);
			
			ДокументРезультат.Вывести(ТекущийЛист);
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		Если РазделыЕФС1.ДСВ И ВыборкаДСВ.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаДСВ.СледующийПоЗначениюПоля("Ссылка");
			
			ОбластьПодраздел3.Параметры.НомерПлатежногоПоручения = ВыборкаПоШапкеДокумента.НомерПоручения;
			ОбластьПодраздел3.Параметры.ДатаПлатежногоПоручения	= СправкиПоНДФЛ.ФорматДатыЧислоВКавычкахМесяцПрописью(ВыборкаПоШапкеДокумента.ДатаПоручения, Истина);
			ОбластьПодраздел3.Параметры.ДатаИсполненияПлатежногоПоручения = СправкиПоНДФЛ.ФорматДатыЧислоВКавычкахМесяцПрописью(ВыборкаПоШапкеДокумента.ДатаИсполненияПоручения, Истина);
			ОбластьПодраздел3.Параметры.ПериодУплаты = СтрЗаменить(ВыборкаПоШапкеДокумента.Год, Символы.НПП, "");
			
			ТекущийЛист = Новый ТабличныйДокумент;
			ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
			
			ТекущийЛист.Вывести(ОбластьПодраздел3);
			ТекущийЛист.Вывести(ОбластьШапкаПодраздела3);
			
			ВзносыРаботникаИтог = 0;
			ВзносыРаботодателяИтог = 0;
			
			Пока ВыборкаДСВ.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(ОбластьСтрокаПодраздела3.Параметры, ВыборкаДСВ);
				ВывестиОбластьЕФС1(ОбластьСтрокаПодраздела3, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, ОбластьШапкаПодраздела3);
				ВзносыРаботникаИтог = ВзносыРаботникаИтог + ВыборкаДСВ.ВзносыРаботника;
				ВзносыРаботодателяИтог = ВзносыРаботодателяИтог + ВыборкаДСВ.ВзносыРаботодателя;
			КонецЦикла;
			
			ОбластьПодвалПодраздела3.Параметры.ВзносыРаботника = ВзносыРаботникаИтог;
			ОбластьПодвалПодраздела3.Параметры.ВзносыРаботодателя = ВзносыРаботодателяИтог;
			ОбластьПодвалПодраздела3.Параметры.ВзносыВсего = ВзносыРаботникаИтог + ВзносыРаботодателяИтог;
			ВывестиОбластьЕФС1(ОбластьПодвалПодраздела3, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист);
			
			ДокументРезультат.Вывести(ТекущийЛист);
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Процедура ВывестиСтрокуСтажаЕФС1(ВыборкаСТАЖ, ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, Макет, ОбластьЗаголовок)
	
	ЗаполнитьОбластьСтажЕФС1(ВыборкаСТАЖ, ОбластьСтрокаСтажа);
	Если ЗначениеЗаполнено(ВыборкаСТАЖ.ЗамещениеГосударственныхМуниципальныхДолжностей)
		И Не ЗначениеЗаполнено(ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа) Тогда
		ОбластьСтрокаСтажа.Параметры.ДопИТС = ВыборкаСТАЖ.ЗамещениеГосударственныхМуниципальныхДолжностей;
	КонецЕсли;
	
	ВывестиОбластьЕФС1(ОбластьСтрокаСтажа, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, ОбластьЗаголовок);
	
КонецПроцедуры

Процедура ВывестиОбластьЕФС1(ВыводимаяОбласть, ВыборкаПоШапкеДокумента, ДокументРезультат, ТекущийЛист, ОбластьЗаголовок = Неопределено)
	
	ВыводимыеОбласти = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыводимаяОбласть);
	Если ТекущийЛист.ПроверитьВывод(ВыводимыеОбласти) Тогда 
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	Иначе
		ДокументРезультат.Вывести(ТекущийЛист);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		ТекущийЛист = Новый ТабличныйДокумент;
		ТекущийЛист.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;
		Если ОбластьЗаголовок <> Неопределено Тогда 
			ТекущийЛист.Вывести(ОбластьЗаголовок);
		КонецЕсли;
		ТекущийЛист.Вывести(ВыводимаяОбласть);
	КонецЕсли;	
	
КонецПроцедуры  

Процедура ЗаполнитьОбластьСтажЕФС1(ВыборкаСТАЖ, ОбластьСтаж)
	
	ОбластьСтаж.Параметры.ДатаНачалаПериода = Формат(ВыборкаСТАЖ.ДатаНачалаПериода, "ДФ=""дд.ММ.гггг""");
	ОбластьСтаж.Параметры.ДатаОкончанияПериода = Формат(ВыборкаСТАЖ.ДатаОкончанияПериода, "ДФ=""дд.ММ.гггг""");
	
	Если ВыборкаСТАЖ.ТретийПараметрИсчисляемогоСтажа <> Справочники.ПараметрыИсчисляемогоСтраховогоСтажа.ДЕТИПРЛ Тогда 
		ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = ВыборкаСТАЖ.ТерриториальныеУсловияКод;
		Если ЗначениеЗаполнено(ВыборкаСТАЖ.ТерриториальныеУсловияСтавка) Тогда 
			ОбластьСтаж.Параметры.ТерриториальныеУсловияКод = Строка(ВыборкаСТАЖ.ТерриториальныеУсловияКод) + " " + ВыборкаСТАЖ.ТерриториальныеУсловияСтавка;
		КонецЕсли;
		ОбластьСтаж.Параметры.РайонныйКоэффициент = ВыборкаСТАЖ.РайонныйКоэффициент;
	КонецЕсли;
	
	ОбластьСтаж.Параметры.ОсобыеУсловияТрудаКод = ВыборкаСТАЖ.ОсобыеУсловияТрудаКод;
	ОбластьСтаж.Параметры.ОснованиеИсчисляемогоСтажаКод = ВыборкаСТАЖ.ОснованиеИсчисляемогоСтажаКод;
	ОбластьСтаж.Параметры.ОснованиеВыслугиЛетКод = ВыборкаСТАЖ.ОснованиеВыслугиЛетКод;
	ОбластьСтаж.Параметры.ДопИТС = ПредставлениеПараметровИсчисляемогоТрудовогоСтажа(ВыборкаСТАЖ);
	ОбластьСтаж.Параметры.НомерРабочегоМеста = СокрЛП(ВыборкаСТАЖ.НомерРабочегоМеста);
	ОбластьСтаж.Параметры.КодКлассаУсловийТруда = КодКлассаУсловийТрудаЕФС1(ВыборкаСТАЖ.КлассПодклассУсловийТруда);
	
	ПредставлениеПараметровПенсииЗаВыслугуЛет(ВыборкаСТАЖ, ОбластьСтаж.Параметры.ДопВЛ, ОбластьСтаж.Параметры.ДопВЛСтавка);
	
КонецПроцедуры



