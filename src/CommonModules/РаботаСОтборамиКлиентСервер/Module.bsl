
#Область ПрограммныйИнтерфейс

// Процедура - Устанавливает отбор по периоду в форме списка документов
//
// Параметры:
//  СписокОтбор		 - ОтборКомпоновкиДанных - Отбор элемента формы - списка
//  ДатаНачала		 - Дата					 - Дата начала периода отображения
//  ДатаОкончания	 - Дата					 - Дата окончания периода отображения
//  ИмяПоляОтбора	 - Строка				 - Имя поля отбора периода в панели отборов 
//
Процедура УстановитьОтборПоПериоду(СписокОтбор, ДатаНачала, ДатаОкончания, ИмяПоляОтбора = "Дата") Экспорт
	
	// Отбор на список по периоду
	ГруппаОтборПоПериоду = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		СписокОтбор.Элементы,
		"Период",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоПериоду,
		ИмяПоляОтбора,
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		ДатаНачала,
		"ДатаНачала",
		ЗначениеЗаполнено(ДатаНачала));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоПериоду,
		ИмяПоляОтбора,
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		ДатаОкончания,
		"ДатаОкончания",
		ЗначениеЗаполнено(ДатаОкончания));
		
КонецПроцедуры
	
// Функция - Формирует представление периода для панели отборов
//
// Параметры:
//  Период	 - СтандартныйПериод - Период, для которого требуется сформировать представление
// 
// Возвращаемое значение:
//  Строка - Представление периода
//
Функция ОбновитьПредставлениеПериода(Период) Экспорт

	Если Не ЗначениеЗаполнено(Период) Или (Не ЗначениеЗаполнено(Период.ДатаНачала) И Не ЗначениеЗаполнено(
		Период.ДатаОкончания)) Тогда
		ПредставлениеПериода = НСтр("ru = 'Период: за все время'");
	Иначе
		ДатаОкончанияПериода = ?(ЗначениеЗаполнено(Период.ДатаОкончания), КонецДня(Период.ДатаОкончания),
			Период.ДатаОкончания);
		Если ДатаОкончанияПериода < Период.ДатаНачала Тогда
#Если Клиент Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр(
				"ru = 'Выбрана дата окончания периода, которая меньше даты начала'"));
#КонецЕсли
			ПредставлениеПериода = СтрШаблон(НСтр("ru = 'с %1'"), Формат(Период.ДатаНачала, "ДЛФ=D;"));
		Иначе
			ПредставлениеПериода = СтрШаблон(НСтр("ru = 'за %1'"), НРег(ПредставлениеПериода(Период.ДатаНачала,
				ДатаОкончанияПериода)));
		КонецЕсли;
	КонецЕсли;

	Возврат ПредставлениеПериода;

КонецФункции

Функция УбратьЗапрещенныеСимволы(Текст, Разрешенные) Экспорт
	
	Результат = Текст;
	Для Сч = 1 По СтрДлина(Текст) Цикл
		ТекСимвол = Сред(Текст, Сч, 1);
		Если СтрНайти(Разрешенные, НРег(ТекСимвол))=0 Тогда
			Результат = СтрЗаменить(Результат, ТекСимвол,"");
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИмяОтбора(ЭлементИмя, ДанныеОтборов) Экспорт

	ИмяЭлемента = Прав(ЭлементИмя, СтрДлина(ЭлементИмя)-СтрНайти(ЭлементИмя,"_", НаправлениеПоиска.СНачала));
	НайденнаяСтрокаМассив = ДанныеОтборов.НайтиСтроки(Новый Структура("ИмяЭлемента", ИмяЭлемента));
	Если НайденнаяСтрокаМассив.Количество() > 0 Тогда
		Возврат НайденнаяСтрокаМассив[0].ИмяОтбора;
	Иначе
		Возврат ИмяЭлемента;
	КонецЕсли;
	
КонецФункции // ()

Процедура УстановитьОтображениеКомандыСброситьВсеОтборы(Форма, ИмяТЧДанныеМеток="ДанныеМеток", МассивНетиповыхОтборов = Неопределено) Экспорт
	
	Элементы = Форма.Элементы;
	ИмяМетки = СтрЗаменить(ИмяТЧДанныеМеток, "ДанныеМеток", "");
	
	Если Элементы.Найти("СброситьВсеОтборы" + ИмяМетки) <> Неопределено Тогда
		
		ДанныеМеток = Форма[ИмяТЧДанныеМеток];
		
		КоличествоМеток = 0;
		
		Для каждого Метка Из ДанныеМеток Цикл
			
			ОтборИМеткиДоступны = Истина;
			
			Если Элементы.Найти(Метка.ИмяГруппыРодителя) <> Неопределено Тогда
				
				Для каждого ОтборИМетка Из Элементы[Метка.ИмяГруппыРодителя].ПодчиненныеЭлементы Цикл
					Если Не ОтборИМетка.Доступность Тогда
						ОтборИМеткиДоступны = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ОтборИМеткиДоступны = Ложь;
			КонецЕсли;
			
			Если ОтборИМеткиДоступны Тогда
				КоличествоМеток = КоличествоМеток + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗначениеПоУмолчанию = Неопределено;
		СтруктураСвойств = Новый Структура("ОтборПериод" + ИмяМетки, ЗначениеПоУмолчанию);
		ЗаполнитьЗначенияСвойств(СтруктураСвойств, Форма);
		ЕстьРеквизитПериод = Не (СтруктураСвойств["ОтборПериод" + ИмяМетки] = ЗначениеПоУмолчанию);
		
		// -
		МассивНетиповыхОтборов = Новый Массив;
		Если Форма.ИмяФормы = "Документ.НачислениеЗарплатыУНФ.Форма.ФормаСписка" 
			Или Форма.ИмяФормы = "Документ.ПлатежнаяВедомость.Форма.ФормаСписка" Тогда
			МассивНетиповыхОтборов.Добавить("ОтборПериодРегистрации");
		ИначеЕсли Форма.ИмяФормы = "Документ.ПоложениеОПремировании.Форма.ФормаСписка" Тогда
			МассивНетиповыхОтборов.Добавить("ОтборДействуетНа");
		КонецЕсли;
		// -
		
		Если МассивНетиповыхОтборов.Количество() = 0 Тогда
			Если ЕстьРеквизитПериод Тогда
				Элементы["СброситьВсеОтборы" + ИмяМетки].Видимость = КоличествоМеток > 1 Или (КоличествоМеток > 0 И ЗначениеЗаполнено(Форма["ОтборПериод" + ИмяМетки]));
			Иначе
				Элементы["СброситьВсеОтборы" + ИмяМетки].Видимость = КоличествоМеток > 1;
			КонецЕсли;
		Иначе
			
			Для каждого НетиповойОтбор Из МассивНетиповыхОтборов Цикл
				Если ЗначениеЗаполнено(Форма[НетиповойОтбор]) Тогда
					КоличествоМеток = КоличествоМеток + 1;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьРеквизитПериод Тогда
				Если ЗначениеЗаполнено(Форма["ОтборПериод" + ИмяМетки]) Тогда
					КоличествоМеток = КоличествоМеток + 1;
				КонецЕсли;
			КонецЕсли;
			
			Элементы["СброситьВсеОтборы" + ИмяМетки].Видимость = КоличествоМеток > 1;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


 


