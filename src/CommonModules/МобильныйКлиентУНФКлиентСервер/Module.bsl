#Область ПрограммныйИнтерфейс

// Возвращает адрес публикации мобильного клиента в Google Play
// 
// Параметры:
//  ДляБраузера - Булево - сформировать ссылку для браузера настольного компьютера
//  ЭтоРозница - см. ОбщегоНазначенияУНФ.ЭтоРозница
// Возвращаемое значение:
//  Строка - адрес публикации мобильного клиента в Google Play
Функция АдресПубликацииВGooglePlay(ДляБраузера, ЭтоРозница) Экспорт

	Если ДляБраузера Тогда
		Если ЭтоРозница Тогда
			Возврат "https://play.google.com/store/apps/details?id=com.e1c.retail";
		Иначе
			Возврат "https://play.google.com/store/apps/details?id=com.e1c.SmallBusiness";
		КонецЕсли;
	Иначе
		Если ЭтоРозница Тогда
			Возврат "market://details?id=com.e1c.retail";
		Иначе
			Возврат "market://details?id=com.e1c.SmallBusiness";
		КонецЕсли;
	КонецЕсли;

КонецФункции

// Возвращает адрес публикации мобильного клиента в App Store
// Параметры:
//  ДляБраузера - Булево - сформировать ссылку для браузера настольного компьютера
//  ЭтоРозница - см. ОбщегоНазначенияУНФ.ЭтоРозница
// Возвращаемое значение:
//  Строка - адрес публикации мобильного клиента в App Store
Функция АдресПубликацииВAppStore(ДляБраузера, ЭтоРозница) Экспорт

	Если ДляБраузера Тогда
		Протокол = "https";
	Иначе
		Протокол = "itms-apps";
	КонецЕсли;
	
	Если ЭтоРозница Тогда
		Ид = "1635330273";
		ПутьКПриложению = "1%D1%81-%D1%80%D0%BE%D0%B7%D0%BD%D0%B8%D1%86%D0%B0";
	Иначе
		Ид = "1440287699";
		ПутьКПриложению = "1%D1%81-%D1%83%D0%BD%D1%84-%D0%B2-%D0%BE%D0%B1%D0%BB%D0%B0%D0%BA%D0%B5";
	КонецЕсли;

	Возврат СтрШаблон("%1://apps.apple.com/us/app/%2/id%3", Протокол, ПутьКПриложению, Ид);

КонецФункции

// Возвращает признак того, что мобильный клиент запущен на Android.
//
// Возвращаемое значение:
// 	Булево - признак того, что мобильный клиент запущен на Android.
Функция ЭтоAndroid() Экспорт

	СисИнфо = Новый СистемнаяИнформация;
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "Android") > 0);

КонецФункции

// Возвращает признак того, что мобильный клиент запущен на iOS.
//
// Возвращаемое значение:
// 	Булево - признак того, что мобильный клиент запущен на iOS.
Функция ЭтоiOS() Экспорт // АПК:216 слово содержит и русские и английские символы.

	СисИнфо = Новый СистемнаяИнформация;
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "pple") > 0);

КонецФункции

// Это большой экран или планшет.
// 
// Возвращаемое значение:
//  Булево - это большой экран или планшет
Функция ЭтоБольшойЭкранИлиПланшет() Экспорт

	ИнформацияЭкрана = ПолучитьИнформациюЭкрановКлиента();
	Если ИнформацияЭкрана <> Неопределено Тогда
		Разрешение = ИнформацияЭкрана[0].DPI;
		Высота = ИнформацияЭкрана[0].Высота;
		Ширина = ИнформацияЭкрана[0].Ширина;
		РазмерЭкрана = Sqrt((Высота / Разрешение * Высота / Разрешение) + (Ширина / Разрешение * Ширина / Разрешение));
		ЭтоПланшет = РазмерЭкрана >= 7;
		Если Ширина >= 1280 Или ЭтоПланшет Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;

КонецФункции

#КонецОбласти
