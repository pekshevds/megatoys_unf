#Область СлужебныеПроцедурыИФункции

Функция КонтекстОтправкиПодтвержденияПолучения(Знач МассивСсылок) Экспорт
	СЭДОФСС.ПроверитьНаличиеПраваОбмена();
	УправлениеДоступом.ПроверитьИзменениеРазрешено(МассивСсылок[0]);
	
	ЗначенияРеквизитовОбъектов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивСсылок,
		"Ссылка, Страхователь, ИдентификаторСообщения",
		Истина);
	
	Страхователи = Новый Массив;
	ИдентификаторыСообщенийСтрахователей = Новый Соответствие;
	
	Для Каждого КлючИЗначение Из ЗначенияРеквизитовОбъектов Цикл
		ЗначенияРеквизитов = КлючИЗначение.Значение;
		Идентификаторы = ИдентификаторыСообщенийСтрахователей[ЗначенияРеквизитов.Страхователь];
		Если Идентификаторы = Неопределено Тогда
			Идентификаторы = Новый Массив;
			ИдентификаторыСообщенийСтрахователей.Вставить(ЗначенияРеквизитов.Страхователь, Идентификаторы);
			Страхователи.Добавить(ЗначенияРеквизитов.Страхователь);
		КонецЕсли;
		Идентификаторы.Добавить(ЗначенияРеквизитов.ИдентификаторСообщения);
	КонецЦикла;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Страхователи", Страхователи);
	Контекст.Вставить("ИдентификаторыСообщенийСтрахователей", ИдентификаторыСообщенийСтрахователей);
	
	Возврат Контекст;
КонецФункции

Функция СтрахователиИзОтборовПоОрганизациямФормы(Знач ОтборыПоОрганизациям) Экспорт
	Возврат СЭДОФСС.СтрахователиИзОтборовПоОрганизациямФормы(ОтборыПоОрганизациям);
КонецФункции

Функция СтрахователиОрганизаций(Знач Организации) Экспорт
	Возврат СЭДОФСС.СтрахователиОрганизаций(Организации);
КонецФункции

Функция ТекстXML(Знач Идентификатор) Экспорт
	Возврат СЭДОФСС.ТекстXML(Идентификатор);
КонецФункции

Функция КонтекстПолученияДанныхФайлаИзвещенияФСС(Знач ИзвещениеФСС, ИдентификаторФормы) Экспорт
	Результат = Новый Структура("ИзвещениеФСС, ДанныеФайла, ПравоСохранения, ОтветНаВопрос");
	
	ИменаРеквизитов = "ВходящийФайл, ДатаОтправкиПодтверждения, Обработано, ПодтверждениеПолученоФСС, МаксимальнаяДатаПодтверждения";
	РеквизитыИзвещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИзвещениеФСС, ИменаРеквизитов);
	
	Результат.ИзвещениеФСС = ИзвещениеФСС;
	
	Если ЗначениеЗаполнено(РеквизитыИзвещения.ВходящийФайл) Тогда
		ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
		ДополнительныеПараметры.ИдентификаторФормы = ИдентификаторФормы;
		Результат.ДанныеФайла = РаботаСФайлами.ДанныеФайла(РеквизитыИзвещения.ВходящийФайл, ДополнительныеПараметры);
	КонецЕсли;
	
	Если Результат.ДанныеФайла = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ПравоСохранения = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	
	Если РеквизитыИзвещения.Обработано
		Или ПодтверждениеОтправлено(РеквизитыИзвещения)
		Или Не ПодтверждениеТребуетсяОтправлять(РеквизитыИзвещения)
		Или Не ПравоДоступа("Изменение", Метаданные.Документы.ИзвещениеФСС)
		Или Не Результат.ПравоСохранения Тогда
		Результат.ОтветНаВопрос = 2; // Открыть документ без лишних вопросов.
	Иначе
		Результат.ОтветНаВопрос = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ПособияФСС.СЭДО",
			"ОтветНаВопросОПодтвержденииПолучения");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПодтверждениеОтправлено(РеквизитыИзвещения)
	Возврат ЗначениеЗаполнено(РеквизитыИзвещения.ДатаОтправкиПодтверждения)
		Или РеквизитыИзвещения.ПодтверждениеПолученоФСС;
КонецФункции

Функция ПодтверждениеТребуетсяОтправлять(РеквизитыИзвещения)
	Возврат ЗначениеЗаполнено(РеквизитыИзвещения.МаксимальнаяДатаПодтверждения);
КонецФункции

Процедура СохранитьОтветНаВопросОПодтвержденииПолученияИзвещенияФСС(ВариантОтвета) Экспорт
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПособияФСС.СЭДО",
		"ОтветНаВопросОПодтвержденииПолучения",
		ВариантОтвета);
КонецПроцедуры

#КонецОбласти
