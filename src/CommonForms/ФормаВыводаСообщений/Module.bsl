
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем АдресВоВременномХранилище;
	
	ЛокализованныйЗаголовокЗакрыть = НСтр("ru = 'Закрыть'");
	ЛокализованныйЗаголовокДалее = НСтр("ru = 'Далее >>'");
	
	
	Если Параметры.Свойство("АдресВоВременномХранилище", АдресВоВременномХранилище) Тогда
		
		ПримененныеСкидки = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
		Если Не ПримененныеСкидки.Свойство("ТаблицаСообщений") Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		// В заказе покупателя возможно одновременный просчет нескольких КП.
		// По каждому из них скидки рассчитываются отдельно, сообщений может быть несколько - каждое к своему варианту КП.
		КлючиВариантовКП = Неопределено;
		Если Параметры.Свойство("КлючиВариантов", КлючиВариантовКП) Тогда
			СообщенияВариантов = Новый Соответствие;
			Для каждого СтрокаСообщение Из ПримененныеСкидки.ТаблицаСообщений Цикл
				НомерВариантаКП = КлючиВариантовКП[СтрокаСообщение.КлючСвязи];
				МассивСообщенийВарианта = СообщенияВариантов.Получить(НомерВариантаКП);
				Если МассивСообщенийВарианта = неопределено Тогда 
					МассивСообщенийВарианта = Новый Массив;
					СообщенияВариантов.Вставить(НомерВариантаКП, МассивСообщенийВарианта);
				КонецЕсли;
				МассивСообщенийВарианта.Добавить(СтрокаСообщение.ТекстСообщения);
			КонецЦикла;
			Для каждого СообщенияВарианта Из СообщенияВариантов Цикл
				ШапкаСообщения = НСтр("ru = 'Сообщения для варианта КП №'") + СообщенияВарианта.Ключ;
				Для каждого ТекстСообщения Из СообщенияВарианта.Значение Цикл
					СписокСообщений.Добавить(ТекстСообщения, ШапкаСообщения);
				КонецЦикла;
			КонецЦикла;
		Иначе
			СписокСообщений.ЗагрузитьЗначения(ПримененныеСкидки.ТаблицаСообщений.ВыгрузитьКолонку("ТекстСообщения"));
		КонецЕсли;
		
		Если СписокСообщений.Количество() = 0  Тогда
			Отказ = Истина;
		Иначе
			НомерТекущегоСообщения = 0;
			Сообщение = СписокСообщений[НомерТекущегоСообщения];
			ТекстСообщения = Сообщение.Значение;
			Если ЗначениеЗаполнено(Сообщение.Представление) Тогда
				ЭтаФорма.Заголовок = Сообщение.Представление;
			Иначе
				ЭтаФорма.Заголовок = НСтр("ru = 'Сообщения'");
			КонецЕсли;
			Элементы.Назад.Доступность = Ложь;
			
			Если СписокСообщений.Количество() = 1 Тогда
				Элементы.Далее.Заголовок = ЛокализованныйЗаголовокЗакрыть;
			Иначе
				Элементы.Далее.Заголовок = ЛокализованныйЗаголовокДалее;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Назад(Команда)
	
	НомерТекущегоСообщения = НомерТекущегоСообщения - 1;
	ОбновлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
		
	Если Элементы.Далее.Заголовок = ЛокализованныйЗаголовокЗакрыть Тогда
		Закрыть();
	Иначе
		НомерТекущегоСообщения = НомерТекущегоСообщения + 1;
		ОбновлениеФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновлениеФормы()

	Элементы.Назад.Доступность = НомерТекущегоСообщения >= 1;
	
	ИндексСообщения = НомерТекущегоСообщения;
	Если (СписокСообщений.Количество() - 1) <= НомерТекущегоСообщения Тогда
		Элементы.Далее.Заголовок = ЛокализованныйЗаголовокЗакрыть;
		ИндексСообщения = СписокСообщений.Количество() - 1;
	Иначе
		Элементы.Далее.Заголовок = ЛокализованныйЗаголовокДалее;
	КонецЕсли;
	Сообщение = СписокСообщений[ИндексСообщения];
	ТекстСообщения = Сообщение.Значение;
	Если ЗначениеЗаполнено(Сообщение.Представление) Тогда
		ЭтаФорма.Заголовок = Сообщение.Представление;
	Иначе
		ЭтаФорма.Заголовок =  НСтр("ru = 'Сообщения'");
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти



