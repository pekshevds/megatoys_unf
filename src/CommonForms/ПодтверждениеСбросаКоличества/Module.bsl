#Область ОписаниеПеременных

&НаКлиенте
Перем ЗакрытьССохранением; // Сохранить значение при закрытии формы

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ОбновитьТекстДекорации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗакрытьССохранением = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы И ЗакрытьССохранением И ЗапомнитьВыбранноеЗначение Тогда
		СохранитьНастройку();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗапомнитьВыбранноеЗначениеПриИзменении(Элемент)
	
	Элементы.ДекорацияПодсказка.Видимость = ЗапомнитьВыбранноеЗначение;
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ЗапомнитьВыбранноеЗначение, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура СбрасыватьКоличествоПриИзменении(Элемент)
	
	Если НастройкаСбросаКоличества = -1 Тогда
		СбрасыватьКоличество = Ложь;
	ИначеЕсли НастройкаСбросаКоличества = 0 Тогда
		СбрасыватьКоличество = Истина;
		НовоеКоличествоПриИзмененииНоменклатуры = 0;
	Иначе
		СбрасыватьКоличество = Истина;
		НовоеКоличествоПриИзмененииНоменклатуры = 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	ЗакрытьССохранением = Истина;
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьТекстДекорации()

	НастройкиКоличества = ДокументыУНФКлиентСервер.ПараметрыСбросаКоличества();
	ДокументыУНФ.ЗаполнитьНастройкиКоличества(НастройкиКоличества);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиКоличества);
	
	Если НЕ СбрасыватьКоличество Тогда
		НастройкаСбросаКоличества = -1;
	Иначе
		НастройкаСбросаКоличества = НовоеКоличествоПриИзмененииНоменклатуры;
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройку()
	
	НазваниеНастройки = "СбрасыватьКоличествоПриИзмененииНоменклатуры";
	Если НастройкаСбросаКоличества = -1 Тогда
		ЗначениеНастройки = Перечисления.ВариантыСбросаКоличества.НеСбрасывать;
	ИначеЕсли НастройкаСбросаКоличества = 0 Тогда
		ЗначениеНастройки = Перечисления.ВариантыСбросаКоличества.ДоНуля;
	Иначе
		ЗначениеНастройки = Перечисления.ВариантыСбросаКоличества.ДоЕдиницы;
	КонецЕсли;
	
	РегистрыСведений.НастройкиПользователей.Установить(ЗначениеНастройки, НазваниеНастройки);
	ХранилищеСистемныхНастроек.Сохранить("ОбщиеНастройкиФорм", "ЗапрашиватьСбросКоличества", НЕ ЗапомнитьВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	НастройкаКоличества = Новый Структура();	
	НастройкаКоличества.Вставить("СбрасыватьКоличество",
		СбрасыватьКоличество);
	НастройкаКоличества.Вставить("НовоеКоличествоПриИзмененииНоменклатуры",
		НовоеКоличествоПриИзмененииНоменклатуры);
	НастройкаКоличества.Вставить("ЗапрашиватьСбросКоличества", НЕ ЗапомнитьВыбранноеЗначение);	
	Закрыть(НастройкаКоличества);

КонецПроцедуры

#КонецОбласти
