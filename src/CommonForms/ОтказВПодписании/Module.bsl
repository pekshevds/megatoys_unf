
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ФайлыДокументовКЭДО) Тогда
		ФайлыДокументовКЭДО.ЗагрузитьЗначения(Параметры.ФайлыДокументовКЭДО);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отказать(Команда)
	
	ЗарегистрироватьОтказ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗарегистрироватьОтказ()
	
	Если ПроверитьЗаполнение() Тогда
		
		ЗарегистрироватьОтказНаСервере();
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		Если ВладелецФормы.Открыта() Тогда
			ВладелецФормы.Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьОтказНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ЭлементКоллекции Из ФайлыДокументовКЭДО Цикл
		
		ПрисоединенныйФайл = ЭлементКоллекции.Значение;
		ФайлыКОбработке = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПрисоединенныйФайл);
		
		// Регистрируется комментарий причины отказа в подписании
		НаборЗаписей = РегистрыСведений.КомментарииФайловДокументовКЭДО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПрисоединенныйФайл.Установить(ПрисоединенныйФайл);
		НаборЗаписей.Отбор.Исполнитель.Установить(Пользователи.ТекущийПользователь());
		
		Запись = НаборЗаписей.Добавить();
		Запись.ПрисоединенныйФайл = ПрисоединенныйФайл;
		Запись.Исполнитель = Пользователи.ТекущийПользователь();
		Запись.Комментарий = ПричинаОтказа;
		Запись.ДатаКомментария = ТекущаяДатаСеанса();
		
		НаборЗаписей.Записать();
		
		// Отменяются все запланированные действия
		РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.УдалитьФайлыИзОбработки(ФайлыКОбработке);
		
		// Планируются ознакомления всех пользователей, ранее подписавших электронный документ
		ОзнакомляемыеОтветственные = Новый Массив;
		ПодписиФайла = ЭлектроннаяПодпись.УстановленныеПодписи(ПрисоединенныйФайл);
		Для Каждого СвойстваПодписи Из ПодписиФайла Цикл
			Если ЗначениеЗаполнено(СвойстваПодписи.УстановившийПодпись) Тогда
				ОзнакомляемыеОтветственные.Добавить(СвойстваПодписи.УстановившийПодпись);
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ОзнакомляемыеОтветственные) Тогда
			РегистрыСведений.ЗапланированныеДействияСФайламиДокументовКЭДО.ЗарегистрироватьОбработкуФайловИсполнителям(
				ФайлыКОбработке,
				Перечисления.ДействияСФайламиДокументовКЭДО.Ознакомиться,
				ОзнакомляемыеОтветственные,
				Истина);
		КонецЕсли;
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
