
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Строка(Параметры.ФизическоеЛицо));
	Заголовок = СтрШаблон(НСтр("ru = '%1 (вычеты к налогам)'"), ФамилияИнициалы);
	
	ДанныеВычетов = ПолучитьИзВременногоХранилища(Параметры.ДанныеВычетов);
	Вычеты.Загрузить(ДанныеВычетов);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Вычеты, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВычеты

&НаКлиенте
Процедура ВычетыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Вычеты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Начисление) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыПриАктивизацииСтроки(Элемент)
	
	УстановитьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.МесяцПериодаПредоставленияВычета = НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса());
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(Элемент.ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВычетыМесяцПериодаПредоставленияВычетаСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.Вычеты.ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыМесяцПериодаПредоставленияВычетаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.Вычеты.ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыМесяцПериодаПредоставленияВычетаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.Вычеты.ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой", Направление, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыМесяцПериодаПредоставленияВычетаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетыМесяцПериодаПредоставленияВычетаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Модифицированность Тогда
		Оповестить("ИзмененыВычетыНДФЛ", РезультатРедактированияВычетов(), ЭтаФорма);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбора(Форма)
	
	Элементы = Форма.Элементы;
	ТекущиеДанные = Элементы.Вычеты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СписокГруппВычетов = Новый Массив;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Начисление) Тогда 
		СписокГруппВычетов.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей"));
		СписокГруппВычетов.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыВычетовПоНДФЛ.Имущественные"));
		СписокГруппВычетов.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО"));
		СписокГруппВычетов.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыВычетовПоНДФЛ.Стандартные"));
	Иначе 
		СписокГруппВычетов.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыВычетовПоНДФЛ.ПустаяСсылка"));
	КонецЕсли;
	
	СписокПараметровВыбора = Новый Массив;
	СписокПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ГруппаВычета", Новый ФиксированныйМассив(СписокГруппВычетов)));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ВычетыКодВычета", "ПараметрыВыбора", Новый ФиксированныйМассив(СписокПараметровВыбора));
	
КонецПроцедуры

&НаСервере
Функция РезультатРедактированияВычетов()
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Вычеты, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");
	Возврат ПоместитьВоВременноеХранилище(Вычеты.Выгрузить());
	
КонецФункции

#КонецОбласти

