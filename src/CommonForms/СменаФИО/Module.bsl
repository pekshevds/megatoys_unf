#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Фамилия = Параметры.Фамилия;
	Имя = Параметры.Имя;
	Отчество = Параметры.Отчество;
	Инициалы = Параметры.Инициалы;
	ДатаИзменения = Параметры.ДатаИзменения;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	
	ПриИзмененииИмениОтчества();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)
	
	ПриИзмененииИмениОтчества();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ВозвращаемоеЗначение = НеОпределено;
	Модифицированность = Ложь;
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаИзменения) Тогда
		ДатаИзменения = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Фамилия", Фамилия);
	ВозвращаемоеЗначение.Вставить("Имя", Имя);
	ВозвращаемоеЗначение.Вставить("Отчество", Отчество);
	ВозвращаемоеЗначение.Вставить("Инициалы", Инициалы);
	ВозвращаемоеЗначение.Вставить("ДатаИзменения", ДатаИзменения);
	
	Модифицированность = Ложь;
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИмениОтчества()
	
	Если Не ПустаяСтрока(Имя) Тогда
		Инициалы = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыПоИмениОтчеству(Имя, Отчество);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
