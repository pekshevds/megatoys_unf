#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПоДокументам = Параметры.ТолькоПоДокументам;
	Организация = Параметры.Организация;
	СтруктурнаяЕдиница = Параметры.СтруктурнаяЕдиница;
	
	Если Параметры.Свойство("ТолькоПоОрдерам") Тогда
		ТолькоПоОрдерам = Параметры.ТолькоПоОрдерам;
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		Контрагент = Параметры.Контрагент;
	КонецЕсли;
	
	Если Не ТолькоПоДокументам Тогда

		Номенклатура = Параметры.Номенклатура;
		Характеристика = Параметры.Характеристика;
		Партия = Параметры.Партия;

	Иначе
		
		Элементы.ГруппаОтборНоменклатура.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.ГруппаОтборКонтрагент.Видимость = ЗначениеЗаполнено(Контрагент);
	
	Если Параметры.Свойство("КПоступлению") Тогда
		КПоступлению = Параметры.КПоступлению;
	КонецЕсли;
	
	Если Не КПоступлению Тогда
		
		Если ТолькоПоОрдерам Тогда
			ЭтаФорма.Заголовок = НСтр("ru = 'Расходные складские ордера'");
		Иначе
			ЭтаФорма.Заголовок = НСтр("ru = 'Документы отгрузки'");
		КонецЕсли;
		
	Иначе
		
		Если ТолькоПоОрдерам Тогда
			ЭтаФорма.Заголовок = НСтр("ru = 'Приходные складские ордера'");
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьОстаткиПоДокументам();
	
	Элементы.ГруппаОтборы.Доступность = Ложь;
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, "Организация", "ГруппаОтборОрганизация", Организация, Строка(Организация));
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, "Контрагент", "ГруппаОтборКонтрагент", Контрагент, Строка(Контрагент));
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, "СтруктурнаяЕдиница", "ГруппаОтборСклад", СтруктурнаяЕдиница, Строка(СтруктурнаяЕдиница));
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, "Номенклатура", "ГруппаОтборНоменклатура", Номенклатура, Строка(Номенклатура));
	
	РаботаСОтборами.ОбновитьЭлементыМеток(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОстаткиПоДокументамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОстаткиПоДокументам.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
		
	ОповеститьОВыборе(ТекущиеДанные.ДокументОснование);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьОтборыНажатие(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
	
	Если Элементы.ФильтрыНастройкиИДопИнфо.Видимость Тогда
		Элементы.ПраваяПанель.Ширина = 28;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьОтборыНажатие(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.ОстаткиПоДокументам.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;

	ОповеститьОВыборе(ТекущиеДанные.ДокументОснование);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОстаткиПоДокументам()
	
	Если КПоступлению Тогда
		
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("ТолькоПоОрдерам", ТолькоПоОрдерам);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("ПоКонтрагенту", ЗначениеЗаполнено(Контрагент));
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
		
		Если ТолькоПоДокументам Тогда
			
			ОстаткиПоДокументам.ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование КАК ДокументОснование,
			|	СУММА(ВЫБОР
			|			КОГДА &ТолькоПоОрдерам
			|				ТОГДА ЗапасыКПоступлениюНаСкладыОстатки.КоличествоОстаток
			|			ИНАЧЕ -ЗапасыКПоступлениюНаСкладыОстатки.КоличествоОстаток
			|		КОНЕЦ) КАК Количество,
			|	ЕСТЬNULL(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОснования,
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата КАК Дата,
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Номер КАК Номер,
			|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """") КАК Комментарий,
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование КАК Ссылка
			|ПОМЕСТИТЬ Итог
			|ИЗ
			|	РегистрНакопления.ЗапасыКПоступлениюНаСклады.Остатки(
			|			,
			|			Организация = &Организация
			|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
			|				И ВЫБОР
			|					КОГДА &ТолькоПоОрдерам
			|						ТОГДА ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.ПриходныйОрдер)
			|					ИНАЧЕ НЕ ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.ПриходныйОрдер)
			|				КОНЕЦ
			|				И НЕ ДокументОснование = НЕОПРЕДЕЛЕНО
			|				И ВЫБОР
			|					КОГДА &ПоКонтрагенту
			|							И НЕ ДокументОснование.Контрагент ЕСТЬ NULL
			|						ТОГДА ДокументОснование.Контрагент = &Контрагент
			|					ИНАЧЕ ИСТИНА
			|				КОНЕЦ) КАК ЗапасыКПоступлениюНаСкладыОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование,
			|	ЕСТЬNULL(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)),
			|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """"),
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата,
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Номер,
			|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Итог.ДокументОснование КАК ДокументОснование,
			|	Итог.Количество КАК Количество,
			|	Итог.ДатаОснования КАК ДатаОснования,
			|	Итог.Дата КАК Дата,
			|	Итог.Номер КАК Номер,
			|	Итог.Комментарий КАК Комментарий,
			|	Итог.ДокументОснование.Ссылка КАК Ссылка,
			|	ТИПЗНАЧЕНИЯ(Итог.ДокументОснование) КАК ТипДокумента
			|ИЗ
			|	Итог КАК Итог";
			
			Возврат;
			
		КонецЕсли;
		
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Номенклатура", Номенклатура);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Характеристика", Характеристика);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Партия", Партия);

		ОстаткиПоДокументам.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование КАК ДокументОснование,
		|	СУММА(ВЫБОР
		|			КОГДА &ТолькоПоОрдерам
		|				ТОГДА ЗапасыКПоступлениюНаСкладыОстатки.КоличествоОстаток
		|			ИНАЧЕ -ЗапасыКПоступлениюНаСкладыОстатки.КоличествоОстаток
		|		КОНЕЦ) КАК Количество,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Характеристика КАК Характеристика,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОснования,
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Ссылка КАК Ссылка,
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Номер КАК Номер,
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата КАК Дата,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """") КАК Комментарий
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.ЗапасыКПоступлениюНаСклады.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Номенклатура = &Номенклатура
		|				И Характеристика = &Характеристика
		|				И Партия = &Партия
		|				И ВЫБОР
		|					КОГДА &ТолькоПоОрдерам
		|						ТОГДА ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.ПриходныйОрдер)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|				И НЕ ДокументОснование = НЕОПРЕДЕЛЕНО
		|				И ВЫБОР
		|					КОГДА &ПоКонтрагенту
		|							И НЕ ДокументОснование.Контрагент ЕСТЬ NULL
		|						ТОГДА ДокументОснование.Контрагент = &Контрагент
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ЗапасыКПоступлениюНаСкладыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Номенклатура,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Партия,
		|	ЗапасыКПоступлениюНаСкладыОстатки.Характеристика,
		|	ЕСТЬNULL(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)),
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Номер,
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Дата,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """"),
		|	ЗапасыКПоступлениюНаСкладыОстатки.ДокументОснование.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.ДокументОснование КАК ДокументОснование,
		|	Итог.Количество КАК Количество,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	Итог.ДатаОснования КАК ДатаОснования,
		|	Итог.Ссылка КАК Ссылка,
		|	Итог.Номер КАК Номер,
		|	Итог.Дата КАК Дата,
		|	Итог.Комментарий КАК Комментарий,
		|	ТИПЗНАЧЕНИЯ(Итог.ДокументОснование) КАК ТипДокумента
		|ИЗ
		|	Итог КАК Итог";
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ КПоступлению Тогда
		
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("ТолькоПоОрдерам", ТолькоПоОрдерам);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("ПоКонтрагенту", ЗначениеЗаполнено(Контрагент));
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
		
		Если ТолькоПоДокументам Тогда
			
			ОстаткиПоДокументам.ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование КАК ДокументОснование,
			|	СУММА(ВЫБОР
			|			КОГДА &ТолькоПоОрдерам
			|				ТОГДА -ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
			|			ИНАЧЕ ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
			|		КОНЕЦ) КАК Количество,
			|	ЕСТЬNULL(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОснования,
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата КАК Дата,
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Номер КАК Номер,
			|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """") КАК Комментарий,
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование КАК Ссылка
			|ПОМЕСТИТЬ Итог
			|ИЗ
			|	РегистрНакопления.ЗапасыКРасходуСоСкладов.Остатки(
			|			,
			|			Организация = &Организация
			|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
			|				И ВЫБОР
			|					КОГДА &ТолькоПоОрдерам
			|						ТОГДА ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.РасходныйОрдер)
			|					ИНАЧЕ НЕ ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.РасходныйОрдер)
			|				КОНЕЦ
			|				И НЕ ДокументОснование = НЕОПРЕДЕЛЕНО
			|				И ВЫБОР
			|					КОГДА &ПоКонтрагенту
			|							И НЕ ДокументОснование.Контрагент ЕСТЬ NULL
			|						ТОГДА ДокументОснование.Контрагент = &Контрагент
			|					ИНАЧЕ ИСТИНА
			|				КОНЕЦ) КАК ЗапасыКРасходуСоСкладовОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование,
			|	ЕСТЬNULL(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)),
			|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """"),
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата,
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Номер,
			|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Итог.ДокументОснование КАК ДокументОснование,
			|	Итог.Количество КАК Количество,
			|	Итог.ДатаОснования КАК ДатаОснования,
			|	Итог.Дата КАК Дата,
			|	Итог.Номер КАК Номер,
			|	Итог.Комментарий КАК Комментарий,
			|	Итог.ДокументОснование.Ссылка КАК Ссылка,
			|	ТИПЗНАЧЕНИЯ(Итог.ДокументОснование) КАК ТипДокумента
			|ИЗ
			|	Итог КАК Итог";
			
			Возврат;
			
		КонецЕсли;
		
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Номенклатура", Номенклатура);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Характеристика", Характеристика);
		ОстаткиПоДокументам.Параметры.УстановитьЗначениеПараметра("Партия", Партия);

		ОстаткиПоДокументам.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование КАК ДокументОснование,
		|	СУММА(ВЫБОР
		|			КОГДА &ТолькоПоОрдерам
		|				ТОГДА -ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
		|			ИНАЧЕ ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
		|		КОНЕЦ) КАК Количество,
		|	ЗапасыКРасходуСоСкладовОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыКРасходуСоСкладовОстатки.Характеристика КАК Характеристика,
		|	ЗапасыКРасходуСоСкладовОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОснования,
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Ссылка КАК Ссылка,
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Номер КАК Номер,
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата КАК Дата,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """") КАК Комментарий
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	РегистрНакопления.ЗапасыКРасходуСоСкладов.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Номенклатура = &Номенклатура
		|				И Характеристика = &Характеристика
		|				И Партия = &Партия
		|				И ВЫБОР
		|					КОГДА &ТолькоПоОрдерам
		|						ТОГДА ТИПЗНАЧЕНИЯ(ДокументОснование) = ТИП(Документ.РасходныйОрдер)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|				И НЕ ДокументОснование = НЕОПРЕДЕЛЕНО
		|				И ВЫБОР
		|					КОГДА &ПоКонтрагенту
		|							И НЕ ДокументОснование.Контрагент ЕСТЬ NULL
		|						ТОГДА ДокументОснование.Контрагент = &Контрагент
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ЗапасыКРасходуСоСкладовОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование,
		|	ЗапасыКРасходуСоСкладовОстатки.Номенклатура,
		|	ЗапасыКРасходуСоСкладовОстатки.Партия,
		|	ЗапасыКРасходуСоСкладовОстатки.Характеристика,
		|	ЕСТЬNULL(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата, ДАТАВРЕМЯ(1, 1, 1)),
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Номер,
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Дата,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Комментарий КАК СТРОКА(100)), """"),
		|	ЗапасыКРасходуСоСкладовОстатки.ДокументОснование.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.ДокументОснование КАК ДокументОснование,
		|	Итог.Количество КАК Количество,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Характеристика КАК Характеристика,
		|	Итог.Партия КАК Партия,
		|	Итог.ДатаОснования КАК ДатаОснования,
		|	Итог.Ссылка КАК Ссылка,
		|	Итог.Номер КАК Номер,
		|	Итог.Дата КАК Дата,
		|	Итог.Комментарий КАК Комментарий,
		|	ТИПЗНАЧЕНИЯ(Итог.ДокументОснование) КАК ТипДокумента
		|ИЗ
		|	Итог КАК Итог";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти