
#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущийЭлементЦвета; // Нужен для указания ТекущегоЭлемента

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Очищение кэша по данным формы
	КлючОбъекта = "ОбщаяФорма.ФормаВыбораЦветаУНФ/НастройкиОкна";
	ХранилищеСистемныхНастроек.Удалить(КлючОбъекта, "", ИмяПользователя());
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор;
	
	МассивЦветов = Неопределено;
	Параметры.Свойство("МассивЦветов", МассивЦветов);
	ПринятыйЦвет = Неопределено;
	Параметры.Свойство("Цвет", ПринятыйЦвет);
	
	СтандартныеНастройки = (МассивЦветов = Неопределено);
	
	УстановитьОтображениеЭлементов(СтандартныеНастройки);
	СоздатьСтраницуОсновныхЦветов(МассивЦветов);
	
	Если ПринятыйЦвет <> Неопределено Тогда
		Если ПринятыйЦвет.Вид = ВидЦвета.WebЦвет Тогда
			УстановитьRGBПоWebЦвету(ПринятыйЦвет);
		ИначеЕсли ПринятыйЦвет.Вид = ВидЦвета.ЭлементСтиля Тогда
			УстановитьRGBПоЦвету(РаботаСЦветомСервер.АбсолютныйЦвет(ПринятыйЦвет));
		Иначе
			УстановитьRGBПоЦвету(ПринятыйЦвет);
		КонецЕсли;
		
		ИзменитьАктивныйЦвет();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПринятыйЦвет = Новый Цвет(Красный, Зеленый, Синий);
	ЭтоОсновнойОттенок = Ложь; // ПринятыйЦвет - один из верхних основных цветов
	Для Каждого КнопкаЦвета Из Элементы.ОсновныеОттенки.ПодчиненныеЭлементы Цикл
		
		Если КнопкаЦвета.ЦветФона = ПринятыйЦвет Тогда
			ТекущийЭлементЦвета = КнопкаЦвета;
			ТекущийЭлемент = КнопкаЦвета;
			ЭтоОсновнойОттенок = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВидФормы = "СтандартныеНастройки" Тогда
		Если Не ЭтоОсновнойОттенок Тогда
			ВыделитьЦентральныйОттенок(); // Выделяем оттенок по центру, если его нет среди основных
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("СоздатьДополнительныеЭлементы", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДополнительныеЦвета(Команда)
	
	ОсновныеЭлементыВидимость = Элементы.ОсновныеЭлементы.Видимость;
	Если Не ОсновныеЭлементыВидимость Тогда
		ИзменитьАктивныйЦвет();
	КонецЕсли;
	
	Элементы.ОсновныеЭлементы.Видимость = Не ОсновныеЭлементыВидимость;
	Элементы.ДополнительныеЭлементы.Видимость = ОсновныеЭлементыВидимость;
	
	ОсновныеЭлементыВидимость = Не ОсновныеЭлементыВидимость;
	
	Если ОсновныеЭлементыВидимость Тогда
		Элементы.ДополнительныеЦвета.Заголовок = НСтр("ru = 'Дополнительные цвета'");
	Иначе
		Элементы.ДополнительныеЦвета.Заголовок = НСтр("ru = 'Назад'");
	КонецЕсли;
	
	// В веб-клиенте ТекущийЭлемент перезаписывается, поэтому нужна задержка перед его изменением
	#Если ВебКлиент Тогда
		ПодключитьОбработчикОжидания("ВыделитьАктивныйЭлементПослеПереключения", 0, Истина);
	#Иначе
		ВыделитьАктивныйЭлементПослеПереключения();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ИзмерениеЦветаПриИзменении(Элемент)
	
	ТекущийЭлементЦвета = Неопределено;
	ИзменитьАктивныйЦвет();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыборЦвета(Команда)
	
	ТекущийЭлементЦвета = Элементы[Команда.Имя];
	
	ВыбранныйЦвет = ТекущийЭлементЦвета.ЦветФона;
	
	Если ТипЗнч(ВыбранныйЦвет) = Тип("Строка") Тогда
		
		УстановитьRGBПоWebЦвету(ВыбранныйЦвет);
		
	ИначеЕсли ТипЗнч(ВыбранныйЦвет) = Тип("Цвет") Тогда
		
		Если ВыбранныйЦвет.Вид = ВидЦвета.WebЦвет Тогда
			УстановитьRGBПоWebЦвету(ВыбранныйЦвет);
		ИначеЕсли ВыбранныйЦвет.Вид = ВидЦвета.Абсолютный Тогда
			УстановитьRGBПоЦвету(ВыбранныйЦвет);
		Иначе
			Возврат;
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	// Изменяем отображение оттенков только на основной странице,
	// т.к. иначе веб-клиент отрабатывает некорректно
	Если Элементы.ОсновныеЭлементы.Видимость Тогда
		ИзменитьАктивныйЦвет();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(Новый Цвет(Красный, Зеленый, Синий));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыделитьАктивныйЭлементПослеПереключения()
	
	Если ТекущийЭлементЦвета = Неопределено Тогда
		
		Если Элементы.ОсновныеЭлементы.Видимость Тогда
			ВыделитьЦентральныйОттенок();
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	ТекущийЭлементЭтоОсновнойЦвет = Ложь;
	
	ПолученныйОсновнойЦвет = ОсновнойЦветИзГруппы(
		ТекущийЭлементЭтоОсновнойЦвет, Элементы.ОсновныеОттенки);
	
	Если Не ТекущийЭлементЭтоОсновнойЦвет Тогда
		ПолученныйОсновнойЦвет = ОсновнойЦветИзГруппы(
			ТекущийЭлементЭтоОсновнойЦвет, Элементы.ДополнительныеОттенки);
	КонецЕсли;
	
	Если Элементы.ОсновныеЭлементы.Видимость Тогда
		Если ТекущийЭлементЭтоОсновнойЦвет Тогда
			ТекущийЭлемент = ПолученныйОсновнойЦвет;
		Иначе
			ВыделитьЦентральныйОттенок();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если Не ТекущийЭлементЭтоОсновнойЦвет Тогда
		ТекущийЭлемент = ТекущийЭлементЦвета; // Активизация кнопки дополнительного цвета
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОсновнойЦветИзГруппы(ТекущийЭлементЭтоОсновнойЦвет, ЭлементыОттенки)
	
	Для Каждого КнопкаЦвета Из ЭлементыОттенки.ПодчиненныеЭлементы Цикл
		Если ТекущийЭлементЦвета <> КнопкаЦвета Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийЭлементЭтоОсновнойЦвет = Истина;
		Возврат ТекущийЭлементЦвета;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ВыделитьЦентральныйОттенок()
	
	ДополнительныеОттенки = Элементы.ДополнительныеОттенки.ПодчиненныеЭлементы;
	
	ИндексСерединыОттенков = Цел(ДополнительныеОттенки.Количество() / 2);
	ТекущийЭлемент = ДополнительныеОттенки.Получить(ИндексСерединыОттенков);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЭлементов(СтандартныеНастройки = Истина)
	
	// НеСтандартныеНастройки - настройки без отображения дополнительных оттенков и цветов и RGB
	ВидФормы = ?(СтандартныеНастройки, "СтандартныеНастройки", "НеСтандартныеНастройки");
	
	Элементы.ДополнительныеЭлементы.Видимость = Ложь;
	Элементы.ДополнительныеЦвета.Видимость = СтандартныеНастройки;
	Элементы.УстановкаRGB.Видимость = СтандартныеНастройки;
	Элементы.ОсновныеЭлементы.Высота = ?(СтандартныеНастройки, 11, 0);
	
	// Если не установить ширину хотя бы 27, то будут проблемы с веб-клиентом
	Ширина = ?(СтандартныеНастройки, 43, 27);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьАктивныйЦвет()
	
	Если ВидФормы = "СтандартныеНастройки" Тогда
		СоздатьДополнительныеОттенки();
	КонецЕсли;
	ПоменятьЦветПримера();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДополнительныеОттенки()
	
	Шаг = 2 / (КоличествоЦветов + 2); // Обход происходит слева и справа от цвета
	НомКнопки = 0;
	Для Сч = 1 По КоличествоЦветов Цикл
		
		НомКнопки = НомКнопки + 1;
		Процент = Мин(Шаг * Сч, 2);
		
		КрасныйВнутр = ЦветНовый(Красный, Процент);
		ЗеленыйВнутр = ЦветНовый(Зеленый, Процент);
		СинийВнутр = ЦветНовый(Синий, Процент);
		Если Сч >= КоличествоЦветов / 2 Тогда
			КрасныйВнутр = НормализованноеИзмерениеОттенка(КрасныйВнутр);
			ЗеленыйВнутр = НормализованноеИзмерениеОттенка(ЗеленыйВнутр);
			СинийВнутр = НормализованноеИзмерениеОттенка(СинийВнутр);
		КонецЕсли;
		
		ЦветФона = Новый Цвет(КрасныйВнутр, ЗеленыйВнутр, СинийВнутр);
		НоваяКнопкаЦвета(СтрШаблон("Оттенок%1", Строка(НомКнопки)), ЦветФона, Элементы.ДополнительныеОттенки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЦветНовый(ЦветВход, Процент)
	
	Если Процент < 1 Тогда
		Возврат Цел(ЦветВход * Процент);
	Иначе
		Если ЦветВход = 0 Тогда
			Возврат Цел(255 * (Процент - 1));
		Иначе
			Возврат Цел(ЦветВход + (255 - ЦветВход) * (Процент - 1));
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция НормализованноеИзмерениеОттенка(Измерение)
	
	Если Измерение <> 0 И Измерение <> 255 Тогда
		Возврат Измерение + 1;
	КонецЕсли;
	
	Возврат Измерение;
	
КонецФункции

&НаСервере
Процедура ПоменятьЦветПримера()
	
	ПоловинаСуммыИзмерений = 384;
	
	Если (Красный + Зеленый + Синий) < ПоловинаСуммыИзмерений Тогда
		ЦветОформленияПримера = Новый Цвет(255, 255, 255);
	Иначе
		ЦветОформленияПримера = Новый Цвет(0, 0, 0);
	КонецЕсли; 
	
	НашЦвет = Новый Цвет(Красный, Зеленый, Синий);
	
	Элементы.ПримерФона.ЦветТекста = ЦветОформленияПримера;
	Элементы.ПримерФона.ЦветФона = НашЦвет;
	
	Элементы.ПримерТекста.ЦветФона = ЦветОформленияПримера;
	Элементы.ПримерТекста.ЦветТекста = НашЦвет;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСтраницуОсновныхЦветов(МассивЦветов)
	
	Если МассивЦветов = Неопределено Тогда
		МассивОсновныхОттенков = РаботаСЦветомСервер.ОсновныеЦвета();
	Иначе
		МассивОсновныхОттенков = МассивЦветов;
	КонецЕсли;
	
	КоличествоЦветов = МассивОсновныхОттенков.Количество();
	
	ЭлементыОсновныеОттенки = Элементы.ОсновныеОттенки;
	Индекс = 0;
	Для Каждого ОсновнойОттенок Из МассивОсновныхОттенков Цикл
		
		НоваяКнопкаЦвета(СтрШаблон("Цвет%1", Строка(Индекс)), ОсновнойОттенок, ЭлементыОсновныеОттенки);
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	// Дополнительные оттенки создаются, если в форму не передан массив цветов
	Если МассивЦветов = Неопределено Тогда
		СоздатьДополнительныеОттенки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДополнительныеЭлементы()
	
	НазваниеЭлементаПереданногоЦвета = ДополнительныеЭлементыНаСервере();
	
	Если (НазваниеЭлементаПереданногоЦвета <> Неопределено)
		И (ТекущийЭлементЦвета = Неопределено) Тогда
		
		ТекущийЭлементЦвета = Элементы[НазваниеЭлементаПереданногоЦвета];
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращаемое значение:
// Строка - название переданного в форму дополнительного цвета
&НаСервере
Функция ДополнительныеЭлементыНаСервере()
	
	МассивWebЦветов = РаботаСЦветомСервер.ЦветаWebЦветов();
	
	ЭлементыГруппаДополнительныеЦвета = Элементы.ГруппаДополнительныеЦвета;
	ГруппировкаПодчиненныхЭлементовФормыГоризонтальнаяВсегда = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	КоличествоЦветовГоризонтально = 10;
	
	Индекс = 0;
	ГруппаЦвета = Неопределено;
	НазваниеЭлементаПереданногоЦвета = Неопределено;
	Для Каждого Цвет Из МассивWebЦветов Цикл
		
		Если Индекс % КоличествоЦветовГоризонтально = 0 Тогда
			
			НазваниеГруппы = СтрШаблон("ГруппаДопЦветов%1", Строка(Индекс));
			ГруппаЦвета = НоваяПростаяГруппа(
				НазваниеГруппы,
				ЭлементыГруппаДополнительныеЦвета,
				ГруппировкаПодчиненныхЭлементовФормыГоризонтальнаяВсегда);
				
		КонецЕсли;
		
		НазваниеКнопки = СтрШаблон("ДопЦвет%1", Строка(Индекс));
		НоваяКнопкаЦвета(НазваниеКнопки, Цвет, ГруппаЦвета);
		
		Если Цвет.Красный = Красный И Цвет.Синий = Синий И Цвет.Зеленый = Зеленый Тогда
			НазваниеЭлементаПереданногоЦвета = СтрШаблон("ДопЦвет%1", Строка(Индекс));
		КонецЕсли;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	Возврат НазваниеЭлементаПереданногоЦвета;
	
КонецФункции

&НаСервере
Функция НоваяПростаяГруппа(Название, Родитель, Группировка, РастягиватьПоГоризонтали = Ложь, ЦветФона = Неопределено)
	
	ГруппаОттенков = Элементы.Добавить(Название, Тип("ГруппаФормы"), Родитель);
	ГруппаОттенков.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОттенков.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОттенков.ОтображатьЗаголовок = Ложь;
	ГруппаОттенков.Группировка = Группировка;
	ГруппаОттенков.РастягиватьПоГоризонтали = РастягиватьПоГоризонтали;
	
	Если ЦветФона <> Неопределено Тогда
		ГруппаОттенков.ЦветФона = ЦветФона;
	КонецЕсли;
	
	Возврат ГруппаОттенков;
	
КонецФункции

&НаСервере
Функция НоваяКнопкаЦвета(Название, Цвет, ГруппаФормы)
	
	Если Элементы.Найти(Название) = Неопределено Тогда
		НоваяКоманда = Команды.Добавить(Название);
		НоваяКоманда.Действие = "ВыборЦвета";
		НоваяКоманда.Заголовок = НСтр("ru = 'Выбор цвета'");
		НоваяКоманда.ИзменяетСохраняемыеДанные = Ложь;
		НоваяКоманда.Отображение = ОтображениеКнопки.Авто;
		НоваяКоманда.Подсказка = НСтр("ru = 'Выбор цвета'");
		
		НовыйЭлемент = Элементы.Вставить(Название, Тип("КнопкаФормы"), ГруппаФормы, Неопределено);
		НовыйЭлемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		НовыйЭлемент.Заголовок = " ";
		НовыйЭлемент.ИмяКоманды = Название;
		НовыйЭлемент.ЦветФона = Цвет;
		НовыйЭлемент.АктивизироватьПоУмолчанию = Ложь;
		НовыйЭлемент.Пометка = Ложь;
		
		Возврат НовыйЭлемент;
	КонецЕсли;
	
	Элемент = Элементы.Найти(Название);
	Элемент.ЦветФона = Цвет;
	Возврат Элемент;
	
КонецФункции

&НаСервере
Процедура УстановитьRGBПоЦвету(Цвет)
	
	Красный = Цвет.Красный;
	Зеленый = Цвет.Зеленый;
	Синий = Цвет.Синий;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьRGBПоWebЦвету(ВыбранныйЦвет)
	
	СтруктураRGBЦвета = РаботаСЦветомСервер.RGBПоWebЦвету(ВыбранныйЦвет);
	
	Красный = СтруктураRGBЦвета.Красный;
	Зеленый = СтруктураRGBЦвета.Зеленый;
	Синий = СтруктураRGBЦвета.Синий;
	
КонецПроцедуры

#КонецОбласти