
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой();
	УстановитьЗаголовкиКомандВЗависимостиОтОпций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда 
		Прочитать();
		УстановитьЗаголовкиКомандВЗависимостиОтОпций();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьХранилищеШаблонов(Команда)
	Если ИспользоватьСтаруюОбработкуПечатиЦенников Тогда
		ОткрытьФорму("Справочник.ХранилищеШаблонов.ФормаСписка", , ЭтотОбъект); 
	Иначе
		ОткрытьФорму("Справочник.ШаблоныЭтикетокИЦенниковБПО.ФормаСписка", , ЭтотОбъект);
	КонецЕсли;;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	УстановитьЗаголовкиКомандВЗависимостиОтОпций();
	Элементы.РазделПроизводство.Видимость = Константы.ФункциональнаяОпцияИспользоватьПодсистемуПроизводство.Получить();
	Элементы.РазделРаботы.Видимость = Константы.ФункциональнаяОпцияИспользоватьПодсистемуРаботы.Получить();
	Элементы.РазделПерсонал.Видимость = Константы.ФункциональнаяОпцияИспользоватьПодсистемуЗарплата.Получить();
	ИспользоватьСтаруюОбработкуПечатиЦенников = ПолучитьФункциональнуюОпцию("ИспользоватьСтаруюОбработкуПечатиЦенников");
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиКомандВЗависимостиОтОпций()
	
	Если НаборКонстант.ФункциональнаяОпцияИспользоватьПодсистемуРаботы Тогда
		ЗаголовокСостоянияСобытий = НСтр("ru='Состояния событий и заданий'");
	Иначе
		ЗаголовокСостоянияСобытий = НСтр("ru='Состояния событий'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СправочникСостоянияСобытийОткрытьСписок", 
			"Заголовок",
			ЗаголовокСостоянияСобытий);
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СправочникСостоянияСобытийОткрытьСписок1", 
			"Заголовок",
			ЗаголовокСостоянияСобытий);		

	
	Если НаборКонстант.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности Тогда
		ЗаголовокНаправленияДеятельности = НСтр("ru='Направления деятельности'");
	Иначе
		ЗаголовокНаправленияДеятельности = НСтр("ru='Направление деятельности'");
	КонецЕсли;

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СправочникНаправленияДеятельностиОткрытьСписок", 
			"Заголовок",
			ЗаголовокНаправленияДеятельности);
	
	Если НаборКонстант.ИспользоватьПодсистемуCRM Тогда
		ЗаголовокГруппыCRM = НСтр("ru='CRM'");
	Иначе
		ЗаголовокГруппыCRM = НСтр("ru='Покупатели'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"РазделCRM", 
			"Заголовок",
			ЗаголовокГруппыCRM);
	
КонецПроцедуры

#КонецОбласти