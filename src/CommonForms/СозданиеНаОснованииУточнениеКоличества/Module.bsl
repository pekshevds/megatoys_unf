#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)    
	
	Если ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьТоварыИзЗаказаПокупателя();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	Результат = Новый Структура("АдресТовары,Основание,КоличествоИзделий");
	СтруктураВозврата = АдресТовары();
	Результат.АдресТовары 		= СтруктураВозврата.АдресТовары;
	Результат.Основание 		= Параметры.Основание;
	Результат.КоличествоИзделий = СтруктураВозврата.КоличествоИзделий;
	Закрыть(Результат);
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТоварыИзЗаказаПокупателя()
	
	Элементы.ТоварыСклад.Видимость 	= Параметры.Основание.ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	Элементы.ТоварыЯчейка.Видимость = ПолучитьФункциональнуюОпцию("УчетПоЯчейкам");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.Характеристика КАК Характеристика,
	               |	Товары.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв,
	               |	Товары.Ячейка КАК Ячейка,
	               |	СУММА(Товары.Количество) КАК КоличествоПоДокументу,
	               |	КомплектацииПоУмолчанию.Комплектация КАК Комплектация,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	МАКСИМУМ(ИСТИНА) КАК Отметка
	               |ИЗ
	               |	Документ.ЗаказПокупателя.Запасы КАК Товары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КомплектацииПоУмолчанию КАК КомплектацииПоУмолчанию
	               |		ПО Товары.Номенклатура = КомплектацииПоУмолчанию.Номенклатура
	               |			И Товары.Характеристика = КомплектацииПоУмолчанию.Характеристика
	               |ГДЕ
	               |	Товары.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	КомплектацииПоУмолчанию.Комплектация,
	               |	Товары.Ячейка,
	               |	Товары.Характеристика,
	               |	Товары.СтруктурнаяЕдиницаРезерв,
	               |	Товары.ЕдиницаИзмерения,
	               |	Товары.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Параметры.Основание);
	ТаблицаТовары = Запрос.Выполнить().Выгрузить();
	Товары.Загрузить(ТаблицаТовары);
	//
	
КонецПроцедуры  

&НаСервере
Функция АдресТовары()
	
	ОтмеченныеЗапасы = Товары.Выгрузить(Новый Структура("Отметка", Истина));
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка,
	|	ЗаказПокупателя.ОсновнойВариантКП КАК ОсновнойВариантКП
	|ПОМЕСТИТЬ ВТЗаказы
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ЗаказПокупателяЗапасы.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТМаксНомерСтрокиЗапасы
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	ЗаказПокупателяЗапасы.Ссылка = &Ссылки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПокупателяЗапасы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.Ссылка КАК Ссылка,
	|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.НомерВариантаКП КАК НомерВариантаКП,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.ТипНоменклатурыЗапас КАК ТипНоменклатурыЗапас,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ЗаказПокупателяЗапасы.Резерв КАК Резерв,
	|	ЗаказПокупателяЗапасы.РезервОтгрузка КАК РезервОтгрузка,
	|	ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв,
	|	ЗаказПокупателяЗапасы.Ячейка КАК Ячейка,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА ВЫРАЗИТЬ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяЗапасы.Цена КАК Цена,
	|	ЗаказПокупателяЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.ПродажаПодарка КАК ПродажаПодарка,
	|	ЗаказПокупателяЗапасы.Сумма КАК Сумма,
	|	ЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПокупателяЗапасы.СуммаНДС КАК СуммаНДС,
	|	ЗаказПокупателяЗапасы.Всего КАК Всего,
	|	ЗаказПокупателяЗапасы.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ЗаказПокупателяЗапасы.Содержание КАК Содержание,
	|	ЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиОплатыБонусом КАК СуммаСкидкиОплатыБонусом,
	|	ЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.ТипНоменклатурыЗапас
	|			ТОГДА ЗаказПокупателяЗапасы.КоличествоСобрано
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Количество
	|	КОНЕЦ КАК КоличествоСобрано,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияИспользоватьСборкуЗаказов.Значение
	|			ТОГДА ЗаказПокупателяЗапасы.Ссылка.СтатусСборки > 0
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьСборка,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ЗаказПокупателяЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗаказПокупателяЗапасы.НомерГТД КАК НомерГТД,
	|	ЗаказПокупателяЗапасы.Вес КАК Вес,
	|	ЗаказПокупателяЗапасы.Объем КАК Объем,
	|	ЗаказПокупателяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ЗаказПокупателяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЗаказПокупателяЗапасы.ДоляСтоимости КАК ДоляСтоимости,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
	|ПОМЕСТИТЬ ВТЗаказПокупателяЗапасы
	|ИЗ
	|	Документ.ЗаказПокупателя.Запасы КАК ЗаказПокупателяЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаказы КАК Заказы
	|		ПО (Заказы.Ссылка = ЗаказПокупателяЗапасы.Ссылка)
	|			И (Заказы.ОсновнойВариантКП = ЗаказПокупателяЗапасы.НомерВариантаКП),
	|	Константа.ФункциональнаяОпцияИспользоватьСборкуЗаказов КАК ФункциональнаяОпцияИспользоватьСборкуЗаказов
	|ГДЕ
	|	НЕ ЗаказПокупателяЗапасы.ЭтоРазделитель
	|	И ЗаказПокупателяЗапасы.Номенклатура В(&Запасы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка,
	|	ЕСТЬNULL(ВТМаксНомерСтрокиЗапасы.НомерСтроки, 0) + 1,
	|	Заказы.ОсновнойВариантКП,
	|	ЗаказПокупателя.НоменклатураДоставки,
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка),
	|	"""",
	|	0,
	|	0,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
	|	NULL,
	|	ЗаказПокупателя.НоменклатураДоставки.ЕдиницаИзмерения,
	|	1,
	|	ЗаказПокупателя.СтоимостьДоставки,
	|	0,
	|	NULL,
	|	ЛОЖЬ,
	|	ЗаказПокупателя.СтоимостьДоставки,
	|	ЗаказПокупателя.СтавкаНДСДоставки,
	|	ЗаказПокупателя.СуммаНДСДоставки,
	|	ЗаказПокупателя.СтоимостьДоставки + ВЫБОР
	|		КОГДА ЗаказПокупателя.СуммаВключаетНДС
	|			ТОГДА 0
	|		ИНАЧЕ ЗаказПокупателя.СуммаНДСДоставки
	|	КОНЕЦ,
	|	ЗаказПокупателя.ДатаОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
	|	"""",
	|	0,
	|	0,
	|	0,
	|	1,
	|	1,
	|	ЛОЖЬ,
	|	0,
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
	|	0,
	|	0,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	0,
	|	NULL
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаказы КАК Заказы
	|		ПО (Заказы.Ссылка = ЗаказПокупателя.Ссылка)
	|			И (ЗаказПокупателя.НоменклатураДоставки <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (ЗаказПокупателя.СпособДоставки <> ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка))
	|			И (ЗаказПокупателя.СпособДоставки <> ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.Самовывоз))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМаксНомерСтрокиЗапасы КАК ВТМаксНомерСтрокиЗапасы
	|		ПО (ВТМаксНомерСтрокиЗапасы.Ссылка = ЗаказПокупателя.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя,
	|	ВТЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ВТЗаказПокупателяЗапасы.НомерВариантаКП КАК НомерВариантаКП,
	|	ВТЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ВТЗаказПокупателяЗапасы.ТипНоменклатурыЗапас КАК ТипНоменклатурыЗапас,
	|	ВТЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ВТЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ВТЗаказПокупателяЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ВТЗаказПокупателяЗапасы.Резерв КАК Резерв,
	|	ВТЗаказПокупателяЗапасы.РезервОтгрузка КАК РезервОтгрузка,
	|	ВТЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв КАК СтруктурнаяЕдиницаРезерв,
	|	ВТЗаказПокупателяЗапасы.Ячейка КАК Ячейка,
	|	ВТЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТЗаказПокупателяЗапасы.Коэффициент КАК Коэффициент,
	|	ВТЗаказПокупателяЗапасы.Цена КАК Цена,
	|	ВТЗаказПокупателяЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ВТЗаказПокупателяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	ВТЗаказПокупателяЗапасы.ПродажаПодарка КАК ПродажаПодарка,
	|	ВТЗаказПокупателяЗапасы.Сумма КАК Сумма,
	|	ВТЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВТЗаказПокупателяЗапасы.СуммаНДС КАК СуммаНДС,
	|	ВТЗаказПокупателяЗапасы.Всего КАК Всего,
	|	ВТЗаказПокупателяЗапасы.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВТЗаказПокупателяЗапасы.Спецификация КАК Спецификация,
	|	ВТЗаказПокупателяЗапасы.Содержание КАК Содержание,
	|	ВТЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ВТЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ВТЗаказПокупателяЗапасы.СуммаСкидкиОплатыБонусом КАК СуммаСкидкиОплатыБонусом,
	|	ВТЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ВТЗаказПокупателяЗапасы.КоличествоСобрано КАК КоличествоСобрано,
	|	ВТЗаказПокупателяЗапасы.ЕстьСборка КАК ЕстьСборка,
	|	ВТЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ВТЗаказПокупателяЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТЗаказПокупателяЗапасы.НомерГТД КАК НомерГТД,
	|	ВТЗаказПокупателяЗапасы.Вес КАК Вес,
	|	ВТЗаказПокупателяЗапасы.Объем КАК Объем,
	|	ВТЗаказПокупателяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ВТЗаказПокупателяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ВТЗаказПокупателяЗапасы.ДоляСтоимости КАК ДоляСтоимости,
	|	ВТЗаказПокупателяЗапасы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	КомплектацииПоУмолчанию.Комплектация КАК Комплектация
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ВТЗаказПокупателяЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КомплектацииПоУмолчанию КАК КомплектацииПоУмолчанию
	|		ПО ВТЗаказПокупателяЗапасы.Номенклатура = КомплектацииПоУмолчанию.Номенклатура
	|			И ВТЗаказПокупателяЗапасы.Характеристика = КомплектацииПоУмолчанию.Характеристика";
	
	Запрос.УстановитьПараметр("Ссылки", Параметры.Основание);
	Запрос.УстановитьПараметр("Запасы", ОтмеченныеЗапасы);  
	
	Результаты 				= Запрос.Выполнить();
	ТаблицаТоваров 			= Результаты.Выгрузить();
	КоличествоИзделий		= ОтмеченныеЗапасы.Количество();
	АдресТовары 			= ПоместитьВоВременноеХранилище(ТаблицаТоваров);
	Возврат Новый Структура("АдресТовары,КоличествоИзделий", АдресТовары, КоличествоИзделий);  
КонецФункции

&НаКлиенте
Процедура Отметить(Команда)
ЗаполнитьОтметки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	ЗаполнитьОтметки(ложь);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтметки(ЗначениеОтметки)
	
	ТаблицаЗаполняемыхЗначений = Товары.Выгрузить();
	ТаблицаЗаполняемыхЗначений.ЗаполнитьЗначения(ЗначениеОтметки, "Отметка");
	Товары.Загрузить(ТаблицаЗаполняемыхЗначений);
	
КонецПроцедуры

#КонецОбласти

