
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	СписокПолучателей = Параметры.Получатели;
	ЗаполнитьТаблицуПолучателейИзМассиваСтруктур(СписокПолучателей);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
	КонецЕсли;
		
	// Если есть только один номер, то установим флаг в строке с этим адресом.
	ЗаполненоНомеров = 0;
	СтрокаСНомером = Неопределено;
	Для Каждого Получатель Из Получатели Цикл
		Если НЕ ПустаяСтрока(Получатель.ПредставлениеНомера) Тогда
			СтрокаСНомером = Получатель;
			ЗаполненоНомеров = ЗаполненоНомеров + 1;
		КонецЕсли;
	КонецЦикла;
	Если ЗаполненоНомеров = 1 Тогда
		СтрокаСНомером.Выбран = Истина;
	КонецЕсли;
	// УНФ. Конец
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолучатели

&НаКлиенте
Процедура ПолучателиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	Выбран = Не Элементы.Получатели.ТекущиеДанные.Выбран;
	Для Каждого ВыделеннаяСтрока Из Элементы.Получатели.ВыделенныеСтроки Цикл
		Получатель = Получатели.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Получатель.Выбран = Выбран;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть();
	ОтправкаSMSКлиент.ОтправитьSMS(ВыбранныеПолучатели(), "", Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсехПолучателей(Команда)
	УстановитьВыборДляВсехПолучателей(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборДляВсех(Команда)
	УстановитьВыборДляВсехПолучателей(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуПолучателейИзМассиваСтруктур(СписокПолучателей)
	
	Для Каждого Получатель Из СписокПолучателей Цикл
		НовыйПолучатель = Получатели.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйПолучатель, Получатель);
		НовыйПолучатель.ПредставлениеНомера = Получатель.Телефон;
		НовыйПолучатель.Телефон = Получатель.Телефон;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ВыбранныеПолучатели()
	Результат = Новый Массив;
	Для Каждого ВыбранныйПолучатель Из Получатели Цикл
		Если ВыбранныйПолучатель.Выбран Тогда
			СтруктураПолучателя = Новый Структура("Телефон,Представление,ИсточникКонтактнойИнформации");
			ЗаполнитьЗначенияСвойств(СтруктураПолучателя, ВыбранныйПолучатель);
			Результат.Добавить(СтруктураПолучателя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура УстановитьВыборДляВсехПолучателей(Выбор)
	Для Каждого Получатель Из Получатели Цикл
		Получатель.Выбран = Выбор;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
