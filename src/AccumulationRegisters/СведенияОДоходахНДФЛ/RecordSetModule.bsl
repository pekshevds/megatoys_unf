#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Если ПривилегированныйРежим() Тогда
		
		// Проверим даты получения дохода.
		ПараметрыРасчета = УчетНДФЛ.ПараметрыРасчетаНДФЛПоПрогрессивнойШкале();
		НачалоРасчетаНДФЛПоПрогрессивнойШкале = ПараметрыРасчета.НачалоРасчетаНДФЛПоПрогрессивнойШкале;
		ГодОбъединенияНалоговыхБаз = ПараметрыРасчета.ГодОбъединенияНалоговыхБаз;
		ПерваяСтрокаНабора = ЭтотОбъект[0];
		НачалоПериода = ПерваяСтрокаНабора.ДатаПолученияДохода;
		КонецПериода = ПерваяСтрокаНабора.ДатаПолученияДохода;
		ФизическиеЛица = Новый Массив;
		Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
			НачалоПериода = Мин(НачалоПериода, СтрокаНабора.ДатаПолученияДохода);
			КонецПериода = Макс(КонецПериода, СтрокаНабора.ДатаПолученияДохода);
			Если СтрокаНабора.ДатаПолученияДохода >= НачалоРасчетаНДФЛПоПрогрессивнойШкале Тогда
				ФизическиеЛица.Добавить(СтрокаНабора.ФизическоеЛицо)
			КонецЕсли;
		КонецЦикла;
		
		// Если доходы относятся еще к 2020 году или ранее, проверять доходы не требуется.
		Если КонецПериода < НачалоРасчетаНДФЛПоПрогрессивнойШкале Тогда
			Возврат;
		КонецЕсли;
		
		// Проверим состояние учетной политики.
		ГоловнаяОрганизация = ПерваяСтрокаНабора.ГоловнаяОрганизация;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаПоНДФЛ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	УчетнаяПолитикаПоНДФЛ.ВыполнятьРасчетНДФЛПоПрогрессивнойШкале КАК ВыполнятьРасчетНДФЛПоПрогрессивнойШкале
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаПоНДФЛ КАК УчетнаяПолитикаПоНДФЛ
		|ГДЕ
		|	УчетнаяПолитикаПоНДФЛ.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|	И УчетнаяПолитикаПоНДФЛ.ВыполнятьРасчетНДФЛПоПрогрессивнойШкале";
		// Если флажок расчета по прогрессивной шкале взведен, проверять доходы не требуется.
		Если Не Запрос.Выполнить().Пустой() Тогда 
			Возврат;
		КонецЕсли;
		
		НачалоПериода = ДобавитьМесяц(Макс(НачалоГода(НачалоПериода), НачалоРасчетаНДФЛПоПрогрессивнойШкале), -3);
		КонецПериода = КонецГода(КонецПериода);
		Запрос.УстановитьПараметр("НачалоРасчетаНДФЛПоПрогрессивнойШкале", НачалоРасчетаНДФЛПоПрогрессивнойШкале);
		Запрос.УстановитьПараметр("ГодОбъединенияНалоговыхБаз", ГодОбъединенияНалоговыхБаз);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
		Запрос.УстановитьПараметр("Предел", ПараметрыРасчета.Предел);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.ДатаПолученияДохода, ГОД) КАК НалоговыйПериод,
		|	ВЫБОР
		|		КОГДА ГОД(СведенияОДоходахНДФЛ.ДатаПолученияДохода) >= &ГодОбъединенияНалоговыхБаз
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
		|		КОГДА СведенияОДоходахНДФЛ.КатегорияДохода В (ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыОтДолевогоУчастия), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыПоВыигрышам), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыЦБ), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыРЕПО), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыЗаймЦБ), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыИИС), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыПоОблигациям))
		|			ТОГДА СведенияОДоходахНДФЛ.КатегорияДохода
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
		|	КОНЕЦ КАК НалоговаяБаза
		|ИЗ
		|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
		|ГДЕ
		|	СведенияОДоходахНДФЛ.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|	И СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)
		|	И СведенияОДоходахНДФЛ.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ СведенияОДоходахНДФЛ.КатегорияДохода В (ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДивидендыПоСтавке05), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДивидендыПоСтавке10), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ДивидендыПоСтавке12), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПоСтавке03), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке05), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПоСтавке06), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке07), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке10), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.АвторскиеРоялтиПроцентыПоСтавке15), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыПоСтавкеПункта11Статьи224НКРФ))
		|	И (СведенияОДоходахНДФЛ.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ИЛИ СведенияОДоходахНДФЛ.КатегорияДохода = ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды))
		|	И СведенияОДоходахНДФЛ.ДатаПолученияДохода >= &НачалоРасчетаНДФЛПоПрогрессивнойШкале
		|	И &УсловиеОблагаемостиУНалоговогоАгента
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА ГОД(СведенияОДоходахНДФЛ.ДатаПолученияДохода) >= &ГодОбъединенияНалоговыхБаз
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
		|		КОГДА СведенияОДоходахНДФЛ.КатегорияДохода В (ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.Дивиденды), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыОтДолевогоУчастия), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыПоВыигрышам), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыЦБ), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыРЕПО), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыЗаймЦБ), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыИИС), ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПрочиеДоходыПоОблигациям))
		|			ТОГДА СведенияОДоходахНДФЛ.КатегорияДохода
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.КатегорииДоходовНДФЛ.ПустаяСсылка)
		|	КОНЕЦ,
		|	НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.ДатаПолученияДохода, ГОД)
		|
		|ИМЕЮЩИЕ
		|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода - СведенияОДоходахНДФЛ.СуммаВычета) >= &Предел";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОблагаемостиУНалоговогоАгента", УчетНДФЛ.УсловиеОблагаемостиУНалоговогоАгента());
		Если Не Запрос.Выполнить().Пустой() Тогда
			УчетнаяПолитикаПоНДФЛЗначение = РегистрыСведений.УчетнаяПолитикаПоНДФЛ.СоздатьНаборЗаписей();
			УчетнаяПолитикаПоНДФЛЗначение.Отбор.ГоловнаяОрганизация.Установить(ГоловнаяОрганизация);
			УчетнаяПолитикаПоНДФЛЗначение.Прочитать();
			Если УчетнаяПолитикаПоНДФЛЗначение.Количество() = 0 Тогда
				Запись = УчетнаяПолитикаПоНДФЛЗначение.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, РегистрыСведений.УчетнаяПолитикаПоНДФЛ.Умолчания());
				Запись.ГоловнаяОрганизация = ГоловнаяОрганизация;
			Иначе
				Запись = УчетнаяПолитикаПоНДФЛЗначение[0] 
			КонецЕсли;
			Запись.ВыполнятьРасчетНДФЛПоПрогрессивнойШкале = Истина;
			УчетнаяПолитикаПоНДФЛЗначение.Записать();
		КонецЕсли;;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли