
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСвойстваЗадания();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = Элементы.ЗавершеноСОшибкой.Имя Тогда
		ОткрытьФорму(
			"РегистрСведений.ОчередьЗадачАссистентаУправления.Форма.ОшибкаВыполнения",
			Новый Структура("Ключ", ВыбраннаяСтрока));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОчередь(Команда)
	
	ВыполнитьОчередьНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОчередьСообщений(Команда)
	
	ВыполнитьОчередьСообщенийНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОчередь(Команда)
	
	Элементы.Список.Обновить();
	ЗаполнитьСвойстваЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьКоличествоПопыток(Команда)
	
	ВыделенныеСтроки = ВыделенныеСтроки();
	СброситьКоличествоПопытокУСтрок(ВыделенныеСтроки);
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСвойстваЗадания()
	
	Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
		ЗаполнитьСвойстваЗаданияРежимСервиса();
	Иначе
		ЗаполнитьСвойстваЗаданияРежимЛокальный();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваЗаданияРежимСервиса()
	
	ЗаданиеАссистента = ЗаданиеАссистента();
	Если НЕ ЗначениеЗаполнено(ЗаданиеАссистента) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданиеВключено = ЗаданиеАссистента.Использование;
	СостояниеЗадания = ЗаданиеАссистента.СостояниеЗадания;
	ЧасовойПояс = ЧасовойПоясСеанса();
	ЗапланированныйМоментЗапускаЗадания = МестноеВремя(
		ЗаданиеАссистента.Идентификатор.ЗапланированныйМоментЗапуска, ЧасовойПояс);
	ДатаЗавершенияПоследнегоЗапуска = МестноеВремя(
		ЗаданиеАссистента.Идентификатор.ДатаЗавершенияПоследнегоЗапуска, ЧасовойПояс);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваЗаданияРежимЛокальный()
	
	Элементы.ЗапланированныйМоментЗапускаЗадания.Видимость = Ложь;
	
	ЗаданиеАссистента = ЗаданиеАссистента();
	Если ЗаданиеАссистента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданиеВключено = ЗаданиеАссистента.Использование;
	
	Если ЗаданиеАссистента.ПоследнееЗадание = Неопределено Тогда
		СостояниеЗадания = НСтр("ru='Не определено'");
	Иначе
		СостояниеЗадания = ЗаданиеАссистента.ПоследнееЗадание.Состояние;
		ДатаЗавершенияПоследнегоЗапуска = ЗаданиеАссистента.ПоследнееЗадание.Конец;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОчередьНаСервере()
	
	АссистентУправления.ВыполнитьТекущиеЗадачиСейчас();
	ЗаполнитьСвойстваЗадания();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОчередьСообщенийНаСервере()
	
	ОбсужденияУНФ.ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия();
	
КонецПроцедуры

&НаСервере
Функция ЗаданиеАссистента()
	
	Возврат РегламентныеЗаданияСервер.Задание(Метаданные.РегламентныеЗадания.ВыполнениеЗадачАссистентаУправления);
	
КонецФункции

&НаКлиенте
Функция ВыделенныеСтроки()
	
	ВыделенныеСтроки = Новый Массив;
	Для каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		ТекущаяСтрока = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		Если ТекущаяСтрока <> Неопределено Тогда
			ОписаниеСтроки = Новый Структура;
			ОписаниеСтроки.Вставить("Предмет", ТекущаяСтрока.Предмет);
			ОписаниеСтроки.Вставить("Задача", ТекущаяСтрока.Задача);
			ОписаниеСтроки.Вставить("Событие", ТекущаяСтрока.Событие);
			ОписаниеСтроки.Вставить("Источник", ТекущаяСтрока.Источник);
			ВыделенныеСтроки.Добавить(ОписаниеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВыделенныеСтроки;
	
КонецФункции

&НаСервере
Процедура СброситьКоличествоПопытокУСтрок(ВыделенныеСтроки)
	
	Для каждого Строка Из ВыделенныеСтроки Цикл
		МенеджерЗаписи = РегистрыСведений.ОчередьЗадачАссистентаУправления.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Предмет = Строка.Предмет;
		МенеджерЗаписи.Задача = Строка.Задача;
		МенеджерЗаписи.Событие = Строка.Событие;
		МенеджерЗаписи.Источник = Строка.Источник;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() И МенеджерЗаписи.КоличествоПопыток <> 0 Тогда
			МенеджерЗаписи.КоличествоПопыток = 0;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
