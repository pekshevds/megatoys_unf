#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает данные, записанные в регистре сведений НастройкиСинхронизацииСВнешнимиКалендарями.
// 
// Параметры:
//  КалендарьСотрудника - СправочникСсылка.КалендариСотрудников - ссылка на на запись в справочнике КалендариСотрудников
// 
// Возвращаемое значение:
//  Структура - данные регистра НастройкиСинхронизацииСВнешнимиКалендарями
//
Функция НайтиПоКалендарюСотрудника(КалендарьСотрудника) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиСинхронизацииСВнешнимиКалендарями.КодВнешнегоКалендаря КАК КодВнешнегоКалендаря,
	|	НастройкиСинхронизацииСВнешнимиКалендарями.УчетнаяЗаписьВнешнегоКалендаря КАК УчетнаяЗаписьВнешнегоКалендаря,
	|	НастройкиСинхронизацииСВнешнимиКалендарями.КалендарьDAVНаименование КАК КалендарьDAVНаименование,
	|	НастройкиСинхронизацииСВнешнимиКалендарями.КалендарьСотрудника КАК КалендарьСотрудника,
	|	НастройкиСинхронизацииСВнешнимиКалендарями.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.НастройкиСинхронизацииСВнешнимиКалендарями КАК НастройкиСинхронизацииСВнешнимиКалендарями
	|ГДЕ
	|	НастройкиСинхронизацииСВнешнимиКалендарями.КалендарьСотрудника = &КалендарьСотрудника");
	Запрос.УстановитьПараметр("КалендарьСотрудника", КалендарьСотрудника);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		НастройкаСинхронизации = Новый Структура();
		НастройкаСинхронизации.Вставить("КодВнешнегоКалендаря", Выборка.КодВнешнегоКалендаря);
		НастройкаСинхронизации.Вставить("НаименованиеВнешнегоКалендаря", Выборка.КалендарьDAVНаименование);
		НастройкаСинхронизации.Вставить("КалендарьСотрудника", Выборка.КалендарьСотрудника);
		НастройкаСинхронизации.Вставить("УчетнаяЗаписьВнешнегоКалендаря", Выборка.УчетнаяЗаписьВнешнегоКалендаря);
		НастройкаСинхронизации.Вставить("Статус", Выборка.Статус);
		Возврат НастройкаСинхронизации;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Удаление записи в регистре сведений НастройкиСинхронизацииСВнешнимиКалендарями
// 
// Параметры:
//  УчетнаяЗаписьВнешнегоКалендаря - СправочникСсылка.УчетныеЗаписиВнешнихКалендарей - ссылка на запись в справочнике УчетныеЗаписиВнешнихКалендарей
//
Процедура УдалитьНастройкиПоУчетнойЗаписиВнешнегоКалендаря(УчетнаяЗаписьВнешнегоКалендаря) Экспорт
	
	НаборЗаписей = РегистрыСведений.НастройкиСинхронизацииСВнешнимиКалендарями.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УчетнаяЗаписьВнешнегоКалендаря.Установить(УчетнаяЗаписьВнешнегоКалендаря);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли