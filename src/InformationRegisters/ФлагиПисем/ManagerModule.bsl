#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ФлагиПисьма(Событие) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФлагиПисем.Непрочитанное КАК Непрочитанное
	|ИЗ
	|	РегистрСведений.ФлагиПисем КАК ФлагиПисем
	|ГДЕ
	|	ФлагиПисем.Событие = &Событие";
	Запрос.УстановитьПараметр("Событие", Событие);
	
	ФлагиПисьма = Новый Структура;
	ФлагиПисьма.Вставить("Непрочитанное", Ложь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ФлагиПисьма;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ФлагиПисьма, Выборка);
	КонецЕсли;
	
	Возврат ФлагиПисьма;
	
КонецФункции

Процедура ИзменитьФлагиПисьма(УчетнаяЗапись, Событие, ФлагиПисьма, ЗагрузкаПочты = Ложь) Экспорт
	
	ФлагиПисьмаСтруктура = КонвертироватьФлагПрочитанное(ФлагиПисьма);
	ОбновляемыеФлагиПисьма = УдалитьНеОбновляемыеФлагиИзСтруктуры(ФлагиПисьмаСтруктура);
	
	МенеджерЗаписи = РегистрыСведений.ФлагиПисем.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.Событие = Событие;
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Событие = Событие;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ОбновляемыеФлагиПисьма);
	Если МенеджерЗаписи.Непрочитанное = Ложь
		И МенеджерЗаписи.Помеченное = Истина Тогда
		МенеджерЗаписи.Удалить();
	Иначе
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
	Если Не ЗагрузкаПочты Тогда
		НоваяЗапись = РегистрыСведений.ДействияСПисьмами.СоздатьМенеджерЗаписи();
		НоваяЗапись.УчетнаяЗапись = УчетнаяЗапись;
		НоваяЗапись.Событие = Событие;
		НоваяЗапись.Действие = Перечисления.ДействияСПисьмом.УстановкаФлага;
		НоваяЗапись.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьФлагиПисем(УчетнаяЗапись, ФлагиПисем, ЗагрузкаПочты = Ложь) Экспорт
	
	СтрокиКУдалению = СтрокиСПустымиФлагами(ФлагиПисем);
	Для каждого Строка Из СтрокиКУдалению Цикл
		ФлагиПисем.Удалить(Строка);
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ФлагиПисем.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
	НаборЗаписей.Загрузить(ФлагиПисем);
	НаборЗаписей.Записать(Истина);
	
	Если Не ЗагрузкаПочты Тогда
		Для каждого ОписаниеФлагов Из ФлагиПисем Цикл
			НоваяЗапись = РегистрыСведений.ДействияСПисьмами.СоздатьМенеджерЗаписи();
			НоваяЗапись.УчетнаяЗапись = УчетнаяЗапись;
			НоваяЗапись.Событие = ОписаниеФлагов.Событие;
			НоваяЗапись.Действие = Перечисления.ДействияСПисьмом.УстановкаФлага;
			НоваяЗапись.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КонвертироватьФлагПрочитанное(ФлагиПисьма)
	
	ФлагиПисьмаСтруктура = Новый Структура;
	ФлагиПисьмаСтруктура.Вставить("Прочитанное", Неопределено);
	ФлагиПисьмаСтруктура.Вставить("Непрочитанное", Неопределено);
	ФлагиПисьмаСтруктура.Вставить("Помеченное", Неопределено);
	ЗаполнитьЗначенияСвойств(ФлагиПисьмаСтруктура, ФлагиПисьма);
	
	Если ФлагиПисьмаСтруктура.Прочитанное <> Неопределено Тогда
		ФлагиПисьмаСтруктура.Непрочитанное = Не ФлагиПисьмаСтруктура.Прочитанное;
	КонецЕсли;
	ФлагиПисьмаСтруктура.Удалить("Прочитанное");
	
	Возврат ФлагиПисьмаСтруктура;
	
КонецФункции

Функция УдалитьНеОбновляемыеФлагиИзСтруктуры(ФлагиПисьма)
	
	ФлагиПисьмаСтруктура = Новый Структура;
	Для каждого КлючЗначение Из ФлагиПисьма Цикл
		Если КлючЗначение.Значение <> Неопределено Тогда
			ФлагиПисьмаСтруктура.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ФлагиПисьмаСтруктура;
	
КонецФункции

Функция СтрокиСПустымиФлагами(ФлагиПисем)
	
	Результат = Новый Массив;
	Для каждого Строка Из ФлагиПисем Цикл
		Если Строка.Помеченное = Ложь И Строка.Непрочитанное = Ложь Тогда
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
