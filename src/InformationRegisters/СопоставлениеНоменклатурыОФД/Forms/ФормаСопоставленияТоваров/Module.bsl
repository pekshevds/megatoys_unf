
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.СопоставлениеПослеЗагрузки Тогда
		
		НесопоставленныеТовары = Параметры.НесопоставленныеТоварыОФД;
		
		Для Каждого Товар Из НесопоставленныеТовары Цикл
			НовыйТовар = СписокТоваровОФД.Добавить();
			НовыйТовар.ТоварОФД = Товар.Ключ;
			НовыйТовар.ПризнакПредметаРасчета = Товар.Значение.ПризнакПредметаРасчета;
			НовыйТовар.Штрихкод = Товар.Значение.Штрихкод;
			НовыйТовар.НоменклатураСопоставлена = Ложь;
		КонецЦикла;
		
		Элементы.ПродолжитьЗагрузку.Доступность = Ложь;
		
	Иначе
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СопоставлениеНоменклатурыОФД.Наименование КАК Наименование,
		|	СопоставлениеНоменклатурыОФД.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ТаблицаСопоставленнойНоменклатуры
		|ИЗ
		|	РегистрСведений.СопоставлениеНоменклатурыОФД КАК СопоставлениеНоменклатурыОФД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСопоставленнойНоменклатуры.Номенклатура.Штрихкод КАК Штрихкод,
		|	ТаблицаСопоставленнойНоменклатуры.Номенклатура.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
		|	ТаблицаСопоставленнойНоменклатуры.Наименование КАК Наименование
		|ИЗ
		|	ТаблицаСопоставленнойНоменклатуры КАК ТаблицаСопоставленнойНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСопоставления
		|		ПО ТаблицаСопоставленнойНоменклатуры.Номенклатура = НоменклатураСопоставления.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование");
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаИзРезультатаЗапроса = РезультатЗАпроса.Выбрать(); 
		
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			
			НовыйТовар = СписокТоваровОФД.Добавить();
			НовыйТовар.ТоварОФД = ВыборкаИзРезультатаЗапроса.Наименование;
			НовыйТовар.ПризнакПредметаРасчета = ВыборкаИзРезультатаЗапроса.ПризнакПредметаРасчета;
			НовыйТовар.Штрихкод = ВыборкаИзРезультатаЗапроса.Штрихкод;
			НовыйТовар.НоменклатураСопоставлена = Истина;
			
		КонецЦикла;
		
		Элементы.СоздатьНоменклатуру.Видимость = Ложь;
		Элементы.ПродолжитьЗагрузку.Видимость  = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Номенклатура" Тогда
		
		Если Элементы.СписокТоваровОФД.ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		МассивТоваровОФД = Новый Массив;
		
		Для каждого ТоварОФД Из Элементы.СписокТоваровОФД.ВыделенныеСтроки Цикл
			
			ДанныеТовара = Элементы.СписокТоваровОФД.ДанныеСтроки(ТоварОФД);
			СтруктураТовара = Новый Структура;
			СтруктураТовара.Вставить("ТоварОФД",    ДанныеТовара.ТоварОФД);
			СтруктураТовара.Вставить("НомерСтроки", ТоварОФД);
			МассивТоваровОФД.Добавить(СтруктураТовара);
			
		КонецЦикла;
		
		НоменклатураДляЗаписи = Новый Структура;
		НоменклатураДляЗаписи.Вставить("Номенклатура", Параметр);
		НоменклатураДляЗаписи.Вставить("Характеристика", Неопределено);
		
		ВыполнитьСопоставлениеТоваров(МассивТоваровОФД, НоменклатураДляЗаписи); 
		
		Элементы.СписокТоваровОФД.Обновить();
		Элементы.СписокНоменклатуры.Обновить();
		
		ОтборТоваровОФД = Новый Структура();
		ОтборТоваровОФД.Вставить("НоменклатураСопоставлена", Ложь);
		
		НесопоставленныеСтроки = СписокТоваровОФД.НайтиСтроки(ОтборТоваровОФД); 
		
		Если НесопоставленныеСтроки.Количество() = 0 Тогда
			Элементы.ПродолжитьЗагрузку.Доступность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СопоставитьТовары(Команда)
	
	Если Элементы.СписокНоменклатуры.ВыделенныеСтроки.Количество() = 0 
		ИЛИ Элементы.СписокТоваровОФД.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивТоваровОФД = Новый Массив;
	
	Для каждого ТоварОФД Из Элементы.СписокТоваровОФД.ВыделенныеСтроки Цикл
		
		ДанныеТовара = Элементы.СписокТоваровОФД.ДанныеСтроки(ТоварОФД);
		СтруктураТовара = Новый Структура;
		СтруктураТовара.Вставить("ТоварОФД",    ДанныеТовара.ТоварОФД);
		СтруктураТовара.Вставить("НомерСтроки", ТоварОФД);
		МассивТоваровОФД.Добавить(СтруктураТовара);
		
	КонецЦикла; 
	СтрокаНоменклатуры = Элементы.СписокНоменклатуры.ВыделенныеСтроки[0];
	ДанныеНоменклатуры = Элементы.СписокНоменклатуры.ДанныеСтроки(СтрокаНоменклатуры); 
	НоменклатураДляЗаписи = Новый Структура;
	НоменклатураДляЗаписи.Вставить("Номенклатура", ДанныеНоменклатуры.Номенклатура);
	НоменклатураДляЗаписи.Вставить("Характеристика", ДанныеНоменклатуры.Характеристика);
	
	ВыполнитьСопоставлениеТоваров(МассивТоваровОФД, НоменклатураДляЗаписи); 
	
	Элементы.СписокТоваровОФД.Обновить();
	Элементы.СписокНоменклатуры.Обновить();
	
	ОтборТоваровОФД = Новый Структура();
	ОтборТоваровОФД.Вставить("НоменклатураСопоставлена", Ложь);
	
	НесопоставленныеСтроки = СписокТоваровОФД.НайтиСтроки(ОтборТоваровОФД); 
	
	Если НесопоставленныеСтроки.Количество() = 0 Тогда
		Элементы.ПродолжитьЗагрузку.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	
	РодительЗначение = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	
	ЗначенияЗаполнения = Новый Структура("Родитель", РодительЗначение);
	ЗначенияЗаполнения.Вставить("ЭтоНабор", Ложь); 
	ЗначенияЗаполнения.Вставить("ПризнакПредметаРасчета", Элементы.СписокТоваровОФД.ТекущиеДанные.ПризнакПредметаРасчета);
	ЗначенияЗаполнения.Вставить("Наименование", Элементы.СписокТоваровОФД.ТекущиеДанные.ТоварОФД);
	ЗначенияЗаполнения.Вставить("НаименованиеПолное", Элементы.СписокТоваровОФД.ТекущиеДанные.ТоварОФД);
	
	СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	СоздатьНоменклатуруЗавершение = Новый ОписаниеОповещения("СоздатьНоменклатуруЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта", СтруктураПараметров, ЭтаФорма,,,,);
	
КонецПроцедуры 

&НаКлиенте
Процедура Рассопоставить(Команда)
	
	МассивЭлементов = Новый Массив;
	Для каждого ТоварОФД Из Элементы.СписокТоваровОФД.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.СписокТоваровОФД.ДанныеСтроки(ТоварОФД);
		Если ДанныеСтроки.НоменклатураСопоставлена Тогда
			МассивЭлементов.Добавить(ДанныеСтроки.ТоварОФД);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивЭлементов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасспопоставитьНаСервере(МассивЭлементов);
	
	Элементы.СписокТоваровОФД.Обновить();
	Элементы.СписокНоменклатуры.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗагрузку(Команда)
	
	ПараметрыЗакрытия = Новый Структура();
	ПараметрыЗакрытия.Вставить("СопоставлениеВыполнено", Истина);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокТоваровОФДПриАктивизацииСтроки(Элемент)
	
	Если СписокТоваровОФД.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоллекцияЭлементов = СписокНоменклатуры.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных("ПризнакПредметаРасчета");
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			КоллекцияЭлементов.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементОтбора = КоллекцияЭлементов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("ПризнакПредметаРасчета");
	ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.Использование    = Истина;
	СписокОтбора = Новый СписокЗначений;
	СписокОтбора.Добавить(Элемент.ТекущиеДанные.ПризнакПредметаРасчета);
	СписокОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.ПустаяСсылка"));
	ЭлементОтбора.ПравоеЗначение   = СписокОтбора;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьСопоставлениеТоваров(МассивТоваровОФД, НоменклатураДляЗаписи)
	
	Для Каждого СтрокаТовара Из МассивТоваровОФД Цикл
		
		Запись = РегистрыСведений.СопоставлениеНоменклатурыОФД.СоздатьМенеджерЗаписи();
		Запись.Наименование   = СтрокаТовара.ТоварОФД;
		Запись.Номенклатура   = НоменклатураДляЗаписи.Номенклатура;
		Запись.Характеристика = НоменклатураДляЗаписи.Характеристика;
		Запись.Записать();
		
		СтрокаНоменклатуры = СписокТоваровОФД.Получить(СтрокаТовара.НомерСтроки);
		СтрокаНоменклатуры.НоменклатураСопоставлена = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуруЗавершение() Экспорт
	
	Элементы.СписокТоваровОФД.Обновить();
	Элементы.СписокНоменклатуры.Обновить();
	
	Элементы.СписокНоменклатуры.ВыделенныеСтроки.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура РасспопоставитьНаСервере(МассивТоваровОФД)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТоварыОФД", МассивТоваровОФД);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СопоставлениеНоменклатурыОФД.Наименование КАК Наименование,
	|	СопоставлениеНоменклатурыОФД.Номенклатура КАК Номенклатура,
	|	СопоставлениеНоменклатурыОФД.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.СопоставлениеНоменклатурыОФД КАК СопоставлениеНоменклатурыОФД
	|ГДЕ
	|	СопоставлениеНоменклатурыОФД.Наименование В(&ТоварыОФД)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Запись = РегистрыСведений.СопоставлениеНоменклатурыОФД.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать(); 
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("ТоварОФД", Выборка.Наименование);
		СтрокиТоваровОФД = СписокТоваровОФД.НайтиСтроки(ОтборСтрок);
		ТоварОФД = СтрокиТоваровОФД[0];
		ТоварОФД.НоменклатураСопоставлена = Ложь;
		
		Если Запись.Выбран() Тогда
			Запись.Удалить(); 
			Элементы.ПродолжитьЗагрузку.Доступность = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
