
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования.ОсновноеСредство) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ЗначениеКопирования);
	КонецЕсли;
	
	Организация = Параметры.Организация;
	Период = Параметры.Период;
	ОсновноеСредство = Параметры.ОсновноеСредство;
	Основание = Параметры.Основание;
	
	Если Параметры.РежимОткрытия Тогда
		
		Организация = Запись.Организация;
		Период = Запись.ПериодСобытия;
		ОсновноеСредство = Запись.ОсновноеСредство;
		Основание = Запись.Основание;
		
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.ВидОперации = Перечисления.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ИнвентаризацияОС Тогда
		Запись.Сумма = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВидОперации = Перечисления.ВидыОперацийРегистрацииПрослеживаемыхТоваров.РегистрацияКомплектующихОС;
	
	ТекущийОбъект.ВидОперации = ВидОперации;
	ТекущийОбъект.Организация = Организация;
	ТекущийОбъект.ОсновноеСредство = ОсновноеСредство;
	ТекущийОбъект.ПериодСобытия = Период;
	ТекущийОбъект.Основание = Основание;
	ТекущийОбъект.ТНВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Номенклатура, "КодТНВЭД");
	ТекущийОбъект.ВсегоКоличествоКомплектующих = ТекущийОбъект.Количество;
	ТекущийОбъект.ВсегоКомлектующихНаСумму = ТекущийОбъект.Сумма;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнена номенклатура'"),
		,,, Отказ);
		
	ИначеЕсли Запись.Количество = 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнено количество'"),
		,,, Отказ);
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Запись.СтранаПроисхождения) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнена страна происхождения'"),
		,,, Отказ);
		
	КонецЕсли;
	
	// Определеяем, что это новая запись
	Если НЕ ЗначениеЗаполнено(Запись.ОсновноеСредство) Тогда
		
		НаборЗаписей = РегистрыСведений.РегистрацияПрослеживаемыхТоваров.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.ПериодСобытия.Установить(Период);
		НаборЗаписей.Отбор.ВидОперации.Установить(
		Перечисления.ВидыОперацийРегистрацииПрослеживаемыхТоваров.РегистрацияКомплектующихОС);
		НаборЗаписей.Отбор.ОсновноеСредство.Установить(ОсновноеСредство);
		НаборЗаписей.Отбор.Номенклатура.Установить(Запись.Номенклатура);
		НаборЗаписей.Отбор.Основание.Установить(Основание);
		НаборЗаписей.Отбор.ТНВЭД.Установить(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Номенклатура, "КодТНВЭД"));
		НаборЗаписей.Отбор.РНПТ.Установить(Запись.РНПТ);
		НаборЗаписей.Прочитать();
	
		Если НаборЗаписей.Количество() <> 0 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Запись с такими значениями существует'"),,,, Отказ);
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		
		Запись.СтранаПроисхождения = ПолучитьСтрануПроисхожденияНаСервере(Запись.Номенклатура);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСтрануПроисхожденияНаСервере(Номенклатура)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СтранаПроисхождения");
	
КонецФункции

#КонецОбласти


