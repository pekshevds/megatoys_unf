#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтказатьсяОтРегистрацииИзменений() Тогда
		Возврат;
	КонецЕсли;
	
	// Отказываемся от выполнения типового механизма регистрации объектов.
	ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов");
	
	// Удаляем все узлы, добавленные по авторегистрации, если признак авторегистрации был ошибочно установлен.
	ОбменДанными.Получатели.Очистить();
	
	// Набор записей должен регистрироваться только на одном узле, указанном в отборе.
	ОбменДанными.Получатели.Добавить(Отбор.УзелИнформационнойБазы.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтказатьсяОтРегистрацииИзменений()
	
	Если Не ЗначениеЗаполнено(Отбор.УзелИнформационнойБазы.Значение) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Отбор.СсылкаНаОбъект.Значение) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.СсылкаСуществует(Отбор.УзелИнформационнойБазы.Значение) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Отбор.УзелИнформационнойБазы.Значение = ПланыОбмена.ОбменУправлениеНебольшойФирмойСайт.ЭтотУзел() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли