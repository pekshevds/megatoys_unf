#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПоместитьВПочтовыйЯщик(УчетнаяЗапись, ПочтовыйЯщик, Событие) Экспорт
	
	ОжидаемыеТипы = Новый Массив;
	ОжидаемыеТипы.Добавить(Тип("СправочникСсылка.ПочтовыеЯщики"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ПоместитьВПочтовыйЯщик", "ПочтовыйЯщик", ПочтовыйЯщик, ОжидаемыеТипы);
	
	НаборЗаписей = РегистрыСведений.СодержимоеПочтовыхЯщиков.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Событие.Установить(Событие);
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.ПочтовыйЯщик = ПочтовыйЯщик;
	НоваяСтрока.Событие = Событие;
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Функция ПочтовыйЯщикСобытия(Событие) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СодержимоеПочтовыхЯщиков.ПочтовыйЯщик КАК ПочтовыйЯщик
	|ИЗ
	|	РегистрСведений.СодержимоеПочтовыхЯщиков КАК СодержимоеПочтовыхЯщиков
	|ГДЕ
	|	СодержимоеПочтовыхЯщиков.Событие = &Событие";
	Запрос.УстановитьПараметр("Событие", Событие);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПочтовыеЯщики.ПустаяСсылка();
	КонецЕсли;
	
	СодержимоеПочтовыхЯщиков = Результат.Выбрать();
	Если СодержимоеПочтовыхЯщиков.Следующий() Тогда
		Возврат СодержимоеПочтовыхЯщиков.ПочтовыйЯщик;
	Иначе
		Возврат Справочники.ПочтовыеЯщики.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
