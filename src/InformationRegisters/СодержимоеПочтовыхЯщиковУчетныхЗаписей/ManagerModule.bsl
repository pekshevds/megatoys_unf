#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПоместитьВПочтовыйЯщик(УчетнаяЗапись, ПочтовыйЯщик, Событие, ЗагрузкаПочты = Ложь) Экспорт
	
	ОжидаемыеТипы = Новый Массив;
	ОжидаемыеТипы.Добавить(Тип("Строка"));
	ОжидаемыеТипы.Добавить(Тип("СправочникСсылка.ПочтовыеЯщикиУчетныхЗаписей"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ПоместитьВПочтовыйЯщик", "ПочтовыйЯщик", ПочтовыйЯщик, ОжидаемыеТипы);
	
	Если ТипЗнч(ПочтовыйЯщик) = Тип("Строка") Тогда
		ПочтовыйЯщикСсылка = Справочники.ПочтовыеЯщикиУчетныхЗаписей.ПочтовыйЯщикПоИмени(УчетнаяЗапись, ПочтовыйЯщик);
	Иначе
		ПочтовыйЯщикСсылка = ПочтовыйЯщик;
	КонецЕсли;
	
	ТекущийПочтовыйЯщик = ПочтовыйЯщикСобытия(Событие);
	Если Не ЗначениеЗаполнено(ТекущийПочтовыйЯщик) И Не ЗначениеЗаполнено(ПочтовыйЯщикСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПочтовыйЯщикСсылка) Тогда
		НаборЗаписей = РегистрыСведений.СодержимоеПочтовыхЯщиковУчетныхЗаписей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Событие.Установить(Событие);
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.ПочтовыйЯщик = ПочтовыйЯщикСсылка;
		НоваяСтрока.Событие = Событие;
		НаборЗаписей.Записать(Истина);
	КонецЕсли;
	
	Если Не ЗагрузкаПочты И ЗначениеЗаполнено(ТекущийПочтовыйЯщик) Тогда
		МенеджерЗаписи = РегистрыСведений.ДействияСПисьмами.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
		МенеджерЗаписи.Событие = Событие;
		МенеджерЗаписи.Действие = Перечисления.ДействияСПисьмом.ПеремещениеВПочтовыйЯщик;
		МенеджерЗаписи.ПочтовыйЯщик = ПочтовыйЯщикСсылка;
		МенеджерЗаписи.СтарыйПочтовыйЯщик = ТекущийПочтовыйЯщик;
		МенеджерЗаписи.НовыйПочтовыйЯщик = ПочтовыйЯщикСсылка;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПочтовыйЯщикСобытия(Событие) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СодержимоеПочтовыхЯщиковУчетныхЗаписей.ПочтовыйЯщик КАК ПочтовыйЯщик
	|ИЗ
	|	РегистрСведений.СодержимоеПочтовыхЯщиковУчетныхЗаписей КАК СодержимоеПочтовыхЯщиковУчетныхЗаписей
	|ГДЕ
	|	СодержимоеПочтовыхЯщиковУчетныхЗаписей.Событие = &Событие";
	Запрос.УстановитьПараметр("Событие", Событие);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПочтовыеЯщики.ПустаяСсылка();
	КонецЕсли;
	
	СодержимоеПочтовыхЯщиков = Результат.Выбрать();
	Если СодержимоеПочтовыхЯщиков.Следующий() Тогда
		Возврат СодержимоеПочтовыхЯщиков.ПочтовыйЯщик;
	Иначе
		Возврат Справочники.ПочтовыеЯщики.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
