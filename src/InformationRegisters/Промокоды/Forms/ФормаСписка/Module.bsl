#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	МножественноеВыделение = Истина;
	Если Параметры.Свойство("МножественноеВыделение", МножественноеВыделение) Тогда
		Если МножественноеВыделение Тогда
			Элементы.Промокоды.РежимВыделения = РежимВыделенияТаблицы.Множественный;
		Иначе
			Элементы.Промокоды.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
		КонецЕсли; 
	КонецЕсли;


	ТекущаяРассылка = Неопределено;
	РежимПодбора = Ложь;
	Если Параметры.Свойство("ТекущаяРассылка", ТекущаяРассылка) Тогда
		// Если подбор осуществляется в ранее записанную рассылку, то не будем считать отправленными промокоды, фигурирующие в этой рассылке
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяРассылка", ТекущаяРассылка);
		РежимПодбора = Истина;
	КонецЕсли;	
	Элементы.ФормаВыбор.Видимость = РежимПодбора;
	
	// Доступные отборы
	Конечный = Истина;
	Если Параметры.Свойство("Конечный", Конечный) Тогда
		Представление = "";
		Если Конечный Тогда 
			Представление = НСтр("ru = 'С ограниченным количеством применений'");
		Иначе
			Представление = НСтр("ru = 'Без ограничений по количеству применений'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Конечный", Конечный,
		,
		Представление,
		,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;		
	
	Отправлен = Ложь;
	Если Параметры.Свойство("Отправлен", Отправлен) Тогда
		Представление = "";
		Если Отправлен Тогда 
			Представление = НСтр("ru = 'Уже отправленные ранее'");
		Иначе
			Представление = НСтр("ru = 'Ранее не отправлялись'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Отправлен", Отправлен,
		,
		Представление,
		,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;	
	
	// Недоступные отборы
	Условие = Неопределено;
	Если Параметры.Свойство("Условие", Условие) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Условие", Условие);
	КонецЕсли;	

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПромокоды

&НаКлиенте
Процедура ПромокодыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если РежимПодбора Тогда
		СтандартнаяОбработка = Ложь;
		Выбор(Команды.Выбор);
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПромокоды(Команда)
	ОткрытьФормуГенерацииПромокодов();
КонецПроцедуры

&НаКлиенте
Процедура Выбор(Команда)
	ВыделенныеПромокоды = Новый Массив;
	Для Каждого Идентификатор Из Элементы.Промокоды.ВыделенныеСтроки Цикл
		ТекущаяСтрока = Элементы.Промокоды.ДанныеСтроки(Идентификатор);
		
		ВыбранныйПромокод = Новый Структура;
		ВыбранныйПромокод.Вставить("Промокод", ТекущаяСтрока.Промокод);
		ВыбранныйПромокод.Вставить("ДокументАктивации", ТекущаяСтрока.Регистратор);
		ВыделенныеПромокоды.Добавить(ВыбранныйПромокод);
	
	КонецЦикла;
	Закрыть(ВыделенныеПромокоды);
	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуГенерацииПромокодов()
	ОткрытьФорму("Обработка.ГенерацияПромокодов.Форма.Форма",, ЭтотОбъект); 
КонецПроцедуры

&НаКлиенте
Процедура ПромокодыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если РежимПодбора Тогда
		СтандартнаяОбработка = Ложь;
		Выбор(Команды.Выбор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПромокодыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если РежимПодбора Тогда
		СтандартнаяОбработка = Ложь;
		Выбор(Команды.Выбор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти