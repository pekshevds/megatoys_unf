#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция КомментарииФайла(ЭлектронныйДокумент) Экспорт
	
	Комментарии = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПрисоединенныйФайл", ЭлектронныйДокумент);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КомментарииФайловДокументовКЭДО.Исполнитель КАК Исполнитель,
		|	КомментарииФайловДокументовКЭДО.ДатаКомментария КАК ДатаКомментария,
		|	КомментарииФайловДокументовКЭДО.Комментарий КАК Комментарий,
		|	ВЫБОР
		|		КОГДА ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Ознакомиться
		|ИЗ
		|	РегистрСведений.КомментарииФайловДокументовКЭДО КАК КомментарииФайловДокументовКЭДО
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗапланированныеДействияСФайламиДокументовКЭДО КАК ЗапланированныеДействияСФайламиДокументовКЭДО
		|		ПО КомментарииФайловДокументовКЭДО.ПрисоединенныйФайл = ЗапланированныеДействияСФайламиДокументовКЭДО.ПрисоединенныйФайл
		|			И (ЗапланированныеДействияСФайламиДокументовКЭДО.Исполнитель = &ТекущийПользователь)
		|			И (ЗапланированныеДействияСФайламиДокументовКЭДО.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовКЭДО.Ознакомиться))
		|ГДЕ
		|	КомментарииФайловДокументовКЭДО.ПрисоединенныйФайл = &ПрисоединенныйФайл
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаКомментария,
		|	Исполнитель,
		|	Комментарий";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Комментарий = Новый Структура("Исполнитель,ДатаКомментария,Комментарий,Ознакомиться");
		ЗаполнитьЗначенияСвойств(Комментарий, Выборка);
		
		Комментарии.Добавить(Комментарий);
		
	КонецЦикла;
	
	Возврат Комментарии;
	
КонецФункции

#КонецОбласти

#КонецЕсли