
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
	ТекстОтветственный = НСтр("ru='Ответственный за работу с сервисом'");
	ФорматнаяСтрока = СтрШаблон("БЛ=; БИ='%1'",ТекстОтветственный);
	Элементы.ЭтоОтветственный.Формат = ФорматнаяСтрока;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТаблицыСписок

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Элемент.Текущиеданные.ЭтоОтветственный Тогда
		ТекстПредупреждения = СтрШаблон(
				НСтр("ru = '%1 назначен ответственным за работу с сервисом 1С:Кабинет сотрудника, удаление недоступно.'"),
				Элемент.Текущиеданные.ФизическоеЛицо);
		ПоказатьПредупреждение(,ТекстПредупреждения);
	Иначе
		ТекстВопроса = СтрШаблон(
				НСтр("ru ='Удалить сотрудника %1 из списка исключений?'"),
				Элемент.Текущиеданные.ФизическоеЛицо);
		ДополнительныеПараметры = Новый Структура("ФизическоеЛицо", Элемент.Текущиеданные.ФизическоеЛицо);
		Оповещение = Новый ОписаниеОповещения("УдалениеСтрокиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РедактированиеЗавершение", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.ИсключенияИзПравилПубликацииКабинетСотрудника.Форма.ФормаРедактирования",
		, ЭтаФорма, ЭтаФорма,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РедактированиеЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("ФизическоеЛицо", Элемент.ТекущиеДанные.ФизическоеЛицо);
	ОткрытьФорму("РегистрСведений.ИсключенияИзПравилПубликацииКабинетСотрудника.Форма.ФормаРедактирования",
		ПараметрыОткрытия, ЭтаФорма, ЭтаФорма,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалениеСтрокиЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	УдалитьСтроку(ДополнительныеПараметры.ФизическоеЛицо);
	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере
Процедура УдалитьСтроку(ФизическоеЛицо)

	НачатьТранзакцию();
	Попытка
		
		Запись = РегистрыСведений.ИсключенияИзПравилПубликацииКабинетСотрудника.СоздатьМенеджерЗаписи();
		Запись.ФизическоеЛицо = ФизическоеЛицо;
		Запись.Удалить();
		
		КабинетСотрудника.ОбновитьСведенияОПубликацииФизическихЛиц(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура РедактированиеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Элементы.Список.Обновить();

КонецПроцедуры

#КонецОбласти
