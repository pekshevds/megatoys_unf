
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОформитьПодсказкуПроТонкийКлиент();
	Если ЗначениеЗаполнено(Константы.ИдентификацияПриложенияGoogle.Получить()) Тогда
		
		Если Параметры.Свойство("ОписанияОбластейДоступа") Тогда
			ЗаполнитьОбластиДоступа(Параметры.ОписанияОбластейДоступа);
		Иначе
			ЗаполнитьОбластиДоступа(ОписанияВсехОбластейДоступа());
		КонецЕсли;
		
		ИдентификаторАвторизации = Новый УникальныйИдентификатор;
		ИдентификацияПриложения = РегистрыСведений.СеансовыеДанныеGoogle.ИдентификацияПриложения();
		ТекущийЭлемент = Элементы.АвторизоватьсяВGoogle;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПанель;
		
	Иначе
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОтсутствуетВозможностьАвторизации;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстHTMLДокументСформирован(Элемент)
	
	Если СтрНачинаетсяС(Элементы.ТекстHTML.Документ.URL, АдресПеренаправления) Тогда
		ОбрабатываемаяСтрока = Элементы.ТекстHTML.Документ.URL;
	ИначеЕсли Элементы.ТекстHTML.Документ.URL = "about:blank"
		И СтрНайти(Элементы.ТекстHTML.Документ.body.innerText, АдресПеренаправления) <> 0 Тогда
		ОбрабатываемаяСтрока = Элементы.ТекстHTML.Документ.body.innerText;
	Иначе
		ОбрабатываемаяСтрока = "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОбрабатываемаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	// Выделить code из url адреса.
	// http://localhost/?code=4%2F0AWgavdfIYIOPuIXZnCRN-C90f1Ll1s65FILHi102gJox3RQmmFdQLVr0twVYEE99eyTbAQ&scope=https%3A%2F%2Fwww.googleapis.com%2Fauth%2Fcalendar
	// Couldn't connect to server: http://localhost/?code=4%2F0AWgavd541IOPuIXZnCRN-C90f1453s65FILHiR8OgJox3RQmmFdQLVr0twVYEE99eyTbAQ&scope=https%3A%2F%2Fwww.googleapis.com%2Fauth%2Fcalendar
	
	ТекстHTML = "";
	
	ПозицияCode = СтрНайти(ОбрабатываемаяСтрока, "code=");
	ПозицияКонца = СтрНайти(ОбрабатываемаяСтрока, "&", НаправлениеПоиска.СНачала, ПозицияCode + 1);
	ПозицияКонцаCode = ПозицияCode + СтрДлина("code=");
	ТокенЗапроса = Сред(ОбрабатываемаяСтрока, ПозицияКонцаCode, ПозицияКонца - ПозицияКонцаCode);
	
	ОбменятьТокенЗапросаНаТокенДоступа();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АвторизоватьсяВGoogle(Команда)
	
	ВыполнитьЗапросНаПодтверждениеДоступа();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьЗапросНаПодтверждениеДоступа()
	
	Если ИдентификацияПриложения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаАвторизацияВСервисе;
	ТекстHTML = АдресЗапросаНаПодтверждениеДоступа(Ложь);
	
КонецПроцедуры

&НаСервере
Функция АдресЗапросаНаПодтверждениеДоступа(БраузерВОтдельномОкне)
	
	АдресПеренаправления = ОбменСGoogleКлиентСервер.АдресПеренаправления(ИдентификацияПриложения);
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"%1?scope=%2&state=%3&redirect_uri=%4&response_type=code&client_id=%5&approval_prompt=force",
	ИдентификацияПриложения.auth_uri,											 // 1
	ПараметрОбластьДоступа(),													 // 2
	ПараметрАвторизации(ИдентификаторАвторизации, БраузерВОтдельномОкне),		 // 3
	АдресПеренаправления												,		 // 4
	ИдентификацияПриложения.client_id);											 // 5
	
КонецФункции

&НаСервере
Функция ПараметрОбластьДоступа()
	
	Результат = Новый Массив;
	
	Для Каждого ТекОбластьДоступа Из ОбластиДоступа Цикл
		
		Если Не ТекОбластьДоступа.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекОбластьДоступа.ОбластьДоступа) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ТекОбластьДоступа.ОбластьДоступа);
		
	КонецЦикла;
	
	Возврат СтрСоединить(Результат, " ");
	
КонецФункции

// Выполняет заполнение таблицы формы ОбластиДоступа на основании пер
//
// Параметры:
//  ОписанияОбластейДоступа	 - Массив - массив структур,
//  описания полей см. в ОбменСGoogleКлиентСервер.НовоеОписаниеОбластиДоступа()
//
&НаСервере
Процедура ЗаполнитьОбластиДоступа(ОписанияОбластейДоступа)
	
	ОбластиДоступа.Очистить();
	
	Для Индекс = 0 По ОписанияОбластейДоступа.ВГраница() Цикл
		
		ЗаполнитьЗначенияСвойств(ОбластиДоступа.Добавить(), ОписанияОбластейДоступа[Индекс]);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОписанияВсехОбластейДоступа()
	
	Результат = Новый Массив;
	
	ПоддерживаемыеОбласти = Новый Массив;
	ПоддерживаемыеОбласти.Добавить(Перечисления.ОбластиДоступаGoogle.Профиль);
	ПоддерживаемыеОбласти.Добавить(Перечисления.ОбластиДоступаGoogle.Календарь);
	ПоддерживаемыеОбласти.Добавить(Перечисления.ОбластиДоступаGoogle.Почта);
	
	Для Каждого ТекОбластьДоступа Из ПоддерживаемыеОбласти Цикл
		
		ОписаниеОбластиДоступа = ОбменСGoogleКлиентСервер.НовоеОписаниеОбластиДоступа();
		ОписаниеОбластиДоступа.Представление = ТекОбластьДоступа;
		ОписаниеОбластиДоступа.ОбластьДоступа = ОбменСGoogleКлиентСервер.ОбластьДоступа(ТекОбластьДоступа);
		
		Если ТекОбластьДоступа<>Перечисления.ОбластиДоступаGoogle.Профиль Тогда
			ОписаниеОбластиДоступа.Использование = Ложь;
			ОписаниеОбластиДоступа.Редактирование = Истина;
		КонецЕсли;
		
		Результат.Добавить(ОписаниеОбластиДоступа);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрАвторизации(ИдентификаторАвторизации, БраузерВОтдельномОкне)
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"id=%1;zone=%2%3",
	ИдентификаторАвторизации,
	Формат(РаботаВМоделиСервиса.ЗначениеРазделителяСеанса(), "ЧГ="),
	?(БраузерВОтдельномОкне, ";closebrowser=true", ""));
	
	Возврат КодироватьСтроку(Результат, СпособКодированияСтроки.КодировкаURL);
	
КонецФункции

&НаКлиенте
Процедура ОбменятьТокенЗапросаНаТокенДоступа()
	
	Если Не ЗначениеЗаполнено(ТокенЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
	НСтр("ru = 'Токен запроса получен, выполняется обмен на токен доступа...'"),,,
	БиблиотекаКартинок.Информация32);
	
	СеансовыеДанные = ОбменятьТокенЗапросаНаСеансовыеДанные(
	ТокенЗапроса,
	ИдентификацияПриложения,
	ПараметрОбластьДоступа());
	
	Если ОбменСGoogleКлиентСервер.НеЗаполненТокенДоступа(СеансовыеДанные) Тогда
		ПоказатьОповещениеПользователя(
		НСтр("ru = 'Ошибка при получении токена доступа'"),,,
		БиблиотекаКартинок.Ошибка32);
	Иначе
		ПоказатьОповещениеПользователя(
		НСтр("ru = 'Токен доступа получен успешно'"),,,
		БиблиотекаКартинок.Успешно32);
	КонецЕсли;
	
	Закрыть(СеансовыеДанные);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбменятьТокенЗапросаНаСеансовыеДанные(ТокенЗапроса, ИдентификацияПриложения, ОбластьДанных)
	
	АдресДляПолученияТокенаДоступа = ОбщегоНазначенияКлиентСервер.СтруктураURI(
	ИдентификацияПриложения.token_uri);
	
	ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(АдресДляПолученияТокенаДоступа.Схема);
	
	HTTPСоединение = Новый HTTPСоединение(
	АдресДляПолученияТокенаДоступа.Хост,
	АдресДляПолученияТокенаДоступа.Порт,,,
	ИнтернетПрокси,
	120,
	ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение());
	
	ЗапросHTTP = Новый HTTPЗапрос;
	ЗапросHTTP.АдресРесурса = АдресДляПолученияТокенаДоступа.ПутьНаСервере;
	ЗапросHTTP.Заголовки["Content-Type"] = "application/x-www-form-urlencoded";
	ЗапросHTTP.УстановитьТелоИзСтроки(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"code=%1&client_id=%2&client_secret=%3&redirect_uri=%4&grant_type=authorization_code",
	ТокенЗапроса,
	ИдентификацияПриложения.client_id,
	ИдентификацияПриложения.client_secret,
	ОбменСGoogleКлиентСервер.АдресПеренаправления(ИдентификацияПриложения)));
	
	ОтветHTTP = HTTPСоединение.ОтправитьДляОбработки(ЗапросHTTP);
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ОтветHTTP.ПолучитьТелоКакСтроку());
	Результат = ПрочитатьJSON(ЧтениеJSON);
	
	Результат.Вставить("scope", СтрЗаменить(ОбластьДанных, " ", "+"));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОформитьПодсказкуПроТонкийКлиент()
	
	ЭтоВебКлиент = ОбщегоНазначения.ЭтоВебКлиент();
	Элементы.ГруппаАвторизоваться.Видимость = Не ЭтоВебКлиент;
	Элементы.ГруппаТонкийКлиент.Видимость = ЭтоВебКлиент;
	
	Если ЭтоВебКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	АдресИнструкцииПоНастройкеТонкогоКлиента = ИнформационныйЦентрСервер.КонтекстнаяСсылкаПоИдентификатору("ИнструкцияПоНастройкеТонкогоКлиента");
	Если АдресИнструкцииПоНастройкеТонкогоКлиента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстПодсказки = НСтр("ru='Для авторизации в сервисе Google используйте тонкий клиент.
	|Ознакомиться как использовать тонкий клиент на компьютере можно по <a href=""%1"">ссылке</a>.'");
	ТекстПодсказки = СтрШаблон(ТекстПодсказки, АдресИнструкцииПоНастройкеТонкогоКлиента.Адрес);
	
	Элементы.ПодсказкаИспользованиеТонкогоКлиента.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстПодсказки);
	
КонецПроцедуры

#КонецОбласти

