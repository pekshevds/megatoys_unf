#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения о размере фиксированного взноса.
// Данные включают в себя сам размер и дату, с которой он действует.
//
// Параметры:
//  ДатаАктуальности - Дата - дата, на которую нужно получить размер взносов.
//                            Если параметр не указан, возвращается последнее действующее значение.
//                            
// Возвращаемое значение:
//  Структура  - содержит:
//     * Размер - Число
//     * Период - Дата
//
Функция ДанныеРазмераФиксированныхВзносовОтНесчастныхСлучаев(ДатаАктуальности = Неопределено) Экспорт
	
	ДанныеФиксированныхВзносов = Новый Структура;
	ДанныеФиксированныхВзносов.Вставить("Размер", 0);
	ДанныеФиксированныхВзносов.Вставить("Период", '00010101');
	
	СрезПоследних = РегистрыСведений.РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН.СрезПоследних(ДатаАктуальности);
	ЗаполнитьЗначенияСвойств(ДанныеФиксированныхВзносов, СрезПоследних[0]);
	
	Возврат ДанныеФиксированныхВзносов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Начальное заполнение и обновление информационной базы.
Процедура ЗаполнитьПоставляемыеДанные() Экспорт
	Обновить(ПолучитьМакет("ПредопределенныеЗначения").ПолучитьТекст());
КонецПроцедуры

// Обновляет данные регистра.
Процедура Обновить(ТекстXML) Экспорт
	
	НаборЗаписей = РегистрыСведений.РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН.СоздатьНаборЗаписей();
	ЗагружаемыеСведения = ОбщегоНазначения.ЗначениеИзСтрокиXML(ТекстXML);
	НаборЗаписей.Загрузить(ЗагружаемыеСведения.Данные);
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет заполненность данных о размере фиксированного взноса.
//
// Возвращаемое значение - булево, Истина - если есть хотя бы одно значение.
//
Функция РазмерФиксированныхВзносовОтНесчастныхСлучаевЗаполнен() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрСведений.РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН КАК РазмерФиксированныхВзносовОтНесчастныхСлучаевАУСН");
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли