#Область ОписаниеПеременных

&НаКлиенте
Перем МассивИзмененныхРеквизитов;

&НаКлиенте
Перем ПодтвержденоЗакрытиеФормы; // Для подтверждения закрытия формы пользователем

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОбъектовМетаданных = Новый Массив(1);
	МассивОбъектовМетаданных[0] = Параметры.КонтекстПечати.Ссылка.Метаданные();

	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = МассивОбъектовМетаданных;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	ЗаполнитьЗначенияСвойств(КонтекстПечати, Параметры.КонтекстПечати, "Организация, СтруктурнаяЕдиница,
		|ПодписьРуководителя, ПодписьГлавногоБухгалтера, ПодписьКладовщика, Контрагент, КонтактноеЛицоПодписант,
		|УсловияГарантии, УсловияПриемки,
		|ТекстУсловийГарантийногоТалона, ТекстУсловийПриемки");
	
	ЗаголовокФормы();
	ЗаполнитьПодсказкиПодписей();
	УстановитьКартинкиКнопок();
	УстановитьВидФормы();
	
	ДоступностьКомандФормы();

	РаботаСФормойДокумента.НастроитьВидимостьГруппыИнформации(ЭтотОбъект, "ГруппаИнформацияПоНовымРеквизитам",
							"ПоказыватьИнформациюПоНовойСхемеРеквизитовПечати", "ФормыОбработкиРеквизитыПечати");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МассивИзмененныхРеквизитов = Новый Массив;
	ПодтвержденоЗакрытиеФормы = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ ЗавершениеРаботы И НЕ ПодтвержденоЗакрытиеФормы Тогда
		
		Отказ = Истина;
        ОписаниеОповещенияОЗавершении    = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);                
        ТекстВопроса        = НСтр("ru = 'Выполненные изменения будут отменены. Продолжить?'");                
        ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет); 		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
    
    Если Ответ <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    ПодтвержденоЗакрытиеФормы = Истина;
               
    Закрыть();
    
КонецПроцедуры // ПередЗакрытиемЗавершение()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтекстПечатиПодписьРуководителяПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("ПодписьРуководителя");
	ЗаполнитьПодсказкиПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстПечатиПодписьГлавногоБухгалтераПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("ПодписьГлавногоБухгалтера");
	ЗаполнитьПодсказкиПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстПечатиПодписьКладовщикаПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("ПодписьКладовщика");
	ЗаполнитьПодсказкиПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстПечатиКонтактноеЛицоПодписантПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("КонтактноеЛицоПодписант");
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияГарантииПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("УсловияГарантии");
	КонтекстПечати.ТекстУсловийГарантийногоТалона = 
		ПолучитьТекстУсловийШаблона(КонтекстПечати.УсловияГарантии);
	Элементы.ГруппаТекстУсловийГарантии.Видимость = НЕ КонтекстПечати.УсловияГарантии.Пустая();
	Элементы.ТекстУсловийГарантийногоТалона.Заголовок = НСтр("ru='Текст условий талона'");
	Элементы.ТекстУсловийГарантийногоТалона.Вид = ВидПоляФормы.ПолеНадписи;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияПриемкиПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита("УсловияПриемки");
	КонтекстПечати.ТекстУсловийПриемки = 
		ПолучитьТекстУсловийШаблона(КонтекстПечати.УсловияПриемки);
	Элементы.ГруппаТекстУсловийПриемки.Видимость = НЕ КонтекстПечати.УсловияПриемки.Пустая();
	Элементы.ТекстУсловийПриемки.Заголовок = НСтр("ru='Текст условий приемки'");
	Элементы.ТекстУсловийПриемки.Вид = ВидПоляФормы.ПолеНадписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстУсловийПриемкиПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстУсловийГарантийногоТалонаПриИзменении(Элемент)
	
	ЗафиксироватьИзменениеЗначенияРеквизита(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнформацияПоНовойСхемеЗакрытьНажатие(Элемент)

	Элементы.ГруппаИнформацияПоНовымРеквизитам.Видимость = Ложь;	
	СохранитьНастройкуПоказыватьИнформациюПоНовойСхеме();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьИзмененияИЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПодписиПоУмолчанию(Команда)
	
	ПредыдущиеЗначения = Новый Структура("ПодписьРуководителя, ПодписьГлавногоБухгалтера, ПодписьКладовщика");
	ЗаполнитьЗначенияСвойств(ПредыдущиеЗначения, КонтекстПечати);
	
	ПолучитьПодписиПоУмолчаниюНаСервере();
	
	Для каждого ЭлементСтруктуры Из ПредыдущиеЗначения Цикл
		
		Если ЭлементСтруктуры.Значение <> КонтекстПечати[ЭлементСтруктуры.Ключ] Тогда
			
			ЗафиксироватьИзменениеЗначенияРеквизита(ЭлементСтруктуры.Ключ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПодписьРуководителяНажатие(Команда)
	
    ЗаписатьНастройкуПользователя("ПодписьРуководителя", КонтекстПечати.ПодписьРуководителя);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПодписьГлавногоБухгалтераНажатие(Команда)

	ЗаписатьНастройкуПользователя("ПодписьГлавногоБухгалтера", КонтекстПечати.ПодписьГлавногоБухгалтера);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПодписьКладовщикаНажатие(Команда)
	
    ЗаписатьНастройкуПользователя("ПодписьМОЛ", КонтекстПечати.ПодписьКладовщика);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьУсловияГарантии(Команда)
	
	ЗаписатьНастройкуПользователя("УсловияГарантии", КонтекстПечати.УсловияГарантии);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьУсловияПриемки(Команда)
	
	ЗаписатьНастройкуПользователя("УсловияПриемки", КонтекстПечати.УсловияПриемки);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьУсловияПриемки(Команда)

	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ТекстУсловий", КонтекстПечати.ТекстУсловийПриемки);
	ДопПараметры.Вставить("Условия", КонтекстПечати.УсловияПриемки);	
	ДопПараметры.Вставить("Заголовок", НСтр("ru='Условия приемки'"));
	ДопПараметры.Вставить("ИмяРеквизитаУсловий", "ТекстУсловийПриемки");
	Оповещение = Новый ОписаниеОповещения("ПослеВводаТекстаУсловий", ЭтотОбъект, ДопПараметры);	
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РедакторУсловий", ДопПараметры, ЭтотОбъект, , , , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьУсловияГарантийногоТалона(Команда)

	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ТекстУсловий", КонтекстПечати.ТекстУсловийГарантийногоТалона);
	ДопПараметры.Вставить("Условия", КонтекстПечати.УсловияГарантии);	
	ДопПараметры.Вставить("Заголовок", НСтр("ru='Условия гарантийного талона'"));
	ДопПараметры.Вставить("ИмяРеквизитаУсловий", "ТекстУсловийГарантийногоТалона");
	Оповещение = Новый ОписаниеОповещения("ПослеВводаТекстаУсловий", ЭтотОбъект, ДопПараметры);	
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РедакторУсловий", ДопПараметры, ЭтотОбъект, , , , Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидФормы()
	
	Элементы.ГруппаТекстУсловийГарантии.Видимость = НЕ КонтекстПечати.УсловияГарантии.Пустая();
	Элементы.ГруппаТекстУсловийПриемки.Видимость = НЕ КонтекстПечати.УсловияПриемки.Пустая();
	
	Если НЕ ПустаяСтрока(КонтекстПечати.ТекстУсловийПриемки) Тогда
		УстановитьВидПолеВвода("ТекстУсловийПриемки");
	Иначе
		КонтекстПечати.ТекстУсловийПриемки = 
			ПолучитьТекстУсловийШаблона(КонтекстПечати.УсловияПриемки);
	КонецЕсли;	

	Если НЕ ПустаяСтрока(КонтекстПечати.ТекстУсловийГарантийногоТалона) Тогда
		УстановитьВидПолеВвода("ТекстУсловийГарантийногоТалона");
	Иначе
		КонтекстПечати.ТекстУсловийГарантийногоТалона = 
			ПолучитьТекстУсловийШаблона(КонтекстПечати.УсловияГарантии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗафиксироватьИзменениеЗначенияРеквизита(ИмяРеквизита)
	
	Если МассивИзмененныхРеквизитов.Найти(ИмяРеквизита) = Неопределено Тогда
		
		МассивИзмененныхРеквизитов.Добавить(ИмяРеквизита);
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзмененияИЗакрытьФорму(Команда = Неопределено)
	
	ПараметрыЗакрытия = Новый Структура;
	Если Команда <> Неопределено Тогда
		
		ПараметрыЗакрытия.Вставить("Команда", Команда);
		
	КонецЕсли;
	
	ИзмененныеРеквизиты = Новый Структура;
	Для каждого ЭлементМассива Из МассивИзмененныхРеквизитов Цикл
		
		ИзмененныеРеквизиты.Вставить(ЭлементМассива, КонтекстПечати[ЭлементМассива]);
		
	КонецЦикла;
	
	ДополнитьРеквизитыТекстамиУсловий(ИзмененныеРеквизиты);
	ПараметрыЗакрытия.Вставить("ИзмененныеРеквизиты", ИзмененныеРеквизиты);
	
	ПодтвержденоЗакрытиеФормы = Истина;
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаголовокФормы()
	
	Заголовок = Обработки.РеквизитыПечати.ЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПодписиПоУмолчаниюНаСервере()
	
	ДокументОбъект = ДанныеФормыВЗначение(КонтекстПечати, Тип("ДокументОбъект.ПриемИПередачаВРемонт"));
	Обработки.РеквизитыПечати.ПодписиПоУмолчанию(ДокументОбъект);
	ЗначениеВДанныеФормы(ДокументОбъект, КонтекстПечати);
	
	КонтекстПечати.КонтактноеЛицоПодписант = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтекстПечати.Контрагент, "КонтактноеЛицоПодписант");
	
КонецПроцедуры

&НаСервере
Процедура ДоступностьКомандФормы()
	
	Если НЕ ПравоДоступа("Изменение", КонтекстПечати.Ссылка.Метаданные()) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаВосстановитьПодписиПоУмолчанию", "Доступность", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодсказкиПодписей() 
	
	Если КонтекстПечати.ПодписьГлавногоБухгалтера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтекстПечати.Организация,
			"ПодписьГлавногоБухгалтера") Тогда
		Элементы.ПодписьГлавногоБухгалтера.Подсказка = НСтр("ru='Подпись из реквизитов организации'");
	Иначе
		Элементы.ПодписьГлавногоБухгалтера.Подсказка = НСтр("ru='Подпись из настроек пользователя'");
	КонецЕсли;
	
	Если КонтекстПечати.ПодписьРуководителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтекстПечати.Организация,
			"ПодписьРуководителя") Тогда
		Элементы.ПодписьРуководителя.Подсказка = НСтр("ru='Подпись из реквизитов организации'");
	Иначе
		Элементы.ПодписьРуководителя.Подсказка = НСтр("ru='Подпись из настроек пользователя'");
	КонецЕсли;

	Если КонтекстПечати.ПодписьКладовщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтекстПечати.СтруктурнаяЕдиница,
			"ПодписьМОЛ") Тогда
		Элементы.ПодписьКладовщика.Подсказка = НСтр("ru='Подпись из реквизитов структурной единицы'");
	Иначе
		Элементы.ПодписьКладовщика.Подсказка = НСтр("ru='Подпись из настроек пользователя'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКартинкиКнопок()
	
	КартинкаСохранения = ОбщегоНазначенияУНФКлиентСервер.КартинкаСохраненияНастроек();
	Элементы.СохранитьПодписьРуководителя.Картинка = КартинкаСохранения;
	Элементы.СохранитьПодписьГлавногоБухгалтера.Картинка = КартинкаСохранения;
	Элементы.СохранитьПодписьКладовщика.Картинка = КартинкаСохранения;
	Элементы.СохранитьУсловияГарантии.Картинка = КартинкаСохранения;
	Элементы.СохранитьУсловияПриемки.Картинка = КартинкаСохранения;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкуПользователя(НазваниеНастройки, ЗначениеНастройки)
	
	ЗаписатьНастройкуПользователяСервер(НазваниеНастройки, ЗначениеНастройки);	
	
	СтрокаЗаголовка = НСтр("ru='Сохранение настроек пользователя'");
	ШаблонСообщения = НСтр("ru='Значение сохранено для использования в новых документах %1 %2'");
	
	ПараметрОрганизация = "";
	
	Если ИспользоватьНесколькоОрганизаций Тогда
		
		НаименованиеОрганизации = УправлениеНебольшойФирмойВызовСервера.ЗначениеРеквизитаОбъекта(
			КонтекстПечати.Организация, "Наименование");
		ПараметрОрганизация = СтрШаблон(НСтр("ru='по организации %1'"), НаименованиеОрганизации);
		
	КонецЕсли;
	
	ПараметрПользователь = СтрШаблон(НСтр("ru='пользователем %1'"), ПользователиКлиент.АвторизованныйПользователь());

	СтрокаТекста = СтрШаблон(ШаблонСообщения, ПараметрОрганизация, ПараметрПользователь);
	КартинкаСохранения = ОбщегоНазначенияУНФКлиентСервер.КартинкаСохраненияНастроек();
	
	ПоказатьОповещениеПользователя(СтрокаЗаголовка, , СтрокаТекста, КартинкаСохранения,
		СтатусОповещенияПользователя.Важное);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкуПользователяСервер(НазваниеНастройки, ЗначениеНастройки)

	РегистрыСведений.НастройкиПользователей.Установить(ЗначениеНастройки, НазваниеНастройки, ,
		КонтекстПечати.Организация);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуПоказыватьИнформациюПоНовойСхеме()

	РаботаСФормойДокумента.СохранитьВидимостьГруппыИнформации(ИмяФормы,
			"ПоказыватьИнформациюПоНовойСхемеРеквизитовПечати", Ложь, "ФормыОбработкиРеквизитыПечати");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаТекстаУсловий(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ЗафиксироватьИзменениеЗначенияРеквизита(ДополнительныеПараметры.ИмяРеквизитаУсловий);
		НастроитьПоляУсловий(ДополнительныеПараметры.ИмяРеквизитаУсловий, ДополнительныеПараметры.Условия,
			Результат);
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура НастроитьПоляУсловий(ИмяРеквизита, Условия, ТекстУсловий)

	ТекстУсловийШаблона = ПолучитьТекстУсловийШаблона(Условия);
	ИмяРеквизитаУсловий = ИмяРеквизита;
	КонтекстПечати[ИмяРеквизитаУсловий] = ТекстУсловий;		
	Если ТекстУсловийШаблона <> ТекстУсловий Тогда
		УстановитьВидПолеВвода(ИмяРеквизитаУсловий);
	Иначе
		УстановитьВидПолеНадписи(ИмяРеквизитаУсловий);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПолеВвода(ИмяРеквизитаУсловий)
	
	Элемент = Элементы[ИмяРеквизитаУсловий];
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.МногострочныйРежим = Истина;
	Элемент.Высота = 1;
	Элемент.Заголовок = НСтр("ru='Измененный текст условий'");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПолеНадписи(ИмяРеквизитаУсловий)
	
	Элемент = Элементы[ИмяРеквизитаУсловий];
	Элемент.Вид = ВидПоляФормы.ПолеНадписи;
	Если ИмяРеквизитаУсловий = "ТекстУсловийПриемки" Тогда
		ТекстЗаголовка = НСтр("ru='Текст условий приемки'");
	ИначеЕсли ИмяРеквизитаУсловий = "ТекстУсловийГарантийногоТалона" Тогда
		ТекстЗаголовка = НСтр("ru='Текст условий талона'");
	КонецЕсли;
	Элемент.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстУсловийШаблона(Условия)
	
	ТекстУсловий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Условия, "ТекстУсловий");
	Возврат ТекстУсловий;
	
КонецФункции

&НаСервере
Процедура ДополнитьРеквизитыТекстамиУсловий(ИзмененныеРеквизиты)
	
	Если КонтекстПечати.ТекстУсловийПриемки <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		КонтекстПечати.УсловияПриемки, "ТекстУсловий") Тогда
			ИзмененныеРеквизиты.Вставить("ТекстУсловийПриемки", 
			КонтекстПечати.ТекстУсловийПриемки);
	Иначе
		ИзмененныеРеквизиты.Вставить("ТекстУсловийПриемки", "");			
	КонецЕсли;	

	Если КонтекстПечати.ТекстУсловийГарантийногоТалона <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		КонтекстПечати.УсловияГарантии, "ТекстУсловий") Тогда
			ИзмененныеРеквизиты.Вставить("ТекстУсловийГарантийногоТалона", 
			КонтекстПечати.ТекстУсловийГарантийногоТалона);
	Иначе
		ИзмененныеРеквизиты.Вставить("ТекстУсловийГарантийногоТалона", "");			
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область Библиотеки

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ЗаписатьИзмененияИЗакрытьФорму(Команда);
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, КонтекстПечати);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, КонтекстПечати, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	Возврат; // работа типового метода не предусмотрена
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, КонтекстПечати);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
