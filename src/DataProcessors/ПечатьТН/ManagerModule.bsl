#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сформировать печатную форму
// Параметры:
//   МассивОбъектов - Массив - массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - структура дополнительных параметров печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы
//   ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//   ПараметрыВывода - Структура - .
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПредставлениеПФ = ПредставлениеПФ();
	ПечатнаяФорма = ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТН",
		ПредставлениеПФ, ПечатнаяФорма);

КонецПроцедуры

// Функция возвращает представление печатной формы транспортной накладной
// 
// Возвращаемое значение:
//   ПредставлениеПФ - строка
//
Функция ПредставлениеПФ() Экспорт

	Возврат НСтр("ru ='Транспортная накладная'")

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает табличный документ для печати ТН
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	Если ТипЗнч(МассивОбъектов) = Тип("Массив") И МассивОбъектов.Количество() > 0 Тогда

		ТекущийДокумент = МассивОбъектов[0];

	Иначе

		ТекущийДокумент = Неопределено;

	КонецЕсли;

	ТабличныйДокумент = Новый ТабличныйДокумент;
	Если ПараметрыПечати.Пункт0_2 < '20151203' Тогда

		Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТН.ПФ_MXL_ТранспортнаяНакладная");

	ИначеЕсли ПараметрыПечати.Пункт0_2 < '20171222' Тогда

		Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТН.ПФ_MXL_ТранспортнаяНакладная1208");

	ИначеЕсли ПараметрыПечати.Пункт0_2 < '20210101' Тогда

		Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТН.ПФ_MXL_ТранспортнаяНакладная1529");
	
	ИначеЕсли ПараметрыПечати.Пункт0_2 < '20220301' Тогда 
			
		Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТН.ПФ_MXL_ТранспортнаяНакладная2200");  
		
	Иначе 
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТН.ПФ_MXL_ТранспортнаяНакладная2116"); 
	
	КонецЕсли;

	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПечатьТН_ТН";
	
	// :::Лицевая
	ОбластьЧастьПервая = Макет.ПолучитьОбласть("ЧастьПервая");
	ОбластьЧастьПервая.Параметры.Заполнить(ПараметрыПечати);
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЧастьПервая,
		ТекущийДокумент);
	ТабличныйДокумент.Вывести(ОбластьЧастьПервая);

	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	// :::Оборотная
	ОбластьЧастьВторая = Макет.ПолучитьОбласть("ЧастьВторая");
	ОбластьЧастьВторая.Параметры.Заполнить(ПараметрыПечати);
	ТабличныйДокумент.Вывести(ОбластьЧастьВторая);

	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);

	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 5;
	ТабличныйДокумент.ПолеСнизу = 5;
	
	Возврат ТабличныйДокумент;

КонецФункции   

//Функция ФормаЗаполненияТН(ОбъектПечати) Экспорт 

//	Если ОбъектПечати.Дата < '20220301' Тогда
//		Возврат "Обработка.ПечатьТН.Форма");
//	Иначе
//		Возврат "Обработка.ПечатьТН.Форма.Форма2116";
//	КонецЕсли;	
//	
//КонецФункции

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Документ.Дата < '20220301' Тогда
		ВыбраннаяФорма = "Форма";
	Иначе
		ВыбраннаяФорма = "Форма2116";
	КонецЕсли;		

КонецПроцедуры

#КонецОбласти

#КонецЕсли
