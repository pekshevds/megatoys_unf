#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ИдентификаторПечатнойФормы() Экспорт

	Возврат "Спецификация";

КонецФункции

Функция КлючПараметровПечати() Экспорт

	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_Спецификация";

КонецФункции

Функция ПолныйПутьКМакету() Экспорт

	Возврат "Обработка.mega_ПечатьСпецификации.ПФ_MXL_Спецификация";

КонецФункции

Функция ПредставлениеПФ() Экспорт

	Возврат НСтр("ru ='Спецификация'");

КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати) Экспорт
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;

	Макет = УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент = ОписаниеПечатнойФормы.ТабличныйДокумент;
	
	НоменклатураОбъектовПечати = НоменклатураОбъектовПечати(ДанныеОбъектовПечати);
	ДанныеОбъектовПечати.Сбросить();
	
	ДополнительныеРеквизитыНоменклатуры = ДополнительныеРеквизитыНоменклатуры();
	ЗначенияДополнительныхРеквизитовНоменклатуры = ЗначенияДополнительныхРеквизитовНоменклатуры(
		НоменклатураОбъектовПечати, ДополнительныеРеквизитыНоменклатуры);
		
	Пока ДанныеОбъектовПечати.Следующий() Цикл 

		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Столбец");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Для Каждого ДополнительныйРеквизит Из ДополнительныеРеквизитыНоменклатуры Цикл 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Свойства");
			ОбластьМакета.Параметры.Свойство = ДополнительныйРеквизит.Свойство;
			ТабличныйДокумент.Присоединить(ОбластьМакета);			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Описание");
		ТабличныйДокумент.Присоединить(ОбластьМакета);
		
		Запасы = ДанныеОбъектовПечати.Выбрать();
		Пока Запасы.Следующий() Цикл 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|Столбец");
			ОбластьМакета.Параметры.Заполнить(Запасы);
			ТабличныйДокумент.Вывести(ОбластьМакета);    
						
			Для Каждого ДополнительныйРеквизит Из ДополнительныеРеквизитыНоменклатуры Цикл 
				
				Значение = "";
				ЗначенияДополнительныхРеквизитовНоменклатуры.Сбросить();
				Если ЗначенияДополнительныхРеквизитовНоменклатуры.НайтиСледующий(
					Новый Структура("Номенклатура,Свойство", Запасы.Номенклатура, ДополнительныйРеквизит.Свойство)) Тогда 
					
					Значение = ЗначенияДополнительныхРеквизитовНоменклатуры.Значение;
				КонецЕсли;
				
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Свойства");
				ОбластьМакета.Параметры.Значение = Значение;
				ТабличныйДокумент.Присоединить(ОбластьМакета);			
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|Описание");
			ОбластьМакета.Параметры.Описание = "";
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеОбъектовПечати.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоменклатураОбъектовПечати(ДанныеОбъектовПечати)
	
	Номенклатура = Новый Массив;
	
	Пока ДанныеОбъектовПечати.Следующий() Цикл 

		Запасы = ДанныеОбъектовПечати.Выбрать();
		Пока Запасы.Следующий() Цикл 
			
			Если Номенклатура.Найти(Запасы.Номенклатура) = Неопределено Тогда 
				
				Номенклатура.Добавить(Запасы.Номенклатура);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Номенклатура;
КонецФункции

Функция ДополнительныеРеквизитыНоменклатуры()
	Реквизиты = УправлениеСвойствамиСлужебный.СписокСвойствДляВидаОбъектов("Справочник_Номенклатура_Общие", "ДополнительныеРеквизиты");
	
	Возврат Реквизиты;
КонецФункции

Функция ЗначенияДополнительныхРеквизитовНоменклатуры(Номенклатура, ДополнительныеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеРеквизиты.Свойство КАК Свойство
	|ПОМЕСТИТЬ втДополнительныеРеквизиты
	|ИЗ
	|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураДополнительныеРеквизиты.Ссылка КАК Номенклатура,
	|	НоменклатураДополнительныеРеквизиты.Свойство КАК Свойство,
	|	НоменклатураДополнительныеРеквизиты.Значение КАК Значение
	|ПОМЕСТИТЬ втЗначенияДополнительныхРеквизитовНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|ГДЕ
	|	НоменклатураДополнительныеРеквизиты.Ссылка В(&Номенклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗначенияДополнительныхРеквизитовНоменклатуры.Номенклатура КАК Номенклатура,
	|	втДополнительныеРеквизиты.Свойство КАК Свойство,
	|	втЗначенияДополнительныхРеквизитовНоменклатуры.Значение КАК Значение
	|ИЗ
	|	втДополнительныеРеквизиты КАК втДополнительныеРеквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗначенияДополнительныхРеквизитовНоменклатуры КАК втЗначенияДополнительныхРеквизитовНоменклатуры
	|		ПО втДополнительныеРеквизиты.Свойство = втЗначенияДополнительныхРеквизитовНоменклатуры.Свойство";
	
	Запрос.УстановитьПараметр("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Результат = Запрос.Выполнить();
	Возврат Результат.Выбрать();
КонецФункции

#КонецОбласти

#КонецЕсли