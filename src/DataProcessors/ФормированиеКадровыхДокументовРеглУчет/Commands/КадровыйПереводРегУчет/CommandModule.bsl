
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ДанныеЗаполнения = ДанныеДокумента(ПараметрКоманды);
	Если ДанныеЗаполнения.Отказ Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ДанныеЗаполнения.СообщениеПользователю);
	ИначеЕсли ДанныеЗаполнения.Данные.Количество() = 1 Тогда
		ДанныеДокумента = ДанныеЗаполнения.Данные[0];
		ПараметрыФормы = Новый Структура;
		ДанныеДляЗаполнения = ДанныеДокумента;
		ДанныеДляЗаполнения.Вставить("Действие", "ЗаполнитьПоПараметрамЗаполнения");
    	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ДанныеДляЗаполнения);
		
		// АПК:65-выкл - при создании формы часть реквизитов заполняется текущими данными сотрудника независимо от переданных значений
		ФормаДокумента = ПолучитьФорму("Документ.КадровыйПеревод.ФормаОбъекта", ПараметрыФормы,
			ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		// АПК:65-вкл
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(ФормаДокумента, "Объект.ОбособленноеПодразделение",
																	ДанныеДокумента.Организация);
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(ФормаДокумента, "Объект.Подразделение",
																	ДанныеДокумента.Подразделение);
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(ФормаДокумента, "Объект.Должность",
																	ДанныеДокумента.Должность);
		ОткрытьФорму(ФормаДокумента, ПараметрыВыполненияКоманды.Окно);
	ИначеЕсли ДанныеЗаполнения.Данные.Количество() > 0 Тогда
		СписокДокументов = Новый Массив;
		СписокДокументов.Добавить(ПараметрКоманды);
		ПараметрыФормы = Новый Структура("КадровыеДокументыУпрУчет", СписокДокументов);
		ОткрытьФорму("Обработка.ФормированиеКадровыхДокументовРеглУчет.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не найдено данных для заполнения'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДанныеДокумента(СписокДокументов)
	Возврат Обработки.ФормированиеКадровыхДокументовРеглУчет.ДанныеДляЗаполненияПоУпрУчету(СписокДокументов);
КонецФункции

#КонецОбласти


