
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Номенклатура") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Параметры.Свойство("Режим", Режим);
	Для каждого ОписаниеНоменклатуры Из Параметры.Номенклатура Цикл
		НоваяСтрокаНоменклатура = СписокНоменклатуры.ПолучитьЭлементы().Добавить();
		НоваяСтрокаНоменклатура.Номенклатура = ОписаниеНоменклатуры.Номенклатура;
		НоваяСтрокаНоменклатура.Представление = Строка(НоваяСтрокаНоменклатура.Номенклатура);
		Для каждого ОписаниеДвижения Из ОписаниеНоменклатуры.Документы Цикл
			НоваяСтрокаДвижение = НоваяСтрокаНоменклатура.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДвижение, ОписаниеДвижения);
			НоваяСтрокаДвижение.Представление = Строка(НоваяСтрокаДвижение.Документ);
		КонецЦикла;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Поле = Элементы.СписокНоменклатурыПредставление
		И ТекущаяСтрока.ПолучитьРодителя() = Неопределено Тогда
		ПоказатьЗначение(, ТекущаяСтрока.Номенклатура);
	ИначеЕсли Поле = Элементы.СписокНоменклатурыПредставление Тогда
		ПоказатьЗначение(, ТекущаяСтрока.Документ);
	ИначеЕсли Поле = Элементы.СписокНоменклатурыЕдиницаИзмерения Тогда
		ПоказатьЗначение(, ТекущаяСтрока.ЕдиницаИзмерения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОбъект(Команда)
	
	ТекущаяСтрока = Элементы.СписокНоменклатуры.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТекущаяСтрока.ПолучитьРодителя() = Неопределено Тогда
		ПоказатьЗначение(, ТекущаяСтрока.Номенклатура);
	Иначе
		ПоказатьЗначение(, ТекущаяСтрока.Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

