
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ДатаРожденияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)

	ДатаТекстомКПреобразованию = СтрШаблон("%1 00:00:00", Текст);
	УказаннаяДата = Дата(ДатаТекстомКПреобразованию);
	
	ТекущийДень = ОбщегоНазначенияКлиент.ДатаСеанса();
	ВведенаДатаРожденияИзПрошлого = (ТекущийДень >= УказаннаяДата);

	Элементы.ФормаРаспечататьЗаявлениеНаВозврат.Доступность = ВведенаДатаРожденияИзПрошлого;
	
	Если НЕ ВведенаДатаРожденияИзПрошлого Тогда
	
		ТекстПредупреждения = НСтр("ru = 'Пожалуйста, укажите актуальную дату рождения.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПараметрыОткрытия(Параметры);
	Если НЕ ЗначениеЗаполнено(ДолжностьРуководителя) Тогда
	
		ДолжностьРуководителя = НСтр("ru = 'Директору'");
	
	КонецЕсли;
	
	ОбщегоНазначенияРМК.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РаспечататьЗаявлениеНаВозврат(Команда)
	СформироватьЗаявлениеНаВозврат();
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьЗаявлениеНаВозврат()
	
	ДанныеПолучателяЗаявления = СтрШаблон("%1 %2",
		ДолжностьРуководителя,
		Элементы.НаименованиеОрганизации.Заголовок);
		
	ИсходныеДанные = Новый Структура();
	ИсходныеДанные.Вставить("ФИОПокупателя", ФИО);
	ИсходныеДанные.Вставить("ДатаРождения", ДатаРождения);
	ИсходныеДанные.Вставить("УдостоверениеЛичности", УдостоверениеЛичности);
	ИсходныеДанные.Вставить("Кому", ДанныеПолучателяЗаявления);
	ИсходныеДанные.Вставить("ФИОРуководителя", ФИОРуководителя);
	Закрыть(ИсходныеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрыОткрытия(Параметры)

	Если НЕ Параметры = Неопределено Тогда
		
		Если Параметры.Свойство("ДисконтнаяКартаКлиента")
			И ЗначениеЗаполнено(Параметры.ДисконтнаяКартаКлиента) Тогда
			ЗаполнитьПерсональныеДанныеПоКартеКлиента(Параметры.ДисконтнаяКартаКлиента);
		КонецЕсли;
		
		Если Параметры.Свойство("Организация")
			И ЗначениеЗаполнено(Параметры.Организация) Тогда
			
			НаименованиеОрганизации = НСтр("ru = ''");
			ОбщегоНазначенияРМКПереопределяемый.ЗаполнитьКраткоеНаименованиеОрганизации(Параметры.Организация,
				НаименованиеОрганизации);
			Элементы.НаименованиеОрганизации.Заголовок = НаименованиеОрганизации;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерсональныеДанныеПоКартеКлиента(ДисконтнаяКартаКлиента)
	
	ОбщегоНазначенияРМКПереопределяемый.
		ЗаполнитьПерсональныеДанныеПоКартеКлиента(ДисконтнаяКартаКлиента, ФИО,ДатаРождения);
		
КонецПроцедуры

#КонецОбласти
