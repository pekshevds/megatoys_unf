#Область ОбработчикиСобытийФормы

&НаСервере

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивИдентификаторовВыбранныхСкидок = Новый Массив;
	Если Параметры.Свойство("МассивИдентификаторовВыбранныхСкидок") И ТипЗнч(Параметры.МассивИдентификаторовВыбранныхСкидок) = Тип("Массив") Тогда
		МассивИдентификаторовВыбранныхСкидок = Параметры.МассивИдентификаторовВыбранныхСкидок;
	КонецЕсли;
	Если Параметры.Свойство("МассивУправляемыхСкидок") И ТипЗнч(Параметры.МассивУправляемыхСкидок) = Тип("Массив") Тогда
		Для каждого СтрокаМассива Из Параметры.МассивУправляемыхСкидок Цикл
			СтрокаТаблицы = ТаблицаСкидок.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаМассива);
			СтрокаТаблицы.ПредставлениеТипаСкидки = СтрШаблон("%1, %2", СтрокаТаблицы.ТипСкидки, СтрокаТаблицы.ОбластьДействия);
			Если Не МассивИдентификаторовВыбранныхСкидок.Найти(СтрокаМассива.ИдентификаторСкидки) = Неопределено Тогда
				СтрокаТаблицы.Отметка = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТаблицаСкидокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗафиксироватьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСкидокОтметкаПриИзменении(Элемент)
	
	ВыделеннаяСтрока = Элементы.ТаблицаСкидок.ТекущиеДанные;
	Если ВыделеннаяСтрока.ТипСкидки = "Ручная" Тогда
		Для Каждого СтрокаУправляемойСкидки Из ТаблицаСкидок Цикл
			Если ВыделеннаяСтрока.Отметка
				И Не ВыделеннаяСтрока = СтрокаУправляемойСкидки
				И СтрокаУправляемойСкидки.ТипСкидки = "Ручная" 
				И СтрокаУправляемойСкидки.Отметка Тогда
				СтрокаУправляемойСкидки.Отметка = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ЗафиксироватьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗафиксироватьВыбор()
	
	ИдентификаторыСтрок = Элементы.ТаблицаСкидок.ВыделенныеСтроки;
	
	Если ИдентификаторыСтрок .Количество() = 0 Тогда
		Закрыть();
	Иначе
		МассивВыбранныхСкидок = Новый Массив;
		Для Каждого ИдентификаторВыбраннойСтроки Из ИдентификаторыСтрок Цикл
			СтрокаВыбраннойСкидки = ТаблицаСкидок.НайтиПоИдентификатору(ИдентификаторВыбраннойСтроки);
			Если СтрокаВыбраннойСкидки = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("ЗначениеСкидки", СтрокаВыбраннойСкидки.ЗначениеСкидки);
			СтруктураСтроки.Вставить("ИдентификаторСкидки", СтрокаВыбраннойСкидки.ИдентификаторСкидки);
			СтруктураСтроки.Вставить("ТипСкидки", СтрокаВыбраннойСкидки.ТипСкидки);
			СтруктураСтроки.Вставить("ОбластьДействия", СтрокаВыбраннойСкидки.ОбластьДействия);
			СтруктураСтроки.Вставить("СкидкаНаценка", СтрокаВыбраннойСкидки.СкидкаНаценка);
			МассивВыбранныхСкидок.Добавить(СтруктураСтроки);
		КонецЦикла;
		Закрыть(МассивВыбранныхСкидок);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти