
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Ширина", ШиринаИзображения);
	Параметры.Свойство("Высота", ВысотаИзображения);
	Параметры.Свойство("ПервоначальнаяВысота", ПервоначальнаяВысота);
	Параметры.Свойство("ПервоначальнаяШирина", ПервоначальнаяШирина);
	
	Если ШиринаИзображения И ВысотаИзображения Тогда
		ШиринаКВысоте = Окр(ШиринаИзображения / ВысотаИзображения, 2);
	КонецЕсли;
	
	СохранятьПропорции = Истина;
	
	ОтобразитьЭлементы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	
	Если СохранятьПропорции Тогда
		ВысотаИзображения = ШиринаИзображения / ШиринаКВысоте;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	
	Если СохранятьПропорции Тогда
		ШиринаИзображения = ВысотаИзображения * ШиринаКВысоте;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗакрытьФорму(ШиринаИзображения, ВысотаИзображения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПервоначальныйРазмер(Команда)
	
	ЗакрытьФорму(ПервоначальнаяШирина, ПервоначальнаяВысота);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФорму(Ширина, Высота)
	
	Результат = Новый Структура;
	Результат.Вставить("Ширина", Ширина);
	Результат.Вставить("Высота", Высота);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьЭлементы()
	
	Если ПервоначальнаяВысота = 0 Или ПервоначальнаяШирина = 0 Тогда
		Элементы.ПервоначальныйРазмер.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти