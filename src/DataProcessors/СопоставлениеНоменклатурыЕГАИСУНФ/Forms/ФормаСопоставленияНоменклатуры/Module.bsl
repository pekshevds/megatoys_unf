
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	
	ТаблицаАлкогольнойПродукции = СтруктураДанных.АлкогольнаяПродукция;
	ТаблицаНоменклатуры         = СтруктураДанных.Номенклатура;
	
	ТаблицаНеСопоставленнойАлкогольнойПродукции.Загрузить(ТаблицаАлкогольнойПродукции);
	ТаблицаНеСопоставленнойНоменклатуры.Загрузить(ТаблицаНоменклатуры);
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ИспользуютсяХарактеристики Тогда
		ПроверяемыеРеквизиты.Добавить("Характеристика");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	Элементы.ТаблицаНеСопоставленнойНоменклатуры.ТекущиеДанные.Характеристика = Характеристика;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНеСопоставленнойАлкогольнойПродукцииПриАктивизацииСтроки(Элемент)
	АлкогольнаяПродукция = Элементы.ТаблицаНеСопоставленнойАлкогольнойПродукции.ТекущиеДанные.АлкогольнаяПродукция;
	УстановитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНеСопоставленнойНоменклатурыПриАктивизацииСтроки(Элемент)
	ТекущиеДанные  = Элементы.ТаблицаНеСопоставленнойНоменклатуры.ТекущиеДанные;
	Номенклатура   = ТекущиеДанные.Номенклатура;
	Характеристика = ТекущиеДанные.Характеристика;
	ПриАктивацииСтрокиТаблицаНеСопоставленнойНоменклатурыНаСервере(Номенклатура);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьСоответствие(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьПредупреждения() Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьСоответствие_Завершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеПриЗавершении, НСтр("ru = 'Между связываемыми элементами есть расхождения. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
	Иначе
		ЗаписатьСоответствие_Завершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьСоответствиеНаСервере();
	
	ПараметрыЗакрытия = Новый Структура();
	ПараметрыЗакрытия.Вставить("СопоставлениеВыполнено", Истина);
	ПараметрыЗакрытия.Вставить("Номенклатура", Номенклатура);
	ПараметрыЗакрытия.Вставить("Характеристика", Характеристика);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоответствиеНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АлкогольнаяПродукция", ТаблицаНеСопоставленнойАлкогольнойПродукции.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Порядок) КАК Порядок
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция В(&АлкогольнаяПродукция)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого СтрокаАлкогольнаяПродукция Из ТаблицаНеСопоставленнойАлкогольнойПродукции Цикл
		
		Порядок = 0;

		СтруктураПоиска = Новый Структура("АлкогольнаяПродукция");
		СтруктураПоиска.АлкогольнаяПродукция = СтрокаАлкогольнаяПродукция.АлкогольнаяПродукция;
		
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если ТипЗнч(Выборка.Порядок) = Тип("Число") Тогда
				Порядок = Выборка.Порядок;
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого СтрокаНоменклатура Из ТаблицаНеСопоставленнойНоменклатуры Цикл 
			
			Запись = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьМенеджерЗаписи();
			Запись.АлкогольнаяПродукция = СтрокаАлкогольнаяПродукция.АлкогольнаяПродукция;
			Запись.Номенклатура         = СтрокаНоменклатура.Номенклатура;
			Запись.Характеристика       = СтрокаНоменклатура.Характеристика;
			Запись.Порядок              = Порядок + 1;
			Запись.Записать();
			
			Порядок = Запись.Порядок;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДополнитьРеквизитыНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.Характеристика.Доступность = ИспользуютсяХарактеристики;
	
	Если АлкогольнаяПродукция.Импортер.Пустая() Тогда
		Элементы.АлкогольнаяПродукцияИмпортер.Видимость = Ложь;
		Элементы.НоменклатураПроизводитель.Видимость = Ложь;
		Элементы.НоменклатураПроизводительИмпортерАлкогольнойПродукции.Заголовок = НСтр("ru = 'Производитель'");
	КонецЕсли;
	Элементы.ПредупреждениеОбъем.Видимость         = Номенклатура.ОбъемДАЛ <> 0 И Номенклатура.ОбъемДАЛ <> АлкогольнаяПродукция.Объем / 10;
	Элементы.ПредупреждениеКрепость.Видимость      = Номенклатура.Крепость <> 0 И Номенклатура.Крепость <> АлкогольнаяПродукция.Крепость;
	Элементы.ПредупреждениеВидПродукции.Видимость  = Номенклатура.ВидАлкогольнойПродукции <> АлкогольнаяПродукция.ВидПродукции;
	Элементы.ПредупреждениеПроизводитель.Видимость = Элементы.НоменклатураПроизводитель.Видимость
	                                                 И НЕ Номенклатура.Производитель.Пустая()
	                                                 И НЕ АлкогольнаяПродукция.Производитель.Пустая()
	                                                 И ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Производитель) <> Номенклатура.Производитель;
	Элементы.ПредупреждениеИмпортер.Видимость      = НЕ Номенклатура.ПроизводительИмпортерАлкогольнойПродукции.Пустая()
	                                                 И (НЕ АлкогольнаяПродукция.Импортер.Пустая()
	                                                    И ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Импортер) <> Номенклатура.ПроизводительИмпортерАлкогольнойПродукции
	                                                    ИЛИ НЕ АлкогольнаяПродукция.Производитель.Пустая()
	                                                        И АлкогольнаяПродукция.Импортер.Пустая()
	                                                        И ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Производитель) <> Номенклатура.ПроизводительИмпортерАлкогольнойПродукции);
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьПредупреждения()
	
	Возврат Элементы.ПредупреждениеОбъем.Видимость
		ИЛИ Элементы.ПредупреждениеКрепость.Видимость
		ИЛИ Элементы.ПредупреждениеВидПродукции.Видимость
		ИЛИ Элементы.ПредупреждениеПроизводитель.Видимость
		ИЛИ Элементы.ПредупреждениеИмпортер.Видимость;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтрагентаПоСоответствию(ОрганизацияЕГАИС)
	
	Если ОрганизацияЕГАИС.Пустая() Тогда
		Возврат Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Контрагент КАК Контрагент
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Сопоставлено
	|	И КлассификаторОрганизацийЕГАИС.Ссылка = &ОрганизацияЕГАИС";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Контрагент;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ДополнитьРеквизитыНоменклатуры()
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.КлассификаторАлкогольнойПродукцииЕГАИС");
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.Номенклатура");
		
		БлокировкаДанных.Заблокировать();
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Объем,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Крепость,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Производитель,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Импортер
		|ИЗ
		|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|ГДЕ
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", АлкогольнаяПродукция);
		
		Результат = Запрос.Выполнить();
		ВыборкаАлкогольнаяПродукция = Результат.Выбрать();
		
		Если ВыборкаАлкогольнаяПродукция.Следующий() Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Номенклатура.Ссылка,
			|	Номенклатура.ОбъемДАЛ,
			|	Номенклатура.Крепость,
			|	Номенклатура.ВидАлкогольнойПродукции,
			|	Номенклатура.Производитель,
			|	Номенклатура.ПроизводительИмпортерАлкогольнойПродукции
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Ссылка = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Номенклатура);
			
			Результат = Запрос.Выполнить();
			ВыборкаНоменклатура = Результат.Выбрать();
			
			Если ВыборкаНоменклатура.Следующий() Тогда
				
				НеобходимостьЗаписи = Ложь;
				НоменклатураОбъект = Номенклатура.ПолучитьОбъект();
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.ОбъемДАЛ) Тогда
					НоменклатураОбъект.ОбъемДАЛ = ВыборкаАлкогольнаяПродукция.Объем / 10;
					НеобходимостьЗаписи = Истина;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.Крепость) Тогда
					НоменклатураОбъект.Крепость = ВыборкаАлкогольнаяПродукция.Крепость;
					НеобходимостьЗаписи = Истина;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.ВидАлкогольнойПродукции) Тогда
					НоменклатураОбъект.ВидАлкогольнойПродукции = ВыборкаАлкогольнаяПродукция.ВидПродукции;
					НеобходимостьЗаписи = Истина;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.Производитель) Тогда
					НоменклатураОбъект.Производитель = ПолучитьКонтрагентаПоСоответствию(ВыборкаАлкогольнаяПродукция.Производитель);
					НеобходимостьЗаписи = Истина;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатура.ПроизводительИмпортерАлкогольнойПродукции) Тогда
					НоменклатураОбъект.ПроизводительИмпортерАлкогольнойПродукции = ПолучитьКонтрагентаПоСоответствию(ВыборкаАлкогольнаяПродукция.Импортер);
					НеобходимостьЗаписи = Истина;
				КонецЕсли;
				
				Если НеобходимостьЗаписи Тогда
					
					НоменклатураОбъект.Записать();
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПриАктивацииСтрокиТаблицаНеСопоставленнойНоменклатурыНаСервере(Номенклатура)
	ИспользуютсяХарактеристики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ИспользоватьХарактеристики");
	УстановитьВидимостьЭлементовФормы();
КонецПроцедуры

#КонецОбласти
