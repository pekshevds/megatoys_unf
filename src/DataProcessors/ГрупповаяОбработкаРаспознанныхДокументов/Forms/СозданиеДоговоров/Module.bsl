
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ДанныеСтроки Из Параметры.ДанныеСоздания Цикл
		СтрокаТаблицы = ТаблицаДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеСтроки, , "ИдСтрокДокументов, РаспознанныеДокументы");
		Если ПустаяСтрока(СтрокаТаблицы.Наименование) Тогда
			СтрокаТаблицы.Наименование = СтрокаТаблицы.ВидДоговора;
		КонецЕсли;
		СтрокаТаблицы.ИдСтрокДокументов.ЗагрузитьЗначения(ДанныеСтроки.ИдСтрокДокументов);
		СтрокаТаблицы.РаспознанныеДокументы.ЗагрузитьЗначения(ДанныеСтроки.РаспознанныеДокументы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКонтрагентов

&НаКлиенте
Процедура ТаблицаКонтрагентовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьВсе(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеСоздания", ВсеДоговоры());
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВсеДоговоры()
	
	Таблица = ТаблицаДоговоров.Выгрузить();               
	
	РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(Таблица.Колонки.Номер.Имя, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(Таблица.Колонки.Дата.Имя, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	
	Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(Таблица);
	Для Каждого СтрокаРезультата Из Результат Цикл
		СтрокаРезультата.ИдСтрокДокументов = СтрокаРезультата.ИдСтрокДокументов.ВыгрузитьЗначения();
		СтрокаРезультата.РаспознанныеДокументы = СтрокаРезультата.РаспознанныеДокументы.ВыгрузитьЗначения();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


