
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, 
		"ВариантПоиска, ДополнительныйРеквизитПоиска, ИскатьПоАртикулу, ИскатьПоДополнительномуРеквизиту, 
		|ИскатьПоКоду, ИскатьПоНаименованию, ИскатьПоНаименованиюЛевойЧасти, ИскатьПоНаименованиюПолному,
		|ИскатьПоНаименованиюПолномуЛевойЧасти, ИскатьПоШтрихкоду, НаборСвойствДляДополнительногоРеквизита, 
		|КудаЗагружаемДопРеквизиты");
	
	УстановитьВидимостьДоступностьРеквизитов();
	
	Набор_Справочник_Номенклатура_Общие = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РеквизитПоискаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("Отбор", Новый Структура("НаборСвойств", Набор_Справочник_Номенклатура_Общие));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормыВыбораПодРеквизита", ЭтотОбъект);
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаВыбора", ПараметрыФормыВыбора,
		ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьПоДополнительномуРеквизитуПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РеквизитПоиска", "Доступность",
		ИскатьПоДополнительномуРеквизиту);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	РезультатНастройки = Новый Структура;
	РезультатНастройки.Вставить("ВариантПоиска",	ВариантПоиска);
	РезультатНастройки.Вставить("ИскатьПоКоду",		ИскатьПоКоду);
	РезультатНастройки.Вставить("ИскатьПоШтрихкоду",ИскатьПоШтрихкоду);
	РезультатНастройки.Вставить("ИскатьПоАртикулу", ИскатьПоАртикулу);
	РезультатНастройки.Вставить("ИскатьПоНаименованию",	ИскатьПоНаименованию);
	РезультатНастройки.Вставить("ИскатьПоНаименованиюЛевойЧасти", ИскатьПоНаименованиюЛевойЧасти);
	РезультатНастройки.Вставить("ИскатьПоНаименованиюПолному",	ИскатьПоНаименованиюПолному);
	РезультатНастройки.Вставить("ИскатьПоНаименованиюПолномуЛевойЧасти", ИскатьПоНаименованиюПолномуЛевойЧасти);
	РезультатНастройки.Вставить("ИскатьПоДополнительномуРеквизиту", ИскатьПоДополнительномуРеквизиту);
	РезультатНастройки.Вставить("ДополнительныйРеквизитПоиска", ДополнительныйРеквизитПоиска);
	РезультатНастройки.Вставить("КудаЗагружаемДопРеквизиты", КудаЗагружаемДопРеквизиты);
	
	Закрыть(РезультатНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьРеквизитов()
	
	ВидимостьГруппыДопРеквизита = ЗначениеЗаполнено(НаборСвойствДляДополнительногоРеквизита);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаДополнительныйРеквизит", "Видимость", ВидимостьГруппыДопРеквизита);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РеквизитПоиска", "Доступность", ИскатьПоДополнительномуРеквизиту);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыВыбораПодРеквизита(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ДополнительныйРеквизитПоиска = Результат;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти