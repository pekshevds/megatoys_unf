#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьСвойстваФормы();
	
	РазрешенРасширенныйРежимНастройки =
		ИнтеграцияУРМКПереопределяемый.РазрешенРасширенныйРежимНастройкиОбменаУРМК();
		
	Если Не РазрешенРасширенныйРежимНастройки Тогда
		ИспользоватьРасширенныйРежимНастройкиУРМК = Ложь;
		УпрощенныйРежимНастройкиУРМКПриИзмененииНаСервере();
		
	КонецЕсли;
	
	ИспользоватьРасширенныйРежимНастройкиУРМК =
		ИнтеграцияУРМКСлужебный.ИспользоватьРасширенныйРежимНастройкиУРМК();
		
	БлокировкаСнятияРасширенногоРежима = БлокировкаСнятияРасширенногоРежимаНаСервере();
		
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьФорму();
		
КонецПроцедуры      

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеОтборовНаУзле" Тогда
		БлокировкаСнятияРасширенногоРежима = БлокировкаСнятияРасширенногоРежимаНаСервере();
		НастроитьФорму();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УпрощенныйРежимНастройкиУРМКПриИзменении(Элемент)
	
	УпрощенныйРежимНастройкиУРМКПриИзмененииНаСервере();
	
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНажатие(Элемент)
	
	Если Элемент.Имя = "НастройкиУРМК" Тогда
		
		ОткрытьФорму("Обработка.ПомощникНастройкиОбменаУРМК.Форма.ПомощникНастройкиОбменаДанными");
		
	ИначеЕсли Элемент.Имя = "СпособыОплаты" Тогда

		УстановитьОпцииДляРаботыПодсистемы();
		
		ТипыПлатежныхСистем = Новый Массив;
		ТипыПлатежныхСистем.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.СБП"));
		ТипыПлатежныхСистем.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСпособовОплат.ЮKassa"));

		ТипыПлатежныхСистем = Новый ФиксированныйМассив(ТипыПлатежныхСистем);
		Отбор 				= Новый Структура("Тип", ТипыПлатежныхСистем);
		
		ОткрытьФорму("Справочник.СпособыОплаты.Форма.ФормаСписка", Новый Структура("РежимПлатежнойСистемы, Отбор", Истина, Отбор));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УРМКНажатие(Элемент)
	ОткрытьФорму("Справочник.УниверсальныеРабочиеМестаКассиров.ФормаСписка");
КонецПроцедуры

#КонецОбласти

#Область ИнициализацияФормыПриСоздании

&НаСервере
Процедура ИнициализироватьСвойстваФормы()
	
	Заголовок = НСтр("ru = 'Режим настройки обмена с 1С:РМК'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УпрощенныйРежимНастройкиУРМКПриИзмененииНаСервере()
	
	Константы.ИспользоватьРасширенныйРежимНастройкиУРМК.Установить(ИспользоватьРасширенныйРежимНастройкиУРМК);
	
КонецПроцедуры

&НаСервере
Функция БлокировкаСнятияРасширенногоРежимаНаСервере()
	Возврат ИнтеграцияУРМКСлужебный.БлокировкаСнятияРасширенногоРежима();
КонецФункции
	
&НаСервере
Процедура УстановитьОпцииДляРаботыПодсистемы()
	ИнтеграцияСПлатежнымиСистемамиУНФ.УстановитьОпцииДляРаботыПодсистемы();
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыИФункции

&НаКлиенте
Процедура НастроитьФорму()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПолеФлажкаРасширенныйРежим",
		"Доступность",
		 Не БлокировкаСнятияРасширенногоРежима);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Прочее",
		"Видимость",
		ИспользоватьРасширенныйРежимНастройкиУРМК);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаДекорацияКомментарийДоступности",
		"Видимость",
		БлокировкаСнятияРасширенногоРежима);

	КомментарийДоступности =
		НСтр("ru = 'Отключение расширенного режима настройки недоступно, потому что:
		|- существуют Универсальные рабочие места с двумя подключенными кассами ККМ.'");

	Если БлокировкаСнятияРасширенногоРежима Тогда
		Элементы.ДекорацияКомментарийДоступности.Подсказка = КомментарийДоступности;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКомментарийДоступностиНажатие(Элемент)
	
	ТекстСообщения = Элемент.Подсказка;
	ПоказатьПредупреждение(, ТекстСообщения);

КонецПроцедуры

#КонецОбласти
