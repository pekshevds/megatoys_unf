
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДоступныхАбонентов();
	ПрочитатьНастройкиСвязей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиСвязей();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступныхАбонентов()
	
	ДоступныеАбоненты = ПрограммныйИнтерфейсСервиса.Абоненты();
	Для каждого Абонент Из ДоступныеАбоненты Цикл
		НоваяСтрока = СвязьКодаАбонентаСправочникОрганизации.Добавить();
		НоваяСтрока.КодАбонента = Абонент.Код;
		НоваяСтрока.ПредставлениеАбонента = Абонент.Наименование;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиСвязей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвязьАбонентСервисаОрганизация.КодАбонента КАК КодАбонента,
	|	СвязьАбонентСервисаОрганизация.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.СвязьАбонентСервисаОрганизация КАК СвязьАбонентСервисаОрганизация";
	СвязьОрганизацияАбонент = Запрос.Выполнить().Выбрать();
	Пока СвязьОрганизацияАбонент.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("КодАбонента", СвязьОрганизацияАбонент.КодАбонента);
		Строки = СвязьКодаАбонентаСправочникОрганизации.НайтиСтроки(ПараметрыОтбора);
		Для каждого Строка Из Строки Цикл
			Строка.Организация = СвязьОрганизацияАбонент.Организация;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиСвязей()
	
	СвязьОрганизацияАбонент = РегистрыСведений.СвязьАбонентСервисаОрганизация.СоздатьНаборЗаписей();
	Для каждого Связь Из СвязьКодаАбонентаСправочникОрганизации Цикл
		Если ЗначениеЗаполнено(Связь.Организация) Тогда
			НоваяСтрока = СвязьОрганизацияАбонент.Добавить();
			НоваяСтрока.КодАбонента = Связь.КодАбонента;
			НоваяСтрока.Организация = Связь.Организация;
		КонецЕсли;
	КонецЦикла;
	СвязьОрганизацияАбонент.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
