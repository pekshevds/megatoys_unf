#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(МассивСсылок, ПараметрыВыполненияКоманды)
	ИдентификаторыСообщений = ИдентификаторыСообщенийСтрахователей(МассивСсылок);
	СЭДОФССКлиент.ПовторноПолучитьСообщенияИзФСС(ИдентификаторыСообщений);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИдентификаторыСообщенийСтрахователей(Знач МассивСсылок)
	ДанныеСсылок = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивСсылок,
		"Страхователь, ИдентификаторСообщения",
		Истина);
	
	Результат = Новый Соответствие;
	Для Каждого КлючИЗначение Из ДанныеСсылок Цикл
		ДанныеДокумента = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(ДанныеДокумента.Страхователь) Тогда
			Текст = НСтр("ru = 'Не заполнено поле ""Страхователь""'");
			СообщенияБЗК.СообщитьОПроблеме(Текст, КлючИЗначение.Ключ, "Страхователь");
			Продолжить;
		КонецЕсли;
		ИдентификаторыСообщений = Результат[ДанныеДокумента.Страхователь];
		Если ИдентификаторыСообщений = Неопределено Тогда
			ИдентификаторыСообщений = Новый Массив;
			Результат.Вставить(ДанныеДокумента.Страхователь, ИдентификаторыСообщений);
		КонецЕсли;
		Если ИдентификаторыСообщений.Найти(ДанныеДокумента.ИдентификаторСообщения) = Неопределено Тогда
			ИдентификаторыСообщений.Добавить(ДанныеДокумента.ИдентификаторСообщения);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти
