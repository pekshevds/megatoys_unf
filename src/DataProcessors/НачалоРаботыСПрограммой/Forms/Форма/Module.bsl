
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	КэшироватьСвойстваЭлементовФормы();
	ДобавитьОрганизацию(Ложь, Справочники.Организации.ОсновнаяОрганизация);
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("Пользователь", Пользователи.АвторизованныйПользователь());
	КэшЗначений.Вставить("РазрешитьЗакрытие", Ложь);
	КэшЗначений.Вставить("ДополнительныйПараметр",
		НачалоРаботыСПрограммойКлиентСервер.ЗначениеДополнительногоПараметра());
	КэшЗначений.Вставить("Доходы", Перечисления.ВидыОбъектовНалогообложения.Доходы);
	КэшЗначений.Вставить("ДоходыМинусРасходы", Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы);
	КэшЗначений.Вставить("ЮрЛицо", Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	КэшЗначений.Вставить("ФизЛицо", Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	КэшЗначений.Вставить("ТекущийИндексОрганизации", Неопределено);
	КэшЗначений.Вставить("ПараметрыНачалаРаботы", Новый Структура);
	КэшЗначений.ПараметрыНачалаРаботы.Вставить("РазделениеВключено", ОбщегоНазначения.РазделениеВключено());
	КэшЗначений.ПараметрыНачалаРаботы.Вставить("ВерсияДанныхОсновнойКонфигурации", Метаданные.Версия);
	
	ЗаполнитьДанныеПользователя();
	
	ПодключенаИПП = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	Элементы.ГруппаПользовательЛокально.Видимость = Не РазделениеВключено;
	Элементы.ГруппаПользовательСервис.Видимость = РазделениеВключено;
	
	ЭтоФизическоеЛицо = (Справочники.Организации.ОсновнаяОрганизация.ЮридическоеФизическоеЛицо = КэшЗначений.ФизЛицо);
	
	НастроитьФормуОграниченияКонфигурации();
	НастроитьФормуМобильноеПриложение();
	
	ЦветФонУправляющегоПоля = Метаданные.ЭлементыСтиля.ФонУправляющегоПоля.Значение;
	ЦветФонаКнопки = Элементы.ВидБизнесаОптоваяТорговля.ЦветФона;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой(ЭтотОбъект, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Файл" Тогда
		
		СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(КэшЗначений.ТекущийИндексОрганизации);
		СтрокаОрганизации.ФайлЛоготип = ?(ТипЗнч(Источник) = Тип("Массив"), Источник[0], Источник);
		
		ОбновитьИзображениеЛоготипа(СтрокаОрганизации);
		
		КэшЗначений.ТекущийИндексОрганизации = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы <> Истина Тогда
		
		Если НЕ КэшЗначений.РазрешитьЗакрытие Тогда
			
			ТекстПредупреждения = НСтр("ru ='Заполните, пожалуйста, обязательные поля.
				|Работа программы без указанных сведений затруднительна.'");
				
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЕстьАУСНПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьАУСН");
	ЗначениеЕстьАУСН = ЭтотОбъект["ЕстьАУСН" + Индекс];
	ЗначениеСтавкаНалога = 8; 
	ЗначениеНалогообложение = КэшЗначений.Доходы;
	
	ЗаписатьЗначениеОрганизации("ЕстьАУСН", ЗначениеЕстьАУСН, Индекс);
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс, Истина);
	
	ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога; 
	ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс] = ЗначениеНалогообложение;
	
	УправлениеФормой(ЭтотОбъект, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьУСНПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьУСН");
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс];   
	ЗначениеЕстьАУСН = ЭтотОбъект["ЕстьАУСН" + Индекс];
	ЗначениеСтавкаНалога = 6;
	ЗначениеНалогообложение = КэшЗначений.Доходы;
	
	ЗаписатьЗначениеОрганизации("ЕстьУСН", ЗначениеЕстьУСН, Индекс);
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс, Истина);
	ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
	
	ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
	ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс] = ЗначениеНалогообложение;
	
	УправлениеФормой(ЭтотОбъект, Индекс);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЕстьПатентПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьПатент");
	ЗначениеЕстьПатент = ЭтотОбъект["ЕстьПатент" + Индекс];
	ЗаписатьЗначениеОрганизации("ЕстьПатент", ЗначениеЕстьПатент, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЮридическоеФизическоеЛицо");
	ЗначениеЮридическоеФизическоеЛицо = ЭтотОбъект["ЮридическоеФизическоеЛицо" + Индекс];
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс]; 
	ЗначениеЕстьАУСН = ЭтотОбъект["ЕстьАУСН" + Индекс];
	
	ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮридическоеФизическоеЛицо, ЗначениеЕстьУСН, ЗначениеЕстьАУСН, Индекс);
	
	ЭтотОбъект["ИНН" + Индекс] = "";
	ЗаписатьЗначениеОрганизации("ИНН", "", Индекс);
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс);
	УправлениеФормой(ЭтотОбъект, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "Наименование");
	ЗначениеНаименование = ЭтотОбъект["Наименование" + Индекс];
	
	ЗаписатьЗначениеОрганизации("Наименование", ЗначениеНаименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", ЗначениеНаименование, Индекс);
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ИПФамилияПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Фамилия");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Фамилия", ЭтотОбъект["ИП_Фамилия" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура ИПИмяПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Имя");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Имя", ЭтотОбъект["ИП_Имя" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура ИПОтчествоПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Отчество");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Отчество", ЭтотОбъект["ИП_Отчество" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура НалогообложениеПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ВидыОбъектовНалогообложения");
	ЗначениеНалогообложение = ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс];
	ЗначениеЕстьАУСН = ЭтотОбъект["ЕстьАУСН" + Индекс];
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс];
	ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
	
	Если ЗначениеЕстьАУСН Тогда
		ЗначениеСтавкаНалога = ?(ЗначениеНалогообложение = КэшЗначений.Доходы, 8, 20);
	ИначеЕсли ЗначениеЕстьУСН Тогда
		ЗначениеСтавкаНалога = ?(ЗначениеНалогообложение = КэшЗначений.Доходы, 6, 15);
	Иначе
		ЗначениеСтавкаНалога = 0;
	КонецЕсли;
	
	ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "СтавкаНалога");
	ЗначениеСтавкаНалога = ЭтотОбъект["СтавкаНалога" + Индекс];
	
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьСотрудникиПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИПИспользуетТрудНаемныхРаботников");
	ЗначениеЕстьСотрудники = ЭтотОбъект["ИПИспользуетТрудНаемныхРаботников" + Индекс];
	
	ЗаписатьЗначениеОрганизации("ИПИспользуетТрудНаемныхРаботников", ЗначениеЕстьСотрудники, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЛоготипаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИндексОрганизации = ИндексПоИмениЭлемента(Элемент.Имя, "АдресЛоготипа");
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
	ЗначениеФайлЛоготипа = СтрокаОрганизации.ФайлЛоготип;
	
	Если ЗначениеЗаполнено(ЗначениеФайлЛоготипа) Тогда
		
		ПросмотретьПрисоединенныйФайл(ЗначениеФайлЛоготипа);
		
	Иначе
		
		ДобавитьИзображениеНаКлиенте(ИндексОрганизации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(Элемент.Имя, "ИНН");
	
	ПроверитьИНН(ИндексОрганизации);
	УправлениеФормой(ЭтотОбъект, ИндексОрганизации);
	ЗаполнитьРеквизитыПоИНН(ИндексОрганизации);
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ИННИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(Элемент.Имя, "ИНН");
	
	ТекстИНН = СокрЛП(Текст);
	ДлинаИНН = СтрДлина(ТекстИНН);
	Если ДлинаИНН <> 0 И ДлинаИНН <> 10 И ДлинаИНН <> 12 Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект["ИНН" + ИндексОрганизации] = ТекстИНН;
	ПроверитьИНН(ИндексОрганизации);
	УправлениеФормой(ЭтотОбъект, ИндексОрганизации);
	ЗаполнитьРеквизитыПоИНН(ИндексОрганизации);
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИНН(ИндексОрганизации)
	
	ЭтотОбъект["ИННОшибкаВвода" + ИндексОрганизации] = "";
	ЗначениеИНН = ЭтотОбъект["ИНН" + ИндексОрганизации];
	Если Не ЗначениеЗаполнено(ЗначениеИНН) Тогда
		Возврат;
	КонецЕсли;
	
	ДлинаИНН = СтрДлина(СокрЛП(ЗначениеИНН));
	Если ДлинаИНН = 12 Тогда
		ЭтоЮрЛицо = Ложь;
	ИначеЕсли ДлинаИНН = 10 Тогда
		ЭтоЮрЛицо = Истина;
	Иначе
		ЭтоЮрЛицо = ЭтотОбъект["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = КэшЗначений.ЮрЛицо;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	ИННВведенКорректно = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ЗначениеИНН, ЭтоЮрЛицо, ОписаниеОшибки);
	
	Если Не ИННВведенКорректно Тогда
		ЭтотОбъект["ИННОшибкаВвода" + ИндексОрганизации] = ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоИНН(ИндексОрганизации)
	Перем Ошибки;
	
	ЗначениеИНН = ЭтотОбъект["ИНН" + ИндексОрганизации];
	ЗаписатьЗначениеОрганизации("ИНН", ЗначениеИНН, ИндексОрганизации);
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + ИндексОрганизации];
	ЗначениеЕстьАУСН = ЭтотОбъект["ЕстьАУСН" + ИндексОрганизации];
	
	ИННВведенКорректно = ПустаяСтрока(ЭтотОбъект["ИННОшибкаВвода" + ИндексОрганизации]);
	Если Не ИННВведенКорректно Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеЮрФизЛицо = ?(СтрДлина(ЗначениеИНН) = 12, КэшЗначений.ФизЛицо, КэшЗначений.ЮрЛицо);
	ЭтотОбъект["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = ЗначениеЮрФизЛицо;
	
	ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, ЗначениеЕстьАУСН, ИндексОрганизации);
	
	Если Не ПодключенаИПП Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеГосРеестра = Новый Структура;
	ЗаполнитьРеквизитыПоИНННаСервере(ЗначениеИНН, ДанныеГосРеестра, ИндексОрганизации, Ошибки);
	
	// Стартовое окно не должно сообщать о существующих ошибках в учетных данных. Цель быстро собрать сведения, 
	// если они ошибочные - продолжить работу.
	
	СведенияПолучены = Ложь;
	Если ДанныеГосРеестра.Свойство("СведенияПолучены") Тогда
		СведенияПолучены = ДанныеГосРеестра.СведенияПолучены;
	КонецЕсли;
	
	Если СведенияПолучены Тогда
		
		ДанныеСтрокиОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
		
		Если ДанныеГосРеестра.Свойство("Наименование") 
			И НЕ ПустаяСтрока(ДанныеГосРеестра.Наименование) Тогда
			
			ЭтотОбъект["Наименование" + ИндексОрганизации] = ДанныеГосРеестра.Наименование;
			ДанныеСтрокиОрганизации.Наименование = ДанныеГосРеестра.Наименование;
			
		КонецЕсли;
		
		Если ДанныеГосРеестра.Свойство("КПП") Тогда
			
			ДанныеСтрокиОрганизации.КПП = ДанныеГосРеестра.КПП;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.НаименованиеПолное = ДанныеГосРеестра.НаименованиеПолное;
		ДанныеСтрокиОрганизации.ОГРН = ДанныеГосРеестра.ОГРН;
		ДанныеСтрокиОрганизации.КодОКВЭД = ДанныеГосРеестра.КодОКВЭД;
		ДанныеСтрокиОрганизации.АдресЮридический = ДанныеГосРеестра.ЮридическийАдрес;
		ДанныеСтрокиОрганизации.АдресФактический = ДанныеГосРеестра.ЮридическийАдрес;
		ДанныеСтрокиОрганизации.Телефон = ДанныеГосРеестра.Телефон;
		
		Если НЕ ДанныеГосРеестра.ЭтоЮрЛицо Тогда
			
			ДанныеСтрокиОрганизации.СвидетельствоСерияНомер = ДанныеГосРеестра.СвидетельствоСерияНомер;
			ДанныеСтрокиОрганизации.СвидетельствоДатаВыдачи = ДанныеГосРеестра.СвидетельствоДатаВыдачи;
			
			ДанныеСтрокиОрганизации.Фамилия = ДанныеГосРеестра.Фамилия;
			ЭтотОбъект["ИП_Фамилия" + ИндексОрганизации] = ДанныеГосРеестра.Фамилия;
			
			ДанныеСтрокиОрганизации.Имя = ДанныеГосРеестра.Имя;
			ЭтотОбъект["ИП_Имя" + ИндексОрганизации] = ДанныеГосРеестра.Имя;
			
			ДанныеСтрокиОрганизации.Отчество = ДанныеГосРеестра.Отчество;
			ЭтотОбъект["ИП_Отчество" + ИндексОрганизации] = ДанныеГосРеестра.Отчество;
			
			ДанныеСтрокиОрганизации.Пол = ДанныеГосРеестра.Пол;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.КодНалоговогоОргана = ДанныеГосРеестра.КодНалоговогоОрганаПолучателя;
		ДанныеСтрокиОрганизации.КодНалоговогоОрганаПолучателя = ДанныеГосРеестра.КодНалоговогоОрганаПолучателя;
		
		Если ДанныеГосРеестра.Свойство("РегистрацияВПенсионномФонде")
			И ЗначениеЗаполнено(ДанныеГосРеестра.РегистрацияВПенсионномФонде) Тогда
			
			ДанныеСтрокиОрганизации.КодОрганаПФР = ДанныеГосРеестра.РегистрацияВПенсионномФонде.КодОрганаПФР;
			
			Если ДанныеГосРеестра.ЭтоЮрЛицо Тогда
				
				ДанныеСтрокиОрганизации.РегистрационныйНомерПФР = ДанныеГосРеестра.РегистрационныйНомерПФР;
				
			Иначе
				
				ДанныеСтрокиОрганизации.РегистрационныйНомерПФР_ИП = ДанныеГосРеестра.РегистрационныйНомерПФР_ИП;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.КодОрганаФСС = ДанныеГосРеестра.КодОрганаФСС;
		ДанныеСтрокиОрганизации.НаименованиеТерриториальногоОрганаФСС = ДанныеГосРеестра.НаименованиеТерриториальногоОрганаФСС;
		ДанныеСтрокиОрганизации.РегистрационныйНомерФСС = ДанныеГосРеестра.РегистрационныйНомерФСС;
		ДанныеСтрокиОрганизации.НаименованиеПлательщикаПриПеречисленииНалогов = ДанныеГосРеестра.НаименованиеПлательщикаПриПеречисленииНалогов;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПодтверждениеПриИзменении(Элемент)
	
	Если ПользовательПароль <> ПользовательПарольПодтверждение Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru ='Не совпадают пароли'"), , "ПользовательПарольПодтверждение");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаВосстановлениеПароляНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуВосстановленияПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаРегистрацияНажатие(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуРегистрацииНовогоПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "СистемаНалогообложения");
	ЗначениеСистемаНалогообложения = ЭтотОбъект["СистемаНалогообложения" + Индекс];
	ЗаписатьЗначениеОрганизации("СистемаНалогообложения", ЗначениеСистемаНалогообложения, Индекс);
	
	Если ЗначениеСистемаНалогообложения = 0 Тогда
		ЗаписатьЗначениеОрганизации("ЕстьУСН", Ложь, Индекс);
		ЗаписатьЗначениеОрганизации("ЕстьАУСН", Ложь, Индекс);
		ЭтотОбъект["ЕстьУСН" + Индекс] = Ложь;     
		ЭтотОбъект["ЕстьАУСН" + Индекс] = Ложь;
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс);
	ИначеЕсли ЗначениеСистемаНалогообложения = 1 Тогда
		ЭтотОбъект["ЕстьУСН" + Индекс] = Истина;
		ЗаписатьЗначениеОрганизации("ЕстьУСН", Истина, Индекс); 
		ЭтотОбъект["ЕстьАУСН" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьАУСН", Ложь, Индекс);
		ЗначениеСтавкаНалога = 6;
		ЗначениеНалогообложение = КэшЗначений.Доходы;
		ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс, Истина);
		ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
		
		ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
		ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс] = ЗначениеНалогообложение;
		
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс); 
	ИначеЕсли ЗначениеСистемаНалогообложения = 4 Тогда	
		ЭтотОбъект["ЕстьАУСН" + Индекс] = Истина;
		ЭтотОбъект["ЕстьУСН" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьУСН", Ложь, Индекс);
		ЗаписатьЗначениеОрганизации("ЕстьАУСН", Истина, Индекс);
		ЗначениеСтавкаНалога = 8; 
		ЗначениеНалогообложение = КэшЗначений.Доходы;		
		ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс, Истина);
		ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
		
		ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
		ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс] = ЗначениеНалогообложение;
		
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс);
	ИначеЕсли ЗначениеСистемаНалогообложения = 2 Тогда
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс);
		ЗаписатьЗначениеОрганизации("ЕстьУСН", Ложь, Индекс); 
		ЗаписатьЗначениеОрганизации("ЕстьАУСН", Ложь, Индекс);
		ЭтотОбъект["ЕстьУСН" + Индекс] = Ложь;  
		ЭтотОбъект["ЕстьАУСН" + Индекс] = Ложь;
	ИначеЕсли ЗначениеСистемаНалогообложения = 3 Тогда
		ЭтотОбъект["ЕстьПатент" + Индекс] = Истина;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Истина, Индекс);
		ЗаписатьЗначениеОрганизации("ЕстьУСН", Ложь, Индекс); 
		ЗаписатьЗначениеОрганизации("ЕстьАУСН", Ложь, Индекс);
		ЭтотОбъект["ЕстьУСН" + Индекс] = Ложь;  
		ЭтотОбъект["ЕстьАУСН" + Индекс] = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект, Индекс);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКоманд

&НаКлиенте
Процедура НачатьРаботуСПрограммой(Команда)
	
	ОчиститьСообщения();
	ПроверитьЗаполнениеФормы();
	Если Не КэшЗначений.РазрешитьЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОжиданий = 3;
	ОжидатьНачалаРаботыСПрограммой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикКомандыДобавитьОрганизацию(Команда)
	
	ИндексОрганизации = ДобавитьОрганизацию(Истина);
	УправлениеФормой(ЭтотОбъект, ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеЛоготипа(Команда)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "АдресЛоготипа");
	
	ДобавитьИзображениеНаКлиенте(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображениеЛоготипа(Команда)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "АдресЛоготипа");
	
	ЗаписатьЗначениеОрганизации("ФайлЛоготип", Неопределено, ИндексОрганизации);
	ЭтотОбъект["АдресЛоготипа" + ИндексОрганизации] = "";
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОрганизацию(Команда)
	
	#Если МобильныйКлиент Тогда
		
		ИндексОрганизации = Объект.Организации.Количество();
		
	#Иначе
		
		ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "УдалитьОрганизацию");
		
	#КонецЕсли
	
	УдалитьОрганизациюНаСервере(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаИнтернетМагазин(Команда)
	
	ПриВыбореВидаБизнеса("ИнтернетМагазин");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаОптоваяТорговля(Команда)
	
	ПриВыбореВидаБизнеса("ОптоваяТорговля");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаПроизводство(Команда)
	
	ПриВыбореВидаБизнеса("Производство");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаРаботыУслуги(Команда)
	
	ПриВыбореВидаБизнеса("РаботыУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаРозничнаяТорговля(Команда)
	
	ПриВыбореВидаБизнеса("РозничнаяТорговля");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБизнесаНеУказан(Команда)
	
	ПриВыбореВидаБизнеса("");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИПП(Команда)
	
	Если Не ЗаполнениеЛогинаИПароляКорректно() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	РезультатАутентификации = АутентифицироватьПользователя(ЛогинИнтернетПоддержки, ПарольИнтернетПоддержки);
	ПодключенаИПП = РезультатАутентификации.Результат;
	
	Элементы.ПарольИнтернетПоддержки.ТолькоПросмотр = ПодключенаИПП;
	Элементы.ЛогинИнтернетПоддержки.ТолькоПросмотр  = ПодключенаИПП;
	Элементы.ПодключениеИППУспех.Видимость          = ПодключенаИПП;
	
	Элементы.ПодключениеИППОшибка.Видимость     = НЕ ПодключенаИПП;
	Элементы.ПодключитьИПП.Видимость            = НЕ ПодключенаИПП;
	Элементы.ПропуститьПодключениеИПП.Видимость = НЕ ПодключенаИПП;
	Элементы.ГруппаСсылкиПодЛогином.Видимость   = НЕ ПодключенаИПП;
	
	Если НЕ ПустаяСтрока(РезультатАутентификации.КодОшибки) Тогда
		Если РезультатАутентификации.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
			ТекстОшибки = НСтр("ru='Неверный логин или пароль.'")
		Иначе
			ТекстОшибки = СтрШаблон(НСтр("ru='Не удалось подключиться:%1%2'"), Символы.ПС, РезультатАутентификации.СообщениеОбОшибке);
		КонецЕсли;
		Элементы.ПодключениеИППТекстОшибки.Заголовок = ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьПодключениеИПП(Команда)
	
	Элементы.ПодключениеИПП.Скрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПрограммнаяРаботаСЭлементамиФормы

&НаСервере
Функция НаследуемыеСвойстваПоляВвода()
	
	Возврат "АвтоМаксимальнаяВысота,АвтоМаксимальнаяШирина,АвтоОтметкаНезаполненного,Высота,Заголовок,КнопкаСоздания,МаксимальнаяВысота,МаксимальнаяШирина,МаксимальноеЗначение,ОтображениеПодсказки,ПодсказкаВвода,ПоложениеЗаголовка,РастягиватьПоВертикали,РастягиватьПоГоризонтали,РежимРедактирования,ЦветРамки,ЦветТекста,ЦветТекстаЗаголовка,ЦветФона,Ширина,Шрифт,ШрифтЗаголовка";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваПоляНадписи()
	
	Возврат "АвтоМаксимальнаяВысота,АвтоМаксимальнаяШирина,Высота,Заголовок,МаксимальнаяВысота,МаксимальнаяШирина,ОтображениеПодсказки,ПоложениеЗаголовка,РастягиватьПоВертикали,РастягиватьПоГоризонтали,ЦветРамки,ЦветТекста,ЦветТекстаЗаголовка,ЦветФона,Ширина,Шрифт,ШрифтЗаголовка";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваПоляПереключателя()
	
	Возврат "ВидПереключателя,Заголовок,ОдинаковаяШиринаКолонок,ПоложениеЗаголовка,РежимРедактирования,ЦветТекстаЗаголовка,Шрифт,ШрифтЗаголовка,ШиринаЭлемента";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваПоляФлажка()
	
	Возврат "Видимость,Заголовок,ПоложениеЗаголовка,Шрифт,ШрифтЗаголовка";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваПоляКартинки()
	
	Возврат "Высота,Гиперссылка,ПоложениеЗаголовка,РазмерКартинки,ТекстНевыбраннойКартинки,РастягиватьПоВертикали,РастягиватьПоГоризонтали,Ширина,Шрифт,ЦветТекста";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваГруппыФормы()
	
	Возврат "ВертикальноеПоложениеВГруппе,ВертикальноеПоложениеПодчиненных,ВертикальныйИнтервал,Видимость,ВыравниваниеЭлементовИЗаголовков,Высота,ГоризонтальноеПоложениеПодчиненных,ГоризонтальныйИнтервал,Группировка,ГоризонтальныйИнтервал,ЗаголовокСвернутогоОтображения,Объединенная,Отображение,ОтображатьЗаголовок,ОтображатьОтступСлева,Отображение,СквозноеВыравнивание,Ширина";
	
КонецФункции

&НаСервере
Функция НаследуемыеСвойстваДекорацииФормы()
	
	Возврат "АвтоМаксимальнаяВысота,АвтоМаксимальнаяШирина,Видимость,Высота,Заголовок,МаксимальнаяВысота,МаксимальнаяШирина,РастягиватьПоВертикали,РастягиватьПоГоризонтали,Ширина,Шрифт";
	
КонецФункции

&НаСервере
Процедура КэшироватьСвойстваЭлементовФормы()
	
	ЗначенияСвойствЭлементов = Новый Соответствие;
	
	НаследуемыеПоляВвода = Новый Массив;
	НаследуемыеПоляВвода.Добавить(Элементы.ИНН1);
	НаследуемыеПоляВвода.Добавить(Элементы.Наименование1);
	НаследуемыеПоляВвода.Добавить(Элементы.ИП_Фамилия1);
	НаследуемыеПоляВвода.Добавить(Элементы.ИП_Имя1);
	НаследуемыеПоляВвода.Добавить(Элементы.ИП_Отчество1);
	НаследуемыеПоляВвода.Добавить(Элементы.СтавкаНалога1);
	НаследуемыеСвойстваПоляВвода = НаследуемыеСвойстваПоляВвода();
	Для каждого Элемент Из НаследуемыеПоляВвода Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваПоляВвода);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеПоляНадписи = Новый Массив;
	НаследуемыеПоляНадписи.Добавить(Элементы.ИННОшибкаВвода1);
	НаследуемыеСвойстваПоляНадписи = НаследуемыеСвойстваПоляНадписи();
	Для каждого Элемент Из НаследуемыеПоляНадписи Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваПоляНадписи);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеПоляПереключателя = Новый Массив;
	НаследуемыеПоляПереключателя.Добавить(Элементы.ЮридическоеФизическоеЛицо1);
	НаследуемыеПоляПереключателя.Добавить(Элементы.СистемаНалогообложения1);
	НаследуемыеПоляПереключателя.Добавить(Элементы.ВидыОбъектовНалогообложения1);
	НаследуемыеСвойстваПоляПереключателя = НаследуемыеСвойстваПоляПереключателя();
	Для каждого Элемент Из НаследуемыеПоляПереключателя Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваПоляПереключателя);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеПоляФлажка = Новый Массив;
	НаследуемыеПоляФлажка.Добавить(Элементы.ЕстьУСН1);
	НаследуемыеПоляФлажка.Добавить(Элементы.ЕстьАУСН1);
	НаследуемыеПоляФлажка.Добавить(Элементы.ЕстьПатент1);
	НаследуемыеПоляФлажка.Добавить(Элементы.ИПИспользуетТрудНаемныхРаботников1);
	НаследуемыеСвойстваПоляФлажка = НаследуемыеСвойстваПоляФлажка();
	Для каждого Элемент Из НаследуемыеПоляФлажка Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваПоляФлажка);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеПоляКартинки = Новый Массив;
	НаследуемыеПоляКартинки.Добавить(Элементы.АдресЛоготипа1);
	НаследуемыеСвойстваПоляКартинки = НаследуемыеСвойстваПоляКартинки();
	Для каждого Элемент Из НаследуемыеПоляКартинки Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваПоляКартинки);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеГруппыФормы = Новый Массив;
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаОрганизация1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаОсновныеСведения1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаИНН_ВидСобственности1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаИНН_ОшибкаВвода1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаНаименованиеОрганизации1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаВидНалогообложения1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаСистемаНалогообложения1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаУСН_ЕНВД1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаНалогообложение1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаСотрудники1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаЛоготип1);
	НаследуемыеГруппыФормы.Добавить(Элементы.ГруппаНаименованиеОрганизацииФИО1);
	НаследуемыеСвойстваГруппыФормы = НаследуемыеСвойстваГруппыФормы();
	Для каждого Элемент Из НаследуемыеГруппыФормы Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваГруппыФормы);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	НаследуемыеДекорацииФормы = Новый Массив;
	НаследуемыеДекорацииФормы.Добавить(Элементы.ДекорацияОтступСистемаНалогообложение1);
	НаследуемыеДекорацииФормы.Добавить(Элементы.ДекорацияОтступЗаголовокНалогообложение1);
	НаследуемыеДекорацииФормы.Добавить(Элементы.ДекорацияОтступВидНалогообложение1);
	НаследуемыеДекорацииФормы.Добавить(Элементы.ДекорацияОтступСотрудники1);
	НаследуемыеСвойстваГруппыФормы = НаследуемыеСвойстваДекорацииФормы();
	Для каждого Элемент Из НаследуемыеДекорацииФормы Цикл
		СвойстваЭлемента = СтруктураСвойствЭлементаФормы(Элемент, НаследуемыеСвойстваГруппыФормы);
		ЗначенияСвойствЭлементов.Вставить(Элемент.Имя, СвойстваЭлемента);
	КонецЦикла;
	
	КэшЗначенийСвойствЭлементовАдрес = ПоместитьВоВременноеХранилище(ЗначенияСвойствЭлементов, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция СтруктураСвойствЭлементаФормы(ЭлементФормы, ИменаСвойств)
	
	ИменаСвойствМассив = СтрРазделить(ИменаСвойств, ",", Ложь);
	СвойстваЭлемента = Новый Структура;
	Для каждого ИмяСвойства Из ИменаСвойствМассив Цикл
		СвойстваЭлемента.Вставить(ИмяСвойства, ЭлементФормы[ИмяСвойства]);
	КонецЦикла;
	Возврат СвойстваЭлемента;
	
КонецФункции

&НаСервере
Функция СвойствоЭлементаФормыИзКэша(СвойстваЭлементов, ИмяЭлемента, ИмяСвойства)
	
	СвойстваЭлемента = СвойстваЭлементов.Получить(ИмяЭлемента);
	Возврат СвойстваЭлемента[ИмяСвойства];
	
КонецФункции

&НаСервере
Процедура НаследоватьСвойстваПоляВвода(НовоеПолеВвода, НаследуемоеПолеВвода, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваПоляВвода();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НовоеПолеВвода[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемоеПолеВвода.Имя, ИмяСвойства);
	КонецЦикла;
	
	НовоеПолеВвода.СписокВыбора.Очистить();
	Для каждого ЭлементВыбора Из НаследуемоеПолеВвода.СписокВыбора Цикл
		НовоеПолеВвода.СписокВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваПоляНадписи(НовоеПолеВвода, НаследуемоеПолеВвода, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваПоляНадписи();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НовоеПолеВвода[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемоеПолеВвода.Имя, ИмяСвойства);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваПоляПереключателя(НовоеПолеВвода, НаследуемоеПолеВвода, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваПоляПереключателя();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НовоеПолеВвода[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемоеПолеВвода.Имя, ИмяСвойства);
	КонецЦикла;
	
	НовоеПолеВвода.СписокВыбора.Очистить();
	Для каждого ЭлементВыбора Из НаследуемоеПолеВвода.СписокВыбора Цикл
		НовоеПолеВвода.СписокВыбора.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваПоляФлажка(НовоеПолеВвода, НаследуемоеПолеВвода, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваПоляФлажка();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НовоеПолеВвода[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемоеПолеВвода.Имя, ИмяСвойства);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваПоляКартинки(НовоеПолеВвода, НаследуемоеПолеВвода, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваПоляКартинки();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НовоеПолеВвода[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемоеПолеВвода.Имя, ИмяСвойства);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваГруппыФормы(НоваяГруппаФормы, НаследуемаяГруппаФормы, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваГруппыФормы();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НоваяГруппаФормы[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемаяГруппаФормы.Имя, ИмяСвойства);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НаследоватьСвойстваДекорацииФормы(НоваяДекорацияФормы, НаследуемаяДекорацияФормы, ЗначенияСвойствЭлементов)
	
	НаследуемыеСвойства = НаследуемыеСвойстваДекорацииФормы();
	НаследуемыеСвойстваМассив = СтрРазделить(НаследуемыеСвойства, ",", Ложь);
	Для каждого ИмяСвойства Из НаследуемыеСвойстваМассив Цикл
		НоваяДекорацияФормы[ИмяСвойства] = СвойствоЭлементаФормыИзКэша(ЗначенияСвойствЭлементов, НаследуемаяДекорацияФормы.Имя, ИмяСвойства);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура УправлениеФормой(Форма, ИндексОрганизации)
	
	ЗначениеСистемаНалогообложения = Форма["СистемаНалогообложения" + ИндексОрганизации];
	ЗначениеЕстьУСН = Форма["ЕстьУСН" + ИндексОрганизации];
	ЗначениеЕстьАУСН = Форма["ЕстьАУСН" + ИндексОрганизации];
	
	ЗначениеЮридическоеФизическоеЛицо = Форма["ЮридическоеФизическоеЛицо" + ИндексОрганизации];
	ЭтоПредприниматель = (Форма["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = КэшЗначений.ФизЛицо);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ЕстьПатент" + ИндексОрганизации, "Видимость", ЗначениеСистемаНалогообложения < 2 И ЭтоПредприниматель);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ЕстьПатент" + ИндексОрганизации, "Доступность", ЭтоПредприниматель);	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаНалогообложение" + ИндексОрганизации, "Видимость", (Форма["ЕстьУСН" + ИндексОрганизации] И ЭтоПредприниматель)
		Или (Форма["ЕстьАУСН" + ИндексОрганизации] И ЭтоПредприниматель));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаСотрудники" + ИндексОрганизации, "Видимость", Форма["ЕстьУСН" + ИндексОрганизации]
		Или Форма["ЕстьАУСН" + ИндексОрганизации]);
	
	Если ЗначениеЮридическоеФизическоеЛицо = КэшЗначений.ЮрЛицо Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаНалогообложение" + ИндексОрганизации, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияОтступСотрудники" + ИндексОрганизации, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияЗаголовокНаименование" + ИндексОрганизации, "Заголовок", НСтр("ru ='Название:'"));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "Наименование" + ИндексОрганизации, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаНаименованиеОрганизацииФИО" + ИндексОрганизации, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияОтступСистемаНалогообложение" + ИндексОрганизации, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "СистемаНалогообложения" + ИндексОрганизации, "ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "СистемаНалогообложения" + ИндексОрганизации, "ШиринаЭлемента", 0);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаНалогообложение" + ИндексОрганизации, "Видимость", ЗначениеЕстьУСН ИЛИ ЗначениеЕстьАУСН);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияОтступСотрудники" + ИндексОрганизации, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияЗаголовокНаименование" + ИндексОрганизации, "Заголовок", НСтр("ru ='ФИО:'"));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "Наименование" + ИндексОрганизации, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ГруппаНаименованиеОрганизацииФИО" + ИндексОрганизации, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ДекорацияОтступСистемаНалогообложение" + ИндексОрганизации, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "СистемаНалогообложения" + ИндексОрганизации, "ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Лево);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "СистемаНалогообложения" + ИндексОрганизации, "ШиринаЭлемента", 11);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ИПИспользуетТрудНаемныхРаботников" + ИндексОрганизации,
		"Видимость", ЭтоПредприниматель);
	
	Форма.Элементы["СистемаНалогообложения" + ИндексОрганизации].СписокВыбора.Очистить();
	Форма.Элементы["СистемаНалогообложения" + ИндексОрганизации].СписокВыбора.Добавить(0,НСтр("ru = 'Общая'"));
	Форма.Элементы["СистемаНалогообложения" + ИндексОрганизации].СписокВыбора.Добавить(1,НСтр("ru = 'УСН'")); 
	Форма.Элементы["СистемаНалогообложения" + ИндексОрганизации].СписокВыбора.Добавить(4,НСтр("ru = 'АУСН'"));
	Если ЗначениеЮридическоеФизическоеЛицо <> КэшЗначений.ЮрЛицо Тогда
		Форма.Элементы["СистемаНалогообложения" + ИндексОрганизации].СписокВыбора.Добавить(3,НСтр("ru = 'Только патент'")); 
	КонецЕсли;
	
	Форма.Элементы["ГруппаИНН_ОшибкаВвода" + ИндексОрганизации].Видимость = ЗначениеЗаполнено(Форма["ИНН" + ИндексОрганизации])
		И ЗначениеЗаполнено(Форма["ИННОшибкаВвода" + ИндексОрганизации]);
	
	НастроитьФормуМобильныйКлиент(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНаименованиеДляБюджета(Индекс)
	
	ДанныеСтрокиОрганизации = Объект.Организации[Индекс - 1];
	
	// Для организации заполняем из полного наименования
	Если ДанныеСтрокиОрганизации.ЮридическоеФизическоеЛицо = КэшЗначений.ЮрЛицо Тогда
		
		Возврат ДанныеСтрокиОрганизации.НаименованиеПолное;
		
	КонецЕсли;
	
	// Для ИП:
	// п.4 Приложения 1 к Приказу Минфина 107н:
	// Фамилия имя отчество "(ИП)" "//"Адрес регистрации"//"
	Шаблон = "%1 (ИП) //%2//";
	
	ФИО = ДанныеСтрокиОрганизации.Фамилия 
		+ ?(ПустаяСтрока(ДанныеСтрокиОрганизации.Имя), "", " " + ДанныеСтрокиОрганизации.Имя) 
		+ ?(ПустаяСтрока(ДанныеСтрокиОрганизации.Отчество), "", " " + ДанныеСтрокиОрганизации.Отчество);
	
	АдресРегистрации = ДанныеСтрокиОрганизации.АдресЮридический;
	
	Возврат СтрШаблон(Шаблон, ФИО, АдресРегистрации);
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
	ЗначениеНаименованиеПлательщикаПриПеречисленииНалогов = ПолучитьНаименованиеДляБюджета(Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПлательщикаПриПеречисленииНалогов", ЗначениеНаименованиеПлательщикаПриПеречисленииНалогов, Индекс);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПользователя()
	
	ПользовательСсылка	= КэшЗначений.Пользователь;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		ПользовательИмя = ПользовательСсылка.Наименование;
		
		УстановитьПривилегированныйРежим(Истина);
		ПрочитанныеСвойства = Пользователи.СвойстваПользователяИБ(ПользовательСсылка.ИдентификаторПользователяИБ);
		УстановитьПривилегированныйРежим(Ложь);
		
		КэшЗначений.Вставить("ИмяПользователяВСервисе", ПрочитанныеСвойства.Имя);
		
	КонецЕсли;
	
	ПользовательEmail	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
	ПользовательТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
	ПользовательСайт	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.СайтПользователя);
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации)
	
	НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
	Возврат НайденныеСтроки[0];
	
КонецФункции

&НаСервере
Функция СтрокаОрганизацииПоИндексуНаСервере(ИндексОрганизации)
	
	НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
	Возврат НайденныеСтроки[0];
	
КонецФункции

&НаСервере
Функция СформироватьУникальныйИндексОрганизации(СтрокаОрганизации)
	
	ИндексОрганизации = Объект.Организации.Количество();
	
	Пока Истина Цикл
		
		НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			Прервать;
			
		Иначе
			
			ИндексОрганизации = ИндексОрганизации + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИндексОрганизации;
	
КонецФункции

&НаКлиенте
Функция ИндексПоИмениЭлемента(ИмяЭлемента, ОбщаяЧастьИмени)
	
	Возврат Число(СтрЗаменить(ИмяЭлемента, ОбщаяЧастьИмени, ""));
	
КонецФункции

&НаСервере
Процедура ДобавитьРеквизитыИЭлементыФормы(ИндексОрганизации)
	
	ЗначенияСвойствЭлементов = ПолучитьИзВременногоХранилища(КэшЗначенийСвойствЭлементовАдрес);
	
	ПостФикс = ИндексОрганизации;
	
	// :::Реквизиты
	МассивРеквизитов = Новый Массив;
	
	НовыйРеквизитИНН = Новый РеквизитФормы("ИНН" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(12)), , "");
	МассивРеквизитов.Добавить(НовыйРеквизитИНН);
	
	НовыйРеквизитИННОшибкаВвода = Новый РеквизитФормы("ИННОшибкаВвода" + ПостФикс, Новый ОписаниеТипов("Строка"), , "");
	МассивРеквизитов.Добавить(НовыйРеквизитИННОшибкаВвода);
	
	НовыйЮридическоеФизическоеЛицо = Новый РеквизитФормы("ЮридическоеФизическоеЛицо" + ПостФикс, Новый ОписаниеТипов("ПеречислениеСсылка.ЮридическоеФизическоеЛицо"), , "");
	МассивРеквизитов.Добавить(НовыйЮридическоеФизическоеЛицо);
	
	НовыйНаименование = Новый РеквизитФормы("Наименование" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйНаименование);
	
	НовыйИПФамилия = Новый РеквизитФормы("ИП_Фамилия" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПФамилия);
	
	НовыйИПИмя = Новый РеквизитФормы("ИП_Имя" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПИмя);
	
	НовыйИПОтчество = Новый РеквизитФормы("ИП_Отчество" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПОтчество);
	
	НовыйЕстьУСН = Новый РеквизитФормы("ЕстьУСН" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьУСН);
	
	НовыйЕстьУСН = Новый РеквизитФормы("ЕстьАУСН" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьУСН);
	
	НовыйЕстьПатент = Новый РеквизитФормы("ЕстьПатент" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьПатент);
	
	НовыйВидыОбъектовНалогообложения = Новый РеквизитФормы("ВидыОбъектовНалогообложения" + ПостФикс, Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОбъектовНалогообложения"), , "");
	МассивРеквизитов.Добавить(НовыйВидыОбъектовНалогообложения);
	
	НовыйСтавкаНалога = Новый РеквизитФормы("СтавкаНалога" + ПостФикс, Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
	МассивРеквизитов.Добавить(НовыйСтавкаНалога);
	
	НовыйСистемаНалогообложения = Новый РеквизитФормы("СистемаНалогообложения" + ПостФикс, Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный)), , "");
	МассивРеквизитов.Добавить(НовыйСистемаНалогообложения);
	
	НовыйИПИспользуетТрудНаемныхРаботников = Новый РеквизитФормы("ИПИспользуетТрудНаемныхРаботников" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйИПИспользуетТрудНаемныхРаботников);
	
	НовыйАдресЛоготипа = Новый РеквизитФормы("АдресЛоготипа" + ПостФикс, Новый ОписаниеТипов("Строка"), , "");
	МассивРеквизитов.Добавить(НовыйАдресЛоготипа);
	
	ЭтотОбъект.ИзменитьРеквизиты(МассивРеквизитов);
	
	// :::Структура групп
	//
	//ГруппаОрганизация
	//	ГруппаОсновныеСведения
	//		ГруппаИНН_ВидСобственности
	//		ГруппаИНН_ОшибкаВвода
	//		ГруппаНаименованиеОрганизации
	//			ГруппаНаименованиеОрганизацииФИО
	//		ГруппаУСН_ЕНВД
	//		ГруппаНалогообложение
	//		ГруппаСотрудники
	//
	//	ГруппаЛоготип
	
	ГруппаОрганизация = Элементы.Добавить("ГруппаОрганизация" + ПостФикс, Тип("ГруппаФормы"), Элементы.ГруппаОрганизации);
	ГруппаОрганизация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаОрганизация, Элементы.ГруппаОрганизация1, ЗначенияСвойствЭлементов);
	
	ГруппаОсновныеСведения = Элементы.Добавить("ГруппаОсновныеСведения" + ПостФикс, Тип("ГруппаФормы"), ГруппаОрганизация);
	ГруппаОсновныеСведения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаОсновныеСведения, Элементы.ГруппаОсновныеСведения1, ЗначенияСвойствЭлементов);
	
	ГруппаИНН_ВидСобственности = Элементы.Добавить("ГруппаИНН_ВидСобственности" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаИНН_ВидСобственности.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаИНН_ВидСобственности, Элементы.ГруппаИНН_ВидСобственности1, ЗначенияСвойствЭлементов);
	
	ГруппаИНН_ОшибкаВвода = Элементы.Добавить("ГруппаИНН_ОшибкаВвода" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаИНН_ОшибкаВвода.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаИНН_ОшибкаВвода, Элементы.ГруппаИНН_ОшибкаВвода1, ЗначенияСвойствЭлементов);
	
	ГруппаНаименованиеОрганизации = Элементы.Добавить("ГруппаНаименованиеОрганизации" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаНаименованиеОрганизации.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаНаименованиеОрганизации, Элементы.ГруппаНаименованиеОрганизации1, ЗначенияСвойствЭлементов);
	
	ГруппаВидНалогообложения = Элементы.Добавить("ГруппаВидНалогообложения" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаВидНалогообложения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаВидНалогообложения, Элементы.ГруппаВидНалогообложения1, ЗначенияСвойствЭлементов);
	
	ГруппаСистемаНалогообложения = Элементы.Добавить("ГруппаСистемаНалогообложения" + ПостФикс, Тип("ГруппаФормы"), ГруппаВидНалогообложения);
	ГруппаСистемаНалогообложения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаСистемаНалогообложения, Элементы.ГруппаСистемаНалогообложения1, ЗначенияСвойствЭлементов);
	
	ГруппаУСН_ЕНВД = Элементы.Добавить("ГруппаУСН_ЕНВД" + ПостФикс, Тип("ГруппаФормы"), ГруппаВидНалогообложения);
	ГруппаУСН_ЕНВД.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаУСН_ЕНВД, Элементы.ГруппаУСН_ЕНВД1, ЗначенияСвойствЭлементов);
	
	ГруппаНалогообложение = Элементы.Добавить("ГруппаНалогообложение" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаНалогообложение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаНалогообложение, Элементы.ГруппаНалогообложение1, ЗначенияСвойствЭлементов);
	
	ГруппаСотрудники = Элементы.Добавить("ГруппаСотрудники" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаСотрудники.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаСотрудники, Элементы.ГруппаСотрудники1, ЗначенияСвойствЭлементов);
	
	ГруппаЛоготип = Элементы.Добавить("ГруппаЛоготип" + ПостФикс, Тип("ГруппаФормы"), ГруппаОрганизация);
	ГруппаЛоготип.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаЛоготип, Элементы.ГруппаЛоготип1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаИНН_ВидСобственности
	ЭлементИНН = Элементы.Добавить("ИНН" + ПостФикс, Тип("ПолеФормы"), ГруппаИНН_ВидСобственности);
	ЭлементИНН.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИНН.ПутьКДанным = "ИНН" + ПостФикс;
	ЭлементИНН.УстановитьДействие("ПриИзменении", "ИННПриИзменении");
	ЭлементИНН.УстановитьДействие("ИзменениеТекстаРедактирования", "ИННИзменениеТекстаРедактирования");
	НаследоватьСвойстваПоляВвода(ЭлементИНН, Элементы.ИНН1, ЗначенияСвойствЭлементов);
	
	ЭлементЮридическоеФизическоеЛицо = Элементы.Добавить("ЮридическоеФизическоеЛицо" + ПостФикс, Тип("ПолеФормы"), ГруппаИНН_ВидСобственности);
	ЭлементЮридическоеФизическоеЛицо.ПутьКДанным = "ЮридическоеФизическоеЛицо" + ПостФикс;
	ЭлементЮридическоеФизическоеЛицо.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементЮридическоеФизическоеЛицо.УстановитьДействие("ПриИзменении", "ЮрФизЛицоПриИзменении");
	НаследоватьСвойстваПоляПереключателя(ЭлементЮридическоеФизическоеЛицо, Элементы.ЮридическоеФизическоеЛицо1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаИНН_ОшибкаВвода
	ЭлементИННОшибкаВвода = Элементы.Добавить("ИННОшибкаВвода" + ПостФикс, Тип("ПолеФормы"), ГруппаИНН_ОшибкаВвода);
	ЭлементИННОшибкаВвода.Вид = ВидПоляФормы.ПолеНадписи;
	ЭлементИННОшибкаВвода.ПутьКДанным = "ИННОшибкаВвода" + ПостФикс;
	НаследоватьСвойстваПоляНадписи(ЭлементИННОшибкаВвода, Элементы.ИННОшибкаВвода1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаНаименованиеОрганизации
	ЭлементНаименование = Элементы.Добавить("Наименование" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизации);
	ЭлементНаименование.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементНаименование.ПутьКДанным = "Наименование" + ПостФикс;
	ЭлементНаименование.УстановитьДействие("ПриИзменении", "НаименованиеПриИзменении");
	НаследоватьСвойстваПоляВвода(ЭлементНаименование, Элементы.Наименование1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаНаименованиеОрганизацииФИО (создаем строго после декорации-заголовка)
	ГруппаНаименованиеОрганизацииФИО = Элементы.Добавить("ГруппаНаименованиеОрганизацииФИО" + ПостФикс, Тип("ГруппаФормы"), ГруппаНаименованиеОрганизации);
	ГруппаНаименованиеОрганизацииФИО.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НаследоватьСвойстваГруппыФормы(ГруппаНаименованиеОрганизацииФИО, Элементы.ГруппаНаименованиеОрганизацииФИО1, ЗначенияСвойствЭлементов);
	
	ЭлементИП_Фамилия = Элементы.Добавить("ИП_Фамилия" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Фамилия.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Фамилия.ПутьКДанным = "ИП_Фамилия" + ПостФикс;
	НаследоватьСвойстваПоляВвода(ЭлементИП_Фамилия, Элементы.ИП_Фамилия1, ЗначенияСвойствЭлементов);
	Если ЭтоМобильныйКлиент Тогда
		ЭлементИП_Фамилия.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		ЭлементИП_Фамилия.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	КонецЕсли;
	ЭлементИП_Фамилия.УстановитьДействие("ПриИзменении", "ИПФамилияПриИзменении");
	
	ЭлементИП_Имя = Элементы.Добавить("ИП_Имя" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Имя.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Имя.ПутьКДанным = "ИП_Имя" + ПостФикс;
	ЭлементИП_Имя.УстановитьДействие("ПриИзменении", "ИПИмяПриИзменении");
	НаследоватьСвойстваПоляВвода(ЭлементИП_Имя, Элементы.ИП_Имя1, ЗначенияСвойствЭлементов);
	
	ЭлементИП_Отчество = Элементы.Добавить("ИП_Отчество" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Отчество.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Отчество.ПутьКДанным = "ИП_Отчество" + ПостФикс;
	ЭлементИП_Отчество.УстановитьДействие("ПриИзменении", "ИПОтчествоПриИзменении");
	НаследоватьСвойстваПоляВвода(ЭлементИП_Отчество, Элементы.ИП_Отчество1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаУСН_ЕНВД
	Если НЕ ЭтоМобильныйКлиент Тогда
		ДекорацияОтступСистемаНалогообложение = Элементы.Добавить("ДекорацияОтступСистемаНалогообложение" + ПостФикс, Тип("ДекорацияФормы"), ГруппаСистемаНалогообложения);
		ДекорацияОтступСистемаНалогообложение.Вид = ВидДекорацииФормы.Надпись;
		НаследоватьСвойстваДекорацииФормы(ДекорацияОтступСистемаНалогообложение, Элементы.ДекорацияОтступСистемаНалогообложение1, ЗначенияСвойствЭлементов);
		
		ДекорацияОтступЗаголовокНалогообложение = Элементы.Добавить("ДекорацияОтступЗаголовокНалогообложение" + ПостФикс, Тип("ДекорацияФормы"), ГруппаУСН_ЕНВД);
		ДекорацияОтступЗаголовокНалогообложение.Вид = ВидДекорацииФормы.Надпись;
		НаследоватьСвойстваДекорацииФормы(ДекорацияОтступЗаголовокНалогообложение, Элементы.ДекорацияОтступЗаголовокНалогообложение1, ЗначенияСвойствЭлементов);
	КонецЕсли;
	
	ЭлементСистемаНалогообложения = Элементы.Добавить("СистемаНалогообложения" + ПостФикс, Тип("ПолеФормы"), ГруппаСистемаНалогообложения);
	ЭлементСистемаНалогообложения.ПутьКДанным = "СистемаНалогообложения" + ПостФикс;
	ЭлементСистемаНалогообложения.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементСистемаНалогообложения.УстановитьДействие("ПриИзменении", "СистемаНалогообложенияПриИзменении");
	НаследоватьСвойстваПоляПереключателя(ЭлементСистемаНалогообложения, Элементы.СистемаНалогообложения1, ЗначенияСвойствЭлементов);
	
	ЭлементЕстьУСН = Элементы.Добавить("ЕстьУСН" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьУСН.ПутьКДанным = "ЕстьУСН" + ПостФикс;
	ЭлементЕстьУСН.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьУСН.УстановитьДействие("ПриИзменении", "ЕстьУСНПриИзменении");
	НаследоватьСвойстваПоляФлажка(ЭлементЕстьУСН, Элементы.ЕстьУСН1, ЗначенияСвойствЭлементов);
	
	ЭлементЕстьАУСН = Элементы.Добавить("ЕстьАУСН" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьАУСН.ПутьКДанным = "ЕстьАУСН" + ПостФикс;
	ЭлементЕстьАУСН.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьАУСН.УстановитьДействие("ПриИзменении", "ЕстьАУСНПриИзменении");
	НаследоватьСвойстваПоляФлажка(ЭлементЕстьАУСН, Элементы.ЕстьАУСН1, ЗначенияСвойствЭлементов);
	
	ЭлементЕстьПатент = Элементы.Добавить("ЕстьПатент" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьПатент.ПутьКДанным = "ЕстьПатент" + ПостФикс;
	ЭлементЕстьПатент.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьПатент.УстановитьДействие("ПриИзменении", "ЕстьПатентПриИзменении");
	НаследоватьСвойстваПоляФлажка(ЭлементЕстьПатент, Элементы.ЕстьПатент1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаНалогообложение
	ДекорацияОтступВидНалогообложение = Элементы.Добавить("ДекорацияОтступВидНалогообложение" + ПостФикс, Тип("ДекорацияФормы"), ГруппаНалогообложение);
	ДекорацияОтступВидНалогообложение.Вид = ВидДекорацииФормы.Надпись;
	НаследоватьСвойстваДекорацииФормы(ДекорацияОтступВидНалогообложение, Элементы.ДекорацияОтступВидНалогообложение1, ЗначенияСвойствЭлементов);
	
	ЭлементВидыОбъектовНалогообложения = Элементы.Добавить("ВидыОбъектовНалогообложения" + ПостФикс, Тип("ПолеФормы"), ГруппаНалогообложение);
	ЭлементВидыОбъектовНалогообложения.ПутьКДанным = "ВидыОбъектовНалогообложения" + ПостФикс;
	ЭлементВидыОбъектовНалогообложения.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементВидыОбъектовНалогообложения.УстановитьДействие("ПриИзменении", "НалогообложениеПриИзменении");
	НаследоватьСвойстваПоляПереключателя(ЭлементВидыОбъектовНалогообложения, Элементы.ВидыОбъектовНалогообложения1, ЗначенияСвойствЭлементов);
	
	ЭлементСтавкаНалога = Элементы.Добавить("СтавкаНалога" + ПостФикс, Тип("ПолеФормы"), ГруппаНалогообложение);
	ЭлементСтавкаНалога.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСтавкаНалога.ПутьКДанным = "СтавкаНалога" + ПостФикс;
	ЭлементСтавкаНалога.УстановитьДействие("ПриИзменении", "СтавкаНалогаПриИзменении");
	НаследоватьСвойстваПоляВвода(ЭлементСтавкаНалога, Элементы.СтавкаНалога1, ЗначенияСвойствЭлементов);
	
	// :::ГруппаСотрудники
	Если НЕ ЭтоМобильныйКлиент Тогда
		ДекорацияОтступСотрудники = Элементы.Добавить("ДекорацияОтступСотрудники" + ПостФикс, Тип("ДекорацияФормы"), ГруппаСотрудники);
		ДекорацияОтступСотрудники.Вид = ВидДекорацииФормы.Надпись;
		НаследоватьСвойстваДекорацииФормы(ДекорацияОтступСотрудники, Элементы.ДекорацияОтступСотрудники1, ЗначенияСвойствЭлементов);
	КонецЕсли;
	
	ЭлементИПИспользуетТрудНаемныхРаботников = Элементы.Добавить("ИПИспользуетТрудНаемныхРаботников" + ПостФикс, Тип("ПолеФормы"), ГруппаСотрудники);
	ЭлементИПИспользуетТрудНаемныхРаботников.ПутьКДанным = "ИПИспользуетТрудНаемныхРаботников" + ПостФикс;
	ЭлементИПИспользуетТрудНаемныхРаботников.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементИПИспользуетТрудНаемныхРаботников.УстановитьДействие("ПриИзменении", "ЕстьСотрудникиПриИзменении");
	НаследоватьСвойстваПоляФлажка(ЭлементИПИспользуетТрудНаемныхРаботников, Элементы.ИПИспользуетТрудНаемныхРаботников1, ЗначенияСвойствЭлементов);
	
	// :::АдресЛоготипа
	ЭлементАдресЛоготипа = Элементы.Добавить("АдресЛоготипа" + ПостФикс, Тип("ПолеФормы"), ГруппаЛоготип);
	ЭлементАдресЛоготипа.ПутьКДанным = "АдресЛоготипа" + ПостФикс;
	ЭлементАдресЛоготипа.Вид = ВидПоляФормы.ПолеКартинки;
	ЭлементАдресЛоготипа.УстановитьДействие("Нажатие", "АдресЛоготипаНажатие");
	НаследоватьСвойстваПоляКартинки(ЭлементАдресЛоготипа, Элементы.АдресЛоготипа1, ЗначенияСвойствЭлементов);
	
	ЭлементКомандаДобавитьИзображениеЛоготипа = Элементы.Добавить("ДобавитьИзображениеЛоготипа" + ПостФикс, Тип("КнопкаФормы"), ЭлементАдресЛоготипа.КонтекстноеМеню);
	ЭлементКомандаДобавитьИзображениеЛоготипа.ИмяКоманды = "ДобавитьИзображениеЛоготипа";
	
	ЭлементКомандаОчиститьИзображение = Элементы.Добавить("ОчиститьИзображение" + ПостФикс, Тип("КнопкаФормы"), ЭлементАдресЛоготипа.КонтекстноеМеню);
	ЭлементКомандаОчиститьИзображение.ИмяКоманды = "ОчиститьИзображениеЛоготипа";
	
	ЭлементУдалитьОрганизацию = Элементы.Добавить("УдалитьОрганизацию" + ПостФикс, Тип("КнопкаФормы"), ГруппаЛоготип);
	ЭлементУдалитьОрганизацию.Вид = ВидКнопкиФормы.Гиперссылка;
	ЭлементУдалитьОрганизацию.ИмяКоманды = "УдалитьОрганизацию";
	ЭлементУдалитьОрганизацию.Ширина = 2;
	
	Если ПостФикс > 2 Тогда
		Элементы["УдалитьОрганизацию" + (ПостФикс-1)].Видимость = Ложь;
	КонецЕсли;
	
	ДекорацияМалыйОтступ = Элементы.Добавить("ДекорацияМалыйОтступ1" + ПостФикс, Тип("ДекорацияФормы"), Элементы.ГруппаОрганизации);
	ДекорацияМалыйОтступ.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияМалыйОтступ.Шрифт = Элементы.ДекорацияМалыйОтступ1.Шрифт;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзображениеЛоготипа(СтрокаОрганизации)
	
	ЭтотОбъект["АдресЛоготипа" + СтрокаОрганизации.ИндексОрганизации] = 
		?(ЗначениеЗаполнено(СтрокаОрганизации.ФайлЛоготип), НавигационнаяСсылкаКартинки(СтрокаОрганизации.ФайлЛоготип, УникальныйИдентификатор), "");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НавигационнаяСсылкаКартинки(ФайлКартинки, ИдентификаторФормы)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ДополнительныеПараметры.ИдентификаторФормы = ИдентификаторФормы;
	
	Возврат РаботаСФайлами.ДанныеФайла(ФайлКартинки, ДополнительныеПараметры).СсылкаНаДвоичныеДанныеФайла;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ЛоготипРеквизит, УникальныйИдентификатор)
	
	ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ДополнительныеПараметры.ИдентификаторФормы = УникальныйИдентификатор;
	
	Возврат РаботаСФайлами.ДанныеФайла(ЛоготипРеквизит, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура ПросмотретьПрисоединенныйФайл(ФайлЛоготип)
	
	ДанныеФайла = ПолучитьДанныеФайла(ФайлЛоготип, УникальныйИдентификатор);
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиенте(ИндексОрганизации)
	Перем ИдентификаторФайла, Фильтр;
	
	КэшЗначений.ТекущийИндексОрганизации = ИндексОрганизации;
	
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
	
	ЭтотОбъект["АдресЛоготипа" + СтрокаОрганизации.ИндексОрганизации] = "";
	
	ИдентификаторФайла = Новый УникальныйИдентификатор;
	Фильтр = РаботаСФайламиУНФКлиентСервер.ФильтрФайловИзображений();
	
	РаботаСФайламиКлиент.ДобавитьФайлы(СтрокаОрганизации.Ссылка, ИдентификаторФайла, Фильтр);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗначениеОрганизации(ИмяРеквизита, ЗначениеРеквизита, Индекс, НеЗатиратьТекущее = Ложь)
	
	ДанныеСтрокиОрганизации = Объект.Организации[Индекс - 1];
	
	Если НеЗатиратьТекущее Тогда
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтрокиОрганизации[ИмяРеквизита]) Тогда
			
			ДанныеСтрокиОрганизации[ИмяРеквизита] = ЗначениеРеквизита;
			
		КонецЕсли;
		
	Иначе
		
		ДанныеСтрокиОрганизации[ИмяРеквизита] = ЗначениеРеквизита;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, ЗначениеЕстьАУСН, Индекс)
	
	ЗаписатьЗначениеОрганизации("ЮридическоеФизическоеЛицо", ЗначениеЮрФизЛицо, Индекс);
	
	ПослеЗаписиЗначенияЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, ЗначениеЕстьАУСН, Индекс);
	
КонецПроцедуры

&НаКлиенте 
Процедура ПослеЗаписиЗначенияЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, ЗначениеЕстьАУСН, Индекс)
	
	Если ЗначениеЮрФизЛицо = КэшЗначений.ЮрЛицо Тогда
		ЭтотОбъект["ИПИспользуетТрудНаемныхРаботников" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ИПИспользуетТрудНаемныхРаботников", Ложь, Индекс);
		
		ЭтотОбъект["ИП_Фамилия" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Фамилия", "", Индекс);
		
		ЭтотОбъект["ИП_Имя" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Имя", "", Индекс);
		
		ЭтотОбъект["ИП_Отчество" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Отчество", "", Индекс);
		
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект, Индекс);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьОрганизацию(СоздатьРеквизитыЭлементы, ОрганизацияСсылка = Неопределено)
	
	ДобавленнаяСтрока = Объект.Организации.Добавить();
	
	ИндексОрганизации = СформироватьУникальныйИндексОрганизации(ДобавленнаяСтрока);
	
	Если СоздатьРеквизитыЭлементы Тогда
		
		ДобавитьРеквизитыИЭлементыФормы(ИндексОрганизации);
		
	КонецЕсли;
	
	Если ОрганизацияСсылка = Неопределено Тогда
		
		ОрганизацияСсылка = Справочники.Организации.ПолучитьСсылку();
		
	КонецЕсли;
	
	СтрокаТаблицы = Объект.Организации.НайтиПоИдентификатору(ДобавленнаяСтрока.ПолучитьИдентификатор());
	
	СтрокаТаблицы.Ссылка = ОрганизацияСсылка;
	СтрокаТаблицы.ИндексОрганизации = ИндексОрганизации;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияСсылка,
		"Наименование, НаименованиеПолное, ЮридическоеФизическоеЛицо");
	СтрокаТаблицы.Наименование = РеквизитыОрганизации.Наименование;
	СтрокаТаблицы.НаименованиеПолное = РеквизитыОрганизации.НаименованиеПолное;
	ЭтотОбъект["Наименование" + ИндексОрганизации] = СтрокаТаблицы.Наименование;
	
	СтрокаТаблицы.ЮридическоеФизическоеЛицо = РеквизитыОрганизации.ЮридическоеФизическоеЛицо;
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.ЮридическоеФизическоеЛицо) Тогда
		СтрокаТаблицы.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	КонецЕсли;
	ЭтотОбъект["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = СтрокаТаблицы.ЮридическоеФизическоеЛицо;
	
	СтрокаТаблицы.ВидыОбъектовНалогообложения = Перечисления.ВидыОбъектовНалогообложения.Доходы;
	ЭтотОбъект["ВидыОбъектовНалогообложения" + ИндексОрганизации] = СтрокаТаблицы.ВидыОбъектовНалогообложения;
	
	Возврат ИндексОрганизации;
	
КонецФункции

&НаСервере
Процедура УдалитьОрганизациюНаСервере(ИндексОрганизации)
	
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаСервере(ИндексОрганизации);
	Объект.Организации.Удалить(СтрокаОрганизации);
	
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты.Добавить("ИНН" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИННОшибкаВвода" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЮридическоеФизическоеЛицо" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("Наименование" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Фамилия" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Имя" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Отчество" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьУСН" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьАУСН" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьПатент" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ВидыОбъектовНалогообложения" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("СтавкаНалога" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИПИспользуетТрудНаемныхРаботников" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("АдресЛоготипа" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("СистемаНалогообложения" + ИндексОрганизации);
	
	ЭтотОбъект.ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
	Элементы.Удалить(Элементы["ГруппаОрганизация" + ИндексОрганизации]);
	Элементы.Удалить(Элементы["ДекорацияМалыйОтступ1" + ИндексОрганизации]);
	Если ИндексОрганизации > 2 Тогда
		Элементы["УдалитьОрганизацию" + (ИндексОрганизации-1)].Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииПФР(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодОрганаПФР) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыОтделенияПФР = ДанныеГосударственныхОрганов.РеквизитыОтделенияПФРПоКоду(Лев(ДанныеГосРеестра.КодОрганаПФР, 3));
	
	Если ЗначениеЗаполнено(РеквизитыОтделенияПФР.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыОтделенияПФР.ОписаниеОшибки;
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОтделенияПФР.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыОтделенияПФР);
		
	КонецЕсли;
	
	ПлатежныеРеквизитыПФРПредставление = ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыОтделенияПФР);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыПФРПредставление", ПлатежныеРеквизитыПФРПредставление);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииФСС(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодОрганаФСС) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КодПодчиненностиФСС = ДанныеГосРеестра.КодОрганаФСС + ?(ДанныеГосРеестра.ЭтоЮрЛицо, 1, 3);
	РеквизитыОтделенияФСС = ДанныеГосударственныхОрганов.РеквизитыОтделенияФССПоКоду(КодПодчиненностиФСС);
	
	Если ЗначениеЗаполнено(РеквизитыОтделенияФСС.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыОтделенияФСС.ОписаниеОшибки;
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОтделенияФСС.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыОтделенияФСС);
		
	КонецЕсли;
	
	ПлатежныеРеквизитыФССПредставление = ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыОтделенияФСС);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыФССПредставление", ПлатежныеРеквизитыФССПредставление);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", РеквизитыОтделенияФСС.Наименование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодНалоговогоОрганаПолучателя) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыНалоговогоОргана = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(ДанныеГосРеестра.КодНалоговогоОрганаПолучателя);
	
	Если ЗначениеЗаполнено(РеквизитыНалоговогоОргана.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыНалоговогоОргана.ОписаниеОшибки;
		Возврат;
	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыНалоговогоОргана);
		
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = Новый Структура;
	РегистрацияВНалоговомОргане.Вставить("Наименование", РеквизитыНалоговогоОргана.Наименование);
	РегистрацияВНалоговомОргане.Вставить("НаименованиеИФНС", РеквизитыНалоговогоОргана.ПолноеНаименование);
	
	ДанныеГосРеестра.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОНалоговойИнспекцииПоАдресу(ЮридическийАдресПоИНН, ДанныеГосРеестра, ОписаниеОшибки = "")
	
	ЭтоФизЛицо = НЕ ДанныеГосРеестра.ЭтоЮрЛицо;
	ЗначенияПолейАдреса = ЮридическийАдресПоИНН.КонтактнаяИнформация;
	
	Если НЕ ЗначениеЗаполнено(ЗначенияПолейАдреса) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СведенияОНалоговомОрганеПоАдресу = ДанныеАдресногоКлассификатора.НалоговыйОрганПоАдресу(ЗначенияПолейАдреса);
	
	Если СведенияОНалоговомОрганеПоАдресу = Неопределено Тогда
		КодНалоговогоОрганаПолучателя = "";
	Иначе
		КодНалоговогоОрганаПолучателя = ?(ЭтоФизЛицо, 
			СведенияОНалоговомОрганеПоАдресу.КодНалоговойДляФизическихЛиц,
			СведенияОНалоговомОрганеПоАдресу.КодНалоговойДляЮридическихЛиц);
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", КодНалоговогоОрганаПолучателя);
	
	ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
	
	Если СведенияОНалоговомОрганеПоАдресу <> Неопределено Тогда
		ДанныеГосРеестра.РегистрацияВНалоговомОргане.Вставить("КодПоОКАТО", СведенияОНалоговомОрганеПоАдресу.КодПоОКАТО);
		ДанныеГосРеестра.РегистрацияВНалоговомОргане.Вставить("КодПоОКТМО", СведенияОНалоговомОрганеПоАдресу.КодПоОКТМО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере(ИНН, ДанныеГосРеестра, ИндексОрганизации, Ошибки)
	
	ЭтоЮрЛицо = ?(СтрДлина(ИНН) = 12, Ложь, Истина);
	
	ЗначениеРеквизитов = ?(ЭтоЮрЛицо,
		РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ИНН),
		РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ИНН));
	
	Если ЗначениеЗаполнено(ЗначениеРеквизитов.ОписаниеОшибки) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ЗначениеРеквизитов.ОписаниеОшибки, "");
		Возврат;
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("ЭтоЮрЛицо", ЭтоЮрЛицо);
	ДанныеГосРеестра.Вставить("ИНН", ЗначениеРеквизитов.ИНН);
	ДанныеГосРеестра.Вставить("СведенияПолучены", Ложь);
	Если ЭтоЮрЛицо Тогда
		
		Если ЗначениеРеквизитов.ЕГРЮЛ = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеГосРеестра.СведенияПолучены = Истина;
		ДанныеГосРеестра.Вставить("Наименование", ЗначениеРеквизитов.ЕГРЮЛ.Наименование);
		ДанныеГосРеестра.Вставить("НаименованиеПолное", ЗначениеРеквизитов.ЕГРЮЛ.НаименованиеСокращенное);
		ДанныеГосРеестра.Вставить("ОГРН", ЗначениеРеквизитов.ЕГРЮЛ.РегистрационныйНомер);
		ДанныеГосРеестра.Вставить("КПП", ЗначениеРеквизитов.ЕГРЮЛ.КПП);
		ДанныеГосРеестра.Вставить("ЮридическийАдрес", ЗначениеРеквизитов.ЕГРЮЛ.ЮридическийАдрес);
		ДанныеГосРеестра.Вставить("Телефон", ЗначениеРеквизитов.ЕГРЮЛ.Телефон);
		
		Если ЗначениеРеквизитов.ЕГРЮЛ.ВидДеятельности <> Неопределено Тогда
			ДанныеГосРеестра.Вставить("КодОКВЭД", ЗначениеРеквизитов.ЕГРЮЛ.ВидДеятельности.Код);
		Иначе
			ДанныеГосРеестра.Вставить("КодОКВЭД", Неопределено);
		КонецЕсли;
		
		ЮридическийАдресПоИНН = ЗначениеРеквизитов.ЕГРЮЛ.ЮридическийАдрес;
		ДанныеПоИННРегистрацияВНалоговомОргане = ЗначениеРеквизитов.ЕГРЮЛ.РегистрацияВНалоговомОргане;
		ДанныеПоИННРегистрацияВПенсионномФонде = ЗначениеРеквизитов.ЕГРЮЛ.РегистрацияВПенсионномФонде;
		ДанныеПоИННРегистрацияВФСС = ЗначениеРеквизитов.ЕГРЮЛ.РегистрацияВФСС;
		
	Иначе
		
		ДанныеГосРеестра.СведенияПолучены = Истина;
		ДанныеГосРеестра.Вставить("Наименование", ЗначениеРеквизитов.Наименование);
		ДанныеГосРеестра.Вставить("НаименованиеПолное", ЗначениеРеквизитов.НаименованиеСокращенное);
		ДанныеГосРеестра.Вставить("ОГРН", ЗначениеРеквизитов.РегистрационныйНомер);
		ДанныеГосРеестра.Вставить("КодОКВЭД", Неопределено);
		ДанныеГосРеестра.Вставить("ЮридическийАдрес", Неопределено);
		ДанныеГосРеестра.Вставить("Телефон", Неопределено);
		
		ДанныеГосРеестра.Вставить("СвидетельствоСерияНомер", Неопределено);
		ДанныеГосРеестра.Вставить("СвидетельствоДатаВыдачи", Неопределено);
		Если ЗначениеРеквизитов.СвидетельствоОРегистрации <> Неопределено Тогда
			
			СвидетельствоСерияНомер = "" + ЗначениеРеквизитов.СвидетельствоОРегистрации.Серия + " " + ЗначениеРеквизитов.СвидетельствоОРегистрации.Номер;
			СвидетельствоДатаВыдачи = ЗначениеРеквизитов.СвидетельствоОРегистрации.Дата;
			
			ДанныеГосРеестра.Вставить("СвидетельствоСерияНомер", СвидетельствоСерияНомер);
			ДанныеГосРеестра.Вставить("СвидетельствоДатаВыдачи", СвидетельствоДатаВыдачи);
			
		КонецЕсли;
		
		ДанныеГосРеестра.Вставить("Фамилия", ЗначениеРеквизитов.Фамилия);
		ДанныеГосРеестра.Вставить("Имя", ЗначениеРеквизитов.Имя);
		ДанныеГосРеестра.Вставить("Отчество", ЗначениеРеквизитов.Отчество);
		ДанныеГосРеестра.Вставить("Пол", ?(ЗначениеРеквизитов.Пол = "1", Перечисления.ПолФизическогоЛица.Мужской, Перечисления.ПолФизическогоЛица.Женский));
		
		ДанныеПоИННРегистрацияВНалоговомОргане = ЗначениеРеквизитов.РегистрацияВНалоговомОргане;
		ДанныеПоИННРегистрацияВПенсионномФонде = ЗначениеРеквизитов.РегистрацияВПенсионномФонде;
		ДанныеПоИННРегистрацияВФСС = ЗначениеРеквизитов.РегистрацияВФСС;
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("ЮридическоеФизическоеЛицо", ?(ЭтоЮрЛицо, Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо));
	
	ДанныеГосРеестра.Вставить("КодНалоговогоОргана", Неопределено);
	ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", Неопределено);
	ДанныеГосРеестра.Вставить("КодОрганаПФР", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерПФР", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерПФР_ИП", Неопределено);
	
	// Устанавливаем сведения о налоговой инспекции
	// В том случае, если указан юридический адрес, получаем код налоговой инспекции по адресу
	// и заполняем реквизиты инспекции по коду
	// В том случае, если юридического адреса нет - заполняем данные о налоговой инспекции
	// из кода в ЕГРЮЛ/ЕГРИП
	Если ЗначениеЗаполнено(ЮридическийАдресПоИНН) Тогда
		
		ОписаниеОшибкиАдрес = "";
		ЗаполнитьСведенияОНалоговойИнспекцииПоАдресу(ЮридическийАдресПоИНН, ДанныеГосРеестра, ОписаниеОшибкиАдрес);
		
		Если ЗначениеЗаполнено(ОписаниеОшибкиАдрес)
			И ЗначениеЗаполнено(ДанныеПоИННРегистрацияВНалоговомОргане)
			И ЗначениеЗаполнено(ДанныеПоИННРегистрацияВНалоговомОргане.Код) Тогда
			
			ДанныеГосРеестра.Вставить("КодНалоговогоОргана", ДанныеПоИННРегистрацияВНалоговомОргане.Код);
			ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ДанныеПоИННРегистрацияВНалоговомОргане) Тогда
		
		ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", ДанныеПоИННРегистрацияВНалоговомОргане.Код);
		ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПоИННРегистрацияВПенсионномФонде) Тогда
		
		Ключ = "РегистрационныйНомерПФР" + ?(ЭтоЮрЛицо, "", "_ИП");
		
		ДанныеГосРеестра.Вставить("КодОрганаПФР", ДанныеПоИННРегистрацияВПенсионномФонде.КодОрганаПФР);
		ДанныеГосРеестра.Вставить(Ключ, ДанныеПоИННРегистрацияВПенсионномФонде.РегистрационныйНомерПФР);
		
		ЗаполнитьСведенияОбОтделенииПФР(ДанныеГосРеестра);
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("КодОрганаФСС", Неопределено);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерФСС", Неопределено);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыФССПредставление", Неопределено);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", Неопределено);
	
	Если ЗначениеЗаполнено(ДанныеПоИННРегистрацияВФСС) Тогда
		
		ДанныеГосРеестра.Вставить("КодОрганаФСС", ДанныеПоИННРегистрацияВФСС.КодОрганаФСС);
		ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", ДанныеПоИННРегистрацияВФСС.НаименованиеОрганаФСС);
		ДанныеГосРеестра.Вставить("РегистрационныйНомерФСС", ДанныеПоИННРегистрацияВФСС.РегистрационныйНомерФСС);
		
		ЗаполнитьСведенияОбОтделенииФСС(ДанныеГосРеестра);
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораНаименования(ДанныеГосРеестра, ИндексОрганизации);
	ДанныеГосРеестра.Вставить("НаименованиеПлательщикаПриПеречисленииНалогов", НаименованиеПлательщикаПоУмолчанию(ДанныеГосРеестра));
	
КонецПроцедуры

&НаСервере
Функция НаименованиеПлательщикаПоУмолчанию(ДанныеГосРеестра)
	
	// Для организации заполняем из полного наименования
	Если ДанныеГосРеестра.ЭтоЮрЛицо Тогда
		
		Возврат ДанныеГосРеестра.НаименованиеПолное;
		
	КонецЕсли;
	
	// Для ИП:
	// п.4 Приложения 1 к Приказу Минфина 107н:
	// Фамилия имя отчество "(ИП)" "//"Адрес регистрации"//"
	Шаблон = "%1 (ИП) //%2//";
	
	ФИО = ДанныеГосРеестра.Фамилия + ?(ПустаяСтрока(ДанныеГосРеестра.Имя), "", " " + ДанныеГосРеестра.Имя) + ?(ПустаяСтрока(ДанныеГосРеестра.Отчество), "", " " + ДанныеГосРеестра.Отчество);
	АдресРегистрации = ДанныеГосРеестра.ЮридическийАдрес;
	
	Возврат СтрШаблон(Шаблон, ФИО, АдресРегистрации);
	
КонецФункции

&НаСервере
Функция ВыделитьИзНаименованияОПФ(КлассификаторОПФ, знач НаименованиеОрганизации)
	
	Результат = Новый Структура("НаименованиеБезОПФ, КраткаяФорма, ПолнаяФорма", НаименованиеОрганизации, "", "");
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(НаименованиеОрганизации);
	
	Для Каждого Слово Из МассивСлов Цикл
		Для Каждого ДанныеОПФ Из КлассификаторОПФ Цикл
			Если ВРег(СокрЛП(Слово)) = ВРег(ДанныеОПФ.ПолнаяФорма) Тогда
				Результат.КраткаяФорма = ДанныеОПФ.КраткаяФорма;
				Результат.ПолнаяФорма = ДанныеОПФ.ПолнаяФорма;
				Результат.НаименованиеБезОПФ = СтроковыеФункцииКлиентСервер.СократитьДвойныеКавычки(СокрЛП(СтрЗаменить(НаименованиеОрганизации, Слово, "")));
				Возврат Результат;
			ИначеЕсли Не ПустаяСтрока(ДанныеОПФ.КраткаяФорма) И ВРег(СокрЛП(Слово)) = ВРег(ДанныеОПФ.КраткаяФорма) Тогда
				Результат.КраткаяФорма = ДанныеОПФ.КраткаяФорма;
				Результат.ПолнаяФорма = ДанныеОПФ.ПолнаяФорма;
				Результат.НаименованиеБезОПФ = СтроковыеФункцииКлиентСервер.СократитьДвойныеКавычки(СокрЛП(СтрЗаменить(НаименованиеОрганизации, Слово, "")));
				Возврат Результат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораНаименования(ДанныеГосРеестра, ИндексОрганизации)
	
	КлассификаторОПФ = РегламентированныеДанныеПовтИсп.КлассификаторОрганизационноПравовыхФорм();
	
	СписокВыбора = Элементы["Наименование" + ИндексОрганизации].СписокВыбора;
	СписокВыбора.Очистить();
	
	Если Не ПустаяСтрока(ДанныеГосРеестра.НаименованиеПолное) Тогда
		
		ОрганизационноПравоваяФорма = ВыделитьИзНаименованияОПФ(КлассификаторОПФ, ДанныеГосРеестра.НаименованиеПолное);
		
		СписокВыбора.Добавить(ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
		
		Если НЕ ПустаяСтрока(ОрганизационноПравоваяФорма.КраткаяФорма) Тогда
			
			СписокВыбора.Вставить(0, ОрганизационноПравоваяФорма.НаименованиеБезОПФ + " " + ОрганизационноПравоваяФорма.КраткаяФорма);
			СписокВыбора.Добавить(ОрганизационноПравоваяФорма.КраткаяФорма + " " + ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СписокВыбора.Количество() > 0 Тогда
		
		ДанныеГосРеестра.Наименование = СписокВыбора[0].Значение;
		Элементы["Наименование" + ИндексОрганизации].КнопкаВыпадающегоСписка = Истина;
		
	Иначе
		
		ДанныеГосРеестра.Наименование = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьНаименованиеИП(Индекс)
	
	СтруктураФИО = Новый Структура;
	СтруктураФИО.Вставить("Фамилия", СокрЛП(ЭтотОбъект["ИП_Фамилия" + Индекс]));
	СтруктураФИО.Вставить("Имя", СокрЛП(ЭтотОбъект["ИП_Имя" + Индекс]));
	СтруктураФИО.Вставить("Отчество", СокрЛП(ЭтотОбъект["ИП_Отчество" + Индекс]));
	
	МассивФИО = Новый Массив;
	МассивФИО.Добавить(СтруктураФИО.Фамилия);
	МассивФИО.Добавить(СтруктураФИО.Имя);
	МассивФИО.Добавить(СтруктураФИО.Отчество);
	
	ФамилияИнициалы = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СтруктураФИО);
	
	Результат = Новый Структура;
	Результат.Вставить("Наименование", ФамилияИнициалы + " " + НСтр("ru='ИП'"));
	Результат.Вставить("НаименованиеПолное", НСтр("ru='ИП'") + " " + ФамилияИнициалы);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПриВыбореВидаБизнеса(ВыбранныйВидБизнеса)
	
	Если ЗначениеЗаполнено(ВыбранныйВидБизнеса) Тогда
		ИмяКнопки = "ВидБизнеса" + ВыбранныйВидБизнеса;
	Иначе
		ИмяКнопки = "ВидБизнесаНеУказан";
	КонецЕсли;
	
	ВидБизнесаУжеВыбран = Элементы[ИмяКнопки].ЦветФона = ЦветФонУправляющегоПоля;
	
	Если ВидБизнесаУжеВыбран Тогда
		НайденныйЭлемент = ВидыБизнеса.НайтиПоЗначению(ВыбранныйВидБизнеса);
		Если НайденныйЭлемент <> Неопределено Тогда
			ВидыБизнеса.Удалить(НайденныйЭлемент);
		КонецЕсли;
		Элементы[ИмяКнопки].ЦветФона = ЦветФонаКнопки;
	Иначе
		ВидыБизнеса.Добавить(ВыбранныйВидБизнеса);
		Элементы[ИмяКнопки].ЦветФона = ЦветФонУправляющегоПоля;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АутентифицироватьПользователя(Знач Логин, Знач Пароль)
	
	РезультатАутентификации = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(Логин, Пароль);
	
	Если РезультатАутентификации.КодОшибки <> "НеверныйЛогинИлиПароль" Тогда
		ДанныеАутентификации = Новый Структура("Логин, Пароль", Логин, Пароль);
		СохранитьДанныеАутентификации(ДанныеАутентификации);
	КонецЕсли;
	
	Возврат РезультатАутентификации;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьДанныеАутентификации(Знач ДанныеАутентификации)
	
	УстановитьПривилегированныйРежим(Истина);
	ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(ДанныеАутентификации);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнениеЛогинаИПароляКорректно()
	
	Если ПустаяСтрока(ЛогинИнтернетПоддержки) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Логин"" не заполнено.'"),, "ЛогинИнтернетПоддержки");
		Возврат Ложь;
		
	ИначеЕсли ПустаяСтрока(ПарольИнтернетПоддержки) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Пароль"" не заполнено.'"),, "ПарольИнтернетПоддержки");
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура НастроитьФормуМобильныйКлиент(ИндексОрганизации)
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Лево.Видимость = Ложь;
	Элементы.Право.Видимость = Ложь;
	Элементы.ВидБизнесаОптоваяТорговля.Высота = 1;
	Элементы.ВидБизнесаРозничнаяТорговля.Высота = 1;
	Элементы.ВидБизнесаИнтернетМагазин.Высота = 1;
	Элементы.ВидБизнесаРаботыУслуги.Высота = 1;
	Элементы.ВидБизнесаПроизводство.Высота = 1;
	Элементы.ВидБизнесаНеУказан.Высота = 1;
	Элементы.Центр.ВыравниваниеЭлементовИЗаголовков = ВариантВыравниванияЭлементовИЗаголовков.Авто;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступСистемаНалогообложение" + ИндексОрганизации, "Видимость", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступЗаголовокНалогообложение" + ИндексОрганизации, "Видимость", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступСотрудники" + ИндексОрганизации, "Видимость", Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИП_Фамилия" + ИндексОрганизации, "ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияМалыйОтступ" + ИндексОрганизации, "Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуМобильноеПриложение()
	
	Если МобильноеПриложениеВызовСервера.ЕстьОграничениеМобильногоУНФ() 
		ИЛИ МобильноеПриложениеВызовСервера.ЕстьОграничениеМобильногоУНФ20() Тогда
		Элементы.ГруппаЗаголовок2.Видимость = Ложь;
		Элементы.ГруппаВидДеятельности.Видимость = Ложь;
		Элементы.ГруппаЗаголовок3.Видимость = Ложь;
		Элементы.ГруппаОрганизации.Видимость = Ложь;
		Элементы.ГруппаНавигация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуОграниченияКонфигурации()
	
	Элементы.ГруппаНавигация.Видимость = Не СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации();
	Элементы.ПодключениеИПП.Видимость = ИнтернетПоддержкаПользователей.ДоступноПодключениеИнтернетПоддержки();
	
	Если ОбщегоНазначенияУНФ.ЭтоУНФ() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияУНФ.ЭтоРозница() Тогда
		Элементы.ГруппаЗаголовок2.Видимость = Ложь;
		Элементы.ГруппаВидДеятельности.Видимость = Ложь;  
		Элементы.ДекорацияШагВторой2.Видимость = Истина;
		Элементы.ДекорацияШагТретий.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьНачалаРаботыСПрограммой()
	
	Элементы.НачатьРаботуСПрограммой.Видимость = Ложь;
	Элементы.ГруппаОжиданиеНачалаРаботы.Видимость = Истина;
	
	Если КоличествоОжиданий > 0 Тогда
		Элементы.ПодсказкаОжиданиеНачалаРаботы.Заголовок = СтрШаблон(НСтр("ru='Заполняем информацию об организации
		|Программа будет перезапущена: %1..'"), КоличествоОжиданий);
		КоличествоОжиданий = КоличествоОжиданий - 1;
		ПодключитьОбработчикОжидания("ОжидатьНачалаРаботыСПрограммой", 1, Истина);
		Возврат;
	КонецЕсли;
	
	ПродолжитьНачалоРаботы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеФормы()
	
	Если ВРЕГ(ПользовательИмя) = КэшЗначений.ДополнительныйПараметр Тогда
		Возврат;
	КонецЕсли;
	
	Ошибки = Неопределено;
	
	Если ПустаяСтрока(ПользовательИмя) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ПользовательИмя", НСтр("ru ='Заполните имя пользователя'"), "");
	КонецЕсли;
	
	Если ПользовательПароль <> ПользовательПарольПодтверждение Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ПользовательПароль", НСтр("ru ='Не совпадают пароли'"), "")
	КонецЕсли;
	
	Для каждого СтрокаОрганизации Из Объект.Организации Цикл
		Если ПустаяСтрока(СтрокаОрганизации.Наименование) Тогда
			ТекстОшибки = НСтр("ru ='Заполните название (организация %1)'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Наименование" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
		КонецЕсли;
		
		Если СтрокаОрганизации.ЮридическоеФизическоеЛицо = КэшЗначений.ФизЛицо Тогда
			Если ПустаяСтрока(СтрокаОрганизации.Фамилия) Тогда
				ТекстОшибки = НСтр("ru ='Заполните фамилию (организация %1)'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИП_Фамилия" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
			КонецЕсли;
			
			Если ПустаяСтрока(СтрокаОрганизации.Имя) Тогда
				ТекстОшибки = НСтр("ru ='Заполните имя (организация %1)'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИП_Имя" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	КэшЗначений.РазрешитьЗакрытие = (Ошибки = Неопределено);
	Если Не КэшЗначений.РазрешитьЗакрытие Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьНачалоРаботы()
	
	ДополнительныйПараметр = "";
	Если ВРЕГ(ПользовательИмя) = КэшЗначений.ДополнительныйПараметр Тогда
		ДополнительныйПараметр = КэшЗначений.ДополнительныйПараметр;
	КонецЕсли;
	
	РезультатФормыНачалаРаботы = НачалоРаботыСПрограммойКлиентСервер.НовыйРезультатФормыНачалаРаботы();
	РезультатФормыНачалаРаботы.НачатьРаботу = Истина;
	РезультатФормыНачалаРаботы.Пользователь= КэшЗначений.Пользователь;
	РезультатФормыНачалаРаботы.РазрешитьИнтерактивноеОткрытие = РазрешитьИнтерактивноеОткрытие;
	Если КэшЗначений.Свойство("ИмяПользователяВСервисе") Тогда
		РезультатФормыНачалаРаботы.ПользовательИмя = КэшЗначений.ИмяПользователяВСервисе;
	Иначе
		РезультатФормыНачалаРаботы.ПользовательИмя = ПользовательИмя;
	КонецЕсли;
	РезультатФормыНачалаРаботы.ПользовательПароль = ПользовательПароль;
	РезультатФормыНачалаРаботы.ПользовательEmail = СокрЛП(ПользовательEmail);
	РезультатФормыНачалаРаботы.ПользовательТелефон = СокрЛП(ПользовательТелефон);
	РезультатФормыНачалаРаботы.ПользовательСайт = СокрЛП(ПользовательСайт);
	РезультатФормыНачалаРаботы.ВидБизнесаОрганизации = ВидыБизнеса;
	РезультатФормыНачалаРаботы.Организации = Объект.Организации;
	РезультатФормыНачалаРаботы.ДополнительныйПараметр = ДополнительныйПараметр;
	
	НачалоРаботыСПрограммойКлиент.ОбработкаЗакрытияФормыНачалаРаботы(РезультатФормыНачалаРаботы,
		КэшЗначений.ПараметрыНачалаРаботы);
	
КонецПроцедуры

#КонецОбласти
