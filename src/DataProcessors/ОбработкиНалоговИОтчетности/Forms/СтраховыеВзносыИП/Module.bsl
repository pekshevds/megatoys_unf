#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Объект.Организация = Параметры.Организация;
		Иначе
			Организация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
			Если Не ЗначениеЗаполнено(Организация) Тогда
				Организация = Справочники.Организации.ПредопределеннаяОрганизация();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СобытиеКалендаря) Тогда
		Объект.СобытиеКалендаря = Параметры.СобытиеКалендаря;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СостояниеСобытия) Тогда
		
		Если ЗначениеЗаполнено(Параметры.СостояниеСобытия) Тогда
			
			Объект.СостояниеСобытия = Параметры.СостояниеСобытия;
			
		Иначе
			
			Объект.СостояниеСобытия = КалендарьОтчетности.ПолучитьСостояниеСобытияКалендаря(
			Объект.Организация,
			Объект.СобытиеКалендаря);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатыСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СобытиеКалендаря, "ДатаДокументаОбработкиСобытия,ДатаНачалаДокументов,ДатаОкончанияДокументов");
	ДатаДокументаОбработкиСобытия = ДатыСобытия.ДатаДокументаОбработкиСобытия;
	
	УпрощенныйЗачетФиксированныхВзносов = РегламентированнаяОтчетностьУСН.УпрощенныйЗачетФиксированныхВзносов(
	Объект.Организация,	ДатыСобытия.ДатаДокументаОбработкиСобытия);	
			
	ГодовойРасчет = (КонецКвартала(ДатаДокументаОбработкиСобытия) = КонецГода(ДатаДокументаОбработкиСобытия));	
	ПолучитьДанныеОтчетности();
	
	ПериодЗадачиПредставление = ПредставлениеПериода(
	ДатыСобытия.ДатаНачалаДокументов,
	КонецДня(ДатыСобытия.ДатаОкончанияДокументов),
	"ФП=Истина");
	
	Если Год(ДатаДокументаОбработкиСобытия) > 2022 Тогда
		Элементы.ГруппаСтрока6.Видимость = Ложь; 
		Элементы.ГруппаСтрока7.Видимость = Ложь;    
		Элементы.ГруппаСтрока8.Видимость = Ложь; 
		Элементы.ДекорацияИтого.Заголовок = НСтр("ru = 'Итого:'");
	ИначеЕсли Год(ДатаДокументаОбработкиСобытия) > 2013 Тогда
		Элементы.ДекорацияПФРСтраховая.Заголовок = НСтр("ru = 'ПФР'");
		Элементы.ДекорацияИтого.Заголовок = НСтр("ru = 'Итого (2-в-1):'");
		Элементы.ГруппаСтрока6.Видимость = Ложь; 		
	КонецЕсли;
	
	Если Год(ДатаДокументаОбработкиСобытия) > 2017 Тогда
		Элементы.ГруппаСтрока5.Видимость = Ложь;
	КонецЕсли;
	
	ПериодСобытия = Объект.СобытиеКалендаря.ДатаОкончанияДокументов;
	
	ПлательщикЕНП = РегламентированнаяОтчетностьУСН.ПлательщикЕНП(
	Объект.Организация,
	Объект.СобытиеКалендаря.ДатаОкончанияДокументов);
	
	ОрганизацияПредставление = Объект.Организация;
	ЗаписьКалендаря = Справочники.ЗаписиКалендаряПодготовкиОтчетности.ПолучитьЗаписьКалендаря(Объект.Организация,Объект.СобытиеКалендаря);
	
	ПодаетсяУведомлениеПоНалогуЗаПериод = Ложь;      
	ПодаетсяЗаявлениеОЗачете = ДатаДокументаОбработкиСобытия <> НачалоДня(КонецГода(ДатаДокументаОбработкиСобытия));

	Элементы.ГруппаСтрока12.Видимость = ПлательщикЕНП;
	
	СуммаКУплате = ЕдиныйТарифЗаСебяЧисло;	
	
	ЗаполнитьУведомления();
	УправлениеФормойБлокУведомление();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться") Тогда
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.РезультатРасчета;
		
	ИначеЕсли Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить") Тогда
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.УплатаНалога;
		
	Иначе
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
		
	КонецЕсли;
	
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанДокумент" Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументСсылка.РасходИзКассы") Тогда
			
			СоотвСсылок = Новый Соответствие;
			СоотвСсылок.Вставить(СсылкаДокументаНаличнойОплатыПФРНакопительная,"ДекорацияУплаченоПФРНакопительная");
			СоотвСсылок.Вставить(СсылкаДокументаНаличнойОплатыПФРСтраховая, "ДекорацияУплаченоПФРСтраховая");
			СоотвСсылок.Вставить(СсылкаДокументаНаличнойОплатыФФОМС, "ДекорацияУплаченоФФОМС");
			
			Если СоотвСсылок.Получить(Источник) = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененаТекущаяОрганизация" Тогда
		Если Окно.Основное Тогда
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("e1cib/navigationpoint/НалогиИОтчетность");
		Иначе
			Закрыть();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзменениеСостоянияСобытияКалендаря" Тогда
		Если Параметр = Объект.СобытиеКалендаря
			И Источник <> ЭтаФорма
			И Окно<> Неопределено
			И Не Окно.Основное Тогда
			Попытка
				Закрыть();
			Исключение
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МРОТЗаСебяНажатие(Элемент, СтандартнаяОбработка)
	ОткрытьФорму("РегистрСведений.МРОТ.ФормаСписка", Новый Структура("ДействующийНаДату", НачалоГода(
	ДатаДокументаОбработкиСобытия)));
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеПлатежкиНажатие(Элемент)
	
	оп = Новый ОписаниеОповещения("ПолучениеДанныхПлатежек", ЭтотОбъект, Новый Структура("Файл", Ложь));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеФайлыНажатие(Элемент)
	
	оп = Новый ОписаниеОповещения("ПолучениеДанныхПлатежек", ЭтотОбъект, Новый Структура("Файл", Истина));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеКвитанцииНажатие(Элемент)
	
	Если ПроверитьНаличиеГосОргана("РаспечататьКвитанциюЗавершение") Тогда
		РаспечататьКвитанциюЗавершение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьРасходыВПрограмме(Команда)
	
	ДанныеПоНачислениям = Новый Массив;
	
	Если  Год(ДатаДокументаОбработкиСобытия) < 2023 Тогда
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление",
		ДокументВзаиморасчетовСБюджетомФФОМС, ФФОМСЗаСебяЧисло, НСтр("ru='ФФОМС'")));
	КонецЕсли;	
	
	Если Год(ДатаДокументаОбработкиСобытия) < 2014 Тогда
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление",
		ДокументВзаиморасчетовСБюджетомПФРСтраховая, ПФРСтраховаяЗаСебяЧисло, НСтр("ru='ПФР страховая'")));
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление",
		ДокументВзаиморасчетовСБюджетомПФРНакопительная, ПФРНакопительнаяЗаСебяЧисло, НСтр(
		"ru='ПФР накопительная'")));
	ИначеЕсли Год(ДатаДокументаОбработкиСобытия) > 2014 И Год(ДатаДокументаОбработкиСобытия) < 2023 Тогда
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление",
		ДокументВзаиморасчетовСБюджетомПФРСтраховая, ПФРСтраховаяЗаСебяЧисло, НСтр("ru='ПФР'"))); 
	Иначе
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление",
		ДокументВзаиморасчетовСБюджетомЕдиныйТариф, ЕдиныйТарифЗаСебяЧисло, НСтр("ru='Страховые взносы единый тариф ИП'")));	
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ОбработкиНалоговИОтчетности.Форма.ГрупповаяРегистрацияРасходов",
	Новый Структура("Организация,ДанныеПоНачислениям, СобытиеКалендаря", Объект.Организация, ДанныеПоНачислениям, Объект.СобытиеКалендаря));
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

// -----------------------------------------------------------------------------
// События переходов

&НаКлиенте
Процедура ПереходЗаполнение(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Заполнить");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Объект.Организация, Объект.СобытиеКалендаря, Объект.СостояниеСобытия, "");
	
	ПараметрыФормы = Новый Структура("Организация,СобытиеКалендаря", Объект.Организация,Объект.СобытиеКалендаря);
	
	КалендарьОтчетностиКлиент.ОткрытьФормуНачалаЗаполнения(ЭтаФорма,ПараметрыФормы);
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходУплата(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Объект.Организация,
	Объект.СобытиеКалендаря,
	Объект.СостояниеСобытия,
	ВсегоКУплатеВПФР);
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.УплатаНалога;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходРезультатРасчета(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться");
	
	ЗафиксироватьПереходкУплате(Объект.Организация, Объект.СобытиеКалендаря, Объект.СостояниеСобытия, ВсегоКУплатеВПФР);
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.РезультатРасчета;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходВыполнил(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Завершено");
	
	КалендарьОтчетности.ЗавершитьСобытиеКалендаряОтчетности(
	Объект.Организация, Объект.СобытиеКалендаря, "");
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.ЗаписиКалендаряПодготовкиОтчетности"));
	Оповестить("Запись_ЗаписиКалендаряПодготовкиОтчетности");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗафиксироватьПереходкУплате(Организация, Событие, Состояние, ВсегоКУплате)
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Организация, Событие, Состояние, ВсегоКУплате);
	
	ЗаписьКалендаря = Справочники.ЗаписиКалендаряПодготовкиОтчетности.ПолучитьЗаписьКалендаря(Организация, Событие);
	Если ЗаписьКалендаря <> Неопределено Тогда
		ОбъектЗаписьКалендаря = ЗаписьКалендаря.ПолучитьОбъект();
		ОбъектЗаписьКалендаря.Завершено = Ложь;
		ОбъектЗаписьКалендаря.Записать();
	КонецЕсли;
	
КонецПроцедуры

// -----------------------------------------------------------------------------
// квитанция

&НаКлиенте
Процедура ПолучениеДанныхПлатежек(БанкСчетСтруктура, Параметры) Экспорт
	
	Если Параметры.Файл Тогда
		Если БанкСчетСтруктура <> Неопределено Тогда
			Если ПроверитьНаличиеГосОргана("ПолучитьФайлКлиентБанка") Тогда
				ПолучитьСписокПлатежекНаСервере();
				СписокПлатежек = Новый СписокЗначений;
				Если ПФРНакопительнаяЗаСебя <> "0,00 р." Тогда
					СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРНакопительная);
				КонецЕсли;
				
				Если ПФРСтраховаяЗаСебя <> "0,00 р." Тогда
					СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРСтраховая);
				КонецЕсли;
				
				Если ФФОМСЗаСебя <> "0,00 р." Тогда
					СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыФФОМС);
				КонецЕсли;  
				
				Если ЕдиныйТарифЗаСебя <> "0,00 р." Тогда
					СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРЕдиныйТариф);
				КонецЕсли;
				
				БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
				ОткрытьФорму(
				"Обработка.КлиентБанкУНФ.Форма.СохранениеПлатежек",
				БанкСчетСтруктура,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ПроверитьНаличиеГосОргана("РаспечататьПлатежноеПоручениеЗавершение") Тогда
			РаспечататьПлатежноеПоручениеЗавершение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры




///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


&НаКлиенте
Процедура НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок()
	
	ИмяКнопки = ПолучитьИмяЭлементаКнопкиПоУмолчанию(Элементы.СтраницаВзносыВПФР.ТекущаяСтраница.Имя);
	
	Если НЕ ПустаяСтрока(ИмяКнопки) Тогда
		Элементы[ИмяКнопки].КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.СтраницаВзносыВПФР.ТекущаяСтраница.Заголовок, ПериодЗадачиПредставление);
	РегламентированнаяОтчетностьУСНКлиентСервер.УстановитьЗаголовокФормыЗадачи(ЭтаФорма, Объект.Организация);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяЭлементаКнопкиПоУмолчанию(ИмяСтраницы)
	
	Если ИмяСтраницы = "РезультатРасчета" Тогда
		Возврат "ПереходДалее";
	ИначеЕсли ИмяСтраницы = "УплатаНалога" Тогда
		Возврат "ПереходВыполнил";
	ИначеЕсли ИмяСтраницы = "ЗадачаВыполнена" Тогда
		Возврат "";
	Иначе
		ВызватьИсключение НСтр("ru='Неизвестное имя текущей страницы'");
	КонецЕсли;
	
КонецФункции

// Процедура заполняет данные формы по данным ранее сформированной отчетности
//
&НаСервере
Процедура ПолучитьДанныеОтчетности()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияПоказателейОтчетности.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	ЗначенияПоказателейОтчетности.ПоказательОтчетности.Код КАК КодОтчетности,
	|	ЗначенияПоказателейОтчетности.ПоказательОтчетности
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейОтчетности КАК ЗначенияПоказателейОтчетности
	|ГДЕ
	|	ЗначенияПоказателейОтчетности.Организация = &Организация
	|	И ЗначенияПоказателейОтчетности.ПериодОтчетности = &ПериодОтчетности
	|	И ЗначенияПоказателейОтчетности.ПоказательОтчетности В ИЕРАРХИИ(&ГруппаПоказателя)");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ПериодОтчетности", Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия);
	
	Запрос.УстановитьПараметр("ГруппаПоказателя", ПланыВидовХарактеристик.ПоказателиОтчетности.ВзносыВПФРиФССЗаСебя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивСуммируемыхПоказателей = Новый Массив;
	МассивСуммируемыхПоказателей.Добавить(ПланыВидовХарактеристик.ПоказателиОтчетности.ПФРНакопительнаяЗаСебя);
	МассивСуммируемыхПоказателей.Добавить(ПланыВидовХарактеристик.ПоказателиОтчетности.ПФРСтраховаяЗаСебя);
	МассивСуммируемыхПоказателей.Добавить(ПланыВидовХарактеристик.ПоказателиОтчетности.ФФОМСЗаСебя); 
	МассивСуммируемыхПоказателей.Добавить(ПланыВидовХарактеристик.ПоказателиОтчетности.ЕдиныйТарифЗаСебя);
	
	Итого = 0;
	
	Пока Выборка.Следующий() Цикл
		ЭтаФорма[Выборка.КодОтчетности] = Формат(Выборка.ЗначениеПоказателя, "ЧДЦ=2; ЧРГ=; ЧН=" ) + " р.";
		
		Если МассивСуммируемыхПоказателей.Найти(Выборка.ПоказательОтчетности) <> Неопределено Тогда
			Итого = Итого + Выборка.ЗначениеПоказателя;
		КонецЕсли;
		
		Если Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.ПФРНакопительнаяЗаСебя Тогда
			ПФРНакопительнаяЗаСебяЧисло = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.ПФРСтраховаяЗаСебя Тогда
			ПФРСтраховаяЗаСебяЧисло = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.ФФОМСЗаСебя Тогда 
			ФФОМСЗаСебяЧисло = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.ЕдиныйТарифЗаСебя Тогда	
			ЕдиныйТарифЗаСебяЧисло = Выборка.ЗначениеПоказателя;
		КонецЕсли;
	КонецЦикла;
	
	ИтогоПФР = Формат(Итого, "ЧДЦ=2; ЧРГ=; ЧН=" ) + " р.";
	
	НайтиДокументыВзаиморасчетаСбюджетом();
	НайтиДокументОплаты();
	
	Если УплаченоСНачалоГОДЗаСебя = "0,00 р." Тогда
		Элементы.ГруппаСтрока11.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НайтиДокументОплаты()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходДенежныхСредствИзКассы.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РасходИзКассы КАК РасходДенежныхСредствИзКассы
	|ГДЕ
	|	РасходДенежныхСредствИзКассы.Организация = &Организация
	|	И РасходДенежныхСредствИзКассы.ДокументОснование = &ДокументОснование1
	|	И РасходДенежныхСредствИзКассы.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходДенежныхСредствИзКассы.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РасходИзКассы КАК РасходДенежныхСредствИзКассы
	|ГДЕ
	|	РасходДенежныхСредствИзКассы.Организация = &Организация
	|	И РасходДенежныхСредствИзКассы.ДокументОснование = &ДокументОснование2
	|	И РасходДенежныхСредствИзКассы.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходДенежныхСредствИзКассы.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РасходИзКассы КАК РасходДенежныхСредствИзКассы
	|ГДЕ
	|	РасходДенежныхСредствИзКассы.Организация = &Организация
	|	И РасходДенежныхСредствИзКассы.ДокументОснование = &ДокументОснование3
	|	И РасходДенежныхСредствИзКассы.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходДенежныхСредствИзКассы.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РасходИзКассы КАК РасходДенежныхСредствИзКассы
	|ГДЕ
	|	РасходДенежныхСредствИзКассы.Организация = &Организация
	|	И РасходДенежныхСредствИзКассы.ДокументОснование = &ДокументОснование4
	|	И РасходДенежныхСредствИзКассы.Проведен");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДокументОснование1", ДокументВзаиморасчетовСБюджетомПФРНакопительная);
	Запрос.УстановитьПараметр("ДокументОснование2", ДокументВзаиморасчетовСБюджетомПФРСтраховая);
	Запрос.УстановитьПараметр("ДокументОснование3", ДокументВзаиморасчетовСБюджетомФФОМС);  
	Запрос.УстановитьПараметр("ДокументОснование4", ДокументВзаиморасчетовСБюджетомЕдиныйТариф);
	
	Пакет = Запрос.ВыполнитьПакет();
	
	// ПФР Накопительная
	Выборка = Пакет[0].Выбрать();
	
	Если Выборка.Следующий() Тогда
		СсылкаДокументаНаличнойОплатыПФРНакопительная = Выборка.Ссылка;
	Иначе
		СсылкаДокументаНаличнойОплатыПФРНакопительная = Документы.РасходИзКассы.ПустаяСсылка();
	КонецЕсли;
	
	// ПФР Страховая
	Выборка = Пакет[1].Выбрать();
	
	Если Выборка.Следующий() Тогда
		СсылкаДокументаНаличнойОплатыПФРСтраховая = Выборка.Ссылка;
	Иначе
		СсылкаДокументаНаличнойОплатыПФРСтраховая = Документы.РасходИзКассы.ПустаяСсылка();
	КонецЕсли;
	
	// ФФОМС
	Выборка = Пакет[2].Выбрать();
	
	Если Выборка.Следующий() Тогда
		СсылкаДокументаНаличнойОплатыФФОМС = Выборка.Ссылка;
	Иначе
		СсылкаДокументаНаличнойОплатыФФОМС = Документы.РасходИзКассы.ПустаяСсылка();
	КонецЕсли;    
	
	// ФФОМС
	Выборка = Пакет[3].Выбрать();
	
	Если Выборка.Следующий() Тогда
		СсылкаДокументаНаличнойОплатыЕдиныйТариф = Выборка.Ссылка;
	Иначе
		СсылкаДокументаНаличнойОплатыЕдиныйТариф = Документы.РасходИзКассы.ПустаяСсылка();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура НайтиДокументыВзаиморасчетаСБюджетом()
	
	ДатыСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СобытиеКалендаря, "ДатаДокументаОбработкиСобытия,ДатаОкончанияСобытия");
	
	ДокументВзаиморасчетовСБюджетомПФРСтраховая = РегламентированнаяОтчетностьУСН.ПолучитьДокументВзаиморасчетовСБюджетом(
	Объект.Организация,
	Справочники.ВидыНалогов.ПФРСтраховая,
	ДатыСобытия.ДатаДокументаОбработкиСобытия,
	ДатыСобытия.ДатаОкончанияСобытия,
	Истина);
	
	ДокументВзаиморасчетовСБюджетомПФРНакопительная = РегламентированнаяОтчетностьУСН.ПолучитьДокументВзаиморасчетовСБюджетом(
	Объект.Организация,
	Справочники.ВидыНалогов.ПФРНакопительная,
	ДатыСобытия.ДатаДокументаОбработкиСобытия,
	ДатыСобытия.ДатаОкончанияСобытия,
	Истина);
	
	ДокументВзаиморасчетовСБюджетомФФОМС = РегламентированнаяОтчетностьУСН.ПолучитьДокументВзаиморасчетовСБюджетом(
	Объект.Организация,
	Справочники.ВидыНалогов.ФФОМС,
	ДатыСобытия.ДатаДокументаОбработкиСобытия,
	ДатыСобытия.ДатаОкончанияСобытия,
	Истина); 
	
	ДокументВзаиморасчетовСБюджетомЕдиныйТариф = РегламентированнаяОтчетностьУСН.ПолучитьДокументВзаиморасчетовСБюджетом(
	Объект.Организация,
	Справочники.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф,
	ДатыСобытия.ДатаДокументаОбработкиСобытия,
	ДатыСобытия.ДатаОкончанияСобытия,
	Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокПлатежек(ОповещениеВыборПлатежек)
	
	Если Не ЗначениеЗаполнено(БанковскийСчетПоУмолчанию) Тогда
		оп = Новый ОписаниеОповещения("ОповещениеВыбораСчета", ЭтотОбъект, Новый Структура("ОповещениеВыборПлатежек", ОповещениеВыборПлатежек));
		РегламентированнаяОтчетностьУСНКлиент.ПолучитьБанковскийСчетДляУплатыНалога(оп, Объект.Организация);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеВыборПлатежек, Новый Структура ("БанковскийСчет",БанковскийСчетПоУмолчанию))
	
КонецПроцедуры


&НаКлиенте
Процедура ОповещениеВыбораСчета(БанковскийСчет, Параметры) Экспорт
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		СписокПлатежек = Новый СписокЗначений;
		БанковскийСчетПоУмолчанию = БанковскийСчет;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеВыборПлатежек, Новый Структура ("СписокПлатежек, БанковскийСчет",СписокПлатежек, БанковскийСчетПоУмолчанию))
		
	КонецЕсли
КонецПроцедуры


&наСервере
Процедура ПолучитьСписокПлатежекНаСервере()
	
	Если  Год(ДатаДокументаОбработкиСобытия) < 2023 Тогда
		// Добавляем в массив
		// ПФРНакопительная
		Если ПФРНакопительнаяЗаСебя <> "0,00 р." Тогда
			СсылкаДокументаБезналичнойОплатыПФРНакопительная = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(ДокументВзаиморасчетовСБюджетомПФРНакопительная, БанковскийСчетПоУмолчанию,,ПФРНакопительнаяЗаСебяЧисло);
		КонецЕсли;
		
		// ПФРСтраховая
		Если ПФРСтраховаяЗаСебя <> "0,00 р." Тогда
			СсылкаДокументаБезналичнойОплатыПФРСтраховая = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(ДокументВзаиморасчетовСБюджетомПФРСтраховая, БанковскийСчетПоУмолчанию,,ПФРСтраховаяЗаСебяЧисло);
		КонецЕсли;
		
		// ФФОМС
		Если ФФОМСЗаСебя <> "0,00 р." Тогда
			СсылкаДокументаБезналичнойОплатыФФОМС = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(ДокументВзаиморасчетовСБюджетомФФОМС, БанковскийСчетПоУмолчанию,,ФФОМСЗаСебяЧисло);
		КонецЕсли; 
		
	Иначе
		Если ЕдиныйТарифЗаСебя <> "0,00 р." Тогда
			СсылкаДокументаБезналичнойОплатыПФРЕдиныйТариф = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(ДокументВзаиморасчетовСБюджетомФФОМС, БанковскийСчетПоУмолчанию,,ЕдиныйТарифЗаСебяЧисло);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура РаспечататьКвитанциюЗавершение() Экспорт
	
	ДанныеПоНачислениям = Новый Массив;
	
	Если Год(ДатаДокументаОбработкиСобытия) < 2023 Тогда
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма", ДокументВзаиморасчетовСБюджетомФФОМС, ФФОМСЗаСебяЧисло));
		
		Если Год(ДатаДокументаОбработкиСобытия) < 2014 Тогда
			ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма", ДокументВзаиморасчетовСБюджетомПФРНакопительная, ПФРНакопительнаяЗаСебяЧисло));
		КонецЕсли;
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма", ДокументВзаиморасчетовСБюджетомПФРСтраховая, ПФРСтраховаяЗаСебяЧисло));
	Иначе
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма", ДокументВзаиморасчетовСБюджетомЕдиныйТариф, ЕдиныйТарифЗаСебяЧисло));
	КонецЕсли;
	
	СтруктураПечати = ВыгрузитьДокументВФайл(ДанныеПоНачислениям, "Квитанция");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(НСтр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ФайловаяСистемаКлиент.СохранитьФайл(,СтруктураПечати.АдресФайла, СтруктураПечати.НаименованиеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьПлатежноеПоручениеЗавершение() Экспорт
	
	ПолучитьСписокПлатежекНаСервере();
	
	СписокДокументов = Новый Массив;
	
	Если ПФРНакопительнаяЗаСебя <> "0,00 р." Тогда
		СписокДокументов.Добавить(СсылкаДокументаБезналичнойОплатыПФРНакопительная);
	КонецЕсли;
	
	Если ПФРСтраховаяЗаСебя <> "0,00 р." Тогда
		СписокДокументов.Добавить(СсылкаДокументаБезналичнойОплатыПФРСтраховая);
	КонецЕсли;
	
	Если ФФОМСЗаСебя <> "0,00 р." Тогда
		СписокДокументов.Добавить(СсылкаДокументаБезналичнойОплатыФФОМС);
	КонецЕсли; 
	
	Если ЕдиныйТарифЗаСебя <> "0,00 р." Тогда
		СписокДокументов.Добавить(СсылкаДокументаБезналичнойОплатыПФРЕдиныйТариф);
	КонецЕсли;
	
	СтруктураПечати = ВыгрузитьДокументВФайл(СписокДокументов, "ПлатежноеПоручение");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(НСтр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(СтруктураПечати.АдресФайла, СтруктураПечати.НаименованиеФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьФайлКлиентБанка() Экспорт
	
	БанкСчетСтруктура = Новый Структура("БанковскийСчет", БанковскийСчетПоУмолчанию);
	
	ПолучитьСписокПлатежекНаСервере();
	СписокПлатежек = Новый СписокЗначений;
	Если ПФРНакопительнаяЗаСебя <> "0,00 р." Тогда
		СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРНакопительная);
	КонецЕсли;
	
	Если ПФРСтраховаяЗаСебя <> "0,00 р." Тогда
		СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРСтраховая);
	КонецЕсли;
	
	Если ФФОМСЗаСебя <> "0,00 р." Тогда
		СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыФФОМС);
	КонецЕсли;
	
	БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
	
	ОткрытьФорму(
	"Обработка.КлиентБанкУНФ.Форма.СохранениеПлатежек",
	БанкСчетСтруктура,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьНаличиеГосОргана(ИмяПроцедурыПоЗавершениюСозданияГосОргана)
	КодГосОргана = "";
	
	Если ГосОрганСуществует(Объект.Организация, КодГосОргана) Тогда
		Возврат Истина;
	Иначе
		ДопПараметры = Новый Структура("ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана", ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана);
		ТекстВопроса = НСтр("ru='В справочнике «Контрагенты» не задан налоговый орган для уплаты взноса.
		|Создать его автоматически?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОтветеНаВопросОСозданииГосОргана",
		ЭтаФорма, ДопПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Создать автоматически'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Создать вручную'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,,КодВозвратаДиалога.Да, НСтр("ru='Отсутствует налоговый орган'"));
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОтветеНаВопросОСозданииГосОргана(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеСведенийОбОтделенииПФРПоКоду(ДопПараметры);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидГосударственногоОргана", ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.НалоговыйОрган"));
		ПараметрыФормы.Вставить("КодГосударственногоОргана", ДопПараметры.КодГосОргана);
		ПараметрыФормы.Вставить("ИмяПроцедурыПоЗавершениюСозданияГосОргана",ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана);
		ПараметрыФормы.Вставить("ЗапретРедактированияКода", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзменениеПлатежныхРеквизитовФНС", ЭтотОбъект, ПараметрыФормы);
		
		ОткрытьФорму("Справочник.Контрагенты.Форма.РеквизитыГосударственныхОрганов", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект, , , ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаполнениеСведенийОбОтделенииПФРПоКоду(ДопПараметры)
	
	ОписаниеОшибки = "";
	ЗаполнитьСведенияОбОтделенииПФРПоКоду(ОписаниеОшибки, ДопПараметры.КодГосОргана);
	
	// Обработка ошибок
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			
			ТекстВопроса = НСтр("ru='Для автоматического создания налогового органа
			|в справочнике «Контрагенты» необходимо подключиться к Интернет-поддержке
			|пользователей. Подключиться сейчас?'");
			
			ПараметрыВопроса = Новый Структура("ВызовПослеПодключения,ИмяПроцедурыПоЗавершениюСозданияГосОргана", "ЗаполнитьСведенияОбОтделенииПФРПоКоду, КодГосОргана", ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана, ДопПараметры.КодГосОргана);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержку", ЭтотОбъект, ПараметрыВопроса);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	Иначе
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьПлатежноеПоручениеЗавершение" Тогда
			РаспечататьПлатежноеПоручениеЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "ПолучитьФайлКлиентБанка" Тогда
			ПолучитьФайлКлиентБанка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("ВызовПослеПодключения") Тогда
			
			Если ДополнительныеПараметры.ВызовПослеПодключения = "ЗаполнитьСведенияОбОтделенииПФРПоКоду" Тогда
				
				ЗаполнитьСведенияОбОтделенииПФРПоКоду(,ДополнительныеПараметры.КодГосОргана);
				
				Если ДополнительныеПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
					РаспечататьКвитанциюЗавершение();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииПФРПоКоду(ОписаниеОшибки = "", КодНалоговогоОрганаПолучателя)
	
	РеквизитыОрганаПФР = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(КодНалоговогоОрганаПолучателя);
	
	Если ЗначениеЗаполнено(РеквизитыОрганаПФР.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыОрганаПФР.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОрганаПФР.Ссылка) Тогда
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыОрганаПФР);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПлатежныхРеквизитовФНС(Ответ, ДопПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ГосОрганСуществует(Организация, КодГосОргана)
	
	КодГосОргана = Организация.КодНалоговогоОрганаПолучателя;
	ГосОрган = ДанныеГосударственныхОрганов.ГосударственныйОрган(Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган, КодГосОргана);
	
	Возврат ЗначениеЗаполнено(ГосОрган.Ссылка);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВыгрузитьДокументВФайл(МассивОбъектов, ВидФайла)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Если ВидФайла = "Квитанция" Тогда
		Для Каждого Объект Из МассивОбъектов Цикл
			
			ТабДокОбъекта = Документы.НачислениеНалогов.СформироватьКвитанцию(Объект.НачислениеНалогов, Объект.Сумма);
			ТабДокОбъекта.Область().СоздатьФорматСтрок();
			Если ТабДок = Неопределено Тогда
				ТабДок = ТабДокОбъекта;
			Иначе
				ТабДок.Вывести(ТабДокОбъекта);
			КонецЕсли;
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
		
	ИначеЕсли ВидФайла = "ПлатежноеПоручение" Тогда
		ОбъектыПечати = Новый СписокЗначений;
		ТабДок = Документы.ПлатежноеПоручение.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	СтруктураПечати = Новый Структура ("АдресФайла, НаименованиеФайла");
	
	Если ТабДок = Неопределено Тогда
		Возврат СтруктураПечати;
	КонецЕсли;
	
	Каталог = КаталогВременныхФайлов()+РаботаВМоделиСервиса.ЗначениеРазделителяСеанса()+"\";
	
	ПроверкаКаталога = Новый Файл(Каталог);
	Если НЕ ПроверкаКаталога.Существует() Тогда
		СоздатьКаталог(Каталог);
	КонецЕсли;
	РасширениеФайла = ".pdf" ;
	Если  МассивОбъектов.Количество() > 0 Тогда
		Если ВидФайла = "Квитанция" Тогда
			НаименованиеФайла = НСтр("ru='Квитанция на оплату страх.взносов'", ОбщегоНазначения.КодОсновногоЯзыка());
		Иначе
			НаименованиеФайла = НСтр("ru='Платежные поручения '", ОбщегоНазначения.КодОсновногоЯзыка());
			Для Каждого Платежка Из МассивОбъектов Цикл
				НаименованиеФайла = НаименованиеФайла + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Платежка.Номер, Истина, Истина)+ ", ";
			КонецЦикла;
			НаименованиеФайла = Лев(НаименованиеФайла, СтрДлина(НаименованиеФайла)-2);
		КонецЕсли;
	Иначе
		НаименованиеФайла = НСтр("ru='Результат'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	НаименованиеФайла = ?(СтрДлина(НаименованиеФайла) + СтрДлина(РасширениеФайла) > 64, Лев(НаименованиеФайла, 64
	- СтрДлина(РасширениеФайла)), НаименованиеФайла);
	НаименованиеФайла = СтрЗаменить(НаименованиеФайла, ":","_");
	
	ПолноеИмяФайла = Каталог + НаименованиеФайла + РасширениеФайла;
	ПолноеИмяФайла = СтрЗаменить(ПолноеИмяФайла, """","");
	
	Попытка
		ТабДок.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.PDF);
		
	Исключение
		ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Ошибка сохранения файла на сервере'"), УровеньЖурналаРегистрации.Ошибка, , ,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru= 'При сохранении файла на сервере в области данных %1 произошла ошибка:
		|%2'"), РаботаВМоделиСервиса.ЗначениеРазделителяСеанса(), ОписаниеОшибки()),
		РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
		Возврат СтруктураПечати;
		
	КонецПопытки;
	ДанныеФайла = Новый ДвоичныеДанные(ПолноеИмяФайла);
	АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
	УдалитьФайлы(ПолноеИмяФайла);
	СтруктураПечати.АдресФайла = АдресФайла;
	СтруктураПечати.НаименованиеФайла = НаименованиеФайла+РасширениеФайла;
	
	Возврат СтруктураПечати;
	
КонецФункции

#КонецОбласти

#Область Уведомления
// -----------------------------------------------------------------------------
// Уведомления
&НаКлиенте
Процедура ФормированиеДокументаЕНС(ИмяФормы)
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("ЗаписьКалендаря",                     ЗаписьКалендаря);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму(ИмяФормы,
	ПараметрыУведомления,
	ЭтотОбъект,
	,
	,
	,
	ОткрытьДокументОповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеСформироватьУведомление(Команда)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("ЗаписьКалендаря",                     ЗаписьКалендаря);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму("Документ.УведомлениеОбИсчисленныхСуммахНалогов.Форма.ФормаДокумента",
	ПараметрыУведомления,
	ЭтотОбъект,
	,
	,
	,
	ОткрытьДокументОповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры  

&НаКлиенте
Процедура ВыполнитьДействиеСформироватьОперациюПоЕНС(Команда)
	
	ВыполненоВТихомРежиме = Ложь;
	СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме);
	Если ВыполненоВТихомРежиме Тогда
		// Только перерисуем форму
		ОбработатьРедактированиеУведомления();
		Возврат;
	КонецЕсли;
	
	ФормированиеДокументаЕНС("Документ.ОперацияПоЕдиномуНалоговомуСчету.Форма.ФормаДокумента");
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме)
	
	ЕдиныйНалоговыйСчет.СформироватьОперациюПоЕНСВТихомРежиме(Объект.Организация,
	ОписаниеТаблицыНалоги(),
	ЗаписьКалендаря,
	ПериодСобытия,
	ВыполненоВТихомРежиме);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеТаблицыНалоги()
	
	ТаблицаНалоги = ТаблицаНалогиДляУведомления();
	
	ТаблицыНалогов = Документы.УведомлениеОбИсчисленныхСуммахНалогов.ОписаниеТаблицНалогов();
	ТаблицыНалогов.ТаблицаНалоги = ТаблицаНалоги;
	
	АдресХранилищаТаблицыНалоги = ПоместитьВоВременноеХранилище(ТаблицыНалогов, УникальныйИдентификатор);
	
	Возврат АдресХранилищаТаблицыНалоги;
	
КонецФункции 

&НаСервере
Функция ТаблицаНалогиДляУведомления() Экспорт
	
	ТаблицаНалоги = Документы.УведомлениеОбИсчисленныхСуммахНалогов.ОписаниеТаблицыНалогов();
	
	СуммаКУплате = ЕдиныйТарифЗаСебяЧисло; 	
	
	СрокУплаты = РегламентированнаяОтчетностьУСН.ОпределитьСрокПлатежаПоЕНП(Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия); 
	
	Если КонецМесяца(СрокУплаты) = КонецКвартала(СрокУплаты) Тогда
		Обработки.ОбработкиНалоговИОтчетности.УточнитьСрокУплатыВзносовЕдиныйТариф(СрокУплаты);
	КонецЕсли;	
	
	Если СуммаКУплате > 0 Тогда 
		
		НоваяСтрока = ТаблицаНалоги.Добавить();
		НоваяСтрока.РегистрацияВНалоговомОргане = Объект.Организация.РегистрацияВНалоговомОргане;
		НоваяСтрока.КодПоОКТМО                  = НоваяСтрока.РегистрацияВНалоговомОргане.КодПоОКТМО;
		НоваяСтрока.Налог                       = Справочники.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф;
		НоваяСтрока.КодБК                       = Справочники.ВидыНалогов.КБК(НоваяСтрока.Налог,, Объект.СобытиеКалендаря.ДатаОкончанияСобытия);
		НоваяСтрока.СчетУчета                   = НоваяСтрока.Налог.СчетУчета;
		Если Не ГодовойРасчет Тогда
			НоваяСтрока.Сумма = СуммаКУплате; 
		Иначе
			НоваяСтрока.Сумма =
			РегламентированнаяОтчетностьУСН.ВзносыПоЕдиномуТарифуПодлежащиеУплате(Объект.Организация, ДатаДокументаОбработкиСобытия);
		КонецЕсли;
		НоваяСтрока.СрокУплаты                  = СрокУплаты;		
		НоваяСтрока.НачалоПериода = Объект.СобытиеКалендаря.ДатаНачалаДокументов;
		НоваяСтрока.КонецПериода = Объект.СобытиеКалендаря.ДатаОкончанияДокументов;
		
	КонецЕсли;
	
	Возврат ТаблицаНалоги;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРедактированиеУведомления(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьСведенияОбУведомлениях();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбУведомлениях()
	
	Если Не ПлательщикЕНП Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьУведомления();
	
	УправлениеФормойБлокУведомление();
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьУведомления()
	
	ТаблицаУведомления = Обработки.ОбработкиНалоговИОтчетности.СозданныеДокументыОтраженияНаЕНС(
	Объект.Организация,
	Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия,
	Ложь, ЗаписьКалендаря);     
	
	УведомленияОбИсчисленныхНалогах.Загрузить(ТаблицаУведомления);
	
	УведомлениеОбИсчисленныхНалогахПодготовлено = УведомленияОбИсчисленныхНалогах.Количество() > 0;
	
КонецПроцедуры  

&НаКлиенте
Процедура УведомлениеНажатие(Команда)	
	ОтрытьДокументЕНС(Команда);  
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНажатие(Команда)
	ОтрытьДокументЕНС(Команда);
КонецПроцедуры 

&НаКлиенте
Процедура УдалитьЗаявлениеНажатие(Элемент)
	
	ИдентификаторЗаявления = ЕдиныйНалоговыйСчетКлиент.Идентификатор(Элемент.Имя);
	
	Если ИдентификаторЗаявления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// На форму команды добавляются с идентификатором строки таблицы
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(ИдентификаторЗаявления);
	Если СтрокаУведомления <> Неопределено Тогда
		ПометитьНаУдалениеЗаявлениеНаСервере(СтрокаУведомления.Уведомление);
		ОбновитьСведенияОбУведомлениях();
	КонецЕсли;
	
КонецПроцедуры   

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеЗаявлениеНаСервере(СсылкаНаДокумент)
	
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
		// Необходимо скинуть статус уведомления для корректного отображения статуса задачи
		ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправкиУведомления(ДокументОбъект.Уведомление,
			ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой());
	КонецЕсли;
	
	ДокументОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаявление(Команда)
	ФормированиеДокументаЕНС("Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Форма.ФормаДокумента");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУведомление(Идентификатор)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыФормыУведомления = ПараметрыФормыУведомления(Идентификатор);
	
	Если ПараметрыФормыУведомления.Свойство("Ключ") Тогда
		Если ТипЗнч(ПараметрыФормыУведомления.Ключ) = Тип("ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов") Тогда
			ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов";
		ИначеЕсли ТипЗнч(ПараметрыФормыУведомления.Ключ) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
			ИмяОбъекта = "ЗаявлениеОЗачетеВСчетПредстоящейОбязанности";
		Иначе
			ИмяОбъекта = "ОперацияПоЕдиномуНалоговомуСчету";
		КонецЕсли;
	Иначе
		Если ПодаетсяЗаявлениеОЗачете Тогда
			ИмяОбъекта = "ЗаявлениеОЗачетеВСчетПредстоящейОбязанности";
		ИначеЕсли ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
			ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов";
		Иначе
			ИмяОбъекта = "ОперацияПоЕдиномуНалоговомуСчету";
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("Документ." + ИмяОбъекта + ".ФормаОбъекта",
		ПараметрыФормыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтрытьДокументЕНС(Команда)
	// Команды генерируются в КопироватьЭлементСПодчиненными() и содержат числовой идентификатор,
	// совпадающий с идентификаторами строк в списке Платежи
	Идентификатор = ЕдиныйНалоговыйСчетКлиент.Идентификатор(Команда.Имя);
	Если Идентификатор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьУведомление(Идентификатор);
КонецПроцедуры  

&НаСервере
Функция ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления)
	
	Если ЗначениеЗаполнено(СтрокаУведомления.Уведомление) Тогда
		Возврат Новый Структура("Ключ", СтрокаУведомления.Уведомление);
	Иначе
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПараметрыФормыПустогоУведомления()
	
	ЗначенияЗаполнения = Новый Структура("Организация", Объект.Организация);
	
	Возврат Новый Структура("ЗначенияЗаполнения, ПериодСобытия, СобытиеКалендаря",
	ЗначенияЗаполнения, Объект.ПериодРегистрации, Объект.СобытиеКалендаря);
	
КонецФункции

&НаСервере
Функция ПараметрыФормыУведомления(Идентификатор)
	
	Если Идентификатор = Неопределено Тогда 
		//Получим параметры формы для пустого документа
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(Идентификатор);
	Если СтрокаУведомления = Неопределено Тогда
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	Возврат ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления);
	
КонецФункции

&НаСервере
Процедура УправлениеФормойБлокУведомление() 	
	
	ШрифтИнформацияУведомление = Новый Шрифт(,,,Ложь);
	ФорматнаяСтрока = "ЧЦ=15; ЧН=";   	
	// Подсказка по заявлению
	ТаблицаОстатковНаЕдиномНалоговомСчете = ЕдиныйНалоговыйСчет.ТаблицаОстатковНаЕдиномНалоговомСчете(
	ЭтотОбъект.ОрганизацияПредставление,
	ЭтотОбъект.ПериодСобытия); 
	СуммаУжеЗарезервированная = СозданныеЗаявленияОЗачете(УведомленияОбИсчисленныхНалогах).Итог("Сумма");
	ДоступныйОстаток = ТаблицаОстатковНаЕдиномНалоговомСчете.Итог("Сумма"); 
	ДоступныйОстаток = ДоступныйОстаток - СуммаУжеЗарезервированная;   
	ОсталосьОплатитьВзносов = Макс(0, СуммаКУплате - СуммаУжеЗарезервированная);
	МожноЗарезервироватьНаЕНС = Мин(ОсталосьОплатитьВзносов, Макс(0, ДоступныйОстаток - СуммаУжеЗарезервированная)); 	
	// Взносы ФСС платим отдельной платежкой не через ЕНС	
	Если УпрощенныйЗачетФиксированныхВзносов Тогда
		// По упрощенному зачету взносов нужно пополнить ЕНС и отразить их на счете через операцию
		ТекстИнформацияЗаявление = НСтр("ru = 'Отправлять заявление о зачете сумм не требуется.
		|Для корректного ведения расчетов по налогам нужно отразить взносы на едином налоговом счете.'");  
	ИначеЕсли ГодовойРасчет Тогда
		// В последнем квартале не нужно подавать заявление о зачете - сумма спишется в конце года
		// Если это взносы с доходов, списание произодёт в следующем году
		ТекстИнформацияЗаявление = СтрШаблон(НСтр(
		"ru = 'Фактическое списание страховых взносов с ЕНС произойдет %1 Отправлять заявление о зачете сумм не требуется.
		|
		|Для корректного ведения расчетов по налогам нужно отразить их на едином налоговом счете.'"),
		Формат(Объект.СобытиеКалендаря.ДатаОкончанияСобытия, "ДЛФ=DD")); 
		
	ИначеЕсли ДоступныйОстаток >= ЭтотОбъект.СуммаКУплате Тогда
		ТекстИнформацияЗаявление = СтрШаблон(
		НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
		Формат(ДоступныйОстаток, ФорматнаяСтрока));
	Иначе
		ТекстИнформацияЗаявление = СтрШаблон(
		НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма не позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
		Формат(ДоступныйОстаток, ФорматнаяСтрока));
	КонецЕсли;   
	СтроковыеФункции.ФорматированнаяСтрока(ТекстИнформацияЗаявление);
	Элементы.ПояснениеУменьшения.Заголовок = ТекстИнформацияЗаявление;
	
	// Элементы заявления о зачете добавляются на форму по идентификатору в таблице УведомленияОбИсчисленныхНалогах.
	// После каждого обновления таблицы идентификаторы строк меняются и нужно обновлять элементы на форме.  
	
	Элементы.ГруппаЗаявленияОЗачете.Видимость = УведомлениеОбИсчисленныхНалогахПодготовлено;
	
	СозданныеЗаявления = СозданныеЗаявленияОЗачете(УведомленияОбИсчисленныхНалогах);
	
	КоличествоЭлементов = Элементы.ГруппаЗаявленияОЗачете.ПодчиненныеЭлементы.Количество();
	
	Для Индекс = 1 По КоличествоЭлементов Цикл
		
		ТекущиеЭлементГруппы = Элементы.ГруппаЗаявленияОЗачете.ПодчиненныеЭлементы[КоличествоЭлементов - Индекс];
		
		Если ТекущиеЭлементГруппы.Имя = "ПредставлениеЗаявления" Или ТекущиеЭлементГруппы.Имя = "ИтогПоУведомлениям" Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ПодчиненныйЭлемент Из ТекущиеЭлементГруппы.ПодчиненныеЭлементы Цикл
			Элементы.Удалить(ПодчиненныйЭлемент);
		КонецЦикла;
		
		Элементы.Удалить(ТекущиеЭлементГруппы);
		
	КонецЦикла;
	
	Если УведомлениеОбИсчисленныхНалогахПодготовлено Тогда
		
		Если ГодовойРасчет Или УпрощенныйЗачетФиксированныхВзносов Тогда
			ОтобразитьСозданныеДокументыНаФорме(СозданныеОперацииПоЕНС(УведомленияОбИсчисленныхНалогах));
		КонецЕсли;
		
		Если УпрощенныйЗачетФиксированныхВзносов Или Не ГодовойРасчет Тогда
			ОтобразитьСозданныеДокументыНаФорме(СозданныеЗаявления)
		КонецЕсли;
		
	КонецЕсли;
	
	ВсегоОтраженоПоБУ = СозданныеОперацииПоЕНС(УведомленияОбИсчисленныхНалогах).Итог("Сумма");
	Элементы.СформироватьОперацияПоЕНС.Видимость = ГодовойРасчет И (ВсегоОтраженоПоБУ < СуммаКУплате);
    
	Если УпрощенныйЗачетФиксированныхВзносов Тогда 	
		Элементы.ПояснениеУменьшения.Видимость = Истина;
		Элементы.ГруппаСуммаКРезерву.Видимость = Ложь;
		Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Ложь;
		Элементы.ДекорацияТекстОписание8.Видимость = Ложь;
	Иначе
		
		Элементы.ПояснениеУменьшения.Видимость = Истина;
		Элементы.ГруппаСуммаКРезерву.Видимость = Истина;
		
		Если МожноЗарезервироватьНаЕНС > 0 Тогда
			Элементы.МожноЗарезервироватьНаЕНС.Видимость = Не ГодовойРасчет;
			Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Не ГодовойРасчет;
		Иначе
			Элементы.МожноЗарезервироватьНаЕНС.Видимость = Ложь;
			Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Ложь;  
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры   

&НаСервереБезКонтекста
Функция СозданныеОперацииПоЕНС(Знач УведомленияОбИсчисленныхНалогах)
	
	СозданныеДокументы = Новый ТаблицаЗначений;
	СозданныеДокументы.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету"));
	СозданныеДокументы.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	СозданныеДокументы.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	СозданныеДокументы.Колонки.Добавить("ПредставлениеДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	СозданныеДокументы.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	
	Для Каждого ТекущееУведомление Из УведомленияОбИсчисленныхНалогах Цикл
		Если ТипЗнч(ТекущееУведомление.Уведомление) = Тип("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету") Тогда
			
			НоваяСтрока = СозданныеДокументы.Добавить();
			НоваяСтрока.Ссылка = ТекущееУведомление.Уведомление;
			НоваяСтрока.Сумма = ТекущееУведомление.Сумма;
			НоваяСтрока.НомерСтроки = ТекущееУведомление.ПолучитьИдентификатор() + 1;
			
			РеквизитыЗаявления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееУведомление.Уведомление, "Дата, Номер");
			
			НоваяСтрока.Дата = РеквизитыЗаявления.Дата;
			НоваяСтрока.ПредставлениеДокумента = СтрШаблон(НСтр("ru ='Операция по единому налоговому счету %1 от %2 на сумму %3'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыЗаявления.Номер),
				Формат(РеквизитыЗаявления.Дата, "ДЛФ=D"),
				Формат(ТекущееУведомление.Сумма, "ЧЦ=15; ЧДЦ=2"));
			
		КонецЕсли;
	КонецЦикла;
	
	СозданныеДокументы.Сортировать("Дата Возр");
	Возврат СозданныеДокументы;
	
КонецФункции

&НаСервереБезКонтекста
Функция СозданныеЗаявленияОЗачете(Знач УведомленияОбИсчисленныхНалогах)
	
	СозданныеЗаявления = Новый ТаблицаЗначений;
	СозданныеЗаявления.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности"));
	СозданныеЗаявления.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	СозданныеЗаявления.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	СозданныеЗаявления.Колонки.Добавить("ПредставлениеДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	СозданныеЗаявления.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	СозданныеЗаявления.Колонки.Добавить("Сдано", Новый ОписаниеТипов("Булево"));
	
	Для Каждого ТекущееУведомление Из УведомленияОбИсчисленныхНалогах Цикл
		Если ТипЗнч(ТекущееУведомление.Уведомление) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
			
			НоваяСтрока = СозданныеЗаявления.Добавить();
			НоваяСтрока.Ссылка = ТекущееУведомление.Уведомление;
			НоваяСтрока.Сумма = ТекущееУведомление.Сумма;
			НоваяСтрока.НомерСтроки = ТекущееУведомление.ПолучитьИдентификатор() + 1;
			
			РеквизитыЗаявления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееУведомление.Уведомление, "Дата, Номер, Уведомление");
			
			СостояниеДокумента = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(РеквизитыЗаявления.Уведомление);
			НоваяСтрока.Сдано = СостояниеДокумента.Сдано;
			НоваяСтрока.Дата = РеквизитыЗаявления.Дата;
			НоваяСтрока.ПредставлениеДокумента = СтрШаблон(НСтр("ru ='Заявление о зачете страховых взносов %1 от %2 на сумму %3 (%4)'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыЗаявления.Номер),
				Формат(РеквизитыЗаявления.Дата, "ДЛФ=D"),
				Формат(ТекущееУведомление.Сумма, "ЧЦ=15; ЧДЦ=2"),
				СостояниеДокумента.Статус);
			
		КонецЕсли;
	КонецЦикла;
	
	СозданныеЗаявления.Сортировать("Дата Возр");
	Возврат СозданныеЗаявления;
	
КонецФункции

&НаСервере
Процедура ОтобразитьСозданныеДокументыНаФорме(СозданныеДокументы)
	
	Для Каждого ТекущееЗаявление Из СозданныеДокументы Цикл
		
		НомерКонтейнера = Формат(ТекущееЗаявление.НомерСтроки, "ЧЦ=2; ЧН=0");
		ИмяГруппы = СтрШаблон("ПредставлениеЗаявления%1", НомерКонтейнера);
		Если Элементы.Найти(ИмяГруппы) = Неопределено Тогда
			ЕдиныйНалоговыйСчет.КопироватьЭлементСПодчиненными(
				ЭтотОбъект,
				Элементы.ПредставлениеЗаявления,
				Элементы.ГруппаЗаявленияОЗачете,
				ТекущееЗаявление.НомерСтроки);
		КонецЕсли;
		
		ИмяЭлементаЗаявление        = СтрШаблон("ЗаявлениеОЗачете%1", НомерКонтейнера);
		ИмяГруппыЗаявление          = СтрШаблон("ПредставлениеЗаявления%1", НомерКонтейнера);
		ИмяЭлементаУдалитьЗаявление = СтрШаблон("УдалитьЗаявление%1", НомерКонтейнера);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементаЗаявление, "Заголовок",
			ТекущееЗаявление.ПредставлениеДокумента);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяГруппыЗаявление, "Видимость",
			Истина); 
		Если СозданныеДокументы.Колонки.Найти("Сдано") <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементаУдалитьЗаявление, "Видимость",
				Не ТекущееЗаявление.Сдано);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

