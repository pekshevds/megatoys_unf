#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Объект.Организация = Параметры.Организация;
		Иначе
			Организация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
			Если Не ЗначениеЗаполнено(Организация) Тогда
				Организация = Справочники.Организации.ПредопределеннаяОрганизация();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СобытиеКалендаря) Тогда
		Объект.СобытиеКалендаря = Параметры.СобытиеКалендаря;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СостояниеСобытия) Тогда
		
		Если ЗначениеЗаполнено(Параметры.СостояниеСобытия) Тогда
			
			Объект.СостояниеСобытия = Параметры.СостояниеСобытия;
			
		Иначе
			
			Объект.СостояниеСобытия = КалендарьОтчетности.ПолучитьСостояниеСобытияКалендаря(
			Объект.Организация,
			Объект.СобытиеКалендаря);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатыСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СобытиеКалендаря, "ДатаДокументаОбработкиСобытия,ДатаНачалаДокументов,ДатаОкончанияДокументов");
	ДатаДокументаОбработкиСобытия = ДатыСобытия.ДатаДокументаОбработкиСобытия;
	ДатаОкончанияДокументов = ДатыСобытия.ДатаОкончанияДокументов;
	
	УпрощенныйЗачетФиксированныхВзносов = РегламентированнаяОтчетностьУСН.УпрощенныйЗачетФиксированныхВзносов(
	Объект.Организация,	ДатыСобытия.ДатаДокументаОбработкиСобытия);	
	
	ГодовойРасчет = (КонецКвартала(ДатаДокументаОбработкиСобытия) = КонецГода(ДатаДокументаОбработкиСобытия));	
	ПолучитьДанныеОтчетности(); 
	
	ПолучитьДанныеОтчетности();
	
	Элементы.ГруппаУплатыЧерезБанкОписание.Видимость = Ложь;
	Элементы.ГруппаУплатыЧерезКассуОписание.Видимость = Ложь;	
	
	ПериодЗадачиПредставление = ПредставлениеПериода(
	ДатыСобытия.ДатаНачалаДокументов,
	КонецДня(ДатыСобытия.ДатаОкончанияДокументов),
	"ФП=Истина");
	
	ОрганизацияПредставление = Объект.Организация;
	ПериодСобытия = Объект.СобытиеКалендаря.ДатаОкончанияДокументов;
	
	ПлательщикЕНП = РегламентированнаяОтчетностьУСН.ПлательщикЕНП(
	Объект.Организация,
	Объект.СобытиеКалендаря.ДатаОкончанияДокументов);
	
	ЗаписьКалендаря = Справочники.ЗаписиКалендаряПодготовкиОтчетности.ПолучитьЗаписьКалендаря(Объект.Организация,Объект.СобытиеКалендаря);
	
	ПодаетсяУведомлениеПоНалогуЗаПериод = Ложь; 
	ПодаетсяЗаявлениеОЗачете = ДатаДокументаОбработкиСобытия <> НачалоДня(КонецГода(ДатаДокументаОбработкиСобытия));

	СуммаКУплате = ВсегоКУплатеВПФР;
	
	ЗаполнитьУведомления();
	УправлениеФормойБлокУведомление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться") Тогда
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.РезультатРасчета;
		
	ИначеЕсли Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить") Тогда
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.УплатаНалога;
		
	Иначе
		
		Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
		
	КонецЕсли;
	
	
	УстановитьВидимостьИДоступностьЭлементов();
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// -----------------------------------------------------------------------------
// квитанция

&НаКлиенте
Процедура ДекорацияВсеПлатежкиНажатие(Элемент)
	
	Если ПФРСвыше300тр = "0,00 р." Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;
	
	оп = Новый ОписаниеОповещения("ПолучениеДанныхПлатежек", ЭтотОбъект, Новый Структура("Файл", Ложь));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеФайлыНажатие(Элемент)
	
	Если ПФРСвыше300тр = "0,00 р." Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;
	
	оп = Новый ОписаниеОповещения("ПолучениеДанныхПлатежек", ЭтотОбъект, Новый Структура("Файл", Истина));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеДанныхПлатежек(БанкСчетСтруктура, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(БанкСчетСтруктура) Тогда
		Возврат;
	КонецЕсли;   
	
	Если Параметры.Файл Тогда
		Если ПроверитьНаличиеГосОргана("ПолучитьФайлКлиентБанка") Тогда
			ПолучитьСписокПлатежекНаСервере();
			СписокПлатежек = Новый СписокЗначений;
			Если ПФРСвыше300тр <> "0,00 р." Тогда
				СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРСтраховая);
			КонецЕсли;
			
			БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
			ОткрытьФорму(
			"Обработка.КлиентБанкУНФ.Форма.СохранениеПлатежек", БанкСчетСтруктура, , , , , ,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	Иначе
		Если ПроверитьНаличиеГосОргана("РаспечататьПлатежноеПоручениеЗавершение") Тогда
			РаспечататьПлатежноеПоручениеЗавершение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВсеКвитанцииНажатие(Элемент)
	
	Если ПФРСвыше300тр = "0,00 р." Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;
	
	Если ПроверитьНаличиеГосОргана("РаспечататьКвитанциюЗавершение") Тогда
		РаспечататьКвитанциюЗавершение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходовУСНСвыше300трНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПериодПараметр = КалендарьОтчетностиПовтИсп.ПолучитьСтандартныйПериодДокументовДляСобытия(Объект.СобытиеКалендаря);
	
	ОткрытьФорму("Отчет.ДоходыРасходы.ФормаОбъекта",
	Новый Структура("Организация,СформироватьПриОткрытии, Период,ВидОтчета", Объект.Организация, Истина,
	ПериодПараметр, "Доходы"));
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаРасходовУСНСвыше300трНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПериодПараметр = КалендарьОтчетностиПовтИсп.ПолучитьСтандартныйПериодДокументовДляСобытия(Объект.СобытиеКалендаря);
	
	ОткрытьФорму("Отчет.ДоходыРасходы.ФормаОбъекта",
	Новый Структура("Организация,СформироватьПриОткрытии, Период,ВидОтчета", Объект.Организация, Истина,
	ПериодПараметр, "Расходы"));
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченоСНачалоГОДСвыше300трНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПериодПараметр = КалендарьОтчетностиПовтИсп.ПолучитьСтандартныйПериодДокументовДляСобытия(Объект.СобытиеКалендаря);
	
	СпсВидов = Новый СписокЗначений;
	СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСвыше300тр"));
	
	СпсЗадач = Новый СписокЗначений;
	СпсЗадач.Добавить(ПредопределенноеЗначение(
	"Справочник.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыПриДоходахСвыше300тр"));
	
	ОткрытьФорму("Отчет.УплатыНалоговИВзносов.ФормаОбъекта",
	Новый Структура("Организация, СформироватьПриОткрытии, Период,ВидВзаиморасчетов, УчетВыплатТекущегоГода, ЗадачиКалендаря",
	Объект.Организация, Истина, ПериодПараметр, СпсВидов, Ложь, СпсЗадач));
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходовЕНВДСвыше300трНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму(
	"Обработка.ОбработкиНалоговИОтчетности.Форма.ТаблицаЕНВДРасшифровка",
	Новый Структура(
	"АдресОбщейТаблицы,ПолнаяТаблица,ПоказатьДоходыИКвартал",
	АдресТаблицаЕНВД,
	Истина, Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходовПатентСвыше300трНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму(
	"Обработка.ОбработкиНалоговИОтчетности.Форма.ТаблицаПатентРасшифровка",
	Новый Структура(
	"Организация,НачалоПериода,ОкончаниеПериода",
	Объект.Организация,
	НачалоГода(ДатаОкончанияДокументов), КонецДня(ДатаОкончанияДокументов)));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// -----------------------------------------------------------------------------
// События переходов

&НаКлиенте
Процедура ПереходЗаполнение(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Заполнить");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Объект.Организация,
	Объект.СобытиеКалендаря,
	Объект.СостояниеСобытия,
	"");
	
	ПараметрыФормы = Новый Структура("Организация,СобытиеКалендаря", Объект.Организация,Объект.СобытиеКалендаря);
	
	КалендарьОтчетностиКлиент.ОткрытьФормуНачалаЗаполнения(ЭтаФорма,ПараметрыФормы);
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходУплата(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить");
	
	ЗафиксироватьПереходкУплате(Объект.Организация, Объект.СобытиеКалендаря, Объект.СостояниеСобытия, ВсегоКУплатеВПФР);
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.УплатаНалога;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗафиксироватьПереходкУплате(Организация, Событие, Состояние, ВсегоКУплате)
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Организация,
	Событие,
	Состояние,
	ВсегоКУплате);
	
	
	ЗаписьКалендаря = Справочники.ЗаписиКалендаряПодготовкиОтчетности.ПолучитьЗаписьКалендаря(Организация, Событие);
	Если ЗаписьКалендаря <> Неопределено Тогда
		ОбъектЗаписьКалендаря = ЗаписьКалендаря.ПолучитьОбъект();
		ОбъектЗаписьКалендаря.Завершено = Ложь;
		ОбъектЗаписьКалендаря.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходРезультатРасчета(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
	Объект.Организация,
	Объект.СобытиеКалендаря,
	Объект.СостояниеСобытия,
	ВсегоКУплатеВПФР);
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.РезультатРасчета;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходВыполнил(Команда)
	
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Завершено");
	
	КалендарьОтчетности.ЗавершитьСобытиеКалендаряОтчетности(
	Объект.Организация,
	Объект.СобытиеКалендаря,
	"");
	
	Элементы.СтраницаВзносыВПФР.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.ЗаписиКалендаряПодготовкиОтчетности"));
	Оповестить("Запись_ЗаписиКалендаряПодготовкиОтчетности");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьРасходыВПрограмме(Команда)
	
	Если ВсегоКУплатеВПФР = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;
	
	Список = Новый СписокЗначений;
	
	ДанныеПоНачислениям = Новый Массив;
	Если ПериодСобытия >= '20230101' ИЛИ РегламентированнаяОтчетностьУСН.ПлательщикЕНП(Объект.Организация, ПериодСобытия) Тогда
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление", ДокументВзаиморасчетовСБюджетомПФРСтраховая, ВсегоКУплатеВПФР, НСтр("ru='Единый налоговый платеж'")));
		Список.Добавить(ДокументВзаиморасчетовСБюджетомПФРСтраховая,НСтр("ru='Единый налоговый платеж'"));
	Иначе
		ДанныеПоНачислениям.Добавить(Новый Структура("НачислениеНалогов,Сумма,Представление", ДокументВзаиморасчетовСБюджетомПФРСтраховая, ВсегоКУплатеВПФР, НСтр("ru='ПФР'")));
		Список.Добавить(ДокументВзаиморасчетовСБюджетомПФРСтраховая,НСтр("ru='ПФР'"));
	КонецЕсли;
	
	ОткрытьФорму(
	"Обработка.ОбработкиНалоговИОтчетности.Форма.ГрупповаяРегистрацияРасходов",
	Новый Структура("Организация,СписокКПоиску,ДанныеПоНачислениям, СобытиеКалендаря", Объект.Организация, Список,
	ДанныеПоНачислениям,Объект.СобытиеКалендаря));
	
КонецПроцедуры

&НаКлиенте
Процедура Пересчитать(Команда)
	ОбновитьРасчет();
КонецПроцедуры

&НаКлиенте
Процедура УплатаЧерезБанк(Команда)
	Элементы.ГруппаУплатыЧерезБанкОписание.Видимость = Истина;
	Элементы.ГруппаУплатыЧерезКассуОписание.Видимость = Ложь;
	Элементы.УплатаЧерезБанк.Шрифт = Новый Шрифт(Элементы.УплатаЧерезБанк.Шрифт, , , Истина);
	Элементы.УплатаЧерезКассу.Шрифт = Новый Шрифт(Элементы.УплатаЧерезКассу.Шрифт, , , Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УплатаЧерезКассу(Команда)
	Элементы.ГруппаУплатыЧерезБанкОписание.Видимость = Ложь;
	Элементы.ГруппаУплатыЧерезКассуОписание.Видимость = Истина;
	Элементы.УплатаЧерезБанк.Шрифт = Новый Шрифт(Элементы.УплатаЧерезБанк.Шрифт, , , Ложь);
	Элементы.УплатаЧерезКассу.Шрифт = Новый Шрифт(Элементы.УплатаЧерезКассу.Шрифт, , , Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок()
	
	ИмяКнопки = ПолучитьИмяЭлементаКнопкиПоУмолчанию(Элементы.СтраницаВзносыВПФР.ТекущаяСтраница.Имя);
	
	Если НЕ ПустаяСтрока(ИмяКнопки) Тогда
		Элементы[ИмяКнопки].КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.СтраницаВзносыВПФР.ТекущаяСтраница.Заголовок, ПериодЗадачиПредставление);
	РегламентированнаяОтчетностьУСНКлиентСервер.УстановитьЗаголовокФормыЗадачи(ЭтаФорма, Объект.Организация);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяЭлементаКнопкиПоУмолчанию(ИмяСтраницы)
	
	Если ИмяСтраницы = "РезультатРасчета" Тогда
		Возврат "ПереходДалее";
	ИначеЕсли ИмяСтраницы = "УплатаНалога" Тогда
		Возврат "ПереходВыполнил";
	ИначеЕсли ИмяСтраницы = "ЗадачаВыполнена" Тогда
		Возврат "";
	Иначе
		ВызватьИсключение НСтр("ru='Неизвестное имя текущей страницы'");
	КонецЕсли;
	
КонецФункции

// Процедура заполняет данные формы по данным ранее сформированной отчетности
//
&НаСервере
Процедура ПолучитьДанныеОтчетности()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияПоказателейОтчетности.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	ЗначенияПоказателейОтчетности.ПоказательОтчетности.Код КАК КодОтчетности,
	|	ЗначенияПоказателейОтчетности.ПоказательОтчетности
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейОтчетности КАК ЗначенияПоказателейОтчетности
	|ГДЕ
	|	ЗначенияПоказателейОтчетности.Организация = &Организация
	|	И ЗначенияПоказателейОтчетности.ПериодОтчетности = &ПериодОтчетности
	|	И ЗначенияПоказателейОтчетности.ПоказательОтчетности В ИЕРАРХИИ(&ГруппаПоказателя)");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ПериодОтчетности", ДатаДокументаОбработкиСобытия);
	
	Запрос.УстановитьПараметр("ГруппаПоказателя", ПланыВидовХарактеристик.ПоказателиОтчетности.ВзносыВПФРСвыше300тр);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДоходыУСН = 0;
	РасходыУСН = 0;
	ДоходыЕНВД = 0;
	ДоходыПатент = 0;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.СуммаДоходовУСНСвыше300тр Тогда
			ДоходыУСН = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.СуммаДоходовЕНВДСвыше300тр Тогда
			ДоходыЕНВД = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.СуммаДоходовПатентСвыше300тр Тогда
			ДоходыПатент = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВДСвыше300тр Тогда
			
			Табличка = РегламентированнаяОтчетностьУСН.ПолучитьЗначениеПроизвольногоПоказателя(
			Объект.Организация,
			ДатаДокументаОбработкиСобытия,
			ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВДСвыше300тр);
			
			Если Табличка = Неопределено Тогда
				ЗаписьЖурналаРегистрации(
				НСтр("ru='ЕНВД.Заполнение задачи'"),
				УровеньЖурналаРегистрации.Ошибка,
				,
				,
				НСтр("ru='Не удалось получить данные для ТаблицаЕНВД'"));
			Иначе
				АдресТаблицаЕНВД = ПоместитьВоВременноеХранилище(Табличка,УникальныйИдентификатор);
			КонецЕсли;
			
			Продолжить;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.ПФРСвыше300тр Тогда
			ВсегоКУплатеВПФР = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.СуммаРасходовУСНСвыше300тр Тогда	
			РасходыУСН = Выборка.ЗначениеПоказателя; 
		КонецЕсли;
		
		ЭтаФорма[Выборка.КодОтчетности] = Формат(Выборка.ЗначениеПоказателя, "ЧДЦ=2; ЧРГ=; ЧН=" ) + " р.";
	КонецЦикла;
	Если ПустаяСтрока(СуммаДоходовПатентСвыше300тр) Тогда
		СуммаДоходовПатентСвыше300тр = "0,00 р."
	КонецЕсли;
	
	ДоходыИРасходыУСН = Макс(ДоходыУСН - РасходыУСН, 0);
	
	СуммаДоходовВсего = Формат(ДоходыИРасходыУСН + ДоходыЕНВД + ДоходыПатент, "ЧДЦ=2; ЧРГ=; ЧН=" ) + " р.";
	
	НайтиДокументыВзаиморасчетаСбюджетом();
	НайтиДокументОплаты();
	
	
КонецПроцедуры

&НаСервере
Процедура НайтиДокументОплаты()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасходДенежныхСредствИзКассы.Ссылка
	|ИЗ
	|	Документ.РасходИзКассы КАК РасходДенежныхСредствИзКассы
	|ГДЕ
	|	РасходДенежныхСредствИзКассы.Организация = &Организация
	|	И РасходДенежныхСредствИзКассы.ДокументОснование = &ДокументОснование
	|	И РасходДенежныхСредствИзКассы.Проведен
	|");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументВзаиморасчетовСБюджетомПФРСтраховая);
	
	// ПФР Накопительная
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СсылкаДокументаНаличнойОплатыПФРСтраховая = Выборка.Ссылка;
	Иначе
		СсылкаДокументаНаличнойОплатыПФРСтраховая = Документы.РасходИзКассы.ПустаяСсылка();
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура НайтиДокументыВзаиморасчетаСБюджетом()
	
	ДатыСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СобытиеКалендаря,
	"ДатаДокументаОбработкиСобытия,ДатаОкончанияСобытия");
	
	ДокументВзаиморасчетовСБюджетомПФРСтраховая = РегламентированнаяОтчетностьУСН.ПолучитьДокументВзаиморасчетовСБюджетом(
	Объект.Организация,
	Справочники.ВидыНалогов.ПФРСвыше300тр,
	ДатаДокументаОбработкиСобытия,
	ДатаОкончанияДокументов,
	Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокПлатежек(ОповещениеВыборПлатежек)
	
	Если Не ЗначениеЗаполнено(БанковскийСчетПоУмолчанию) Тогда
		оп = Новый ОписаниеОповещения("ОповещениеВыбораСчета", ЭтотОбъект, Новый Структура("ОповещениеВыборПлатежек", ОповещениеВыборПлатежек));
		РегламентированнаяОтчетностьУСНКлиент.ПолучитьБанковскийСчетДляУплатыНалога(оп, Объект.Организация);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеВыборПлатежек, Новый Структура ("БанковскийСчет", БанковскийСчетПоУмолчанию))
	
КонецПроцедуры


&НаКлиенте
Процедура ОповещениеВыбораСчета(БанковскийСчет, Параметры) Экспорт
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		СписокПлатежек = Новый СписокЗначений;
		БанковскийСчетПоУмолчанию = БанковскийСчет;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеВыборПлатежек, Новый Структура ("СписокПлатежек, БанковскийСчет",СписокПлатежек, БанковскийСчетПоУмолчанию))
		
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеВыборПлатежек, Неопределено);
	КонецЕсли
КонецПроцедуры


&НаСервере
Функция ПолучитьСписокПлатежекНаСервере()
	
	// ПФРСтраховая
	Если ПФРСвыше300тр <> "0,00 р." Тогда
		СсылкаДокументаБезналичнойОплатыПФРСтраховая = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(
		ДокументВзаиморасчетовСБюджетомПФРСтраховая, БанковскийСчетПоУмолчанию, , ВсегоКУплатеВПФР);
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ОбновитьРасчет()
	
	РегламентированнаяОтчетностьУСН.ВыполнитьФормированияВсехЗаписейКУДИР(КонецДня(ДатаДокументаОбработкиСобытия));
	РегламентированнаяОтчетностьУСН.ВыполнитьРасчетВзносовВПФРПриДоходахСвыше300тр(
	Объект.Организация, КонецДня(ДатаДокументаОбработкиСобытия), Объект.СобытиеКалендаря);
	ПолучитьДанныеОтчетности(); 
	ЗаполнитьУведомления();
	УправлениеФормойБлокУведомление();
	
КонецПроцедуры

// Процедура устанавливает видимость и доступность элементов управления
//
&НаКлиенте
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	
	Если ЗначениеЗаполнено(СсылкаДокументаНаличнойОплатыПФРСтраховая) Тогда
		Элементы.ГруппаСтрока10.Видимость = Ложь;
		Элементы.ГруппаСтрока11.Видимость = Ложь;
		Элементы.ГруппаСтрока12.Видимость = Истина;
	Иначе
		Элементы.ГруппаСтрока10.Видимость = Истина;
		Элементы.ГруппаСтрока11.Видимость = Истина;
		Элементы.ГруппаСтрока12.Видимость = Ложь;
	КонецЕсли;	
	
	ПериодРасчета = КонецГода(ПериодРасчета());
	
	ТекущийПериодПользователя = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПериодРасчета,
	ТекущаяДатаПользователя());
	
	РасчетЗаТекущийПериод = (КонецДня(ПериодРасчета) >= ТекущийПериодПользователя);
	
	УчитыватьРасходы = УчитыватьРасходы(); 
	
	Если УчитыватьРасходы() И РасчетЗаТекущийПериод Тогда
		Элементы.ГруппаСтрока3.Видимость = Ложь;
		Элементы.ГруппаРезультат.Видимость = Ложь;
		Элементы.ГруппаСтрока13.Видимость = Истина;
		Элементы.ГруппаСтрока6.Видимость = Истина;
		Элементы.ГруппаРезультат1.Видимость = Истина;
		Элементы.РасходыПоУСН.Видимость = Истина;
		Элементы.СуммаРасходовУСНСвыше300тр.Видимость = Истина;
		Элементы.ДекорацияОтступ5.Видимость = Истина;
		Элементы.ДекорацияМинус3.Видимость = Истина;
		Элементы.ГруппаДекорацииОписания2.Видимость = Истина; 
	Иначе
		Элементы.ГруппаСтрока3.Видимость = Истина;
		Элементы.ГруппаРезультат.Видимость = Истина;
		Элементы.ГруппаСтрока13.Видимость = Ложь;
		Элементы.ГруппаСтрока6.Видимость = Ложь;
		Элементы.ГруппаРезультат1.Видимость = Ложь;		
		Если Не УчитыватьРасходы Тогда
			Элементы.РасходыПоУСН.Видимость = Ложь;
			Элементы.СуммаРасходовУСНСвыше300тр.Видимость = Ложь;
			Элементы.ДекорацияОтступ5.Видимость = Ложь;
			Элементы.ДекорацияМинус3.Видимость = Ложь;
		КонецЕсли;
		Элементы.ГруппаДекорацииОписания2.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущаяДатаПользователя()
	
	Возврат ОбщегоНазначения.ТекущаяДатаПользователя();
	
КонецФункции

Функция ПериодРасчета()
	
	Возврат Объект.СобытиеКалендаря.ДатаОкончанияДокументов;
	
КонецФункции

Функция УчитыватьРасходы()
	
	СистемаНалогообложения = РегистрыСведений.СистемыНалогообложенияОрганизаций.ПолучитьПоследнее(КонецДня(Объект.СобытиеКалендаря.ДатаОкончанияСобытия), Новый Структура("Организация", Объект.Организация));
	
	Если СистемаНалогообложения.ОбъектНалогообложения = Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы 
		И СистемаНалогообложения.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура РаспечататьКвитанциюЗавершение() Экспорт
	
	СтруктураПечати = ВыгрузитьДокументВФайл(Новый Структура("НачислениеНалогов,Сумма,СобытиеКалендаря", ДокументВзаиморасчетовСБюджетомПФРСтраховая, ВсегоКУплатеВПФР, Объект.СобытиеКалендаря), "Квитанция");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(Нстр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(СтруктураПечати.АдресФайла, Нстр("ru = 'Квитанция.pdf'"), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьПлатежноеПоручениеЗавершение() Экспорт
	
	ПолучитьСписокПлатежекНаСервере();
	СтруктураПечати = ВыгрузитьДокументВФайл(СсылкаДокументаБезналичнойОплатыПФРСтраховая, "ПлатежноеПоручение");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(Нстр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(СтруктураПечати.АдресФайла, СтруктураПечати.НаименованиеФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьФайлКлиентБанка() Экспорт
	
	БанкСчетСтруктура = Новый Структура("БанковскийСчет", БанковскийСчетПоУмолчанию);
	
	ПолучитьСписокПлатежекНаСервере();
	СписокПлатежек = Новый СписокЗначений;
	Если ПФРСвыше300тр <> "0,00 р." Тогда
		СписокПлатежек.Добавить(СсылкаДокументаБезналичнойОплатыПФРСтраховая);
	КонецЕсли;
	
	БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
	
	ОткрытьФорму(
	"Обработка.КлиентБанкУНФ.Форма.СохранениеПлатежек",
	БанкСчетСтруктура,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьНаличиеГосОргана(ИмяПроцедурыПоЗавершениюСозданияГосОргана)
	КодГосОргана = "";
	Если ГосОрганСуществует(Объект.Организация, КодГосОргана) Тогда
		Возврат Истина;
	Иначе
		ДопПараметры = Новый Структура("ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана", ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана);
		ТекстВопроса = НСтр("ru='В справочнике «Контрагенты» не задано отделение ПФ РФ для уплаты взноса.
		|Создать его автоматически?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОтветеНаВопросОСозданииГосОргана",
		ЭтаФорма, ДопПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Создать автоматически'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Создать вручную'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,,КодВозвратаДиалога.Да, НСтр("ru='Отсутствует налоговый орган'"));
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОтветеНаВопросОСозданииГосОргана(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеСведенийОНалоговойИнспекции(ДопПараметры);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидГосударственногоОргана", ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.ОрганПФР"));
		ПараметрыФормы.Вставить("КодГосударственногоОргана", ДопПараметры.КодГосОргана);
		ПараметрыФормы.Вставить("ИмяПроцедурыПоЗавершениюСозданияГосОргана",ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана);
		ПараметрыФормы.Вставить("ЗапретРедактированияКода", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзменениеПлатежныхРеквизитовПФР", ЭтотОбъект, ПараметрыФормы);
		
		ОткрытьФорму("Справочник.Контрагенты.Форма.РеквизитыГосударственныхОрганов", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект, , , ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаполнениеСведенийОНалоговойИнспекции(ДопПараметры)
	
	ОписаниеОшибки = "";
	ЗаполнитьСведенияОбОтделенииПФРПоКоду(ОписаниеОшибки, ДопПараметры.КодГосОргана);
	
	// Обработка ошибок
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			
			ТекстВопроса = НСтр("ru='Для автоматического создания отделения ПФ РФ
			|в справочнике «Контрагенты» необходимо подключиться к Интернет-поддержке
			|пользователей. Подключиться сейчас?'");
			
			ПараметрыВопроса = Новый Структура("ВызовПослеПодключения,ИмяПроцедурыПоЗавершениюСозданияГосОргана", "ЗаполнитьСведенияОбОтделенииПФРПоКоду, КодГосОргана", ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана, ДопПараметры.КодГосОргана);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержку", ЭтотОбъект, ПараметрыВопроса);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	Иначе
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьПлатежноеПоручениеЗавершение" Тогда
			РаспечататьПлатежноеПоручениеЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "ПолучитьФайлКлиентБанка" Тогда
			ПолучитьФайлКлиентБанка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("ВызовПослеПодключения") Тогда
			
			Если ДополнительныеПараметры.ВызовПослеПодключения = "ЗаполнитьСведенияОбОтделенииПФРПоКоду" Тогда
				
				ЗаполнитьСведенияОбОтделенииПФРПоКоду(,ДополнительныеПараметры.КодГосОргана);
				
				Если ДополнительныеПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
					РаспечататьКвитанциюЗавершение();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииПФРПоКоду(ОписаниеОшибки = "", КодНалоговогоОрганаПолучателя)
	
	РеквизитыНалоговогоОргана = ДанныеГосударственныхОрганов.РеквизитыОтделенияПФРПоКоду(КодНалоговогоОрганаПолучателя);
	
	Если ЗначениеЗаполнено(РеквизитыНалоговогоОргана.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыНалоговогоОргана.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Ссылка) Тогда
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыНалоговогоОргана);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПлатежныхРеквизитовПФР(Ответ, ДопПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ГосОрганСуществует(Организация, КодГосОргана)
	КодГосОргана = Организация.КодОрганаПФР;
	ГосОрган = ДанныеГосударственныхОрганов.ГосударственныйОрган(Перечисления.ВидыГосударственныхОрганов.ОрганПФР, КодГосОргана);
	
	Возврат ЗначениеЗаполнено(ГосОрган.Ссылка);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВыгрузитьДокументВФайл(Объект, ВидФайла)
	
	ТабДок = Новый ТабличныйДокумент;
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект);
	
	Если ВидФайла = "Квитанция" Тогда
		Для Каждого Объект Из МассивОбъектов Цикл
			
			ТабДокОбъекта = Документы.НачислениеНалогов.СформироватьКвитанцию(Объект.НачислениеНалогов, Объект.Сумма);
			ТабДокОбъекта.Область().СоздатьФорматСтрок();
			Если ТабДок = Неопределено Тогда
				ТабДок = ТабДокОбъекта;
			Иначе
				ТабДок.Вывести(ТабДокОбъекта);
			КонецЕсли;
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
	ИначеЕсли ВидФайла = "ПлатежноеПоручение" Тогда
		ОбъектыПечати = Новый СписокЗначений;
		ТабДок = Документы.ПлатежноеПоручение.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	СтруктураПечати = Новый Структура ("АдресФайла, НаименованиеФайла");
	
	Если ТабДок = Неопределено Тогда
		Возврат СтруктураПечати;
	КонецЕсли;
	
	Каталог = КаталогВременныхФайлов()+РаботаВМоделиСервиса.ЗначениеРазделителяСеанса()+"\";
	
	ПроверкаКаталога = Новый Файл(Каталог);
	Если НЕ ПроверкаКаталога.Существует() Тогда
		СоздатьКаталог(Каталог);
	КонецЕсли;
	РасширениеФайла = ".pdf" ;
	НаименованиеФайла = Строка(Объект);
	НаименованиеФайла = ?(СтрДлина(НаименованиеФайла)+СтрДлина(РасширениеФайла)>64, Лев(НаименованиеФайла,64 - СтрДлина(РасширениеФайла)),НаименованиеФайла);
	НаименованиеФайла = СтрЗаменить(НаименованиеФайла, ":","_");
	
	ПолноеИмяФайла = Каталог + НаименованиеФайла + РасширениеФайла;
	ПолноеИмяФайла = СтрЗаменить(ПолноеИмяФайла, """","");
	
	Попытка
		ТабДок.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.PDF);
		
	Исключение
		ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Ошибка сохранения файла на сервере'"),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru= 'При сохранении файла на сервере в области данных %1 произошла ошибка:
		|%2'"),
		РаботаВМоделиСервиса.ЗначениеРазделителяСеанса(),
		ОписаниеОшибки()),
		РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
		Возврат СтруктураПечати;
		
	КонецПопытки;
	ДанныеФайла = Новый ДвоичныеДанные(ПолноеИмяФайла);
	АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
	УдалитьФайлы(ПолноеИмяФайла);
	СтруктураПечати.АдресФайла = АдресФайла;
	СтруктураПечати.НаименованиеФайла = НаименованиеФайла+РасширениеФайла;
	
	Возврат СтруктураПечати;
	
КонецФункции

&НаКлиенте
Процедура УплаченоФиксированннаяЧастьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПериодПараметр = КалендарьОтчетностиПовтИсп.ПолучитьСтандартныйПериодДокументовДляСобытия(Объект.СобытиеКалендаря);
	
	СпсВидов = Новый СписокЗначений;
	СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСвыше300тр"));
	СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСтраховая"));
	СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ФФОМС"));
	
	СпсЗадач = Новый СписокЗначений;
	СпсЗадач.Добавить(ПредопределенноеЗначение(
	"Справочник.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыПриДоходахСвыше300тр"));
	СпсЗадач.Добавить(ПредопределенноеЗначение(
	"Справочник.ЗадачиКалендаряПодготовкиОтчетности.СтраховыеВзносыИП"));
	
	ОткрытьФорму("Отчет.УплатыНалоговИВзносов.ФормаОбъекта",
	Новый Структура("Организация, СформироватьПриОткрытии, Период,ВидВзаиморасчетов, УчетВыплатТекущегоГода, ЗадачиКалендаря",
	Объект.Организация, Истина, ПериодПараметр, СпсВидов, Ложь, СпсЗадач));
	
	
КонецПроцедуры

#КонецОбласти

#Область Уведомления
// -----------------------------------------------------------------------------
// Уведомления

&НаКлиенте
Процедура ФормированиеДокументаЕНС(ИмяФормы)
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("ЗаписьКалендаря",                     ЗаписьКалендаря);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму(ИмяФормы,
	ПараметрыУведомления,
	ЭтотОбъект,
	,
	,
	,
	ОткрытьДокументОповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеСформироватьУведомление(Команда)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("ЗаписьКалендаря",                     ЗаписьКалендаря);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму("Документ.УведомлениеОбИсчисленныхСуммахНалогов.Форма.ФормаДокумента",
		ПараметрыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецПроцедуры  
	
&НаКлиенте
Процедура ВыполнитьДействиеСформироватьОперациюПоЕНС(Команда)
	
	ВыполненоВТихомРежиме = Ложь;
	СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме);
	Если ВыполненоВТихомРежиме Тогда
		// Только перерисуем форму
		ОбработатьРедактированиеУведомления();
		Возврат;
	КонецЕсли;
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("ЗаписьКалендаря",                     ЗаписьКалендаря);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму("Документ.ОперацияПоЕдиномуНалоговомуСчету.Форма.ФормаДокумента",
		ПараметрыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме)
	
	ЕдиныйНалоговыйСчет.СформироватьОперациюПоЕНСВТихомРежиме(Объект.Организация,
		ОписаниеТаблицыНалоги(),
		ЗаписьКалендаря,
		ПериодСобытия,
		ВыполненоВТихомРежиме);
	
КонецПроцедуры
	
&НаСервере
Функция ОписаниеТаблицыНалоги()
	
	ТаблицаНалоги = ТаблицаНалогиДляУведомления();
		
	ТаблицыНалогов = Документы.УведомлениеОбИсчисленныхСуммахНалогов.ОписаниеТаблицНалогов();
	ТаблицыНалогов.ТаблицаНалоги = ТаблицаНалоги;
	
	АдресХранилищаТаблицыНалоги = ПоместитьВоВременноеХранилище(ТаблицыНалогов, УникальныйИдентификатор);
	
	Возврат АдресХранилищаТаблицыНалоги;
	
КонецФункции 

&НаСервере
Функция ТаблицаНалогиДляУведомления() Экспорт
	
	ТаблицаНалоги = Документы.УведомлениеОбИсчисленныхСуммахНалогов.ОписаниеТаблицыНалогов();
	
	СуммаКУплате = ВсегоКУплатеВПФР;   
	
	Если МожноЗарезервироватьНаЕНС <> 0 И СуммаКУплате > МожноЗарезервироватьНаЕНС Тогда
		
		СуммаКУплате = МожноЗарезервироватьНаЕНС;		
		
	КонецЕсли;
	
	СрокУплаты = РегламентированнаяОтчетностьУСН.ОпределитьСрокПлатежаПоЕНП(Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия); 
	Если КонецМесяца(СрокУплаты) = КонецКвартала(СрокУплаты) Тогда
		Обработки.ОбработкиНалоговИОтчетности.УточнитьСрокУплатыВзносовСвышеПредела(СрокУплаты);
	КонецЕсли;	
	
	Если СуммаКУплате > 0 Тогда 
		
		НоваяСтрока = ТаблицаНалоги.Добавить();
		НоваяСтрока.РегистрацияВНалоговомОргане = Объект.Организация.РегистрацияВНалоговомОргане;
		НоваяСтрока.КодПоОКТМО                  = НоваяСтрока.РегистрацияВНалоговомОргане.КодПоОКТМО;
		НоваяСтрока.Налог                       = Справочники.ВидыНалогов.ПФРСвыше300ТР;
		НоваяСтрока.КодБК                       = Справочники.ВидыНалогов.КБК(НоваяСтрока.Налог,, Объект.СобытиеКалендаря.ДатаОкончанияСобытия);
		НоваяСтрока.СчетУчета                   = НоваяСтрока.Налог.СчетУчета;
		НоваяСтрока.Сумма = СуммаКУплате;
		НоваяСтрока.СрокУплаты                  = СрокУплаты;
		НоваяСтрока.НачалоПериода = Объект.СобытиеКалендаря.ДатаНачалаДокументов;
		НоваяСтрока.КонецПериода = Объект.СобытиеКалендаря.ДатаОкончанияДокументов;
		
	КонецЕсли;
	
	Возврат ТаблицаНалоги;
	
КонецФункции 

Функция СрокУплатыНалогаВБюджет(ГодУплаты, МесяцУплаты, ДеньУплаты) Экспорт
	
	ДатаУплатыНалога = Дата(ГодУплаты, МесяцУплаты, ДеньУплаты);
		
	// Перенесем на рабочие дни
	ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
		
	Если Не ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		Возврат КонецДня(ДатаУплатыНалога);
	КонецЕсли; 
		
	ИсходныеДаты = Новый Массив;
	ИсходныеДаты.Добавить(ДатаУплатыНалога);
		
	ГрафикПереноса = КалендарныеГрафики.ДатыБлижайшихРабочихДней(
		ПроизводственныйКалендарь, 
		ИсходныеДаты,
		Ложь,
		Ложь,
		Истина);
	
	ДатаПереноса = ?(ЗначениеЗаполнено(ГрафикПереноса[ДатаУплатыНалога]), 
		ГрафикПереноса[ДатаУплатыНалога], ДатаУплатыНалога);
	
	Возврат КонецДня(ДатаПереноса);
	
КонецФункции




&НаКлиенте
Процедура ОбработатьРедактированиеУведомления(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьСведенияОбУведомлениях();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбУведомлениях()
	
	Если Не ПлательщикЕНП Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьУведомления();
	
	УправлениеФормойБлокУведомление();
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьУведомления()
	
	ТаблицаУведомления = Обработки.ОбработкиНалоговИОтчетности.СозданныеДокументыОтраженияНаЕНС(
	Объект.Организация,
	Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия,
	Ложь, ЗаписьКалендаря);     
	
	УведомленияОбИсчисленныхНалогах.Загрузить(ТаблицаУведомления);
	
	УведомлениеОбИсчисленныхНалогахПодготовлено = УведомленияОбИсчисленныхНалогах.Количество() > 0;
	
КонецПроцедуры 

&НаКлиенте
Процедура УведомлениеНажатие(Команда)	
	ОтрытьДокументЕНС(Команда);  
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНажатие(Команда)
	ОтрытьДокументЕНС(Команда);
КонецПроцедуры  

&НаКлиенте
Процедура СформироватьЗаявление(Команда)
	ФормированиеДокументаЕНС("Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Форма.ФормаДокумента");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУведомление(Идентификатор)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыФормыУведомления = ПараметрыФормыУведомления(Идентификатор);
	
	Если ПараметрыФормыУведомления.Свойство("Ключ") Тогда
		Если ТипЗнч(ПараметрыФормыУведомления.Ключ) = Тип("ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов") Тогда
			ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов";
		ИначеЕсли ТипЗнч(ПараметрыФормыУведомления.Ключ) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
			ИмяОбъекта = "ЗаявлениеОЗачетеВСчетПредстоящейОбязанности";
		Иначе
			ИмяОбъекта = "ОперацияПоЕдиномуНалоговомуСчету";
		КонецЕсли;
	Иначе
		Если ПодаетсяЗаявлениеОЗачете Тогда
			ИмяОбъекта = "ЗаявлениеОЗачетеВСчетПредстоящейОбязанности";
		ИначеЕсли ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
			ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов";
		Иначе
			ИмяОбъекта = "ОперацияПоЕдиномуНалоговомуСчету";
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьФорму("Документ." + ИмяОбъекта + ".ФормаОбъекта",
		ПараметрыФормыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтрытьДокументЕНС(Команда)
	// Команды генерируются в КопироватьЭлементСПодчиненными() и содержат числовой идентификатор,
	// совпадающий с идентификаторами строк в списке Платежи
	Идентификатор = ЕдиныйНалоговыйСчетКлиент.Идентификатор(Команда.Имя);
	Если Идентификатор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьУведомление(Идентификатор);
КонецПроцедуры  

&НаСервере
Функция ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления)
	
	Если ЗначениеЗаполнено(СтрокаУведомления.Уведомление) Тогда
		Возврат Новый Структура("Ключ", СтрокаУведомления.Уведомление);
	Иначе
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПараметрыФормыПустогоУведомления()
	
	ЗначенияЗаполнения = Новый Структура("Организация", Объект.Организация);
	
	Возврат Новый Структура("ЗначенияЗаполнения, ПериодСобытия, СобытиеКалендаря",
	ЗначенияЗаполнения, Объект.ПериодРегистрации, Объект.СобытиеКалендаря);
	
КонецФункции

&НаСервере
Функция ПараметрыФормыУведомления(Идентификатор)
	
	Если Идентификатор = Неопределено Тогда 
		//Получим параметры формы для пустого документа
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(Идентификатор);
	Если СтрокаУведомления = Неопределено Тогда
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	Возврат ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления);
	
КонецФункции

&НаСервере
Процедура УправлениеФормойБлокУведомление()
	
	ШрифтИнформацияУведомление = Новый Шрифт(,,,Ложь);
	ФорматнаяСтрока = "ЧЦ=15; ЧН=";   	
	// Подсказка по заявлению
	ТаблицаОстатковНаЕдиномНалоговомСчете = ЕдиныйНалоговыйСчет.ТаблицаОстатковНаЕдиномНалоговомСчете(
	ЭтотОбъект.ОрганизацияПредставление,
	ЭтотОбъект.ПериодСобытия); 
	СуммаУжеЗарезервированная = СозданныеЗаявленияОЗачете(УведомленияОбИсчисленныхНалогах).Итог("Сумма");
	ДоступныйОстаток = ТаблицаОстатковНаЕдиномНалоговомСчете.Итог("Сумма"); 
	ДоступныйОстаток = ДоступныйОстаток - СуммаУжеЗарезервированная;   
	ОсталосьОплатитьВзносов = Макс(0, СуммаКУплате - СуммаУжеЗарезервированная);
	МожноЗарезервироватьНаЕНС = Мин(ОсталосьОплатитьВзносов, Макс(0, ДоступныйОстаток - СуммаУжеЗарезервированная)); 	
	// Взносы ФСС платим отдельной платежкой не через ЕНС	
	Если УпрощенныйЗачетФиксированныхВзносов Тогда
		// По упрощенному зачету взносов нужно пополнить ЕНС и отразить их на счете через операцию
		ТекстИнформацияЗаявление = НСтр("ru = 'Отправлять заявление о зачете сумм не требуется.
		|Для корректного ведения расчетов по налогам нужно отразить взносы на едином налоговом счете.'");  
	ИначеЕсли ГодовойРасчет Тогда
		// В последнем квартале не нужно подавать заявление о зачете - сумма спишется в конце года
		// Если это взносы с доходов, списание произодёт в следующем году
		ТекстИнформацияЗаявление = СтрШаблон(НСтр(
		"ru = 'Фактическое списание страховых взносов с ЕНС произойдет %1 Отправлять заявление о зачете сумм не требуется.
		|
		|Для корректного ведения расчетов по налогам нужно отразить их на едином налоговом счете.'"),
		Формат(Объект.СобытиеКалендаря.ДатаОкончанияСобытия, "ДЛФ=DD")); 
		
	ИначеЕсли ДоступныйОстаток >= ЭтотОбъект.СуммаКУплате Тогда
		ТекстИнформацияЗаявление = СтрШаблон(
		НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
		Формат(ДоступныйОстаток, ФорматнаяСтрока));
	Иначе
		ТекстИнформацияЗаявление = СтрШаблон(
		НСтр("ru = 'Доступный остаток по ЕНС составляет %1. Данная сумма не позволяет произвести зачет взносов в счет предстоящей обязанности.'"),
		Формат(ДоступныйОстаток, ФорматнаяСтрока));
	КонецЕсли;   
	СтроковыеФункции.ФорматированнаяСтрока(ТекстИнформацияЗаявление);
	Элементы.ПояснениеУменьшения.Заголовок = ТекстИнформацияЗаявление;
	
	// Элементы заявления о зачете добавляются на форму по идентификатору в таблице УведомленияОбИсчисленныхНалогах.
	// После каждого обновления таблицы идентификаторы строк меняются и нужно обновлять элементы на форме.  
	
	Элементы.ГруппаЗаявленияОЗачете.Видимость = УведомлениеОбИсчисленныхНалогахПодготовлено;
	
	СозданныеЗаявления = СозданныеЗаявленияОЗачете(УведомленияОбИсчисленныхНалогах);
	
	КоличествоЭлементов = Элементы.ГруппаЗаявленияОЗачете.ПодчиненныеЭлементы.Количество();
	
	Для Индекс = 1 По КоличествоЭлементов Цикл
		
		ТекущиеЭлементГруппы = Элементы.ГруппаЗаявленияОЗачете.ПодчиненныеЭлементы[КоличествоЭлементов - Индекс];
		
		Если ТекущиеЭлементГруппы.Имя = "ПредставлениеЗаявления" Или ТекущиеЭлементГруппы.Имя = "ИтогПоУведомлениям" Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ПодчиненныйЭлемент Из ТекущиеЭлементГруппы.ПодчиненныеЭлементы Цикл
			Элементы.Удалить(ПодчиненныйЭлемент);
		КонецЦикла;
		
		Элементы.Удалить(ТекущиеЭлементГруппы);
		
	КонецЦикла;
	
	Если УведомлениеОбИсчисленныхНалогахПодготовлено Тогда
		
		Если ГодовойРасчет Или УпрощенныйЗачетФиксированныхВзносов Тогда
			ОтобразитьСозданныеДокументыНаФорме(СозданныеОперацииПоЕНС(УведомленияОбИсчисленныхНалогах));
		КонецЕсли;
		
		Если УпрощенныйЗачетФиксированныхВзносов Или Не ГодовойРасчет Тогда
			ОтобразитьСозданныеДокументыНаФорме(СозданныеЗаявления)
		КонецЕсли;
		
	КонецЕсли;
	
	ВсегоОтраженоПоБУ = СозданныеОперацииПоЕНС(УведомленияОбИсчисленныхНалогах).Итог("Сумма");
	Элементы.СформироватьОперацияПоЕНС.Видимость = ГодовойРасчет И (ВсегоОтраженоПоБУ < СуммаКУплате);
    
	Если УпрощенныйЗачетФиксированныхВзносов Тогда 	
		Элементы.ПояснениеУменьшения.Видимость = Истина;
		Элементы.ГруппаСуммаКРезерву.Видимость = Ложь;
		Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Ложь;
		Элементы.ДекорацияТекстОписание8.Видимость = Ложь;
	Иначе
		
		Элементы.ПояснениеУменьшения.Видимость = Истина;
		Элементы.ГруппаСуммаКРезерву.Видимость = Истина;
		
		Если МожноЗарезервироватьНаЕНС > 0 Тогда
			Элементы.МожноЗарезервироватьНаЕНС.Видимость = Не ГодовойРасчет;
			Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Не ГодовойРасчет;
		Иначе
			Элементы.МожноЗарезервироватьНаЕНС.Видимость = Ложь;
			Элементы.ГруппаЗарезервироватьОстатокНаЕНС.Видимость = Ложь;  
			Элементы.ПояснениеУменьшения.Видимость = Ложь;
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СозданныеОперацииПоЕНС(Знач УведомленияОбИсчисленныхНалогах)
	
	СозданныеДокументы = Новый ТаблицаЗначений;
	СозданныеДокументы.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету"));
	СозданныеДокументы.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	СозданныеДокументы.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	СозданныеДокументы.Колонки.Добавить("ПредставлениеДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	СозданныеДокументы.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	
	Для Каждого ТекущееУведомление Из УведомленияОбИсчисленныхНалогах Цикл
		Если ТипЗнч(ТекущееУведомление.Уведомление) = Тип("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету") Тогда
			
			НоваяСтрока = СозданныеДокументы.Добавить();
			НоваяСтрока.Ссылка = ТекущееУведомление.Уведомление;
			НоваяСтрока.Сумма = ТекущееУведомление.Сумма;
			НоваяСтрока.НомерСтроки = ТекущееУведомление.ПолучитьИдентификатор() + 1;
			
			РеквизитыЗаявления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееУведомление.Уведомление, "Дата, Номер");
			
			НоваяСтрока.Дата = РеквизитыЗаявления.Дата;
			НоваяСтрока.ПредставлениеДокумента = СтрШаблон(НСтр("ru ='Операция по единому налоговому счету %1 от %2 на сумму %3'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыЗаявления.Номер),
				Формат(РеквизитыЗаявления.Дата, "ДЛФ=D"),
				Формат(ТекущееУведомление.Сумма, "ЧЦ=15; ЧДЦ=2"));
			
		КонецЕсли;
	КонецЦикла;
	
	СозданныеДокументы.Сортировать("Дата Возр");
	Возврат СозданныеДокументы;
	
КонецФункции

&НаСервереБезКонтекста
Функция СозданныеЗаявленияОЗачете(Знач УведомленияОбИсчисленныхНалогах)
	
	СозданныеЗаявления = Новый ТаблицаЗначений;
	СозданныеЗаявления.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности"));
	СозданныеЗаявления.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	СозданныеЗаявления.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	СозданныеЗаявления.Колонки.Добавить("ПредставлениеДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	СозданныеЗаявления.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	СозданныеЗаявления.Колонки.Добавить("Сдано", Новый ОписаниеТипов("Булево"));
	
	Для Каждого ТекущееУведомление Из УведомленияОбИсчисленныхНалогах Цикл
		Если ТипЗнч(ТекущееУведомление.Уведомление) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
			
			НоваяСтрока = СозданныеЗаявления.Добавить();
			НоваяСтрока.Ссылка = ТекущееУведомление.Уведомление;
			НоваяСтрока.Сумма = ТекущееУведомление.Сумма;
			НоваяСтрока.НомерСтроки = ТекущееУведомление.ПолучитьИдентификатор() + 1;
			
			РеквизитыЗаявления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущееУведомление.Уведомление, "Дата, Номер, Уведомление");
			
			СостояниеДокумента = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(РеквизитыЗаявления.Уведомление);
			НоваяСтрока.Сдано = СостояниеДокумента.Сдано;
			НоваяСтрока.Дата = РеквизитыЗаявления.Дата;
			НоваяСтрока.ПредставлениеДокумента = СтрШаблон(НСтр("ru ='Заявление о зачете страховых взносов %1 от %2 на сумму %3 (%4)'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыЗаявления.Номер),
				Формат(РеквизитыЗаявления.Дата, "ДЛФ=D"),
				Формат(ТекущееУведомление.Сумма, "ЧЦ=15; ЧДЦ=2"),
				СостояниеДокумента.Статус);
			
		КонецЕсли;
	КонецЦикла;
	
	СозданныеЗаявления.Сортировать("Дата Возр");
	Возврат СозданныеЗаявления;
	
КонецФункции

&НаСервере
Процедура ОтобразитьСозданныеДокументыНаФорме(СозданныеДокументы)
	
	Для Каждого ТекущееЗаявление Из СозданныеДокументы Цикл
		
		НомерКонтейнера = Формат(ТекущееЗаявление.НомерСтроки, "ЧЦ=2; ЧН=0");
		ИмяГруппы = СтрШаблон("ПредставлениеЗаявления%1", НомерКонтейнера);
		Если Элементы.Найти(ИмяГруппы) = Неопределено Тогда
			ЕдиныйНалоговыйСчет.КопироватьЭлементСПодчиненными(
				ЭтотОбъект,
				Элементы.ПредставлениеЗаявления,
				Элементы.ГруппаЗаявленияОЗачете,
				ТекущееЗаявление.НомерСтроки);
		КонецЕсли;
		
		ИмяЭлементаЗаявление        = СтрШаблон("ЗаявлениеОЗачете%1", НомерКонтейнера);
		ИмяГруппыЗаявление          = СтрШаблон("ПредставлениеЗаявления%1", НомерКонтейнера);
		ИмяЭлементаУдалитьЗаявление = СтрШаблон("УдалитьЗаявление%1", НомерКонтейнера);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементаЗаявление, "Заголовок",
			ТекущееЗаявление.ПредставлениеДокумента);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяГруппыЗаявление, "Видимость",
			Истина); 
		Если СозданныеДокументы.Колонки.Найти("Сдано") <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементаУдалитьЗаявление, "Видимость",
				Не ТекущееЗаявление.Сдано);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаявлениеНажатие(Элемент)
	
	ИдентификаторЗаявления = ЕдиныйНалоговыйСчетКлиент.Идентификатор(Элемент.Имя);
	
	Если ИдентификаторЗаявления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// На форму команды добавляются с идентификатором строки таблицы
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(ИдентификаторЗаявления);
	Если СтрокаУведомления <> Неопределено Тогда
		ПометитьНаУдалениеЗаявлениеНаСервере(СтрокаУведомления.Уведомление);
		ОбновитьСведенияОбУведомлениях();
	КонецЕсли;
	
КонецПроцедуры   

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеЗаявлениеНаСервере(СсылкаНаДокумент)
	
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности") Тогда
		// Необходимо скинуть статус уведомления для корректного отображения статуса задачи
		ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправкиУведомления(ДокументОбъект.Уведомление,
			ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой());
	КонецЕсли;
	
	ДокументОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

#КонецОбласти
