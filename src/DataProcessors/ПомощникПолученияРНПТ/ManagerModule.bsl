#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция заполняет первые два шага в помощнике получения РНПТ
// Провести инвентаризацию и Создать уведомления
//
// Параметры:
//  Параметры - Структура: 
//						- Организация - СправочникСсылка - организация
//						- Период - Дата - дата помощника получения РНПТ
//  АдресРезультата - строка - адрес, куда будет помещен результат 
// 
Функция ДанныеЗаполненияПомощникаПолученияРНПТ(Параметры, АдресРезультата) Экспорт
	
	ДанныеЗаполнения = ПолучитьДанныеЗаполнения(
		Параметры.Период,
		Параметры.Организация);
	
	ПоместитьВоВременноеХранилище(ДанныеЗаполнения, АдресРезультата);
	
КонецФункции

// Создает документы "Корректировочные уведомления о ввозе прослеживаемых товаров" в фоне.
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаКорректировки - ТаблицаЗначений - таблица данных, по которым нужно сформировать корр. уведомления о ввозе
//  АдресРезультата - строка - адрес, куда будет помещен результат 
//
Функция СоздатьКорректировочныеУведомленияВФоне(ПараметрыСоздания, АдресРезультата) Экспорт
	
	ТаблицаКорректировки = ПараметрыСоздания.ТаблицаКорректировкиУведомленийОВвозе;
	Организация  = ПараметрыСоздания.Организация;
	
	СоздатьКорректировочныеУведомленияОВвозе(ТаблицаКорректировки, Организация);
	
КонецФункции

// Записывает полученные РНПТ в документ Уведомление об остатках прослеживаемых товаров 
//  и в документ Уведомление о ввозе прослеживаемых товаров
// 
// Параметры:
//  ПараметрыСоздания - Структура:
//						- Организация - СправочникСсылка - организация
//						- ПериодСобытия - Дата - дата помощника получения РНПТ
//  АдресРезультата - строка - адрес, куда будет помещен результат 
// 
Функция ЗаписьПолученныхРНПТВФоне(ПараметрыСоздания, АдресРезультата) Экспорт

	Организация = ПараметрыСоздания.Организация;
	Период = ПараметрыСоздания.ПериодСобытия;
	
	ЗаписатьПолученныеРНПТ(Организация, Период);
	
КонецФункции

// Создает документы по инвентаризации товаров из помощника получения РНПТ
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления
//  АдресРезультата - строка - адрес, куда будет помещен результат
//
Функция СоздатьИнвентаризациюОбОстаткахВФоне(ПараметрыСоздания, АдресРезультата) Экспорт

	ТаблицаОстатков = ПараметрыСоздания.ТаблицаОстатков;
	Организация = ПараметрыСоздания.Организация;
	Дата = ПараметрыСоздания.Дата;
	
	СоздатьИнвентаризациюОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата);
	
КонецФункции

// Создает документы "Уведомление об остатках прослеживаемых товаров"
// и "Уведомление о ввозе прослеживаемых товаров" в фоне.
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления об остатках
//						ТаблицаВвоза - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления о ввозе
//  АдресРезультата - строка - адрес, куда будет помещен результат 
//
Функция СоздатьУведомленияПоТоварамВФоне(ПараметрыСоздания, АдресРезультата) Экспорт
	
	ТаблицаОстатковСПервичнымиДокументами = ПараметрыСоздания.ТаблицаОстатков;
	ТаблицаВвоза = ПараметрыСоздания.ТаблицаВвоза;
	Организация  = ПараметрыСоздания.Организация;
	
	СоздатьУведомленияОбОстаткахПоТоварам(ТаблицаОстатковСПервичнымиДокументами, Организация);
	
	СоздатьУведомленияОВвозе(ТаблицаВвоза, Организация);
	
КонецФункции

// Создает документы "Уведомление об остатках прослеживаемых ОС"
// и "Уведомление о ввозе прослеживаемых ОС" в фоне.
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления об остатках
//						ТаблицаВвоза - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления о ввозе
//  АдресРезультата - строка - адрес, куда будет помещен результат 
//
Функция СоздатьУведомленияПоОСВФоне(ПараметрыСоздания, АдресРезультата) Экспорт
	
	ТаблицаОстатковСПервичнымиДокументами = ПараметрыСоздания.ТаблицаОстатковОС;
	Организация  = ПараметрыСоздания.Организация;
	
	СоздатьУведомленияОбОстаткахПоОС(
		ТаблицаОстатковСПервичнымиДокументами.Скопировать(Новый Структура("ЕстьСостав", Ложь)),
		Организация
		);
	СоздатьУведомленияОбОстаткахПоТоварамВСоставеОС(
		ТаблицаОстатковСПервичнымиДокументами.Скопировать(Новый Структура("ЕстьСостав", Истина)),
		Организация
		);
	
КонецФункции

// Создает документы по инвентаризации ОС из помощника получения РНПТ
// 
// Параметры:
//  ПараметрыCоздания - Структура:
//						Дата - Дата - текущая дата
//						Организация - СправочникСсылка.Организации - организация
//						ПериодСобытия - Дата - дата в помощнике, начало месяца
//						ТаблицаОстатков - ТаблицаЗначений - таблица данных, по которым нужно сформировать уведомления
//  АдресРезультата - строка - адрес, куда будет помещен результат
//
Функция СоздатьИнвентаризациюОСОбОстаткахВФоне(ПараметрыСоздания, АдресРезультата) Экспорт

	ТаблицаОстатков = ПараметрыСоздания.ТаблицаОстатковОС;
	Организация = ПараметрыСоздания.Организация;
	Дата = ПараметрыСоздания.Дата;
	
	СоздатьИнвентаризациюОСОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьИнвентаризациюОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата)
	
	МассивСкладов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаОстатков.ВыгрузитьКолонку("Склад"));
	ФильтрПоСкладу = Новый Структура("Склад");
	СписокНоменклатуры = Новый Массив();
	СозданныеДокументы = Новый Массив();
	
	Для Каждого ТекущийСклад Из МассивСкладов Цикл
		
		ФильтрПоСкладу.Склад = ТекущийСклад;
		НайденныеСтроки = ТаблицаОстатков.НайтиСтроки(ФильтрПоСкладу);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			СписокНоменклатуры.Добавить(ТекущаяСтрока.Номенклатура);
		КонецЦикла;
		
		СсылкаНаДокумент = СоздатьИнвентаризациюТовара(Дата, Организация, ТекущийСклад, СписокНоменклатуры);
		
		СозданныеДокументы.Добавить(СсылкаНаДокумент);
		
		СписокНоменклатуры.Очистить();
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СозданныеДокументы, АдресРезультата);
	
КонецПроцедуры

Функция СоздатьИнвентаризациюТовара(Дата, Организация, ТекущийСклад, СписокНоменклатуры)
	
	НовыйДокумент = ПрослеживаемостьПереопределяемый.СоздатьИнвентаризациюТовара(
		Дата, Организация, ТекущийСклад, СписокНоменклатуры);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Процедура ЗаписатьПолученныеРНПТ(Организация, Период)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтатусОтправки", Перечисления.СтатусыОтправки.Сдан);
	Запрос.УстановитьПараметр("ПустойРНПТ", ПрослеживаемостьПереопределяемый.ПустоеЗначениеРНПТ());
	
	ТекстЗапросаУведомлениеОбОСтатках = ИнтерфейсыВзаимодействияБРОВызовСервера.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
		"УведомлениеОбОстаткахПрослеживаемыхТоваров");
	ТекстЗапросаУведомлениеОВвозе = ИнтерфейсыВзаимодействияБРОВызовСервера.ТекстыДляЗапросаДокументовРеализацииПолномочийНО(
		"УведомлениеОВвозеПрослеживаемыхТоваров");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	СтатусыОтправки.Статус КАК Статус,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	" + ТекстЗапросаУведомлениеОбОСтатках.ПеречислениеКолонок + "
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|	" + ТекстЗапросаУведомлениеОбОСтатках.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = &Организация
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ <> """"
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ ЕСТЬ НЕ NULL
	|	И СтатусыОтправки.Статус = &СтатусОтправки
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ = &ПустойРНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	СтатусыОтправки.Статус КАК Статус,
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки,
	|	" + ТекстЗапросаУведомлениеОбОСтатках.ПеречислениеКолонок + "
	|ИЗ
	|	Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров КАК УведомлениеОбОстаткахПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОбОстаткахПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|	" + ТекстЗапросаУведомлениеОбОСтатках.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОбОстаткахПрослеживаемыхТоваров.Организация = &Организация
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.НомерКорректировки > 0
	|	И НЕ УведомлениеОбОстаткахПрослеживаемыхТоваров.ПолученоПодтверждениеИзФНС
	|	И СтатусыОтправки.Статус = &СтатусОтправки
	|	И УведомлениеОбОстаткахПрослеживаемыхТоваров.РНПТ <> &ПустойРНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	СтатусыОтправки.Статус,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	" + ТекстЗапросаУведомлениеОВвозе.ПеречислениеКолонок + "
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|	" + ТекстЗапросаУведомлениеОВвозе.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Организация = &Организация
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|	И СтатусыОтправки.Статус = &СтатусОтправки
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ <> """"
	|	И ДокументыРеализацииПолномочийНалоговыхОрганов.РНПТ ЕСТЬ НЕ NULL
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ = &ПустойРНПТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	СтатусыОтправки.Статус,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	" + ТекстЗапросаУведомлениеОВвозе.ПеречислениеКолонок + "
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = СтатусыОтправки.Объект
	|	" + ТекстЗапросаУведомлениеОВвозе.СоединениеСДокументом + "
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Организация = &Организация
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки > 0
	|	И НЕ УведомлениеОВвозеПрослеживаемыхТоваров.ПолученоПодтверждениеИзФНС
	|	И СтатусыОтправки.Статус = &СтатусОтправки
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ <> &ПустойРНПТ";
	
	Выбор = Запрос.Выполнить().Выбрать();
	
	Пока Выбор.Следующий() Цикл
		
		ДокументОбъект = Выбор.Ссылка.ПолучитьОбъект();
		
		Если Выбор.НомерКорректировки = 0 Тогда
	
			
			ДокументОбъект.РНПТ = 
				ПрослеживаемостьПереопределяемый.ПолучитьРНПТНаСервере(Выбор.ДокументыРеализацииПолномочийНО_РНПТ);
			
		Иначе
			
			ДокументОбъект.ПолученоПодтверждениеИзФНС = Истина;
			
		КонецЕсли;
			
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьУведомленияОВвозе(ТаблицаВвоза, Организация)
	
	ТаблицаТНВЭД = ТаблицаВвоза.Скопировать(,"КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	Фильтр = Новый Структура("КодТНВЭД, Основание, ЕдиницаИзмерения");
	
	Если НЕ ЗначениеЗаполнено(ТаблицаВвоза) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата, Контрагент");
		
		НовыйДокумент = Документы.УведомлениеОВвозеПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = РеквизитыОснования.Дата + 1;
		НовыйДокумент.Контрагент = РеквизитыОснования.Контрагент;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		
		ТаблицаТовары = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаВвоза.НайтиСтроки(Фильтр);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСтруктуруТаблицРаспределенныхПоУведомлениямКорректировокОВвозе(ТаблицаКорректировки)
	
	СтруктураТаблицРаспределенныхКорректировок = 
		Новый Структура("ТаблицаРаспределенныхКорректировокРеквизитыШапки,ТаблицаРаспределенныхКорректировокТаблицаТовары,ТаблицаОснований");
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаКорректировок", ТаблицаКорректировки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаКорректировок.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПослеИзменения,
	|	ТаблицаКорректировок.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемостиПослеИзменения,
	|	ТаблицаКорректировок.КодОКПД2 КАК КодОКПД2ПослеИзменения,
	|	ТаблицаКорректировок.КодТНВЭД КАК КодТНВЭДПослеИзменения,
	|	ТаблицаКорректировок.Количество КАК КоличествоКорректировки,
	|	ТаблицаКорректировок.КоличествоПрослеживаемости КАК КоличествоПрослеживаемостиКорректировки,
	|	ТаблицаКорректировок.Номенклатура КАК Номенклатура,
	|	ТаблицаКорректировок.Основание КАК Основание,
	|	ТаблицаКорректировок.РНПТ КАК РНПТ,
	|	ТаблицаКорректировок.СтранаПроисхождения КАК СтранаПроисхожденияПослеИзменения,
	|	ТаблицаКорректировок.Сумма КАК СуммаКорректировки,
	|	ТаблицаКорректировок.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_ТаблицаКорректировок
	|ИЗ
	|	&ТаблицаКорректировок КАК ТаблицаКорректировок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаКорректировок.Основание КАК Основание,
	|	ВТ_ТаблицаКорректировок.РНПТ КАК РНПТ,
	|	ВТ_ТаблицаКорректировок.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_ТаблицаОснований
	|ИЗ
	|	ВТ_ТаблицаКорректировок КАК ВТ_ТаблицаКорректировок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаКорректировок.Основание,
	|	ВТ_ТаблицаКорректировок.РНПТ,
	|	ВТ_ТаблицаКорректировок.ПервичныйДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ,
	|	Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка КАК УведомлениеОВвозе,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки,
	|	МАКСИМУМ(УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки) КАК НомерКорректировкиМаксимум,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ КАК РНПТ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_УведомленияДетальные
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|ГДЕ
	|	(УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ, УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент) В
	|			(ВЫБРАТЬ
	|				ВТ_ТаблицаОснований.РНПТ КАК РНПТ,
	|				ВТ_ТаблицаОснований.ПервичныйДокумент КАК ПервичныйДокумент
	|			ИЗ
	|				ВТ_ТаблицаОснований КАК ВТ_ТаблицаОснований)
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	УведомлениеОВвозе,
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_УведомленияДетальные.НомерКорректировки) КАК МаксимальныйНомерКорректировки,
	|	ВТ_УведомленияДетальные.РНПТ КАК РНПТ,
	|	ВТ_УведомленияДетальные.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_УведомленияОтборПоследнейКорректировки
	|ИЗ
	|	ВТ_УведомленияДетальные КАК ВТ_УведомленияДетальные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_УведомленияДетальные.РНПТ,
	|	ВТ_УведомленияДетальные.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_УведомленияДетальные.УведомлениеОВвозе КАК УведомлениеОВвозе,
	|	ВТ_УведомленияДетальные.НомерКорректировки КАК НомерКорректировки,
	|	ВТ_УведомленияДетальные.РНПТ КАК РНПТ,
	|	ВТ_УведомленияОтборПоследнейКорректировки.МаксимальныйНомерКорректировки КАК НомерКорректировкиМаксимум,
	|	ВТ_УведомленияДетальные.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_Уведомления
	|ИЗ
	|	ВТ_УведомленияОтборПоследнейКорректировки КАК ВТ_УведомленияОтборПоследнейКорректировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УведомленияДетальные КАК ВТ_УведомленияДетальные
	|		ПО (ВТ_УведомленияДетальные.РНПТ = ВТ_УведомленияОтборПоследнейКорректировки.РНПТ)
	|			И (ВТ_УведомленияДетальные.НомерКорректировки = ВТ_УведомленияОтборПоследнейКорректировки.МаксимальныйНомерКорректировки)
	|			И (ВТ_УведомленияДетальные.ПервичныйДокумент = ВТ_УведомленияОтборПоследнейКорректировки.ПервичныйДокумент)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ,
	|	УведомлениеОВвозе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеУведомления.УведомлениеОВвозе КАК УведомлениеОВвозе,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК ЕдиницаПрослеживаемости,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.КодОКПД2Корректировочный
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.КодОКПД2КорректировочныйПослеИзменения
	|	КОНЕЦ КАК КодОКПД2Корректировочный,
	|	ПоследниеУведомления.РНПТ КАК РНПТ,
	|	ПоследниеУведомления.НомерКорректировкиМаксимум + 1 КАК НомерКорректировки,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА ПоследниеУведомления.УведомлениеОВвозе
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ДокументУведомлениеОВвозе
	|	КОНЕЦ КАК ДокументУведомлениеОВвозе,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_НачальныеРеквизитыВШапкеУведомления
	|ИЗ
	|	ВТ_Уведомления КАК ПоследниеУведомления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ПО ПоследниеУведомления.УведомлениеОВвозе = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
	|			И (ПоследниеУведомления.НомерКорректировки = ПоследниеУведомления.НомерКорректировкиМаксимум)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследниеУведомления.УведомлениеОВвозе КАК УведомлениеОВвозе,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	ПоследниеУведомления.РНПТ КАК РНПТ,
	|	ПоследниеУведомления.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления
	|ИЗ
	|	ВТ_Уведомления КАК ПоследниеУведомления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ПО ПоследниеУведомления.УведомлениеОВвозе = УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка
	|			И (ПоследниеУведомления.НомерКорректировки = ПоследниеУведомления.НомерКорректировкиМаксимум)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ПО ПоследниеУведомления.УведомлениеОВвозе = УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка
	|			И (ПоследниеУведомления.НомерКорректировки = ПоследниеУведомления.НомерКорректировкиМаксимум)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаКорректировок.КодОКПД2ПослеИзменения КАК КодОКПД2ПослеИзменения,
	|	ВТ_ТаблицаКорректировок.КодТНВЭДПослеИзменения КАК КодТНВЭДПослеИзменения,
	|	ВТ_ТаблицаКорректировок.ЕдиницаИзмеренияПослеИзменения КАК ЕдиницаИзмеренияПослеИзменения,
	|	ВТ_ТаблицаКорректировок.ЕдиницаПрослеживаемостиПослеИзменения КАК ЕдиницаПрослеживаемостиПослеИзменения,
	|	ВТ_ТаблицаКорректировок.РНПТ КАК РНПТ,
	|	ВТ_ТаблицаКорректировок.ПервичныйДокумент КАК ПервичныйДокумент
	|ПОМЕСТИТЬ ВТ_ТаблицаКорректировкокШапка
	|ИЗ
	|	ВТ_ТаблицаКорректировок КАК ВТ_ТаблицаКорректировок
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаКорректировок.КодОКПД2ПослеИзменения,
	|	ВТ_ТаблицаКорректировок.КодТНВЭДПослеИзменения,
	|	ВТ_ТаблицаКорректировок.ЕдиницаИзмеренияПослеИзменения,
	|	ВТ_ТаблицаКорректировок.ЕдиницаПрослеживаемостиПослеИзменения,
	|	ВТ_ТаблицаКорректировок.РНПТ,
	|	ВТ_ТаблицаКорректировок.ПервичныйДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.КодТНВЭД КАК КодТНВЭД,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.КодОКПД2Корректировочный КАК КодОКПД2Корректировочный,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.РНПТ КАК РНПТ,
	|	ВТ_ТаблицаКорректировкокШапка.КодОКПД2ПослеИзменения КАК КодОКПД2ПослеИзменения,
	|	ВТ_ТаблицаКорректировкокШапка.КодТНВЭДПослеИзменения КАК КодТНВЭДПослеИзменения,
	|	ВТ_ТаблицаКорректировкокШапка.ЕдиницаИзмеренияПослеИзменения КАК ЕдиницаИзмеренияПослеИзменения,
	|	ВТ_ТаблицаКорректировкокШапка.ЕдиницаПрослеживаемостиПослеИзменения КАК ЕдиницаПрослеживаемостиПослеИзменения,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.ЕдиницаПрослеживаемости КАК ЕдиницаПрослеживаемости,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.НомерКорректировки КАК НомерКорректировки,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.ДокументУведомлениеОВвозе КАК ДокументУведомлениеОВвозе,
	|	ВТ_НачальныеРеквизитыВШапкеУведомления.ПервичныйДокумент КАК ПервичныйДокумент
	|ИЗ
	|	ВТ_ТаблицаКорректировкокШапка КАК ВТ_ТаблицаКорректировкокШапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачальныеРеквизитыВШапкеУведомления КАК ВТ_НачальныеРеквизитыВШапкеУведомления
	|		ПО ВТ_ТаблицаКорректировкокШапка.РНПТ = ВТ_НачальныеРеквизитыВШапкеУведомления.РНПТ
	|			И ВТ_ТаблицаКорректировкокШапка.ПервичныйДокумент = ВТ_НачальныеРеквизитыВШапкеУведомления.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.УведомлениеОВвозе КАК УведомлениеОВвозе,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Номенклатура КАК Номенклатура,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Количество КАК Количество,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Сумма КАК Сумма,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаКорректировок.Номенклатура ЕСТЬ NULL
	|			ТОГДА ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Количество
	|		ИНАЧЕ ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Количество - ВТ_ТаблицаКорректировок.КоличествоКорректировки
	|	КОНЕЦ КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаКорректировок.Номенклатура ЕСТЬ NULL
	|			ТОГДА ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.КоличествоПрослеживаемости
	|		ИНАЧЕ ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.КоличествоПрослеживаемости - ВТ_ТаблицаКорректировок.КоличествоПрослеживаемостиКорректировки
	|	КОНЕЦ КАК КоличествоПрослеживаемостиПослеИзменения,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаКорректировок.Номенклатура ЕСТЬ NULL
	|			ТОГДА ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.СтранаПроисхождения
	|		ИНАЧЕ ВТ_ТаблицаКорректировок.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхожденияПослеИзменения,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаКорректировок.Номенклатура ЕСТЬ NULL
	|			ТОГДА ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Сумма
	|		ИНАЧЕ ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Сумма - ВТ_ТаблицаКорректировок.СуммаКорректировки
	|	КОНЕЦ КАК СуммаПослеИзменения,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.РНПТ КАК РНПТ,
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.ПервичныйДокумент КАК ПервичныйДокумент
	|ИЗ
	|	ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления КАК ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаКорректировок КАК ВТ_ТаблицаКорректировок
	|		ПО ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.РНПТ = ВТ_ТаблицаКорректировок.РНПТ
	|			И ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.Номенклатура = ВТ_ТаблицаКорректировок.Номенклатура
	|			И ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления.ПервичныйДокумент = ВТ_ТаблицаКорректировок.ПервичныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОснований.Основание КАК Основание,
	|	ВТ_ТаблицаОснований.РНПТ КАК РНПТ,
	|	ВТ_ТаблицаОснований.ПервичныйДокумент КАК ПервичныйДокумент
	|ИЗ
	|	ВТ_ТаблицаОснований КАК ВТ_ТаблицаОснований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаКорректировок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаОснований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Уведомления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаКорректировкокШапка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_НачальныеРеквизитыВТаблицеТоварыУведомления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_УведомленияДетальные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_УведомленияОтборПоследнейКорректировки";
	
	
	ТаблицаРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураТаблицРаспределенныхКорректировок.ТаблицаРаспределенныхКорректировокРеквизитыШапки = ТаблицаРезультатов[8].Выгрузить();
	СтруктураТаблицРаспределенныхКорректировок.ТаблицаРаспределенныхКорректировокТаблицаТовары = ТаблицаРезультатов[9].Выгрузить();
	СтруктураТаблицРаспределенныхКорректировок.ТаблицаОснований = ТаблицаРезультатов[10].Выгрузить();
	
	Возврат СтруктураТаблицРаспределенныхКорректировок;
	
КонецФункции

Процедура СоздатьКорректировочныеУведомленияОВвозе(ТаблицаКорректировки, Организация)
	
	Если НЕ ЗначениеЗаполнено(ТаблицаКорректировки) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураТаблицРаспределенныхПоУведомлениямКорректировок = 
		ПолучитьСтруктуруТаблицРаспределенныхПоУведомлениямКорректировокОВвозе(ТаблицаКорректировки);
		
	ТаблицаРаспределенныхКорректировокРеквизитыШапки = 
		СтруктураТаблицРаспределенныхПоУведомлениямКорректировок.ТаблицаРаспределенныхКорректировокРеквизитыШапки;
		
	ТаблицаРаспределенныхКорректировокТаблицаТовары = 
		СтруктураТаблицРаспределенныхПоУведомлениямКорректировок.ТаблицаРаспределенныхКорректировокТаблицаТовары;
		
	Фильтр = Новый Структура("РНПТ,ПервичныйДокумент");
	
	Для Каждого ТекущаяСтрокаОснований Из СтруктураТаблицРаспределенныхПоУведомлениямКорректировок.ТаблицаОснований Цикл
		
		РеквизитыОснования = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрокаОснований.Основание, "Дата, Контрагент");
		
		НовыйДокумент = Документы.УведомлениеОВвозеПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокаОснований);
		
		НовыйДокумент.Дата = РеквизитыОснования.Дата + 1;
		НовыйДокумент.Контрагент = РеквизитыОснования.Контрагент;
		НовыйДокумент.ОснованиеКорректировки = ТекущаяСтрокаОснований.Основание;
		НовыйДокумент.Организация = Организация;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокаОснований);
		
		НайденныеСтроки = ТаблицаРаспределенныхКорректировокРеквизитыШапки.НайтиСтроки(Фильтр);
		ЗаполнитьЗначенияСвойств(НовыйДокумент, НайденныеСтроки[0]);
		
		ТаблицаТовары = НовыйДокумент.Товары;
		
		НайденныеСтроки = ТаблицаРаспределенныхКорректировокТаблицаТовары.НайтиСтроки(Фильтр);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеЗаполнения(Период, Организация)
	
	Возврат ПрослеживаемостьПереопределяемый.ПолучитьНачальныеДанныеДляПомощникаПолученияРНПТ(
				Период, 
				Организация
				);
	
КонецФункции

Процедура СоздатьИнвентаризациюОСОбОстатках(Дата, Организация, ТаблицаОстатков, АдресРезультата)
	
	СписокОС = Новый Массив();
	СозданныеДокументы = Новый Массив();
	
	Для Каждого ТекущаяСтрока Из ТаблицаОстатков Цикл
		СписокОС.Добавить(ТекущаяСтрока.ОсновноеСредство);
	КонецЦикла;
	
	СсылкаНаДокумент = СоздатьИнвентаризациюОС(Дата, Организация, СписокОС);
	
	СозданныеДокументы.Добавить(СсылкаНаДокумент);
	
	СписокОС.Очистить();
	
	ПоместитьВоВременноеХранилище(СозданныеДокументы, АдресРезультата);
	
КонецПроцедуры

Функция СоздатьИнвентаризациюОС(Дата, Организация, СписокОС)
	
	НовыйДокумент = Документы.ИнвентаризацияОС.СоздатьДокумент();
	НовыйДокумент.Дата = Дата;
	
	ДанныеЗаполнения = ДанныеЗаполненияИнвентаризацииОС(Дата, Организация, СписокОС);
	
	НовыйДокумент.Заполнить(ДанныеЗаполнения);
	
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Функция ДанныеЗаполненияИнвентаризацииОС(Дата, Организация, СписокОС)
	
	ДанныеЗаполнения = Новый Структура();
	
	ДанныеЗаполнения.Вставить("Дата",                                Дата);
	ДанныеЗаполнения.Вставить("Организация",                         Организация);
	ДанныеЗаполнения.Вставить("СписокОС",                            СписокОС);
	ДанныеЗаполнения.Вставить("ЗаполняетсяИзПомощникаПолученияРНПТ", Истина);
	
	Возврат ДанныеЗаполнения;
КонецФункции

Процедура СоздатьУведомленияОбОстаткахПоОС(ТаблицаОстатковСПервичнымиДокументами, Организация)
	
	ТаблицаТНВЭД = ТаблицаОстатковСПервичнымиДокументами.Скопировать(,"КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	Фильтр = Новый Структура("КодТНВЭД, Основание, ЕдиницаИзмерения");
	
	Если НЕ ЗначениеЗаполнено(ТаблицаОстатковСПервичнымиДокументами) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата");
		
		НовыйДокумент = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = ДатаОснования + 1;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства;
		
		ТаблицаОсновныеСредства = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаОстатковСПервичнымиДокументами.НайтиСтроки(Фильтр);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			НоваяСтрока = ТаблицаОсновныеСредства.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьУведомленияОбОстаткахПоТоварамВСоставеОС(ТаблицаОстатковСПервичнымиДокументами, Организация)
	
	Если НЕ ЗначениеЗаполнено(ТаблицаОстатковСПервичнымиДокументами) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОтборовСоставныхОС = ТаблицаОстатковСПервичнымиДокументами.Скопировать(,"ОсновноеСредство,Основание");
	
	// По этой таблице нужно создавать Уведомдления об остатках
	ТаблицаТоваровВСоставеОС = СобратьТаблицаТоваровВСоставеОС(ТаблицаОтборовСоставныхОС, Организация);
	
	ТаблицаТНВЭД = ТаблицаТоваровВСоставеОС.Скопировать(,"КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	Фильтр = Новый Структура("КодТНВЭД, Основание, ЕдиницаИзмерения");
	
	Для Каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата");
		
		НовыйДокумент = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = ДатаОснования + 1;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС;
		
		ТаблицаОсновныеСредства = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаТоваровВСоставеОС.НайтиСтроки(Фильтр);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			НоваяСтрока = ТаблицаОсновныеСредства.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

// ТаблицаОтборовСоставныхОС - "ОсновноеСредство,Основание")
Функция СобратьТаблицаТоваровВСоставеОС(ТаблицаОтборовСоставныхОС, Организация)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТаблицаОтборовСоставныхОС", ТаблицаОтборовСоставныхОС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОснованияИОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОснованияИОС.Основание КАК Основание
	|ПОМЕСТИТЬ ТаблицаОснованияИОС
	|ИЗ
	|	&ТаблицаОтборовСоставныхОС КАК ТаблицаОснованияИОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияПрослеживаемыхТоваров.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД КАК ТНВЭД,
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура КАК Номенклатура,
	|	РегистрацияПрослеживаемыхТоваров.Основание КАК Основание,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Количество) КАК Количество,
	|	СУММА(РегистрацияПрослеживаемыхТоваров.Сумма) КАК Сумма,
	|	РегистрацияПрослеживаемыхТоваров.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ТаблицаНомеклатурыВСоставеОС
	|ИЗ
	|	ТаблицаОснованияИОС КАК ТаблицаОснованияИОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияПрослеживаемыхТоваров КАК РегистрацияПрослеживаемыхТоваров
	|		ПО ТаблицаОснованияИОС.ОсновноеСредство = РегистрацияПрослеживаемыхТоваров.ОсновноеСредство
	|			И ТаблицаОснованияИОС.Основание = РегистрацияПрослеживаемыхТоваров.Основание
	|ГДЕ
	|	РегистрацияПрослеживаемыхТоваров.Организация = &Организация
	|	И РегистрацияПрослеживаемыхТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.РегистрацияКомплектующихОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияПрослеживаемыхТоваров.ТНВЭД,
	|	РегистрацияПрослеживаемыхТоваров.ОсновноеСредство,
	|	РегистрацияПрослеживаемыхТоваров.Основание,
	|	РегистрацияПрослеживаемыхТоваров.СтранаПроисхождения,
	|	РегистрацияПрослеживаемыхТоваров.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНомеклатурыВСоставеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаНомеклатурыВСоставеОС.ТНВЭД КАК КодТНВЭД,
	|	ТаблицаНомеклатурыВСоставеОС.Номенклатура КАК Номенклатура,
	|	ТаблицаНомеклатурыВСоставеОС.Основание КАК Основание,
	|	ТаблицаНомеклатурыВСоставеОС.Количество КАК Количество,
	|	ТаблицаНомеклатурыВСоставеОС.Сумма КАК Сумма,
	|	ТаблицаНомеклатурыВСоставеОС.СтранаПроисхождения КАК СтранаПроисхождения,
	|	НоменклатураСправочник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КлассификаторТНВЭД.ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости,
	|	ТаблицаНомеклатурыВСоставеОС.Количество КАК КоличествоПрослеживаемости
	|ИЗ
	|	ТаблицаНомеклатурыВСоставеОС КАК ТаблицаНомеклатурыВСоставеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|		ПО ТаблицаНомеклатурыВСоставеОС.ТНВЭД = КлассификаторТНВЭД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ТаблицаНомеклатурыВСоставеОС.Номенклатура = НоменклатураСправочник.Ссылка";
		
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура СоздатьУведомленияОбОстаткахПоТоварам(ТаблицаОстатковСПервичнымиДокументами, Организация)
	
	ТаблицаТНВЭД = ТаблицаОстатковСПервичнымиДокументами.Скопировать(,"КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	ТаблицаТНВЭД.Свернуть("КодТНВЭД,Основание,ЕдиницаИзмерения,ЕдиницаПрослеживаемости");
	Фильтр = Новый Структура("КодТНВЭД, Основание, ЕдиницаИзмерения");
	
	Если НЕ ЗначениеЗаполнено(ТаблицаОстатковСПервичнымиДокументами) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрокТНВЭД Из ТаблицаТНВЭД Цикл
		
		ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокТНВЭД.Основание, "Дата");
		
		НовыйДокумент = Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущаяСтрокТНВЭД);
		
		НовыйДокумент.Дата = ДатаОснования + 1;
		НовыйДокумент.ПервичныйДокумент = ТекущаяСтрокТНВЭД.Основание;
		НовыйДокумент.Организация = Организация;
		НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары;
		
		ТаблицаТовары = НовыйДокумент.Товары;
		
		ЗаполнитьЗначенияСвойств(Фильтр, ТекущаяСтрокТНВЭД);
		НайденныеСтроки = ТаблицаОстатковСПервичнымиДокументами.НайтиСтроки(Фильтр);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			НоваяСтрока = ТаблицаТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			
		КонецЦикла;
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
