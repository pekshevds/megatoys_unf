
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды.Количество() = 1
		И ЭтоПредметШаблона(ПараметрКоманды[0])
		И ЕстьДоступныеШаблоны(ПараметрКоманды[0]) Тогда
		ШаблоныСообщенийКлиент.СформироватьСообщение(ПараметрКоманды[0], "СообщениеSMS",,, Новый Структура("ИмяФормыИсточникаСообщения", ПараметрыВыполненияКоманды.Источник.ИмяФормы));
	Иначе
		ДополнительныеПараметрыПечати = Неопределено;
		
		ФормаИсточник = ПараметрыВыполненияКоманды.Источник;
		
		Если ФормаИсточник.ИмяФормы = "ЖурналДокументов.КомиссионнаяТорговля.Форма.ФормаСписка" Тогда
			
			ИмяФормыОбъектаКоманды = Неопределено;
			
			ПолучитьФормуЭлементаЖурнала(ПараметрКоманды, ФормаИсточник, ИмяФормыОбъектаКоманды);
			
			Если ИмяФормыОбъектаКоманды = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
		Иначе
			ИмяФормыОбъектаКоманды = ФормаИсточник.ИмяФормы;
		КонецЕсли;
		
		Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
			ОбъектыОтправки = Новый ФиксированныйМассив(ПараметрКоманды);
		Иначе
			ОбъектыОтправки = Новый ФиксированныйМассив(Новый Массив);
		КонецЕсли;
		
		ПараметрыОтправкиПФ = Новый Структура("Получатели");
		ВыбратьПолучателейСервер(ОбъектыОтправки, ПараметрыОтправкиПФ);
		ОткрытьФорму("ОбщаяФорма.ПодготовкаНовогоSMS", ПараметрыОтправкиПФ);
		
	КонецЕсли;
	
	СтатистикаИспользованияФормКлиент.ПроверитьЗаписатьСтатистикуКоманды(
		"ОтправитьПоSMS",
		ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьФормуЭлементаЖурнала(ПараметрКоманды, ФормаИсточник, ИмяФормыОбъектаПечати)
	Если ПараметрКоманды.Количество() Тогда
		Если ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПереоценкаТоваровНаКомиссии") Тогда
			ИмяФормыОбъектаПечати = "Документ.ПереоценкаТоваровНаКомиссии.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ПереоценкаТоваровНаКомиссии.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			ИмяФормыОбъектаПечати = "Документ.РасходнаяНакладная.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.РасходнаяНакладная.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ИмяФормыОбъектаПечати = "Документ.ПриходнаяНакладная.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ПриходнаяНакладная.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетКомиссионераОСписании") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетКомиссионераОСписании.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетКомиссионераОСписании.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетКомиссионера.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетКомиссионера.Форма.ФормаДокумента")
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ЭтоПредметШаблона(ПараметрКоманды)
	
	ТипыПредметовШаблонов = Новый Массив;
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.КонтактныеЛица"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаданиеНаРаботу"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ПриемИПередачаВРемонт"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.Событие"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
	
	Возврат ТипыПредметовШаблонов.Найти(ТипЗнч(ПараметрКоманды)) <> Неопределено;
	
КонецФункции

&НаСервере
Функция ЕстьДоступныеШаблоны(ПредметШаблона)
	Возврат ШаблоныСообщенийУНФ.ЕстьДоступныеШаблоны(Ложь, ПредметШаблона);
КонецФункции

&НаСервере
Процедура ВыбратьПолучателейСервер(ОбъектыОтправки, ПараметрыОтправкиПФ)
	
	МассивОбъектов = ОбщегоНазначения.СкопироватьРекурсивно(ОбъектыОтправки);
	ПараметрыОтправкиПФ.Получатели = ЭлектроннаяПочтаУНФ.ПодготовленныеНомераТелефоновПолучателей(МассивОбъектов);
	
КонецПроцедуры

#КонецОбласти
