#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ЭтотУзел", ПланыОбмена.ОбменУправлениеНебольшойФирмойСайт.ЭтотУзел());
	
	ИспользоватьОбменССайтами = ПолучитьФункциональнуюОпцию("ИспользоватьОбменССайтами");
	Элементы.ОткрытьСайты.Видимость = ПолучитьФункциональнуюОпцию("СайтСоздан");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершенСеансОбменаССайтом" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВызовИзПланаОбмена", Истина);
	
	ОткрытьФорму("Обработка.ПомощникСозданияОбменаДаннымиССайтом.Форма.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуИнтеграции();
КонецПроцедуры

&НаКлиенте
Процедура Декорация3Нажатие(Элемент)
	ОткрытьФормуИнтеграции();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИнтеграции()
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОткрытьФормуИнтеграцииЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОповещениеОЗакрытии, НСтр("ru = 'Вы уверены?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИнтеграцииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьФормуИнтеграцииНаСервере(); 
		
		ОткрытьФорму("Справочник.НастройкиИнтеграцииСИнтернетМагазином.ФормаСписка",,Неопределено, Новый УникальныйИдентификатор);
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьФормуИнтеграцииНаСервере()
	
	ПараметрыОбработчика = Новый Структура("ОбработкаЗавершена", Истина);
	Справочники.НастройкиИнтеграцииСИнтернетМагазином.СоздатьИнтеграцииСИнтернетМагазином(ПараметрыОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьОбмен(Команда)
	
	Если НЕ ИспользоватьОбменССайтами Тогда
		ПодключитьОбменСервер();
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВызовИзПланаОбмена", Истина);
	ОткрытьФорму("Обработка.ПомощникСозданияОбменаДаннымиССайтом.Форма.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодключитьОбменСервер()
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ФункциональнаяОпцияИспользоватьОбменССайтами.Установить(Истина);
	ИспользоватьОбменССайтами = Истина;
	
КонецПроцедуры

#КонецОбласти
