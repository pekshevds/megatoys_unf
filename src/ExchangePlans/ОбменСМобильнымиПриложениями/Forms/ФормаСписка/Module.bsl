#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос; 
	МассивВариантов = Новый Массив();        
	МассивВариантов.Добавить("Кладовщик");
	МассивВариантов.Добавить("ПроверкаЦенников");
	Запрос.УстановитьПараметр("МассивВариантов", МассивВариантов);
	Запрос.Текст = "ВЫБРАТЬ
	               |	МобильныеПриложения.Ссылка КАК Ссылка,
	               |	МобильныеПриложения.ВариантНастройки КАК ВариантНастройки
	               |ИЗ
	               |	Справочник.МобильныеПриложения КАК МобильныеПриложения
	               |ГДЕ
	               |	МобильныеПриложения.ВариантНастройки В(&МассивВариантов)
	               |	И НЕ МобильныеПриложения.ПометкаУдаления";
	
	Результат = Запрос.Выполнить(); 
	Выборка = Результат.Выбрать();  
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВариантНастройки = "Кладовщик" Тогда
			ПриложениеКладовщик = Выборка.Ссылка;
		ИначеЕсли Выборка.ВариантНастройки = "ПроверкаЦенников" Тогда
			ПриложениеПроверкаЦенников = Выборка.Ссылка;
		КонецЕсли;
	КонецЦикла;     
	
	УстановитьПараметрыДинамическогоСписка();

	Если Параметры.Свойство("Отбор") Тогда 
		Если Параметры.Отбор.Свойство("МобильноеПриложение") Тогда     
			Если ТипЗнч(Параметры.Отбор.МобильноеПриложение) = Тип("Массив") Тогда    
				ОбъединенноеМобильноеПриложение = Истина;
				Заголовок = СтрШаблон(НСтр("ru = 'Настройки обмена с мобильным приложением %1'"), "1С:Кладовщик"); 
				Возврат;
			Иначе   
				МобильноеПриложение =  Параметры.Отбор.МобильноеПриложение; 
				Заголовок = СтрШаблон(НСтр("ru = 'Настройки обмена с мобильным приложением %1'"), МобильноеПриложение); 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ВыполненОбменДанными"
		Или ИмяСобытия = "Запись_УзелПланаОбмена"
		Или ИмяСобытия = "ЗакрытаФормаПомощникаСопоставленияОбъектов"
		Или ИмяСобытия = "ЗакрытаФормаРезультатовОбменаДанными" Тогда
		
		Элементы.Список.Обновить();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТипЗнч(ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПометкаУдаления
		И ТекущиеДанные.СтатусОбмена > 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'При следующем сеансе обмена в мобильном приложении будут удалены все данные.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоставОтправляемыхДанных(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТипЗнч(ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		ТекстПредупреждения = НСтр("ru = 'Действие недоступно для строки группировки списка!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		ЭтоПроверкаЦенников = ТекущиеДанные.МобильноеПриложение = ПриложениеПроверкаЦенников;      
		
		Если ЭтоПроверкаЦенников Тогда
			УзелОбмена = ТекущиеДанные.УзелПланаОбменаСПодключаемымОборудованием;
		Иначе  
			УзелОбмена = ТекущиеДанные.Ссылка; 
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("УзелОбмена", УзелОбмена);
		ПараметрыФормы.Вставить("ЗапрещеноВыбиратьУзелОбмена", Истина);

		ПараметрыФормы.Вставить("ИменаСкрываемыхМетаданных", Новый СписокЗначений);
		ПараметрыФормы.ИменаСкрываемыхМетаданных.Добавить("РегистрСведений.СоответствияОбъектовИнформационныхБаз");
		НеВыгружатьПоПравилам = ОбменДаннымиВызовСервера.ИменаМетаданныхНеВыгружаемыхОбъектовУзла(УзелОбмена);
		Для Каждого ИмяМетаданных Из НеВыгружатьПоПравилам Цикл
			ПараметрыФормы.ИменаСкрываемыхМетаданных.Добавить(ИмяМетаданных);
		КонецЦикла; 
		
		ОткрытьФорму("Обработка.РегистрацияИзмененийДляОбменаДанными.Форма", ПараметрыФормы,, УзелОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)  
	Если ОбъединенноеМобильноеПриложение Тогда
		ПараметрыФормы = Новый Структура;
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьМобильноеПриложениеЗавершение", ЭтотОбъект);
		ОткрытьФорму("ПланОбмена.ОбменСМобильнымиПриложениями.Форма.ФормаВыбораПриложения",ПараметрыФормы,ЭтотОбъект,,,,ОписаниеОповещения ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);    
	Иначе
		ОткрытьФормуСоздания();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройкуСинхронизации(Команда)
	
	ВыполнитьКомандуСПредварительнойПроверкой(Команда.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыДинамическогоСписка()
	
	Список.Параметры.УстановитьЗначениеПараметра("ПустойУзелОбменаСПодключаемымОборудованием", ПланыОбмена.ОбменСПодключаемымОборудованиемOffline.ПустаяСсылка());

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМобильноеПриложениеЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = "Кладовщик" Тогда
		МобильноеПриложение = ПриложениеКладовщик;
	ИначеЕсли РезультатВыбора = "ПроверкаЦенников" Тогда
		МобильноеПриложение = ПриложениеПроверкаЦенников;
	КонецЕсли;      
	ОткрытьФормуСоздания();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСоздания() 
	
	Если ЗначениеЗаполнено(МобильноеПриложение) Тогда
		ПараметрыФормы = Новый Структура; 
		ЗначенияЗаполнения = Новый Структура("МобильноеПриложение", МобильноеПриложение);  
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("ПланОбмена.ОбменСМобильнымиПриложениями.Форма.ФормаУзла",ПараметрыФормы,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);    
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКомандуСПредварительнойПроверкой(КомандаСтрокой)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПомощника = Новый Структура;
	ПараметрыПомощника.Вставить("УзелОбмена",                   ТекущиеДанные.Ссылка);
	ПараметрыПомощника.Вставить("ИмяПланаОбмена",               "ОбменСМобильнымиПриложениями");
	ПараметрыПомощника.Вставить("НаименованиеКорреспондента",   ТекущиеДанные.МобильноеПриложение);
	
	ИмяОткрываемойФормы = "Обработка.ПомощникСозданияОбменаДанными.Форма.УдалениеНастройкиСинхронизации";
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыПомощника, ЭтотОбъект);
		
КонецПроцедуры  

#КонецОбласти
