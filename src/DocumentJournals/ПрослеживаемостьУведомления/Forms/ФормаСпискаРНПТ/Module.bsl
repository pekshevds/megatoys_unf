#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаГлобальныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.КонтекстныйВызов Тогда
		
		Элементы.Список.Период.ДатаНачала = Параметры.ДатаБольшеИлиРавно;
		Элементы.Список.Период.ДатаОкончания = Параметры.ДатаМеньшеИлиРавно; 
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ЭтаФорма.Список, "Организация", Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно, , Истина, 
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ЭтаФорма.Список, "Проведен", Истина, ВидСравненияКомпоновкиДанных.Равно, , Истина, 
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
				
		Если Параметры.ОтобратьУведомленияСРНПТ Тогда
			
			ГруппаОтбораИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ЭтаФорма.Список.КомпоновщикНастроек.Настройки.Отбор.Элементы, "Отбор", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
					
			ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ГруппаОтбораИЛИ, "Отбор1", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
			
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
					ГруппаОтбораИ, "РНПТ", , ВидСравненияКомпоновкиДанных.Заполнено, , Истина);
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
					ГруппаОтбораИ, "НомерКорректировки", 0, ВидСравненияКомпоновкиДанных.Равно, , Истина);
					
			ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ГруппаОтбораИЛИ, "Отбор2", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
			
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
					ГруппаОтбораИ, "РНПТ", , ВидСравненияКомпоновкиДанных.Заполнено, , Истина);
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
					ГруппаОтбораИ, "НомерКорректировки", 0, ВидСравненияКомпоновкиДанных.БольшеИлиРавно, , Истина);
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
					ГруппаОтбораИ, "ПолученоПодтверждениеИзФНС", Истина, ВидСравненияКомпоновкиДанных.Равно, , Истина);
					
		КонецЕсли;
		
	Иначе
		
		ПрослеживаемостьСобытияФормПереопределяемый.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.УведомлениеОбОстаткахПрослеживаемыхТоваров);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ПрослеживаемостьФормыКлиентПереопределяемый.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, Список)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для каждого ТекущаяСтрока Из Строки Цикл
		
		Данные = ТекущаяСтрока.Значение.Данные;
		
		ПредставлениеТипДокумента = НСтр("ru = 'Уведомление'");
		
		Если ТипЗНЧ(Данные.Ссылка) = Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
			
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Данные.Ссылка, "ВидОперации");
		
			Если ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства Тогда
			
				ТекстОкончаниеДокумента = НСтр("ru=' об остатках ОС'");
			
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС Тогда
			
				ТекстОкончаниеДокумента = НСтр("ru=' об остатках комплектующих ОС'");
			
			Иначе
			
				ТекстОкончаниеДокумента = НСтр("ru=' об остатках'");
			
			КонецЕсли;
			
			ПредставлениеТипДокумента = ПредставлениеТипДокумента + ТекстОкончаниеДокумента;
		
		Иначе
			ПредставлениеТипДокумента = ПредставлениеТипДокумента + НСтр("ru = ' о ввозе'");
		КонецЕсли;
		
		Данные.ПредставлениеТипДокумента = ПредставлениеТипДокумента;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
