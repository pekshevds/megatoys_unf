
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("АдресСхемы") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДанныеРасшифровки") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("Расшифровка") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.ДанныеРасшифровки);
	
	ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Вставить("АдресСхемы", Параметры.АдресСхемы);
	ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Вставить("Отбор", Параметры.Отбор);
	
	ПараметрыРасшифровки = Новый Структура;
	СоответствиеЗаголовка = Новый Соответствие;
	
	Для Каждого ТекЭлемент Из ДанныеРасшифровки.Элементы.Получить(Параметры.Расшифровка).ПолучитьПоля() Цикл
		ПараметрыРасшифровки.Вставить(ТекЭлемент.Поле, ТекЭлемент.Значение);
	КонецЦикла;
	
	ЗаполнитьСоответствиеЗаголовка(СоответствиеЗаголовка);
	
	Отбор = ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Отбор;
	РезультатОбработкиРасшифровки(ПараметрыРасшифровки, Отбор);
	
	Заголовок = СтрШаблон(НСтр("ru = 'Расшифровка: %1'"), СоответствиеЗаголовка.Получить(ПараметрыРасшифровки.ТипСчета));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура РезультатОбработкиРасшифровки(ПараметрыРасшифровки, Отбор)
	
	Макет = Отчеты.Баланс.ПолучитьМакет("МакетРасшифровка");
	Макет.Параметры.Найти("ПериодНачало").Значение = ?(ПараметрыРасшифровки.ЭтоНачалоПериода, Дата('00010101'), Отбор.СтПериод.ДатаНачала);
	Макет.Параметры.Найти("ПериодКонец").Значение = ?(ПараметрыРасшифровки.ЭтоНачалоПериода, Отбор.СтПериод.ДатаНачала, Отбор.СтПериод.ДатаОкончания);
	Макет.Параметры.Найти("ТипСчета").Значение = ПараметрыРасшифровки.ТипСчета;
	Макет.Параметры.Найти("Организация").Значение = Отбор.Организация;
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Макет, Макет.НастройкиПоУмолчанию, ДанныеРасшифровки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
		
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	АдресРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	АдресСхемы = ПоместитьВоВременноеХранилище(Макет, УникальныйИдентификатор);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеЗаголовка(СоответствиеЗаголовка)

	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ВнеоборотныеАктивы, НСтр("ru = 'Имущество'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ПрочиеВнеоборотныеАктивы, НСтр("ru = 'Прочее имущество'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.Запасы, НСтр("ru = 'Запасы'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.КредитыИЗаймы, НСтр("ru = 'Займы и кредиты'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.Дебиторы, НСтр("ru = 'Дебиторская задолженность'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ПрибылиУбытки, НСтр("ru = 'Краткосрочные финансовые вложения'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ДенежныеСредства, НСтр("ru = 'Денежные средства'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ПрочиеОборотныеАктивы, НСтр("ru = 'Прочие оборотные активы'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.Капитал, НСтр("ru = 'Капитал'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.РезервныйИДобавочныйКапитал, НСтр("ru = 'Резервный и добавочный капитал'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.НераспределеннаяПрибыль, НСтр("ru = 'Нераспределенная прибыль'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ДолгосрочныеОбязательства, НСтр("ru = 'Долгосрочные обязательства'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.Кредиторы, НСтр("ru = 'Кредиторская задолженность'"));
	СоответствиеЗаголовка.Вставить(Перечисления.ТипыСчетов.ПрочиеКраткосрочныеОбязательства, НСтр("ru = 'Прочие краткосрочные обязательства'"));

КонецПроцедуры

&НаКлиенте
Процедура ДокументРезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	СтандартнаяОбработка = Ложь;
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(АдресРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	ОбработкаРасшифровки.ПоказатьВыборДействия(Новый ОписаниеОповещения("ПослеВыбораДействия", ЭтотОбъект), Расшифровка, , , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДействия(ВыполненноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ПоказатьЗначение(, ПараметрВыполненногоДействия)
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти