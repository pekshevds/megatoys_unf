#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ТекстЗаголовка = НСтр("ru ='Список договоров контрагента: %1'");
	Заголовок = СтрШаблон(ТекстЗаголовка, Строка(Параметры.Контрагент));
	СортироватьПоДоговорам = Параметры.СортироватьПоДоговорам;

	ЗаполнитьСписокДоговоров(Параметры.ДоговорыКонтрагентов.Выгрузить().ВыгрузитьКолонку("Договор"));

	РедактированиеДоступно = ПравоДоступа("Редактирование", Метаданные.Документы.СверкаВзаиморасчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДоговорыКонтрагентов", "Доступность",
		РедактированиеДоступно);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СортироватьПоДоговорам", "Доступность",
		РедактированиеДоступно);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если ПроверитьЗаполнениеДоговоров Тогда

		Для Каждого Договор Из ДоговорыКонтрагентов Цикл

			Если Не ЗначениеЗаполнено(Договор.Договор) Тогда

				ТекстСообщения = НСтр("ru = 'В таблице присутствуют строки с незаполненным договором контрагента'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
				Прервать;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	ПроверитьЗаполнениеДоговоров = Истина;
	Модифицированность = Ложь;
	УдалитьНеотмеченныеДоговоры();
	Закрыть(Новый Структура("ДоговорыКонтрагентов, СортироватьПоДоговорам", ДоговорыКонтрагентов,
		СортироватьПоДоговорам));

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)

	ПроверитьЗаполнениеДоговоров = Ложь;
	Модифицированность = Ложь;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)

	Для Каждого СтрокаСписка Из ДоговорыКонтрагентов Цикл

		Если Элементы.ДоговорыКонтрагентов.ПроверитьСтроку(ДоговорыКонтрагентов.Индекс(СтрокаСписка)) = Истина Тогда
			СтрокаСписка.Отметка = Истина;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)

	Для Каждого СтрокаСписка Из ДоговорыКонтрагентов Цикл

		Если Элементы.ДоговорыКонтрагентов.ПроверитьСтроку(ДоговорыКонтрагентов.Индекс(СтрокаСписка)) = Истина Тогда
			СтрокаСписка.Отметка = Ложь;
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорыКонтрагентовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокДоговоров(СписокДоговоров)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ИСТИНА КАК Отметка,
	                      |	ДоговорыКонтрагентов.Ссылка КАК Договор
	                      |ИЗ
	                      |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	                      |ГДЕ
	                      |	ДоговорыКонтрагентов.Ссылка В(&СписокДоговоров)
	                      |	И (ДоговорыКонтрагентов.Владелец = &Контрагент
	                      |				И ВЫБОР
	                      |					КОГДА &БезОтбораПоОрганизации
	                      |						ТОГДА ИСТИНА
	                      |					ИНАЧЕ ДоговорыКонтрагентов.Организация = &Организация
	                      |				КОНЕЦ
	                      |			ИЛИ ДоговорыКонтрагентов.ЭтоДоговорПередачиТоваровМеждуОрганизациями)
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ЛОЖЬ,
	                      |	ДоговорыКонтрагентов.Ссылка
	                      |ИЗ
	                      |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	                      |ГДЕ
	                      |	ДоговорыКонтрагентов.Владелец = &Контрагент
	                      |	И НЕ ДоговорыКонтрагентов.Ссылка В (&СписокДоговоров)
	                      |	И ВЫБОР
	                      |			КОГДА &БезОтбораПоОрганизации
	                      |				ТОГДА ИСТИНА
	                      |			ИНАЧЕ ДоговорыКонтрагентов.Организация = &Организация
	                      |		КОНЕЦ");

	ВестиРасчетыПоДоговорам = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Контрагент, "ВестиРасчетыПоДоговорам");
	
	Запрос.УстановитьПараметр("Контрагент", Параметры.Контрагент);
	Запрос.УстановитьПараметр("БезОтбораПоОрганизации", Параметры.УчетПоКомпании Или Не ВестиРасчетыПоДоговорам);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("СписокДоговоров", СписокДоговоров);
	
	ПолнаяТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
	
	ДоговорыКонтрагентов.Загрузить(ПолнаяТаблицаДоговоров);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеотмеченныеДоговоры()
	
	МассивСтрокКУдалению = ДоговорыКонтрагентов.НайтиСтроки(Новый Структура("Отметка", Ложь));
	
	Для Каждого СтрокаКУдалению Из МассивСтрокКУдалению Цикл
		ДоговорыКонтрагентов.Удалить(СтрокаКУдалению);	
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти
