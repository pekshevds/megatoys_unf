#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
// @skip-warning
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаСдельныйНаряд, СтруктураДополнительныеСвойства) Экспорт
	
	РеквизитыСдельногоНаряда = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаСдельныйНаряд,
		"ДокументОснование");
	ОснованиеСборкаЗапасов = ЗначениеЗаполнено(РеквизитыСдельногоНаряда.ДокументОснование) И ТипЗнч(
		РеквизитыСдельногоНаряда.ДокументОснование) = Тип("ДокументСсылка.СборкаЗапасов");
	Если ОснованиеСборкаЗапасов Тогда
		РеквизитыСборки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыСдельногоНаряда.ДокументОснование,
			"Проведен, СтруктурнаяЕдиницаПродукции, СтруктурнаяЕдиницаПродукции.ТипСтруктурнойЕдиницы");
	Иначе
		РеквизитыСборки = Новый Структура;
		РеквизитыСборки.Вставить("Проведен", Ложь);
		РеквизитыСборки.Вставить("СтруктурнаяЕдиницаПродукции", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
		РеквизитыСборки.Вставить("СтруктурнаяЕдиницаПродукцииТипСтруктурнойЕдиницы",
			Перечисления.ТипыСтруктурныхЕдиниц.ПустаяСсылка());
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапросаИнициализации();
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаСдельныйНаряд);
	Запрос.УстановитьПараметр("МоментВремени",
		Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьЭтапыПроизводства",
		СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьЭтапыПроизводства);
	Запрос.УстановитьПараметр("НачислениеЗарплатыУНФ", НСтр("ru = 'Начисление зарплаты'"));
	Запрос.УстановитьПараметр("РазноскаЗарплаты", НСтр("ru = 'Отнесение затрат на продукцию'"));
	
	// ФО Использовать подсистему Зарплата.
	Запрос.УстановитьПараметр("ИспользоватьПодсистемуЗарплата",
		Константы.ФункциональнаяОпцияИспользоватьПодсистемуЗарплата.Получить());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисленияИУдержания",
		МассивРезультатов[8].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПерсоналом",
		МассивРезультатов[9].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСдельныеНаряды", 
		МассивРезультатов[10].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОперации",
		МассивРезультатов[11].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы",
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации.СкопироватьКолонки());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий",
		МассивРезультатов[12].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЭтапыПроизводства",
		МассивРезультатов[16].Выгрузить());
	
	ПустаяСтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
	ПустойСчетУчета = ПланыСчетов.Управленческий.ПустаяСсылка();
	ПустаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
	ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	ПустаяПартия = Справочники.ПартииНоменклатуры.ПустаяСсылка();
	ПустойЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
	ПустойЗаказНаПроизводство = Документы.ЗаказНаПроизводство.ПустаяСсылка();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасыОперации = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации[н];
		
		// Оприходуем затраты по зарплате в НЗП.
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасыОперации);
		
		СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = ПустаяСтруктурнаяЕдиница;
		СтрокаТаблицыПриход.КоррСчетУчета = ПустойСчетУчета;
		СтрокаТаблицыПриход.КоррНоменклатура = ПустаяНоменклатура;
		СтрокаТаблицыПриход.КоррХарактеристика = ПустаяХарактеристика;
		СтрокаТаблицыПриход.КоррПартия = ПустаяПартия;
		СтрокаТаблицыПриход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
		СтрокаТаблицыПриход.КоррЗаказНаПроизводство = ПустойЗаказНаПроизводство;
		
		// Спишем их на продукцию.
		СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасыОперации);
		СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаТаблицыРасход.ВидДвиженияУправленческий = ВидДвиженияБухгалтерии.Кредит;
		СтрокаТаблицыРасход.ФиксированнаяСтоимость = Ложь;
		СтрокаТаблицыРасход.ЗатратыНаВыпуск = Истина;
		
		// Включим в себестоимость продукции.
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ПроизводствоСервер.ЗаполнитьДвижениеЗапасыЗеркально(СтрокаТаблицыПриход, СтрокаТаблицыРасход);
		СтрокаТаблицыПриход.ЗатратыНаВыпуск = Ложь;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Удалить("ТаблицаЗапасыОперации");
	
	ПроизводствоСервер.СформироватьДвиженияЗаказыНаПроизводствоСУчетомОстатков(ДокументСсылкаСдельныйНаряд,
		СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

Функция ТекстЗапросаИнициализации()
	
	Возврат 
	"ВЫБРАТЬ
	|	СУММА(СдельныйНарядСоставБригады.КТУ) КАК СуммаКТУ,
	|	СдельныйНарядСоставБригады.Ссылка КАК Ссылка,
	|	СдельныйНарядСоставБригады.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ТЧСоставБригады
	|ИЗ
	|	Документ.СдельныйНаряд.СоставБригады КАК СдельныйНарядСоставБригады
	|ГДЕ
	|	СдельныйНарядСоставБригады.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СдельныйНарядСоставБригады.Ссылка,
	|	СдельныйНарядСоставБригады.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТЧСоставБригады.СуммаКТУ) КАК СуммаКТУ,
	|	ТЧСоставБригады.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ БазаКТУ
	|ИЗ
	|	ТЧСоставБригады КАК ТЧСоставБригады
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧСоставБригады.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СдельныйНарядСоставБригады.Сотрудник КАК Сотрудник,
	|	СдельныйНарядСоставБригады.Ссылка.Исполнитель КАК Бригада,
	|	ВЫБОР
	|		КОГДА СдельныйНарядСоставБригады.Ссылка.ПоложениеСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|			ТОГДА СдельныйНарядСоставБригады.Ссылка.СтруктурнаяЕдиница
	|		ИНАЧЕ СдельныйНарядСоставБригады.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА БазаКТУ.СуммаКТУ = 0
	|			ТОГДА 0
	|		ИНАЧЕ СдельныйНарядСоставБригады.КТУ / БазаКТУ.СуммаКТУ
	|	КОНЕЦ КАК Коэффициент,
	|	0 КАК КлючСвязи,
	|	СдельныйНарядСоставБригады.НомерСтроки КАК НомерСтрокиСоставБригады
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	Документ.СдельныйНаряд.СоставБригады КАК СдельныйНарядСоставБригады
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаКТУ КАК БазаКТУ
	|		ПО СдельныйНарядСоставБригады.Ссылка = БазаКТУ.Ссылка
	|ГДЕ
	|	СдельныйНарядСоставБригады.Ссылка = &Ссылка
	|	И СдельныйНарядСоставБригады.Ссылка.Исполнитель ССЫЛКА Справочник.Бригады
	|	И СдельныйНарядСоставБригады.Ссылка.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СдельныйНаряд.Исполнитель,
	|	ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка),
	|	1,
	|	0,
	|	0
	|ИЗ
	|	Документ.СдельныйНаряд КАК СдельныйНаряд
	|ГДЕ
	|	СдельныйНаряд.Ссылка = &Ссылка
	|	И СдельныйНаряд.Исполнитель ССЫЛКА Справочник.Сотрудники
	|	И СдельныйНаряд.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СдельныйНарядОперации.Исполнитель,
	|	ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА СдельныйНарядОперации.Ссылка.ПоложениеСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|			ТОГДА СдельныйНарядОперации.Ссылка.СтруктурнаяЕдиница
	|		ИНАЧЕ СдельныйНарядОперации.СтруктурнаяЕдиница
	|	КОНЕЦ,
	|	1,
	|	СдельныйНарядОперации.КлючСвязи,
	|	0
	|ИЗ
	|	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
	|ГДЕ
	|	СдельныйНарядОперации.Ссылка = &Ссылка
	|	И СдельныйНарядОперации.Исполнитель ССЫЛКА Справочник.Сотрудники
	|	И СдельныйНарядОперации.Ссылка.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СдельныйНарядСоставБригады.Сотрудник,
	|	СдельныйНарядОперации.Исполнитель,
	|	ВЫБОР
	|		КОГДА СдельныйНарядСоставБригады.Ссылка.ПоложениеСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|			ТОГДА СдельныйНарядСоставБригады.Ссылка.СтруктурнаяЕдиница
	|		ИНАЧЕ СдельныйНарядСоставБригады.СтруктурнаяЕдиница
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТЧСоставБригады.СуммаКТУ = 0
	|			ТОГДА 0
	|		ИНАЧЕ СдельныйНарядСоставБригады.КТУ / ТЧСоставБригады.СуммаКТУ
	|	КОНЕЦ,
	|	СдельныйНарядОперации.КлючСвязи,
	|	СдельныйНарядСоставБригады.НомерСтроки
	|ИЗ
	|	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СдельныйНаряд.СоставБригады КАК СдельныйНарядСоставБригады
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТЧСоставБригады КАК ТЧСоставБригады
	|			ПО СдельныйНарядСоставБригады.Ссылка = ТЧСоставБригады.Ссылка
	|				И СдельныйНарядСоставБригады.КлючСвязи = ТЧСоставБригады.КлючСвязи
	|		ПО СдельныйНарядОперации.Ссылка = СдельныйНарядСоставБригады.Ссылка
	|			И СдельныйНарядОперации.КлючСвязи = СдельныйНарядСоставБригады.КлючСвязи
	|ГДЕ
	|	СдельныйНарядОперации.Ссылка = &Ссылка
	|	И СдельныйНарядОперации.Исполнитель ССЫЛКА Справочник.Бригады
	|	И СдельныйНарядОперации.Ссылка.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СдельныйНарядОперации.НомерСтроки КАК НомерСтроки,
	|	СдельныйНарядОперации.КлючСвязи КАК КлючСвязи,
	|	СдельныйНарядОперации.Исполнитель КАК Исполнитель,
	|	СдельныйНарядОперации.Период КАК Период,
	|	СдельныйНарядОперации.Этап КАК Этап,
	|	СдельныйНарядОперации.Ссылка.ВалютаДокумента КАК ВалютаУчета,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА СдельныйНарядОперации.Ссылка.ПоложениеСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|			ТОГДА СдельныйНарядОперации.СтруктурнаяЕдиница
	|		ИНАЧЕ СдельныйНарядОперации.Ссылка.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	СдельныйНарядОперации.Операция КАК Операция,
	|	СдельныйНарядОперации.Операция.СчетУчетаЗатрат КАК СчетУчетаОперация,
	|	СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат КАК СчетУчета,
	|	СдельныйНарядОперации.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СдельныйНарядОперации.Номенклатура КАК Номенклатура,
	|	СдельныйНарядОперации.Характеристика КАК Характеристика,
	|	СдельныйНарядОперации.Партия КАК Партия,
	|	СдельныйНарядОперации.Спецификация КАК Спецификация,
	|	СдельныйНарядОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СдельныйНарядОперации.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ЕСТЬNULL(СдельныйНарядОперации.ЗаказНаПроизводство.ЗапланированыОперации, ЛОЖЬ) КАК ЗапланированыОперации,
	|	ВЫБОР
	|		КОГДА СдельныйНарядОперации.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА СдельныйНарядОперации.ЗаказНаПроизводство
	|		КОГДА СдельныйНарядОперации.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА СдельныйНарядОперации.Ссылка.ДокументОснование
	|		КОГДА СдельныйНарядОперации.Ссылка.ДокументОснование ССЫЛКА Документ.СборкаЗапасов
	|				И СдельныйНарядОперации.Ссылка.ДокументОснование.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА СдельныйНарядОперации.Ссылка.ДокументОснование.ЗаказНаПроизводство
	|		КОГДА СдельныйНарядОперации.Ссылка.ДокументОснование ССЫЛКА Документ.СборкаЗапасов
	|				И СдельныйНарядОперации.Ссылка.ДокументОснование.ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА СдельныйНарядОперации.Ссылка.ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументПроизводства,
	|	СдельныйНарядОперации.Стоимость * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК Стоимость,
	|	СдельныйНарядОперации.Стоимость КАК СтоимостьВал,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	|	&НачислениеЗарплатыУНФ КАК СодержаниеПроводки,
	|	СдельныйНарядОперации.Нормочасы КАК Нормочасы,
	|	СдельныйНарядОперации.Расценка КАК Расценка,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	СдельныйНарядОперации.Ссылка.Закрыт КАК Закрыт,
	|	СдельныйНарядОперации.Ссылка.ДатаЗакрытия КАК ДатаЗакрытия,
	|	СдельныйНарядОперации.Ссылка.ПоложениеИсполнителя КАК ПоложениеИсполнителя,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СдельныйНарядОперации.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА СдельныйНарядОперации.КоличествоПлан
	|		ИНАЧЕ СдельныйНарядОперации.КоличествоПлан * СдельныйНарядОперации.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК КоличествоПлан,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СдельныйНарядОперации.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА СдельныйНарядОперации.КоличествоФакт
	|		ИНАЧЕ СдельныйНарядОперации.КоличествоФакт * СдельныйНарядОперации.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК КоличествоФакт,
	|	СдельныйНарядОперации.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
	|	СдельныйНарядОперации.Номенклатура.СчетУчетаЗапасов КАК СчетУчетаЗапасовНоменклатура
	|ПОМЕСТИТЬ ТаблицаОперации
	|ИЗ
	|	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО СдельныйНарядОперации.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	СдельныйНарядОперации.Ссылка = &Ссылка
	|	И (СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство)
	|			ИЛИ СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.КосвенныеЗатраты))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОперации.КлючСвязи КАК КлючСвязи,
	|	СУММА(ТаблицаОперации.Стоимость) КАК Стоимость,
	|	СУММА(ТаблицаОперации.СтоимостьВал) КАК СтоимостьВал,
	|	СУММА(ТаблицаОперации.КоличествоПлан) КАК КоличествоПлан,
	|	СУММА(ТаблицаОперации.КоличествоФакт) КАК КоличествоФакт
	|ПОМЕСТИТЬ ТаблицаОперацииСвернуто
	|ИЗ
	|	ТаблицаОперации КАК ТаблицаОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОперации.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	СдельныйНарядОперации.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НАЧАЛОПЕРИОДА(СдельныйНарядОперации.ДатаЗакрытия, МЕСЯЦ) КАК ПериодРегистрации,
	|	СдельныйНарядОперации.ВалютаУчета КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудники.СтруктурнаяЕдиница ЕСТЬ NULL
	|				ИЛИ ТаблицаСотрудники.СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|			ТОГДА СдельныйНарядОперации.СтруктурнаяЕдиница
	|		ИНАЧЕ ТаблицаСотрудники.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ТаблицаСотрудники.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудники.Бригада КАК Бригада,
	|	СдельныйНарядОперации.Период КАК ДатаНачала,
	|	СдельныйНарядОперации.Период КАК ДатаОкончания,
	|	0 КАК ОтработаноДней,
	|	СдельныйНарядОперации.Нормочасы КАК ОтработаноЧасов,
	|	СдельныйНарядОперации.Расценка КАК Размер,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплата) КАК ВидНачисленияУдержания,
	|	ТаблицаСотрудники.Сотрудник.СчетРасчетовСПерсоналом КАК СчетУчета,
	|	СдельныйНарядОперации.НомерСтроки КАК НомерСтроки,
	|	СдельныйНарядОперации.Этап КАК Этап,
	|	СдельныйНарядОперации.Операция КАК Операция,
	|	СдельныйНарядОперации.СчетУчетаОперация КАК СчетУчетаЗапасов,
	|	СдельныйНарядОперации.СчетУчета КАК КоррСчетУчетаЗапасов,
	|	СдельныйНарядОперации.Номенклатура КАК КоррНоменклатура,
	|	СдельныйНарядОперации.Характеристика КАК КоррХарактеристика,
	|	СдельныйНарядОперации.Партия КАК КоррПартия,
	|	СдельныйНарядОперации.Спецификация КАК КоррСпецификация,
	|	СдельныйНарядОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СдельныйНарядОперации.ДокументПроизводства КАК ДокументПроизводства,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит) КАК ВидДвиженияУправленческий,
	|	&НачислениеЗарплатыУНФ КАК СодержаниеПроводки,
	|	ВЫРАЗИТЬ(СдельныйНарядОперации.СтоимостьВал * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(СдельныйНарядОперации.Стоимость * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	СдельныйНарядОперации.Закрыт КАК Закрыт,
	|	СдельныйНарядОперации.ДатаЗакрытия КАК ДатаЗакрытия,
	|	ВЫРАЗИТЬ(СдельныйНарядОперации.КоличествоПлан * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 3)) КАК КоличествоПлан,
	|	ВЫРАЗИТЬ(СдельныйНарядОперации.КоличествоФакт * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 3)) КАК КоличествоФакт,
	|	ТаблицаСотрудники.Коэффициент КАК Коэффициент,
	|	СдельныйНарядОперации.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
	|	СдельныйНарядОперации.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	СдельныйНарядОперации.КлючСвязи КАК КлючСвязи,
	|	СдельныйНарядОперации.СчетУчетаЗапасовНоменклатура КАК СчетУчетаЗапасовНоменклатура,
	|	ТаблицаСотрудники.НомерСтрокиСоставБригады КАК НомерСтрокиСоставБригады,
	|	СдельныйНарядОперации.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаСотрудникиОперации
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОперации КАК СдельныйНарядОперации
	|		ПО (СдельныйНарядОперации.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВШапке)
	|				ИЛИ СдельныйНарядОперации.ПоложениеИсполнителя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|					И ТаблицаСотрудники.КлючСвязи = СдельныйНарядОперации.КлючСвязи)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудникиОперации.КлючСвязи КАК КлючСвязи,
	|	СУММА(ТаблицаСотрудникиОперации.СуммаВал) КАК СуммаВал,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК Сумма,
	|	СУММА(ТаблицаСотрудникиОперации.КоличествоПлан) КАК КоличествоПлан,
	|	СУММА(ТаблицаСотрудникиОперации.КоличествоФакт) КАК КоличествоФакт
	|ПОМЕСТИТЬ ТаблицаСотрудникиОперацииСвернуто
	|ИЗ
	|	ТаблицаСотрудникиОперации КАК ТаблицаСотрудникиОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиОперации.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
	|	ВложенныйЗапрос.ПериодРегистрации КАК ПериодРегистрации,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
	|	ВложенныйЗапрос.Бригада КАК Бригада,
	|	ВложенныйЗапрос.ДатаНачала КАК ДатаНачала,
	|	ВложенныйЗапрос.ДатаОкончания КАК ДатаОкончания,
	|	ВложенныйЗапрос.ОтработаноДней КАК ОтработаноДней,
	|	ВложенныйЗапрос.ОтработаноЧасов КАК ОтработаноЧасов,
	|	ВложенныйЗапрос.Размер КАК Размер,
	|	ВложенныйЗапрос.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	ВложенныйЗапрос.СчетУчета КАК СчетУчета,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.Этап КАК Этап,
	|	ВложенныйЗапрос.Операция КАК Операция,
	|	ВложенныйЗапрос.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|	ВложенныйЗапрос.КоррСчетУчетаЗапасов КАК КоррСчетУчетаЗапасов,
	|	ВложенныйЗапрос.КоррНоменклатура КАК КоррНоменклатура,
	|	ВложенныйЗапрос.КоррХарактеристика КАК КоррХарактеристика,
	|	ВложенныйЗапрос.КоррПартия КАК КоррПартия,
	|	ВложенныйЗапрос.КоррСпецификация КАК КоррСпецификация,
	|	ВложенныйЗапрос.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВложенныйЗапрос.ДокументПроизводства КАК ДокументПроизводства,
	|	ВложенныйЗапрос.ВидДвиженияУправленческий КАК ВидДвиженияУправленческий,
	|	ВложенныйЗапрос.СодержаниеПроводки КАК СодержаниеПроводки,
	|	СУММА(ВложенныйЗапрос.СуммаВал) КАК СуммаВал,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	ВложенныйЗапрос.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ВложенныйЗапрос.Закрыт КАК Закрыт,
	|	ВложенныйЗапрос.ДатаЗакрытия КАК ДатаЗакрытия,
	|	СУММА(ВложенныйЗапрос.КоличествоПлан) КАК КоличествоПлан,
	|	СУММА(ВложенныйЗапрос.КоличествоФакт) КАК КоличествоФакт,
	|	ВложенныйЗапрос.Коэффициент КАК Коэффициент,
	|	ВложенныйЗапрос.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
	|	ВложенныйЗапрос.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВложенныйЗапрос.КлючСвязи КАК КлючСвязи,
	|	ВложенныйЗапрос.СчетУчетаЗапасовНоменклатура КАК СчетУчетаЗапасовНоменклатура,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаСотрудникиОперацииСУчетомОкруглений
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСотрудникиОперации.Организация КАК Организация,
	|		ТаблицаСотрудникиОперации.Период КАК Период,
	|		ТаблицаСотрудникиОперации.ВидДвижения КАК ВидДвижения,
	|		ТаблицаСотрудникиОперации.ПериодРегистрации КАК ПериодРегистрации,
	|		ТаблицаСотрудникиОперации.Валюта КАК Валюта,
	|		ТаблицаСотрудникиОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ТаблицаСотрудникиОперации.Сотрудник КАК Сотрудник,
	|		ТаблицаСотрудникиОперации.Бригада КАК Бригада,
	|		ТаблицаСотрудникиОперации.ДатаНачала КАК ДатаНачала,
	|		ТаблицаСотрудникиОперации.ДатаОкончания КАК ДатаОкончания,
	|		ТаблицаСотрудникиОперации.ОтработаноДней КАК ОтработаноДней,
	|		ТаблицаСотрудникиОперации.ОтработаноЧасов КАК ОтработаноЧасов,
	|		ТаблицаСотрудникиОперации.Размер КАК Размер,
	|		ТаблицаСотрудникиОперации.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|		ТаблицаСотрудникиОперации.СчетУчета КАК СчетУчета,
	|		ТаблицаСотрудникиОперации.НомерСтроки КАК НомерСтроки,
	|		ТаблицаСотрудникиОперации.Этап КАК Этап,
	|		ТаблицаСотрудникиОперации.Операция КАК Операция,
	|		ТаблицаСотрудникиОперации.СчетУчетаЗапасов КАК СчетУчетаЗапасов,
	|		ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов КАК КоррСчетУчетаЗапасов,
	|		ТаблицаСотрудникиОперации.КоррНоменклатура КАК КоррНоменклатура,
	|		ТаблицаСотрудникиОперации.КоррХарактеристика КАК КоррХарактеристика,
	|		ТаблицаСотрудникиОперации.КоррПартия КАК КоррПартия,
	|		ТаблицаСотрудникиОперации.КоррСпецификация КАК КоррСпецификация,
	|		ТаблицаСотрудникиОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ТаблицаСотрудникиОперации.ДокументПроизводства КАК ДокументПроизводства,
	|		ТаблицаСотрудникиОперации.ВидДвиженияУправленческий КАК ВидДвиженияУправленческий,
	|		ТаблицаСотрудникиОперации.СодержаниеПроводки КАК СодержаниеПроводки,
	|		ТаблицаСотрудникиОперации.СуммаВал КАК СуммаВал,
	|		ТаблицаСотрудникиОперации.Сумма КАК Сумма,
	|		ТаблицаСотрудникиОперации.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|		ТаблицаСотрудникиОперации.Закрыт КАК Закрыт,
	|		ТаблицаСотрудникиОперации.ДатаЗакрытия КАК ДатаЗакрытия,
	|		ТаблицаСотрудникиОперации.КоличествоПлан КАК КоличествоПлан,
	|		ТаблицаСотрудникиОперации.КоличествоФакт КАК КоличествоФакт,
	|		ТаблицаСотрудникиОперации.Коэффициент КАК Коэффициент,
	|		ТаблицаСотрудникиОперации.ПодразделениеЗавершающегоЭтапа КАК ПодразделениеЗавершающегоЭтапа,
	|		ТаблицаСотрудникиОперации.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|		ТаблицаСотрудникиОперации.КлючСвязи КАК КлючСвязи,
	|		ТаблицаСотрудникиОперации.СчетУчетаЗапасовНоменклатура КАК СчетУчетаЗапасовНоменклатура,
	|		ТаблицаСотрудникиОперации.Номенклатура КАК Номенклатура
	|	ИЗ
	|		ТаблицаСотрудникиОперации КАК ТаблицаСотрудникиОперации
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСотрудникиОперации.Организация,
	|		ТаблицаСотрудникиОперации.Период,
	|		ТаблицаСотрудникиОперации.ВидДвижения,
	|		ТаблицаСотрудникиОперации.ПериодРегистрации,
	|		ТаблицаСотрудникиОперации.Валюта,
	|		ТаблицаСотрудникиОперации.СтруктурнаяЕдиница,
	|		ТаблицаСотрудникиОперации.Сотрудник,
	|		ТаблицаСотрудникиОперации.Бригада,
	|		ТаблицаСотрудникиОперации.ДатаНачала,
	|		ТаблицаСотрудникиОперации.ДатаОкончания,
	|		ТаблицаСотрудникиОперации.ОтработаноДней,
	|		ТаблицаСотрудникиОперации.ОтработаноЧасов,
	|		ТаблицаСотрудникиОперации.Размер,
	|		ТаблицаСотрудникиОперации.ВидНачисленияУдержания,
	|		ТаблицаСотрудникиОперации.СчетУчета,
	|		ТаблицаСотрудникиОперации.НомерСтроки,
	|		ТаблицаСотрудникиОперации.Этап,
	|		ТаблицаСотрудникиОперации.Операция,
	|		ТаблицаСотрудникиОперации.СчетУчетаЗапасов,
	|		ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов,
	|		ТаблицаСотрудникиОперации.КоррНоменклатура,
	|		ТаблицаСотрудникиОперации.КоррХарактеристика,
	|		ТаблицаСотрудникиОперации.КоррПартия,
	|		ТаблицаСотрудникиОперации.КоррСпецификация,
	|		ТаблицаСотрудникиОперации.ЗаказПокупателя,
	|		ТаблицаСотрудникиОперации.ДокументПроизводства,
	|		ТаблицаСотрудникиОперации.ВидДвиженияУправленческий,
	|		ТаблицаСотрудникиОперации.СодержаниеПроводки,
	|		ЕСТЬNULL(СуммаОбщая.СтоимостьВал - СуммаПоСотрудникам.СуммаВал, 0),
	|		ЕСТЬNULL(СуммаОбщая.Стоимость - СуммаПоСотрудникам.Сумма, 0),
	|		ТаблицаСотрудникиОперации.ФиксированнаяСтоимость,
	|		ТаблицаСотрудникиОперации.Закрыт,
	|		ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|		ЕСТЬNULL(СуммаОбщая.КоличествоПлан - СуммаПоСотрудникам.КоличествоПлан, 0),
	|		ЕСТЬNULL(СуммаОбщая.КоличествоФакт - СуммаПоСотрудникам.КоличествоФакт, 0),
	|		ТаблицаСотрудникиОперации.Коэффициент,
	|		ТаблицаСотрудникиОперации.ПодразделениеЗавершающегоЭтапа,
	|		ТаблицаСотрудникиОперации.ЗаказНаПроизводство,
	|		ТаблицаСотрудникиОперации.КлючСвязи,
	|		ТаблицаСотрудникиОперации.СчетУчетаЗапасовНоменклатура,
	|		ТаблицаСотрудникиОперации.Номенклатура
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаСотрудникиОперации.КлючСвязи КАК КлючСвязи,
	|			МАКСИМУМ(ТаблицаСотрудникиОперации.НомерСтрокиСоставБригады) КАК НомерСтрокиСоставБригады
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ТаблицаСотрудникиОперации.КлючСвязи КАК КлючСвязи,
	|				МАКСИМУМ(ТаблицаСотрудникиОперации.Коэффициент) КАК Коэффициент
	|			ИЗ
	|				ТаблицаСотрудникиОперации КАК ТаблицаСотрудникиОперации
	|			ГДЕ
	|				ТаблицаСотрудникиОперации.НомерСтрокиСоставБригады <> 0
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ТаблицаСотрудникиОперации.КлючСвязи) КАК МаксимальныйКоэффициент
	|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСотрудникиОперации КАК ТаблицаСотрудникиОперации
	|				ПО МаксимальныйКоэффициент.КлючСвязи = ТаблицаСотрудникиОперации.КлючСвязи
	|					И МаксимальныйКоэффициент.Коэффициент = ТаблицаСотрудникиОперации.Коэффициент
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаСотрудникиОперации.КлючСвязи) КАК СотрудникиКорректировки
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСотрудникиОперации КАК ТаблицаСотрудникиОперации
	|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОперацииСвернуто КАК СуммаОбщая
	|				ПО ТаблицаСотрудникиОперации.КлючСвязи = СуммаОбщая.КлючСвязи
	|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСотрудникиОперацииСвернуто КАК СуммаПоСотрудникам
	|				ПО ТаблицаСотрудникиОперации.КлючСвязи = СуммаПоСотрудникам.КлючСвязи
	|			ПО СотрудникиКорректировки.КлючСвязи = ТаблицаСотрудникиОперации.КлючСвязи
	|				И СотрудникиКорректировки.НомерСтрокиСоставБригады = ТаблицаСотрудникиОперации.НомерСтрокиСоставБригады
	|	ГДЕ
	|		ТаблицаСотрудникиОперации.КлючСвязи <> 0) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.ПериодРегистрации,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница,
	|	ВложенныйЗапрос.Сотрудник,
	|	ВложенныйЗапрос.Бригада,
	|	ВложенныйЗапрос.ДатаНачала,
	|	ВложенныйЗапрос.ДатаОкончания,
	|	ВложенныйЗапрос.ОтработаноДней,
	|	ВложенныйЗапрос.ОтработаноЧасов,
	|	ВложенныйЗапрос.Размер,
	|	ВложенныйЗапрос.ВидНачисленияУдержания,
	|	ВложенныйЗапрос.СчетУчета,
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.Этап,
	|	ВложенныйЗапрос.Операция,
	|	ВложенныйЗапрос.СчетУчетаЗапасов,
	|	ВложенныйЗапрос.КоррСчетУчетаЗапасов,
	|	ВложенныйЗапрос.КоррНоменклатура,
	|	ВложенныйЗапрос.КоррХарактеристика,
	|	ВложенныйЗапрос.КоррПартия,
	|	ВложенныйЗапрос.КоррСпецификация,
	|	ВложенныйЗапрос.ЗаказПокупателя,
	|	ВложенныйЗапрос.ДокументПроизводства,
	|	ВложенныйЗапрос.ВидДвиженияУправленческий,
	|	ВложенныйЗапрос.СодержаниеПроводки,
	|	ВложенныйЗапрос.ФиксированнаяСтоимость,
	|	ВложенныйЗапрос.Закрыт,
	|	ВложенныйЗапрос.ДатаЗакрытия,
	|	ВложенныйЗапрос.Коэффициент,
	|	ВложенныйЗапрос.ПодразделениеЗавершающегоЭтапа,
	|	ВложенныйЗапрос.ЗаказНаПроизводство,
	|	ВложенныйЗапрос.КлючСвязи,
	|	ВложенныйЗапрос.СчетУчетаЗапасовНоменклатура,
	|	ВложенныйЗапрос.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудникиОперации.Организация КАК Организация,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия КАК Период,
	|	ТаблицаСотрудникиОперации.ПериодРегистрации КАК ПериодРегистрации,
	|	ТаблицаСотрудникиОперации.Валюта КАК Валюта,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.Сотрудник КАК Сотрудник,
	|	ТаблицаСотрудникиОперации.Период КАК ДатаНачала,
	|	ТаблицаСотрудникиОперации.Период КАК ДатаОкончания,
	|	СУММА(ТаблицаСотрудникиОперации.ОтработаноДней) КАК ОтработаноДней,
	|	СУММА(ТаблицаСотрудникиОперации.ОтработаноЧасов) КАК ОтработаноЧасов,
	|	ТаблицаСотрудникиОперации.Размер КАК Размер,
	|	ТаблицаСотрудникиОперации.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	СУММА(ТаблицаСотрудникиОперации.СуммаВал) КАК СуммаВал,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК Сумма,
	|	ТаблицаСотрудникиОперации.Операция КАК Операция,
	|	ТаблицаСотрудникиОперации.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации
	|ГДЕ
	|	ТаблицаСотрудникиОперации.Закрыт
	|	И &ИспользоватьПодсистемуЗарплата
	|	И ТаблицаСотрудникиОперации.Сотрудник ССЫЛКА Справочник.Сотрудники
	|	И ТаблицаСотрудникиОперации.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|	ТаблицаСотрудникиОперации.Организация,
	|	ТаблицаСотрудникиОперации.Сотрудник,
	|	ТаблицаСотрудникиОперации.ВидНачисленияУдержания,
	|	ТаблицаСотрудникиОперации.ПериодРегистрации,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.Валюта,
	|	ТаблицаСотрудникиОперации.Размер,
	|	ТаблицаСотрудникиОперации.Период,
	|	ТаблицаСотрудникиОперации.Операция,
	|	ТаблицаСотрудникиОперации.Период,
	|	ТаблицаСотрудникиОперации.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудникиОперации.Организация КАК Организация,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия КАК Период,
	|	ТаблицаСотрудникиОперации.ВидДвижения КАК ВидДвижения,
	|	ТаблицаСотрудникиОперации.ПериодРегистрации КАК ПериодРегистрации,
	|	ТаблицаСотрудникиОперации.Валюта КАК Валюта,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.Сотрудник КАК Сотрудник,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК Сумма,
	|	СУММА(ТаблицаСотрудникиОперации.СуммаВал) КАК СуммаВал,
	|	ТаблицаСотрудникиОперации.СчетУчета КАК СчетУчета,
	|	ТаблицаСотрудникиОперации.ВидДвиженияУправленческий КАК ВидДвиженияУправленческий,
	|	ТаблицаСотрудникиОперации.СодержаниеПроводки КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации
	|ГДЕ
	|	ТаблицаСотрудникиОперации.Закрыт
	|	И &ИспользоватьПодсистемуЗарплата
	|	И ТаблицаСотрудникиОперации.Сотрудник ССЫЛКА Справочник.Сотрудники
	|	И ТаблицаСотрудникиОперации.Сотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиОперации.Организация,
	|	ТаблицаСотрудникиОперации.ПериодРегистрации,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|	ТаблицаСотрудникиОперации.Валюта,
	|	ТаблицаСотрудникиОперации.ВидДвижения,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.ВидДвиженияУправленческий,
	|	ТаблицаСотрудникиОперации.СчетУчета,
	|	ТаблицаСотрудникиОперации.СодержаниеПроводки,
	|	ТаблицаСотрудникиОперации.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудникиОперации.Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Бригада <> ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.Бригада
	|		ИНАЧЕ ТаблицаСотрудникиОперации.Сотрудник
	|	КОНЕЦ КАК Исполнитель,
	|	ТаблицаСотрудникиОперации.Этап КАК Этап,
	|	ТаблицаСотрудникиОперации.Операция КАК Операция,
	|	ТаблицаСотрудникиОперации.КоррНоменклатура КАК Номенклатура,
	|	ТаблицаСотрудникиОперации.КоррХарактеристика КАК Характеристика,
	|	ТаблицаСотрудникиОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаСотрудникиОперации.ДокументПроизводства КАК ДокументПроизводства,
	|	СУММА(ТаблицаСотрудникиОперации.ОтработаноЧасов * ТаблицаСотрудникиОперации.Коэффициент) КАК НормоЧасы,
	|	СУММА(ТаблицаСотрудникиОперации.КоличествоПлан) КАК КоличествоПлан,
	|	СУММА(ТаблицаСотрудникиОперации.КоличествоФакт) КАК КоличествоФакт,
	|	ТаблицаСотрудникиОперации.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиОперации.Период,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.КоррХарактеристика,
	|	ТаблицаСотрудникиОперации.ЗаказПокупателя,
	|	ТаблицаСотрудникиОперации.Операция,
	|	ТаблицаСотрудникиОперации.КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Бригада <> ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.Бригада
	|		ИНАЧЕ ТаблицаСотрудникиОперации.Сотрудник
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.ДокументПроизводства,
	|	ТаблицаСотрудникиОперации.Этап,
	|	ТаблицаСотрудникиОперации.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаСотрудникиОперации.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаСотрудникиОперации.ВидДвижения КАК ВидДвижения,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия КАК Период,
	|	ТаблицаСотрудникиОперации.Организация КАК Организация,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.Этап КАК Этап,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.СтруктурнаяЕдиница
	|		ИНАЧЕ ТаблицаСотрудникиОперации.ПодразделениеЗавершающегоЭтапа
	|	КОНЕЦ КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасов КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.НезавершенноеПроизводство)
	|	КОНЕЦ КАК КоррСчетУчета,
	|	ТаблицаСотрудникиОперации.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаСотрудникиОперации.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаСотрудникиОперации.КоррПартия КАК КоррПартия,
	|	ТаблицаСотрудникиОперации.КоррСпецификация КАК КоррСпецификация,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК Спецификация,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаСотрудникиОперации.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаСотрудникиОперации.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК КоррЗаказПокупателя,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК СуммаБезНДС,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК Сумма,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	ЛОЖЬ КАК ЗатратыНаВыпуск,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	|	ТаблицаСотрудникиОперации.СодержаниеПроводки КАК СодержаниеПроводки,
	|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка) КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаСотрудникиОперации.ЗаказНаПроизводство
	|	КОНЕЦ КАК КоррЗаказНаПроизводство,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасовНоменклатура КАК СчетУчетаЗапасовНоменклатура
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	ТаблицаСотрудникиОперации.Закрыт
	|	И &ИспользоватьПодсистемуЗарплата
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.НезавершенноеПроизводство)
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.КоррНоменклатура,
	|	ТаблицаСотрудникиОперации.КоррХарактеристика,
	|	ТаблицаСотрудникиОперации.КоррПартия,
	|	ТаблицаСотрудникиОперации.СодержаниеПроводки,
	|	ТаблицаСотрудникиОперации.Организация,
	|	ТаблицаСотрудникиОперации.СтруктурнаяЕдиница,
	|	ТаблицаСотрудникиОперации.Этап,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаСотрудникиОперации.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.КоррСпецификация,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|	ТаблицаСотрудникиОперации.ВидДвижения,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ТаблицаСотрудникиОперации.СтруктурнаяЕдиница
	|		ИНАЧЕ ТаблицаСотрудникиОперации.ПодразделениеЗавершающегоЭтапа
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаСотрудникиОперации.ЗаказНаПроизводство
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасовНоменклатура,
	|	ВЫБОР
	|		КОГДА ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаСотрудникиОперации.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаСотрудникиОперации.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасов КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаСотрудникиОперации.СчетУчетаЗапасов.Валютный
	|				ТОГДА ТаблицаСотрудникиОперации.СуммаВал
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВалДт,
	|	ТаблицаСотрудникиОперации.СчетУчета КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчета.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаСотрудникиОперации.СчетУчета.Валютный
	|				ТОГДА ТаблицаСотрудникиОперации.СуммаВал
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВалКт,
	|	СУММА(ТаблицаСотрудникиОперации.Сумма) КАК Сумма,
	|	&НачислениеЗарплатыУНФ КАК Содержание
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации
	|ГДЕ
	|	ТаблицаСотрудникиОперации.Сумма > 0
	|	И ТаблицаСотрудникиОперации.Закрыт
	|	И &ИспользоватьПодсистемуЗарплата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасов,
	|	ТаблицаСотрудникиОперации.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчета.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСотрудникиОперации.НомерСтроки,
	|	ТаблицаСотрудникиОперации.ДатаЗакрытия,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.КоррСчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.СчетУчетаЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаСотрудникиОперации.СчетУчетаЗапасов.Валютный
	|			ТОГДА ТаблицаСотрудникиОперации.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаСотрудникиОперации.Сумма,
	|	&РазноскаЗарплаты
	|ИЗ
	|	ТаблицаСотрудникиОперацииСУчетомОкруглений КАК ТаблицаСотрудникиОперации
	|ГДЕ
	|	ТаблицаСотрудникиОперации.Сумма > 0
	|	И ТаблицаСотрудникиОперации.Закрыт
	|	И &ИспользоватьПодсистемуЗарплата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК Склад,
	|	ТаблицаОперации.Период КАК Период,
	|	ТаблицаОперации.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ТаблицаОперации.Операция КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ТаблицаОперации.КоличествоФакт КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаЗаказыНаПроизводство
	|ИЗ
	|	ТаблицаОперации КАК ТаблицаОперации
	|ГДЕ
	|	ТаблицаОперации.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|	И ТаблицаОперации.Закрыт
	|	И ТаблицаОперации.ЗапланированыОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОперации.Период КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаОперации.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаОперации.ЗаказНаПроизводство
	|		ИНАЧЕ ТаблицаОперации.ЗаказПокупателя
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаОперации.Номенклатура КАК Номенклатура,
	|	ТаблицаОперации.Характеристика КАК Характеристика,
	|	ТаблицаОперации.Спецификация КАК Спецификация,
	|	ТаблицаОперации.Партия КАК Партия,
	|	ТаблицаОперации.Этап КАК Этап,
	|	ТаблицаОперации.КоличествоФакт КАК КоличествоФакт,
	|	ТаблицаОперации.ПодразделениеЗавершающегоЭтапа КАК СтруктурнаяЕдиница
	|ПОМЕСТИТЬ ЭтапыПредварительно
	|ИЗ
	|	ТаблицаОперации КАК ТаблицаОперации
	|ГДЕ
	|	&ИспользоватьЭтапыПроизводства
	|	И ТаблицаОперации.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|	И ТаблицаОперации.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|	И НЕ ТаблицаОперации.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|	И (ТаблицаОперации.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ИЛИ ТаблицаОперации.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|				И ТаблицаОперации.ЗаказНаПроизводство.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаОперации.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаОперации.ЗаказНаПроизводство
	|		ИНАЧЕ ТаблицаОперации.ЗаказПокупателя
	|	КОНЕЦ,
	|	ТаблицаОперации.Характеристика,
	|	ТаблицаОперации.Спецификация,
	|	ТаблицаОперации.Партия,
	|	ТаблицаОперации.ПодразделениеЗавершающегоЭтапа,
	|	ТаблицаОперации.Период,
	|	ТаблицаОперации.Этап,
	|	ТаблицаОперации.Номенклатура,
	|	ТаблицаОперации.КоличествоФакт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОперации.Период,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаОперации.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаОперации.ЗаказНаПроизводство
	|		ИНАЧЕ ТаблицаОперации.ЗаказПокупателя
	|	КОНЕЦ,
	|	ТаблицаОперации.Номенклатура,
	|	ТаблицаОперации.Характеристика,
	|	ТаблицаОперации.Спецификация,
	|	ТаблицаОперации.Партия,
	|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ЗавершениеПроизводства),
	|	ТаблицаОперации.КоличествоФакт,
	|	ТаблицаОперации.СтруктурнаяЕдиница
	|ИЗ
	|	ТаблицаОперации КАК ТаблицаОперации
	|ГДЕ
	|	&ИспользоватьЭтапыПроизводства
	|	И ТаблицаОперации.Спецификация <> ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|	И НЕ ТаблицаОперации.Спецификация.ВидПроизводства <> ЗНАЧЕНИЕ(Справочник.ВидыПроизводства.ПустаяСсылка)
	|	И (ТаблицаОперации.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ИЛИ ТаблицаОперации.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|				И ТаблицаОперации.ЗаказНаПроизводство.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказНаПроизводство.Разборка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОперации.Спецификация,
	|	ТаблицаОперации.Партия,
	|	ТаблицаОперации.Период,
	|	ТаблицаОперации.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаОперации.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаОперации.ЗаказНаПроизводство
	|		ИНАЧЕ ТаблицаОперации.ЗаказПокупателя
	|	КОНЕЦ,
	|	ТаблицаОперации.СтруктурнаяЕдиница,
	|	ТаблицаОперации.Номенклатура,
	|	ТаблицаОперации.КоличествоФакт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПроизводства.Заказ КАК Заказ,
	|	ЭтапыПроизводства.Номенклатура КАК Номенклатура,
	|	ЭтапыПроизводства.Характеристика КАК Характеристика,
	|	ЭтапыПроизводства.Спецификация КАК Спецификация,
	|	ЭтапыПроизводства.Партия КАК Партия,
	|	ЭтапыПроизводства.Этап КАК Этап,
	|	МАКСИМУМ(ЭтапыПроизводства.КоличествоПлан) КАК КоличествоПлан
	|ПОМЕСТИТЬ ЭтапыТекущиеДвижения
	|ИЗ
	|	РегистрНакопления.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	(ЭтапыПроизводства.Заказ, ЭтапыПроизводства.Номенклатура, ЭтапыПроизводства.Характеристика, ЭтапыПроизводства.Спецификация, ЭтапыПроизводства.Партия, ЭтапыПроизводства.Этап) В
	|			(ВЫБРАТЬ
	|				ЭтапыПредварительно.Заказ,
	|				ЭтапыПредварительно.Номенклатура,
	|				ЭтапыПредварительно.Характеристика,
	|				ЭтапыПредварительно.Спецификация,
	|				ЭтапыПредварительно.Партия,
	|				ЭтапыПредварительно.Этап
	|			ИЗ
	|				ЭтапыПредварительно)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыПроизводства.Заказ,
	|	ЭтапыПроизводства.Номенклатура,
	|	ЭтапыПроизводства.Характеристика,
	|	ЭтапыПроизводства.Спецификация,
	|	ЭтапыПроизводства.Партия,
	|	ЭтапыПроизводства.Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПредварительно.Период КАК Период,
	|	ЭтапыПредварительно.Организация КАК Организация,
	|	ЭтапыПредварительно.Заказ КАК Заказ,
	|	ЭтапыПредварительно.Номенклатура КАК Номенклатура,
	|	ЭтапыПредварительно.Характеристика КАК Характеристика,
	|	ЭтапыПредварительно.Спецификация КАК Спецификация,
	|	ЭтапыПредварительно.Партия КАК Партия,
	|	ЭтапыПредварительно.Этап КАК Этап,
	|	0 КАК КоличествоПлан,
	|	ЕСТЬNULL(ЭтапыТекущиеДвижения.КоличествоПлан, ЭтапыПредварительно.КоличествоФакт) КАК КоличествоФакт,
	|	ЭтапыПредварительно.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|ИЗ
	|	ЭтапыПредварительно КАК ЭтапыПредварительно
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭтапыТекущиеДвижения КАК ЭтапыТекущиеДвижения
	|		ПО ЭтапыПредварительно.Заказ = ЭтапыТекущиеДвижения.Заказ
	|			И ЭтапыПредварительно.Номенклатура = ЭтапыТекущиеДвижения.Номенклатура
	|			И ЭтапыПредварительно.Характеристика = ЭтапыТекущиеДвижения.Характеристика
	|			И ЭтапыПредварительно.Партия = ЭтапыТекущиеДвижения.Партия
	|			И ЭтапыПредварительно.Спецификация = ЭтапыТекущиеДвижения.Спецификация
	|			И ЭтапыПредварительно.Этап = ЭтапыТекущиеДвижения.Этап";	
	
КонецФункции

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаСдельныйНаряд, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если ПроведениеДокументовУНФ.КонтрольОстатковВыключен() Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временная таблица "ДвиженияЗаказыНаПроизводствоИзменение", 
	// содержит записи, необходимо выполнить контроль реализации товаров.
	
	Если СтруктураВременныеТаблицы.ДвиженияЗаказыНаПроизводствоИзменение
		Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗаказыНаПроизводствоИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыНаПроизводствоИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыНаПроизводствоИзменение.ЗаказНаПроизводство) КАК ЗаказНаПроизводствоПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыНаПроизводствоИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗаказыНаПроизводствоИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗаказыНаПроизводствоОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыНаПроизводствоИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыНаПроизводствоОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыНаПроизводство,
		|	ЕСТЬNULL(ЗаказыНаПроизводствоОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыНаПроизводство
		|ИЗ
		|	ДвиженияЗаказыНаПроизводствоИзменение КАК ДвиженияЗаказыНаПроизводствоИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыНаПроизводство.Остатки(
		|				&МоментКонтроля,
		|				(Организация, ЗаказНаПроизводство, Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗаказыНаПроизводствоИзменение.Организация КАК Организация,
		|						ДвиженияЗаказыНаПроизводствоИзменение.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|						ДвиженияЗаказыНаПроизводствоИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗаказыНаПроизводствоИзменение.Характеристика КАК Характеристика
		|					ИЗ
		|						ДвиженияЗаказыНаПроизводствоИзменение КАК ДвиженияЗаказыНаПроизводствоИзменение)) КАК ЗаказыНаПроизводствоОстатки
		|		ПО ДвиженияЗаказыНаПроизводствоИзменение.Организация = ЗаказыНаПроизводствоОстатки.Организация
		|			И ДвиженияЗаказыНаПроизводствоИзменение.ЗаказНаПроизводство = ЗаказыНаПроизводствоОстатки.ЗаказНаПроизводство
		|			И ДвиженияЗаказыНаПроизводствоИзменение.Номенклатура = ЗаказыНаПроизводствоОстатки.Номенклатура
		|			И ДвиженияЗаказыНаПроизводствоИзменение.Характеристика = ЗаказыНаПроизводствоОстатки.Характеристика
		|ГДЕ
		|	ЕСТЬNULL(ЗаказыНаПроизводствоОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			Тогда
			
			ДокументОбъектСдельныйНаряд = ДокументСсылкаСдельныйНаряд.ПолучитьОбъект()
			
		КонецЕсли;
		
		// Отрицательный остаток по заказам на производство.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			КонтрольОстатковУНФ.ЗаказыНаПроизводство(ДокументОбъектСдельныйНаряд, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#КонецОбласти 

#Область ИнтерфейсПечати

// Функция формирует печатную форму документа по указанному макету.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
//  ИмяМакета    - Строка, имя макета печатной формы.
//
Функция ПечатнаяФорма(ОписаниеПечатнойФормы, МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = ОписаниеПечатнойФормы.ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	
	Для каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос();
		
		Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(
			ТекущийДокумент.Организация));
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		
		Если ТипЗнч(ТекущийДокумент)=Тип("ДокументСсылка.СдельныйНаряд") Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СдельныйНаряд.Ссылка КАК Ссылка,
			|	СдельныйНаряд.ВерсияДанных КАК ВерсияДанных,
			|	СдельныйНаряд.ПометкаУдаления КАК ПометкаУдаления,
			|	СдельныйНаряд.Номер КАК Номер,
			|	СдельныйНаряд.Дата КАК ДатаДокумента,
			|	СдельныйНаряд.Организация.Префикс КАК Префикс,
			|	СдельныйНаряд.Проведен КАК Проведен,
			|	СдельныйНаряд.Организация КАК Организация,
			|	СдельныйНаряд.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
			|	СдельныйНаряд.Исполнитель КАК Исполнитель,
			|	СдельныйНаряд.Комментарий КАК Комментарий,
			|	СдельныйНаряд.ВалютаДокумента КАК ВалютаДокумента,
			|	СдельныйНаряд.СуммаДокумента КАК СуммаДокумента,
			|	СдельныйНаряд.Автор КАК Автор,
			|	СдельныйНаряд.Закрыт КАК Закрыт,
			|	СдельныйНаряд.ДатаЗакрытия КАК ДатаЗакрытия,
			|	СдельныйНаряд.ПоложениеИсполнителя КАК ПоложениеИсполнителя
			|ИЗ
			|	Документ.СдельныйНаряд КАК СдельныйНаряд
			|ГДЕ
			|	СдельныйНаряд.Ссылка = &ТекущийДокумент
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СдельныйНарядОперации.Ссылка КАК Ссылка,
			|	СдельныйНарядОперации.НомерСтроки КАК НомерСтроки,
			|	СдельныйНарядОперации.Период КАК День,
			|	СдельныйНарядОперации.ЗаказПокупателя КАК ЗаказПокупателя,
			|	СдельныйНарядОперации.Исполнитель КАК Исполнитель,
			|	СдельныйНарядОперации.Номенклатура КАК Номенклатура,
			|	СдельныйНарядОперации.Номенклатура.Код КАК Код,
			|	СдельныйНарядОперации.Номенклатура.Артикул КАК Артикул,
			|	СдельныйНарядОперации.Номенклатура.Штрихкод КАК Штрихкод,
			|	СдельныйНарядОперации.Характеристика.НаименованиеДляПечати КАК Характеристика,
			|	СдельныйНарядОперации.Операция КАК Операция,
			|	СдельныйНарядОперации.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	СдельныйНарядОперации.КоличествоПлан КАК КоличествоПлан,
			|	СдельныйНарядОперации.КоличествоФакт КАК КоличествоФакт,
			|	СдельныйНарядОперации.НормаВремени КАК НормаВремени,
			|	СдельныйНарядОперации.Расценка КАК Расценка,
			|	СдельныйНарядОперации.Нормочасы КАК Нормочасы,
			|	СдельныйНарядОперации.Стоимость КАК Стоимость,
			|	СдельныйНарядОперации.Партия КАК Партия,
			|	СдельныйНарядОперации.Спецификация КАК Спецификация
			|ИЗ
			|	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
			|ГДЕ
			|	СдельныйНарядОперации.Ссылка = &ТекущийДокумент
			|
			|УПОРЯДОЧИТЬ ПО
			|	СдельныйНарядОперации.Период
			|ИТОГИ
			|	СУММА(КоличествоПлан),
			|	СУММА(КоличествоФакт),
			|	СУММА(Нормочасы),
			|	СУММА(Стоимость)
			|ПО
			|	День";
			
		ИначеЕсли ТипЗнч(ТекущийДокумент)=Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаказНаПроизводство.Ссылка КАК Ссылка,
			|	ЗаказНаПроизводство.ВерсияДанных КАК ВерсияДанных,
			|	ЗаказНаПроизводство.ПометкаУдаления КАК ПометкаУдаления,
			|	ЗаказНаПроизводство.Номер КАК Номер,
			|	ЗаказНаПроизводство.Дата КАК ДатаДокумента,
			|	ЗаказНаПроизводство.Организация.Префикс КАК Префикс,
			|	ЗаказНаПроизводство.Проведен КАК Проведен,
			|	ЗаказНаПроизводство.Организация КАК Организация,
			|	ЗаказНаПроизводство.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
			|	ЗаказНаПроизводство.Исполнитель КАК Исполнитель,
			|	ЗаказНаПроизводство.Комментарий КАК Комментарий,
			|	ВалютаУчета.Значение КАК ВалютаДокумента,
			|	0 КАК СуммаДокумента,
			|	ЗаказНаПроизводство.Автор КАК Автор,
			|	ЛОЖЬ КАК Закрыт,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаЗакрытия,
			|	ЗаказНаПроизводство.ПоложениеИсполнителя КАК ПоложениеИсполнителя
			|ИЗ
			|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	Константа.ВалютаУчета КАК ВалютаУчета
			|ГДЕ
			|	ЗаказНаПроизводство.Ссылка = &ТекущийДокумент
			|	И ЗаказНаПроизводство.ЗапланированыОперации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗаказНаПроизводствоОперации.Ссылка КАК Ссылка,
			|	ЗаказНаПроизводствоОперации.НомерСтроки КАК НомерСтроки,
			|	ЗаказНаПроизводствоОперации.Ссылка.Старт КАК День,
			|	ЗаказНаПроизводствоОперации.ЗаказПокупателя КАК ЗаказПокупателя,
			|	ЗаказНаПроизводствоОперации.Исполнитель КАК Исполнитель,
			|	ЗаказНаПроизводствоПродукция.Номенклатура КАК Номенклатура,
			|	ЗаказНаПроизводствоПродукция.Номенклатура.Код КАК Код,
			|	ЗаказНаПроизводствоПродукция.Номенклатура.Артикул КАК Артикул,
			|	ЗаказНаПроизводствоПродукция.Номенклатура.Штрихкод КАК Штрихкод,
			|	ЗаказНаПроизводствоПродукция.Характеристика КАК Характеристика,
			|	ЗаказНаПроизводствоОперации.Операция КАК Операция,
			|	ЗаказНаПроизводствоОперации.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ЗаказНаПроизводствоОперации.КоличествоПлан КАК КоличествоПлан,
			|	0 КАК КоличествоФакт,
			|	ЗаказНаПроизводствоОперации.НормаВремени КАК НормаВремени,
			|	0 КАК Расценка,
			|	ЗаказНаПроизводствоОперации.Нормочасы КАК Нормочасы,
			|	0 КАК Стоимость,
			|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
			|	ЗаказНаПроизводствоПродукция.Спецификация КАК Спецификация
			|ИЗ
			|	Документ.ЗаказНаПроизводство.Операции КАК ЗаказНаПроизводствоОперации
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
			|		ПО ЗаказНаПроизводствоОперации.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка
			|			И ЗаказНаПроизводствоОперации.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
			|ГДЕ
			|	ЗаказНаПроизводствоОперации.Ссылка = &ТекущийДокумент
			|	И НЕ ЗаказНаПроизводствоПродукция.КлючСвязи ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|ИТОГИ
			|	СУММА(КоличествоПлан),
			|	СУММА(КоличествоФакт),
			|	СУММА(Нормочасы),
			|	СУММА(Стоимость)
			|ПО
			|	День";
			
		Иначе
			
			Продолжить;
			
		КонецЕсли; 
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		Шапка = РезультатЗапроса[0].Выбрать();
		Если НЕ Шапка.Следующий() Тогда
			Продолжить;
		КонецЕсли; 
		
		ВыборкаДни = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СдельныйНаряд.ПФ_MXL_СдельныйНаряд");
		
		ИмяОбластиОснова = ?(Шапка.ПоложениеИсполнителя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти,
			"ОсноваИсполнитель", "Основа");
		ОблШапка = Макет.ПолучитьОбласть("Шапка|" + ИмяОбластиОснова);
		ОблШапкаОкончание = Макет.ПолучитьОбласть("Шапка|Данные");
		ОблШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы|" + ИмяОбластиОснова);
		ОблШапкаТаблицыОкончание = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");
		ОблДень = Макет.ПолучитьОбласть("День|" + ИмяОбластиОснова);
		ОблДеньОкончание = Макет.ПолучитьОбласть("День|Данные");
		ОблДетали = Макет.ПолучитьОбласть("Детали|" + ИмяОбластиОснова);
		ОблДеталиОкончание = Макет.ПолучитьОбласть("Детали|Данные");
		ОблПодвал = Макет.ПолучитьОбласть("Подвал|" + ИмяОбластиОснова);
		ОблПодвалОкончание = Макет.ПолучитьОбласть("Подвал|Данные");
		
		ОблШапка.Параметры.Заполнить(Шапка);
		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(Шапка.ДатаДокумента,
			Шапка.Номер, Шапка.Префикс);
		ДатаДокумента = Формат(Шапка.ДатаДокумента, "ДЛФ=DD");
		Если ТипЗнч(ТекущийДокумент) = Тип("ДокументСсылка.ЗаказНаПроизводство") Тогда
			ШаблонЗаголовка = НСтр("ru = 'Сдельный наряд по заказу на производство № %1 от %2'");
		Иначе
			ШаблонЗаголовка = НСтр("ru = 'Сдельный наряд № %1 от %2'");
		КонецЕсли;
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ЗаголовокДокумента", СтрШаблон(ШаблонЗаголовка, НомерДокумента, ДатаДокумента));
		ОблШапка.Параметры.Заполнить(ДанныеПечати);
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОблШапка,
			ТекущийДокумент);
		
		ТабличныйДокумент.Вывести(ОблШапка);
		ОблШапкаОкончание.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Присоединить(ОблШапкаОкончание);
		
		ТабличныйДокумент.Вывести(ОблШапкаТаблицы);
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Стоимость", СтрШаблон(НСтр("ru = 'Стоимость
        	|(%1)'"), Шапка.ВалютаДокумента));
		ОблШапкаТаблицыОкончание.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Присоединить(ОблШапкаТаблицыОкончание);
		
		НПП = 0;
		
		Пока ВыборкаДни.Следующий() Цикл
			
			ОблДень.Параметры.Заполнить(ВыборкаДни);
			ТабличныйДокумент.Вывести(ОблДень);
			ОблДеньОкончание.Параметры.Заполнить(ВыборкаДни);
			ТабличныйДокумент.Присоединить(ОблДеньОкончание);
			
			ВыборкаОпераций = ВыборкаДни.Выбрать();
			Пока ВыборкаОпераций.Следующий() Цикл
				
				НПП = НПП + 1;
				
				ДанныеПечати = Новый Структура;
				ДанныеПечати.Вставить("НПП", НПП);
				ОблДетали.Параметры.Заполнить(ВыборкаОпераций);
				ОблДетали.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОблДетали);
				ОблДеталиОкончание.Параметры.Заполнить(ВыборкаОпераций);
				ТабличныйДокумент.Присоединить(ОблДеталиОкончание);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОблПодвал);
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("Стоимость", Шапка.СуммаДокумента);
		ОблПодвалОкончание.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Присоединить(ОблПодвалОкончание);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ОписаниеПечатнойФормы = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "СдельныйНаряд");
	Если ОписаниеПечатнойФормы <> Неопределено Тогда
		
		ОписаниеПечатнойФормы.ТабличныйДокумент = Новый ТабличныйДокумент;
		ОписаниеПечатнойФормы.ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СдельныйНаряд";
		ОписаниеПечатнойФормы.ПолныйПутьКМакету = "Документ.СдельныйНаряд.ПФ_MXL_СдельныйНаряд";
		ОписаниеПечатнойФормы.СинонимМакета = НСтр("ru ='Сдельный наряд'");
		
		ПечатнаяФорма(ОписаниеПечатнойФормы, МассивОбъектов, ОбъектыПечати);
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	ЭлектроннаяПочтаУНФ.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов,
		КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СдельныйНаряд";
	// Длинное название печатной формы не помещается на форме документа
	КомандаПечати.Представление = НСтр("ru = 'Сдельный наряд'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;

КонецПроцедуры

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//   @skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти 

#КонецЕсли