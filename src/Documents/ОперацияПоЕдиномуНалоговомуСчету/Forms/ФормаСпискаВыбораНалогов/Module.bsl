
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	Период      = Параметры.Период;
	ВидОперации = Параметры.ВидОперации;
	
	ЗаполнитьТаблицуНалогов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отметить(Команда)
	
	ОбработатьОтметку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	
	ОбработатьОтметку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	СписокЗаполнения = Новый СписокЗначений;
	
	Для Каждого СтрокаТаблицы Из ВидыНалоговИПлатежейВБюджет Цикл
		Если СтрокаТаблицы.Отметка Тогда
			СписокЗаполнения.Добавить(СтрокаТаблицы.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(СписокЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуНалогов()
	
	НалогиИсключения = ЕдиныйНалоговыйСчетПовтИсп.МассивДоступныхВидовНалогов();
	//ОбслуживаемыеСчетаУчета = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчета(Период);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НалогиИсключения", НалогиИсключения);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Ссылка,
		|	ВидыНалоговИПлатежейВБюджет.КодБК КАК КодБК,
		|	ВидыНалоговИПлатежейВБюджет.Наименование КАК Наименование,
		|	ВидыНалоговИПлатежейВБюджет.СчетУчета КАК СчетУчета
		|ИЗ
		|	Справочник.ВидыНалогов КАК ВидыНалоговИПлатежейВБюджет
		|ГДЕ
		|	НЕ ВидыНалоговИПлатежейВБюджет.ВидНалога В (&НалогиИсключения)
		|	И ВидыНалоговИПлатежейВБюджет.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
		|	И НЕ ВидыНалоговИПлатежейВБюджет.ПометкаУдаления";
	
	Результат = Запрос.Выполнить().Выгрузить();
	ВидыНалоговИПлатежейВБюджет.Загрузить(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтметку(Отметка)
	
	Для Каждого СтрокаТаблицы Из ВидыНалоговИПлатежейВБюджет Цикл
		СтрокаТаблицы.Отметка = Отметка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

