#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыЗаполненияДокумента

// Обработчик заполнения на основании документа ИнвентаризацияЗапасов.
//
// Параметры:
//  ДокументСсылкаИнвентаризацияЗапасов	 - ДокументСсылка.ИнвентаризацияЗапасов.
//
Процедура ЗаполнитьПоИнвентаризацииЗапасов(ДокументСсылкаИнвентаризацияЗапасов) Экспорт
	
	Если Не ЗначениеЗаполнено(ДокументСсылкаИнвентаризацияЗапасов) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОснование = ДокументСсылкаИнвентаризацияЗапасов;
	Организация = ДокументСсылкаИнвентаризацияЗапасов.Организация;
	СтруктурнаяЕдиница = ДокументСсылкаИнвентаризацияЗапасов.СтруктурнаяЕдиница;
	Ячейка = ДокументСсылкаИнвентаризацияЗапасов.Ячейка;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ИнвентаризацияЗапасов.НомерСтроки) КАК НомерСтроки,
	|	ИнвентаризацияЗапасов.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияЗапасов.Характеристика КАК Характеристика,
	|	ИнвентаризацияЗапасов.Партия КАК Партия,
	|	ИнвентаризацияЗапасов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МАКСИМУМ(ИнвентаризацияЗапасов.КоличествоУчет - ИнвентаризацияЗапасов.Количество) КАК КоличествоОтклонение,
	|	СУММА(ВЫБОР
	|			КОГДА СписаниеЗапасов.Количество ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ СписаниеЗапасов.Количество
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ПересортицаЗапасовСписание.Количество ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ПересортицаЗапасовСписание.Количество
	|		КОНЕЦ) КАК КоличествоСписанное
	|ИЗ
	|	Документ.ИнвентаризацияЗапасов.Запасы КАК ИнвентаризацияЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СписаниеЗапасовЗапасы.Номенклатура КАК Номенклатура,
	|			СписаниеЗапасовЗапасы.Характеристика КАК Характеристика,
	|			СписаниеЗапасовЗапасы.Партия КАК Партия,
	|			СУММА(СписаниеЗапасовЗапасы.Количество) КАК Количество
	|		ИЗ
	|			Документ.СписаниеЗапасов.Запасы КАК СписаниеЗапасовЗапасы
	|		ГДЕ
	|			СписаниеЗапасовЗапасы.Ссылка.ДокументОснование = &ДокументОснование
	|			И СписаниеЗапасовЗапасы.Ссылка.Проведен
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписаниеЗапасовЗапасы.Номенклатура,
	|			СписаниеЗапасовЗапасы.Характеристика,
	|			СписаниеЗапасовЗапасы.Партия) КАК СписаниеЗапасов
	|		ПО ИнвентаризацияЗапасов.Номенклатура = СписаниеЗапасов.Номенклатура
	|			И ИнвентаризацияЗапасов.Характеристика = СписаниеЗапасов.Характеристика
	|			И ИнвентаризацияЗапасов.Партия = СписаниеЗапасов.Партия
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПересортицаЗапасовЗапасы.Номенклатура КАК Номенклатура,
	|			ПересортицаЗапасовЗапасы.Характеристика КАК Характеристика,
	|			ПересортицаЗапасовЗапасы.Партия КАК Партия,
	|			СУММА(ПересортицаЗапасовЗапасы.Количество) КАК Количество
	|		ИЗ
	|			Документ.ПересортицаЗапасов.Запасы КАК ПересортицаЗапасовЗапасы
	|		ГДЕ
	|			ПересортицаЗапасовЗапасы.Ссылка.ДокументОснование = &ДокументОснование
	|			И ПересортицаЗапасовЗапасы.Ссылка.Проведен
	|			И ПересортицаЗапасовЗапасы.Ссылка <> &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПересортицаЗапасовЗапасы.Номенклатура,
	|			ПересортицаЗапасовЗапасы.Характеристика,
	|			ПересортицаЗапасовЗапасы.Партия) КАК ПересортицаЗапасовСписание
	|		ПО ИнвентаризацияЗапасов.Номенклатура = ПересортицаЗапасовСписание.Номенклатура
	|			И ИнвентаризацияЗапасов.Характеристика = ПересортицаЗапасовСписание.Характеристика
	|			И ИнвентаризацияЗапасов.Партия = ПересортицаЗапасовСписание.Партия
	|ГДЕ
	|	ИнвентаризацияЗапасов.Ссылка = &ДокументОснование
	|	И ИнвентаризацияЗапасов.КоличествоУчет - ИнвентаризацияЗапасов.Количество > 0
	|	И ВЫБОР
	|			КОГДА ИнвентаризацияЗапасов.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ИнвентаризацияЗапасов.Партия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.СобственныеЗапасы)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияЗапасов.Номенклатура,
	|	ИнвентаризацияЗапасов.Характеристика,
	|	ИнвентаризацияЗапасов.Партия,
	|	ИнвентаризацияЗапасов.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаИнвентаризацияЗапасов);
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение НСтр("ru = 'Нет данных для оформления списания!'");
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Заполнение табличной части документа.
	Запасы.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		КоличествоСписать = Выборка.КоличествоОтклонение - Выборка.КоличествоСписанное;
		
		Если КоличествоСписать <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабличнойЧасти = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
		
		СтрокаТабличнойЧасти.Количество			= КоличествоСписать;
		
	КонецЦикла;
	
	Если Запасы.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Нет данных для оформления списания!'");
	КонецЕсли;
	
КонецПроцедуры

// Заполнение документа на основании инвентаризации ВЕТИС.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения) Экспорт
	
	Реквизиты = ИнтеграцияВЕТИСУНФ.ДанныеПрикладныхДокументовИзИнвентаризацииПродукцииВЕТИС(ДанныеЗаполнения);
	Реквизиты.Следующий();
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
		
	КонецЕсли;
	
	Товары = Реквизиты.Товары.Выгрузить();
	
	Запасы.Очистить();
	Для Каждого СтрокаТовары Из Товары Цикл
		
		Если СтрокаТовары.Количество >= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТовары);
		НоваяСтрокаЗапасы.Количество = -СтрокаТовары.Количество;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик заполнения на основании документа Расходный ордер.
//
// Параметры:
//  ДокументСсылкаРасходныйОрдер	 - ДокументСсылка.РасходныйОрдер
//
Процедура ЗаполнитьПоРасходномуОрдеру(ДокументСсылкаРасходныйОрдер) Экспорт
	
	Организация = ДокументСсылкаРасходныйОрдер.Организация;
	СтруктурнаяЕдиница = ДокументСсылкаРасходныйОрдер.СтруктурнаяЕдиница;
	
	Если ДокументСсылкаРасходныйОрдер.ПоложениеЯчейки = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		Ячейка = ДокументСсылкаРасходныйОрдер.Ячейка;
	КонецЕсли;
	
	ДокументОснование = ДокументСсылкаРасходныйОрдер;
	
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	СведенияПрослеживаемости.Очистить();
	
	ВидУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ВидУчетаОрдерныхСкладов");
	
	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.ПоСкладуВЦелом Тогда
		ЗаполнитьПоСкладскомуОрдеруБезУчетаОстатков(ДокументСсылкаРасходныйОрдер);
		Возврат;
	КонецЕсли;
	
	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоПрочимДокументам Тогда
		ТекстИсключения = НСтр("ru = 'Вид учета остатков на ордерном складе - по Оприходованию запасов. Ввод Оприходования запасов на основании Расходного ордера недоступен.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	УчетОстатковПоСкладскимОрдерам = СкладскойУчетСервер.УчетОстатковВРазрезеСкладскихОрдеров(СтруктурнаяЕдиница, Ложь);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РасходныйОрдерЗапасы.НомерСтроки КАК НомерСтроки,
	|	РасходныйОрдерЗапасы.Номенклатура КАК Номенклатура,
	|	РасходныйОрдерЗапасы.Характеристика КАК Характеристика,
	|	РасходныйОрдерЗапасы.Партия КАК Партия,
	|	РасходныйОрдерЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасходныйОрдерЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	РасходныйОрдерЗапасы.КлючСвязи КАК КлючСвязи,
	|	РасходныйОрдерЗапасы.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &КонтролироватьОстаткиПоДокументам
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументПоступления,
	|	РасходныйОрдерЗапасы.Ячейка КАК Ячейка,
	|	&СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|ИЗ
	|	Документ.РасходныйОрдер.Запасы КАК РасходныйОрдерЗапасы
	|ГДЕ
	|	РасходныйОрдерЗапасы.Ссылка = &ДокументОснование
	|	И РасходныйОрдерЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыКРасходуСоСкладовОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыКРасходуСоСкладовОстатки.Характеристика КАК Характеристика,
	|	ЗапасыКРасходуСоСкладовОстатки.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток < 0
	|			ТОГДА -ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
	|		ИНАЧЕ ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &КонтролироватьОстаткиПоДокументам
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументПоступления,
	|	&СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыКРасходуСоСкладовОстатки.Ячейка КАК Ячейка
	|ИЗ
	|	РегистрНакопления.ЗапасыКРасходуСоСкладов.Остатки(
	|			,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И ДокументОснование = &ДокументОснование) КАК ЗапасыКРасходуСоСкладовОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапасыКРасходуСоСкладов.Номенклатура,
	|	ЗапасыКРасходуСоСкладов.Характеристика,
	|	ЗапасыКРасходуСоСкладов.Партия,
	|	ВЫБОР
	|		КОГДА ЗапасыКРасходуСоСкладов.Количество < 0
	|			ТОГДА -ЗапасыКРасходуСоСкладов.Количество
	|		ИНАЧЕ ЗапасыКРасходуСоСкладов.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &КонтролироватьОстаткиПоДокументам
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	&СтруктурнаяЕдиница,
	|	ЗапасыКРасходуСоСкладов.Ячейка
	|ИЗ
	|	РегистрНакопления.ЗапасыКРасходуСоСкладов КАК ЗапасыКРасходуСоСкладов
	|ГДЕ
	|	ЗапасыКРасходуСоСкладов.Регистратор = &Ссылка
	|	И ЗапасыКРасходуСоСкладов.ДокументОснование = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасходныйОрдерЗапасы.Номенклатура КАК Номенклатура,
	|	РасходныйОрдерЗапасы.Характеристика КАК Характеристика,
	|	РасходныйОрдерЗапасы.Партия КАК Партия,
	|	РасходныйОрдерСерииНоменклатуры.Серия КАК Серия,
	|	РасходныйОрдерЗапасы.КлючСвязи КАК КлючСвязи,
	|	РасходныйОрдерСерииНоменклатуры.Количество КАК Количество
	|ИЗ
	|	Документ.РасходныйОрдер.СерииНоменклатуры КАК РасходныйОрдерСерииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдер.Запасы КАК РасходныйОрдерЗапасы
	|		ПО (РасходныйОрдерСерииНоменклатуры.Ссылка = &ДокументОснование)
	|			И (РасходныйОрдерЗапасы.Ссылка = &ДокументОснование)
	|			И (РасходныйОрдерЗапасы.КлючСвязи = РасходныйОрдерСерииНоменклатуры.КлючСвязи)
	|ГДЕ
	|	РасходныйОрдерЗапасы.Ссылка = &ДокументОснование
	|	И РасходныйОрдерСерииНоменклатуры.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииНоменклатурыКРасходуОстатки.Серия КАК Серия,
	|	-СерииНоменклатурыКРасходуОстатки.КоличествоОстаток КАК Количество,
	|	СерииНоменклатурыКРасходуОстатки.Номенклатура КАК Номенклатура,
	|	СерииНоменклатурыКРасходуОстатки.Характеристика КАК Характеристика,
	|	СерииНоменклатурыКРасходуОстатки.Партия КАК Партия
	|ПОМЕСТИТЬ ОстаткиСерииИтог
	|ИЗ
	|	РегистрНакопления.СерииНоменклатурыКРасходу.Остатки(
	|			,
	|			Организация = &Организация
	|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				И ДокументОснование = &ДокументОснование) КАК СерииНоменклатурыКРасходуОстатки
	|ГДЕ
	|	СерииНоменклатурыКРасходуОстатки.КоличествоОстаток < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СерииНоменклатурыКРасходу.Серия,
	|	СерииНоменклатурыКРасходу.Количество,
	|	СерииНоменклатурыКРасходу.Номенклатура,
	|	СерииНоменклатурыКРасходу.Характеристика,
	|	СерииНоменклатурыКРасходу.Партия
	|ИЗ
	|	РегистрНакопления.СерииНоменклатурыКРасходу КАК СерииНоменклатурыКРасходу
	|ГДЕ
	|	СерииНоменклатурыКРасходу.Регистратор = &Ссылка
	|	И СерииНоменклатурыКРасходу.ДокументОснование = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСерииИтог.Серия КАК Серия,
	|	СУММА(ОстаткиСерииИтог.Количество) КАК Количество,
	|	ОстаткиСерииИтог.Номенклатура КАК Номенклатура,
	|	ОстаткиСерииИтог.Характеристика КАК Характеристика,
	|	ОстаткиСерииИтог.Партия КАК Партия
	|ИЗ
	|	ОстаткиСерииИтог КАК ОстаткиСерииИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСерииИтог.Серия,
	|	ОстаткиСерииИтог.Характеристика,
	|	ОстаткиСерииИтог.Партия,
	|	ОстаткиСерииИтог.Номенклатура");
	
	Компания = Константы.УчетПоКомпании.Компания(Организация);
	
	КонтролироватьОстаткиПоДокументам = ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоСкладскимОрдерам;
	
	ДокументПоступления = ?(КонтролироватьОстаткиПоДокументам, ДокументСсылкаРасходныйОрдер, Неопределено);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументПоступления);
	
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.УстановитьПараметр("КонтролироватьОстаткиПоДокументам", Истина);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатыЗапроса[1].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСерииНоменклатуры = РезультатыЗапроса[2].Выгрузить();
	ТаблицаОстаткиСерииНоменклатуры = РезультатыЗапроса[4].Выгрузить();
	ТаблицаОстаткиЗапасы = РезультатыЗапроса[1].Выгрузить();
	
	ВыборкаЗапасы = РезультатыЗапроса[0].Выбрать();
	
	ПараметрыПоискаСерии = Новый Структура("КлючСвязи");
	
	Если СкладскойУчетСервер.КонтролироватьОстаткиПоЯчейкамКПоступлениюОтгрзуке(СтруктурнаяЕдиница) Тогда
		ПараметрыПоискаОстаткиЗапасы = Новый Структура("Номенклатура, Характеристика, Партия, Ячейка");
	Иначе
		ПараметрыПоискаОстаткиЗапасы = Новый Структура("Номенклатура, Характеристика, Партия");
	КонецЕсли;

	ПараметрыПоискаОстаткиСерииНоменклатуры = Новый Структура("Номенклатура, Характеристика, Партия, Серия");
	
	Пока ВыборкаЗапасы.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаЗапасы.Количество) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиЗапасы, ВыборкаЗапасы);
		НайденныеОстаткиЗапасы = ТаблицаОстаткиЗапасы.НайтиСтроки(ПараметрыПоискаОстаткиЗапасы);
		
		Если Не НайденныеОстаткиЗапасы.Количество() Или (НайденныеОстаткиЗапасы.Количество() И НайденныеОстаткиЗапасы[0].Количество = 0) Тогда
			Продолжить
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыПоискаСерии, ВыборкаЗапасы);
		НайденныеСерии = ТаблицаСерииНоменклатуры.НайтиСтроки(ПараметрыПоискаСерии);
		
		КоличествоСерий = 0;
		ИспользуютсяСерииВСтроке = Ложь;
		Для Каждого СтрокаСерии Из НайденныеСерии Цикл
			
			ИспользуютсяСерииВСтроке = Истина;
			
			ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиСерииНоменклатуры, СтрокаСерии);
			НайденныеОстаткиСерий = ТаблицаОстаткиСерииНоменклатуры.НайтиСтроки(ПараметрыПоискаОстаткиСерииНоменклатуры);
			
			Если Не НайденныеОстаткиСерий.Количество() Или (НайденныеОстаткиСерий.Количество() И НайденныеОстаткиСерий[0].Количество = 0) Тогда
				Продолжить
			КонецЕсли;
			
			ОстатокСерий = НайденныеОстаткиСерий[0];
			
			Если Не ОстатокСерий.Количество > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаСерии = СерииНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, СтрокаСерии); 
			
			Если НоваяСтрокаСерии.Количество >= ОстатокСерий.Количество Тогда
				НоваяСтрокаСерии.Количество = ОстатокСерий.Количество;
			КонецЕсли;
			
			ОстатокСерий.Количество = ОстатокСерий.Количество - НоваяСтрокаСерии.Количество;
			
			КоличествоСерий = КоличествоСерий + НоваяСтрокаСерии.Количество;
			
		КонецЦикла;
		
		Если ИспользуютсяСерииВСтроке И КоличествоСерий = 0 Тогда
			Продолжить
		КонецЕсли;
		
		ОстатокЗапасов = НайденныеОстаткиЗапасы[0];
		
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ВыборкаЗапасы);
		
		Если ТипЗнч(НоваяСтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
			
			КоличествоВСтрокеЗапасов = ?(КоличествоСерий > 0, КоличествоСерий, НоваяСтрокаЗапасы.Количество);
			
			Если КоличествоВСтрокеЗапасов >= ОстатокЗапасов.Количество Тогда
				КоличествоВСтрокеЗапасов = ОстатокЗапасов.Количество;
				ОстатокЗапасов.Количество = 0;
			Иначе
				ОстатокЗапасов.Количество = ОстатокЗапасов.Количество - КоличествоВСтрокеЗапасов;
			КонецЕсли;
			
			НоваяСтрокаЗапасы.Количество = КоличествоВСтрокеЗапасов;
			
		Иначе
			
			КоличествоСтрокиВЕдиницахХранения = НоваяСтрокаЗапасы.Количество * НоваяСтрокаЗапасы.ЕдиницаИзмерения.Коэффициент;
			КоличествоВСтрокеЗапасов = ?(КоличествоСерий > 0, КоличествоСерий, КоличествоСтрокиВЕдиницахХранения);
			
			Если КоличествоВСтрокеЗапасов >= ОстатокЗапасов.Количество Тогда
				КоличествоВСтрокеЗапасов = ОстатокЗапасов.Количество;
				ОстатокЗапасов.Количество = 0;
			Иначе
				ОстатокЗапасов.Количество = ОстатокЗапасов.Количество - КоличествоВСтрокеЗапасов;
			КонецЕсли;
			
			НоваяСтрокаЗапасы.Количество = КоличествоВСтрокеЗапасов/ НоваяСтрокаЗапасы.ЕдиницаИзмерения.Коэффициент;;
			
		КонецЕсли;
		
		Если СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки() = Истина Тогда
			НоваяСтрокаЗапасы.СерииНоменклатуры = СерииНоменклатурыУНФ.ПредставлениеСерийНоменклатуры(СерииНоменклатуры, НоваяСтрокаЗапасы.КлючСвязи)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик заполнения на основании складского ордера без учета остатков.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ПриходныйОрдер Или ДокументСсылка.РасходныйОрдер.
//	Приходный - Признак вида ордера.
//
Процедура ЗаполнитьПоСкладскомуОрдеруБезУчетаОстатков(ДанныеЗаполнения) Экспорт
	
	Для Каждого СтрокаЗапасы Из ДанныеЗаполнения.Запасы Цикл
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасы);
	КонецЦикла;
	
	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

//+mega
// Обработчик заполнения на основании документа Приходная накладная.
//
// Параметры:
//  ДокументСсылкаПриходнаяНакладная	 - ДокументСсылка.ПриходнаяНакладная
//
Процедура ЗаполнитьПоПриходнойНакладной(ДокументСсылкаПриходнаяНакладная) Экспорт
	
	Организация = ДокументСсылкаПриходнаяНакладная.Организация;
	СтруктурнаяЕдиница = ДокументСсылкаПриходнаяНакладная.СтруктурнаяЕдиница;
	
	Ячейка = ДокументСсылкаПриходнаяНакладная.Ячейка;
	
	
	//ДокументОснование = ДокументСсылкаПриходнаяНакладная;
	
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	СведенияПрослеживаемости.Очистить();
	
	ВидУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ВидУчетаОрдерныхСкладов");
	
	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.ПоСкладуВЦелом Тогда
		ЗаполнитьПоСкладскомуОрдеруБезУчетаОстатков(ДокументСсылкаПриходнаяНакладная);
		Возврат;
	КонецЕсли;
		
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПриходнаяНакладнаяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяЗапасы.Партия КАК Партия,
	|	ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПриходнаяНакладнаяЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ПриходнаяНакладнаяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ПриходнаяНакладнаяЗапасы.Количество КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
	|	ПриходнаяНакладнаяЗапасы.Ячейка КАК Ячейка,
	|	&СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|ИЗ
	|	Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &ДокументОснование
	|	И ПриходнаяНакладнаяЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	ПриходнаяНакладнаяЗапасы.Партия КАК Партия,
	|	ПриходнаяНакладнаяЗапасы.СерииНоменклатуры КАК Серия,
	|	ПриходнаяНакладнаяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ПриходнаяНакладнаяСерииНоменклатуры.Количество КАК Количество
	|ИЗ
	|	Документ.ПриходнаяНакладная.СерииНоменклатуры КАК ПриходнаяНакладнаяСерииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|		ПО (ПриходнаяНакладнаяСерииНоменклатуры.Ссылка = &ДокументОснование)
	|			И (ПриходнаяНакладнаяЗапасы.Ссылка = &ДокументОснование)
	|			И (ПриходнаяНакладнаяЗапасы.КлючСвязи = ПриходнаяНакладнаяСерииНоменклатуры.КлючСвязи)
	|ГДЕ
	|	ПриходнаяНакладнаяЗапасы.Ссылка = &ДокументОснование
	|	И ПриходнаяНакладнаяСерииНоменклатуры.Ссылка = &ДокументОснование");
	
	Компания = Константы.УчетПоКомпании.Компания(Организация);
		
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаПриходнаяНакладная);
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаЗапасы = РезультатыЗапроса[0].Выбрать();
	ТаблицаСерииНоменклатуры = РезультатыЗапроса[1].Выгрузить();		
	
	ПараметрыПоискаСерии = Новый Структура("КлючСвязи");	
	Если СкладскойУчетСервер.КонтролироватьОстаткиПоЯчейкамКПоступлениюОтгрзуке(СтруктурнаяЕдиница) Тогда
		ПараметрыПоискаОстаткиЗапасы = Новый Структура("Номенклатура, Характеристика, Партия, Ячейка");
	Иначе
		ПараметрыПоискаОстаткиЗапасы = Новый Структура("Номенклатура, Характеристика, Партия");
	КонецЕсли;

	ПараметрыПоискаОстаткиСерииНоменклатуры = Новый Структура("Номенклатура, Характеристика, Партия, Серия");
	
	Пока ВыборкаЗапасы.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаЗапасы.Количество) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиЗапасы, ВыборкаЗапасы);
				
		ЗаполнитьЗначенияСвойств(ПараметрыПоискаСерии, ВыборкаЗапасы);
		НайденныеСерии = ТаблицаСерииНоменклатуры.НайтиСтроки(ПараметрыПоискаСерии);
		
		КоличествоСерий = 0;
		ИспользуютсяСерииВСтроке = Ложь;
		Для Каждого СтрокаСерии Из НайденныеСерии Цикл
			
			ИспользуютсяСерииВСтроке = Истина;
			
			ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиСерииНоменклатуры, СтрокаСерии);
			
			
			НоваяСтрокаСерии = СерииНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, СтрокаСерии); 
						
			КоличествоСерий = КоличествоСерий + НоваяСтрокаСерии.Количество;
			
		КонецЦикла;
		
		Если ИспользуютсяСерииВСтроке И КоличествоСерий = 0 Тогда
			Продолжить
		КонецЕсли;
				
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ВыборкаЗапасы);
		
		Если СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки() = Истина Тогда
			НоваяСтрокаЗапасы.СерииНоменклатуры = СерииНоменклатурыУНФ.ПредставлениеСерийНоменклатуры(СерииНоменклатуры, НоваяСтрокаЗапасы.КлючСвязи)
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
//-mega

#КонецОбласти

#Область ОбработчикиСобытий

// В обработчике события ОбработкаЗаполнения документа выполняется
// - заполнение документа по инвентаризации запасов на складе.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	// Конец Прослеживаемость
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.ИнвентаризацияЗапасов")] = "ЗаполнитьПоИнвентаризацииЗапасов";
	СтратегияЗаполнения[Тип("ДокументСсылка.ИнвентаризацияПродукцииВЕТИС")] = "ЗаполнитьПоИнвентаризацииПродукцииВЕТИС";
	СтратегияЗаполнения[Тип("ДокументСсылка.РасходныйОрдер")] = "ЗаполнитьПоРасходномуОрдеру";
	
	//+mega
	СтратегияЗаполнения[Тип("ДокументСсылка.ПриходнаяНакладная")] = "ЗаполнитьПоПриходнойНакладной";
	//-mega
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
	
	// Прослеживаемость
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	
	УчитыватьВНУ = Ложь;
	
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ДополнительныеСвойства.Вставить("Отказ", Отказ);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда
		МассивСтруктурныхЕдиниц = Новый Массив;
		МассивСтруктурныхЕдиниц.Добавить(СтруктурнаяЕдиница);
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = СкладскойУчетСервер.МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам(МассивСтруктурныхЕдиниц);
	Иначе
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = Новый Массив;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам", МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам);
	
	// Инициализация данных документа
	Документы.СписаниеЗапасов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Проверим, можно ли продолжать и не было ли отказа в процедурах формирования движений.
	Если ДополнительныеСвойства.Отказ = Истина Тогда
		Отказ = Истина;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыКРасходуСоСкладов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);

	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыКРасходу", ТаблицыДляДвижений, Движения, Отказ);
	
	// Прослеживаемость
	УправлениеНебольшойФирмойСервер.ОтразитьПрослеживаемыеТовары(ДополнительныеСвойства, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОперацииСПрослеживаемымиТоварами", ТаблицыДляДвижений, Движения, Отказ);
	
	// Комиссия
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыИАгентскиеУслугиПринятые", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыПринятыеВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.СписаниеЗапасов.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ПроведениеДокументовУНФ.ЗакрытьМенеджерВременныхТаблиц(ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда
		МассивСтруктурныхЕдиниц = Новый Массив;
		МассивСтруктурныхЕдиниц.Добавить(СтруктурнаяЕдиница);
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = СкладскойУчетСервер.МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам(МассивСтруктурныхЕдиниц);
	Иначе
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = Новый Массив;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам", МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам);
	
	// Контроль
	Документы.СписаниеЗапасов.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Серии номенклатуры
	Если НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница,"ОрдерныйСклад") = Истина Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Объект", ЭтотОбъект);
		ДополнительныеПараметры.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		СерииНоменклатурыУНФ.ПроверитьЗаполнениеСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, 
			ДополнительныеПараметры);
	КонецЕсли;
	
	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);
	
	ГрузовыеТаможенныеДекларацииСервер.ПриОбработкеПроверкиЗаполнения(Отказ, ЭтотОбъект); 
	
	ВестиУчетПрослеживаемыхТоваров = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеКонстанты("ВестиУчетПрослеживаемыхТоваров");
	Если ВестиУчетПрослеживаемыхТоваров Тогда
		СтруктураПоиска = Новый Структура("ИдентификаторСтроки");
		Для каждого СтрокаЗапасы Из Запасы Цикл
			
			Если Не СтрокаЗапасы.ПрослеживаемыйТовар Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаЗапасы.ПрослеживаемыйКомплект Тогда
				
				СтруктураПоиска.ИдентификаторСтроки = СтрокаЗапасы.ИдентификаторСтроки;
				НайденныеСтроки = СведенияПрослеживаемости.НайтиСтроки(СтруктураПоиска);
				
				Если Не НайденныеСтроки.Количество() Тогда
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
					"ru = 'Номер РНПТ'"), СтрокаЗапасы.НомерСтроки, "Запасы");
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"ЗапасыНомерГТДПриРНПТ");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда
		
		УчетОстатковПоСкладскимОрдерам = СкладскойУчетСервер.УчетОстатковВРазрезеСкладскихОрдеров(СтруктурнаяЕдиница, Ложь);
		
		Если УчетОстатковПоСкладскимОрдерам Тогда  
			ПроверяемыеРеквизиты.Добавить("ДокументПоступления");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// ОбменДанными.Загрузка не требуется.
	// ИнтеграцияГИСМ
	ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМУНФ.ЕстьМаркируемаяПродукцияГИСМ(Запасы);
	ЕстьКиЗГИСМ = ИнтеграцияГИСМУНФ.ЕстьКиЗГИСМ(Запасы);
	// Конец ИнтеграцияГИСМ
	
	// Прослеживаемость
	ТребуетсяПодобратьРНПТ = ПрослеживаемостьУНФ.ТребуетсяПодобратьРНПТ(Запасы, Дата, Истина);
	Если ТребуетсяПодобратьРНПТ Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ПрослеживаемостьУНФ.ПодобратьРНПТОстатки(ЭтотОбъект, Отказ);
		КонецЕсли; 
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из Запасы Цикл
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			СтрокаТабличнойЧасти.НомерГТД = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Конец Прослеживаемость
	
	ЕстьКомиссионныеСписания = РаботаСКомиссионерамиКомитентамиСервер.ВТабличнойЧастиЕстьКомиссионнаяПартия(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ПереданВЕГАИС = Неопределено;
	АктСписанияЕГАИС = Неопределено;
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	// Конец Прослеживаемость
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли