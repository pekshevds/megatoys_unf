#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	ПараметрыПроведения.Вставить("ТаблицаОС", Неопределено);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если НЕ ПрослеживаемостьПереопределяемый.УчетнаяПолитикаСуществует(
			Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
			
		Отказ = Истина;
		Возврат ПараметрыПроведения;
		
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ПрослеживаемостьПереопределяемый.ТекстЗапросаТаблицаТоваровУведомлениеОВвозе(НомераТаблиц, ДокументСсылка);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"УведомлениеОВвозеПрослеживаемыхТоваров", 
			"Уведомление о ввозе прослеживаемых товаров",
			ПрослеживаемостьБРУ.ПечатьУведомленияОВвозеПрослеживаемыхТоваров(МассивОбъектов, ОбъектыПечати));
			
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "УведомлениеОВвозеПрослеживаемыхТоваров";
	КомандаПечати.Представление  = НСтр("ru = 'Печать'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Уведомление о ввозе прослеживаемых товаров'");
	КомандаПечати.Обработчик     = "ПрослеживаемостьФормыКлиент.ВыполнитьКомандуПечатиУведомленияОВвозеПрослеживаемыхТоваров";
	КомандаПечати.СписокФорм     = "ФормаДокументаОсновная,ФормаДокументаКорректировочная";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	Если (Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ)) Тогда
		ВидУведомления= ОпределитьВидУведомления(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "НомерКорректировки"));
	ИначеЕсли (Параметры.Свойство("Основание") И ЗначениеЗаполнено(Параметры.Основание)) Тогда
		ВидУведомления= ОпределитьВидУведомления(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Основание, "НомерКорректировки"));
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ФормыУведомлений = ПолучитьСоответствиеВидовУведомленийФормам();
	ВыбраннаяФорма = ФормыУведомлений[ВидУведомления];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокументаОсновная";
		Параметры.Вставить("ИзменитьВидОперации");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоследнийДокументИсправления(ДокументОснование, ДокументИсключение)
	
	Запрос = Новый Запрос();
		ПараметрыДокумента = 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "НомерКорректировки,ДокументУведомлениеОВвозе");
	Запрос.УстановитьПараметр("ДокументОснование", ?(ПараметрыДокумента.НомерКорректировки>0, 
			ПараметрыДокумента.ДокументУведомлениеОВвозе, ДокументОснование));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ДокументУведомлениеОВвозе = &ДокументОснование
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ТаблицаДокументов.НомерКорректировки КАК НомерКорректировки
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.НомерКорректировки УБЫВ"
	;
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат ДокументОснование;
	КонецЕсли;
	
КонецФункции

Функция ЗаполнитьДокументДанными(Период, Организация, ДокументОснование = Неопределено,
		ДокументИсключение = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ПоследнийДокументИсправления", 
		ПоследнийДокументИсправления(ДокументОснование, ДокументИсключение));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка КАК Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ВерсияДанных КАК ВерсияДанных,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПометкаУдаления КАК ПометкаУдаления,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Дата КАК Дата,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Проведен КАК Проведен,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения
	|	КОНЕЦ КАК КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РНПТ КАК РНПТ,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент КАК ПервичныйДокумент,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ответственный КАК Ответственный,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Комментарий КАК Комментарий,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.РучнаяКорректировка КАК РучнаяКорректировка,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК ЕдиницаПрослеживаемости,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки + 1 КАК НомерКорректировки,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ДокументУведомлениеОВвозе = ЗНАЧЕНИЕ(Документ.УведомлениеОВвозеПрослеживаемыхТоваров.ПустаяСсылка)
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ДокументУведомлениеОВвозе
	|	КОНЕЦ КАК ДокументУведомлениеОВвозе,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭДПослеИзменения
	|	КОНЕЦ КАК КодТНВЭДПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.КодОКПД2Корректировочный
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.КодОКПД2КорректировочныйПослеИзменения
	|	КОНЕЦ КАК КодОКПД2ПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмерения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаИзмеренияПослеИзменения
	|	КОНЕЦ КАК ЕдиницаИзмеренияПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ЕдиницаПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК ЕдиницаПрослеживаемостиПослеИзменения,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодФормыРеорганизации КАК КодФормыРеорганизации,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ИННДоРеорганизации КАК ИННДоРеорганизации,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КППДоРеорганизации КАК КППДоРеорганизации,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваров.ОснованиеКорректировки
	|	КОНЕЦ КАК ОснованиеКорректировки
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = &ПоследнийДокументИсправления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка КАК Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.Номенклатура КАК Номенклатура,
	|	УведомлениеОВвозеПрослеживаемыхТоваровТовары.КодОКПД2 КАК КодОКПД2,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Количество
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПослеИзменения
	|	КОНЕЦ КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемости
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.КоличествоПрослеживаемостиПослеИзменения
	|	КОНЕЦ КАК КоличествоПрослеживаемостиПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.Сумма
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СуммаПослеИзменения
	|	КОНЕЦ КАК СуммаПослеИзменения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки = 0
	|			ТОГДА УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхождения
	|		ИНАЧЕ УведомлениеОВвозеПрослеживаемыхТоваровТовары.СтранаПроисхожденияПослеИзменения
	|	КОНЕЦ КАК СтранаПроисхожденияПослеИзменения
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК УведомлениеОВвозеПрослеживаемыхТоваровТовары
	|		ПО УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = УведомлениеОВвозеПрослеживаемыхТоваровТовары.Ссылка
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка = &ПоследнийДокументИсправления";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = РезультатЗапроса[0].Выгрузить();
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	
	Возврат Новый Структура("Реквизиты,Товары", ТаблицаРеквизиты, ТаблицаТовары)
	
КонецФункции

Процедура ЗаполнитьКорректировочноеУведомлениеОВвозеПрослеживаемыхТоваров(УведомлениеОбъект, 
		ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	УведомлениеОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Проведен,РНПТ");
	
	Если НЕ РеквизитыДокумента.Проведен Тогда
		
		ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ВызватьИсключение ТекстОшибки;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(РеквизитыДокумента.РНПТ) Тогда
		
		ТекстОшибки = НСтр("ru='В документе %Документ% не указан РНПТ. Ввод на основании документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ДанныеТаблиц = ЗаполнитьДокументДанными(
		УведомлениеОбъект.Дата, УведомлениеОбъект.Организация, ДанныеЗаполнения);
		
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, 
		?(ДанныеТаблиц.Реквизиты.Количество()>0, ДанныеТаблиц.Реквизиты[0], Новый ТаблицаЗначений),,"Дата");
	
	УведомлениеОбъект.Товары.Загрузить(ДанныеТаблиц.Товары);
		
КонецПроцедуры

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.РНПТ КАК РНПТ,
	|	ВЫБОР
	|		КОГДА Реквизиты.НомерКорректировки = 0
	|			ТОГДА Реквизиты.ПервичныйДокумент
	|		ИНАЧЕ Реквизиты.ОснованиеКорректировки
	|	КОНЕЦ КАК Основание,
	|	Реквизиты.НомерКорректировки > 0 КАК ЭтоКорректировочноеУведомление,
	|	Реквизиты.ПолученоПодтверждениеИзФНС КАК ПолученоПодтверждениеИзФНС
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.РНПТ КАК РНПТ,
	|	Реквизиты.Основание КАК Основание,
	|	Реквизиты.ЭтоКорректировочноеУведомление КАК ЭтоКорректировочноеУведомление,
	|	Реквизиты.ПолученоПодтверждениеИзФНС КАК ПолученоПодтверждениеИзФНС
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОпределитьВидУведомления(НомерКорректировки)

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	Если НомерКорректировки > 0 Тогда
		Возврат ВидыУведомлений.КорректировочноеУведомление;
	КонецЕсли;
	
	Возврат ВидыУведомлений.Уведомление;

КонецФункции 

Функция ПолучитьСоответствиеВидовУведомленийФормам() Экспорт

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	ФормыУведомленияОбОстатках = Новый Соответствие;
	ФормыУведомленияОбОстатках.Вставить(ВидыУведомлений.Уведомление, "ФормаДокументаОсновная");
	ФормыУведомленияОбОстатках.Вставить(ВидыУведомлений.КорректировочноеУведомление, "ФормаДокументаКорректировочная");
	
	Возврат ФормыУведомленияОбОстатках;

КонецФункции 

#КонецОбласти

#КонецЕсли
