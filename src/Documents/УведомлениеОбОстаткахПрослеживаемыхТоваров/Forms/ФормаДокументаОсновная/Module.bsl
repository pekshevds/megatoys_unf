#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ПодготовитьФормуНаСервере();
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьВременныеРеквизиты();

	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
	ПрослеживаемостьСобытияФормПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодготовитьФормуНаСервере();
	
	ПрослеживаемостьСобытияФормПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПрослеживаемостьФормыКлиент.ПроверитьСоответствиеРеквизитовВШапкеИТабличнойЧасти(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
	ПрослеживаемостьСобытияФормКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_УведомлениеОбОстаткахПрослеживаемыхТоваров", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПрослеживаемостьБРУ.УстановитьЗаголовокФормыУведомлениеОбОстатках(ЭтотОбъект);
	
	ПрослеживаемостьБРУ.СохранитьСтатусОтправки(ЭтотОбъект);
		
	УстановитьСостояниеДокумента();
	ЗаполнитьВременныеРеквизиты();
	
	ПрослеживаемостьСобытияФормПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС Тогда
		
		Для каждого ТекущаяСтрокаТовары Из Объект.Товары Цикл
			
			ЭтоПрослеживаемоеКомплектующее = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТовары.Номенклатура, "ПрослеживаемыйТовар");
			
			Если НЕ ЭтоПрослеживаемоеКомплектующее Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Комплектующее'"),,,НСтр("ru='У комплектующего не установлен признак прослеживаемый товар'"));
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "Комплектующее");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТовары.Номенклатура) Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Заполнение", НСтр("ru = 'Комплектующее'"),,,);
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "Номенклатура");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТовары.ОсновноеСредство) Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Заполнение", НСтр("ru = 'Основное средство'"),,,);
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "ОсновноеСредство");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
			
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства Тогда
		
		Для каждого ТекущаяСтрокаТовары Из Объект.Товары Цикл
			
			ЭтоПрослеживаемыйОС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТовары.ОсновноеСредство, "ПрослеживаемыйТовар");
			
			Если НЕ ЭтоПрослеживаемыйОС Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Основное средство'"),,,НСтр("ru='Основное средство не является прослеживаемым основным средством'"));
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "ОсновноеСредство");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекущаяСтрокаТовары.ОсновноеСредство) Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Заполнение", НСтр("ru = 'Основное средство'"),,,);
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "ОсновноеСредство");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.Товары Тогда
		
		Для каждого ТекущаяСтрокаТовары Из Объект.Товары Цикл
			
			ЭтоПрослеживаемыйТовар = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТовары.Номенклатура, "ПрослеживаемыйТовар");
			
			Если НЕ ЭтоПрослеживаемыйТовар Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле", "Корректность", НСтр("ru = 'Номенклатура'"),,,НСтр("ru='У номенклатуры не установлен признак прослеживаемый товар'"));
				
				ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Объект.Товары", ТекущаяСтрокаТовары.НомерСтроки, "Номенклатура");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаполнитьРНПТ(Команда)
	
	Элементы.ЗаполнитьРНПТ.Видимость = Ложь;
	
	РНПТИзКвитанции = "";
	Для каждого ДокументРеализацииПолномочийНО Из ДокументыРеализацииПолномочийНО Цикл
		Если ЗначениеЗаполнено(ДокументРеализацииПолномочийНО.РНПТ) Тогда
			РНПТИзКвитанции = СокрЛП(ДокументРеализацииПолномочийНО.РНПТ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьРНПТНаСервере(РНПТИзКвитанции);
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДПриИзменении(Элемент)
	
	КодТНВЭДПриИзмененииНаСервере();
	
	ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ЗаполнитьПоТНВЭД(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'При заполнении табличная часть будет очищена. Заполнить?'");
	
		Оповещение = Новый ОписаниеОповещения("ТоварыОбработкаЗаполненияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		ЗаполнитьПоТНВЭДПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаЗаполненияЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоТНВЭДПоДокументу();

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДанныеОбъекта = Новый Структура("КодТНВЭД", Объект.КодТНВЭД);
		
		СведенияОНоменклатуреПрослеживаемогоТовара =
			ПрослеживаемостьВызовСервераПереопределяемый.ПолучитьСведенияПрослеживаемогоТовара(
				ТекущиеДанные.Номенклатура, ДанныеОбъекта);
	
		Если СведенияОНоменклатуреПрослеживаемогоТовара <> Неопределено Тогда
		
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, СведенияОНоменклатуреПрослеживаемогоТовара);
		
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.НаименованиеПервичногоДокумента) 
		И ЗначениеЗаполнено(Объект.ПервичныйДокумент) Тогда

		СтандартнаяОбработка = Ложь;
	
		ПрослеживаемостьФормыКлиент.ТоварыНоменклатураНачалоВыбораУведомления(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПрослеживаемостьФормыКлиент.ОбработкаВыбораУведомления(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиент.ТоварыКоличествоПриИзмененииУведомления(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.НаименованиеПервичногоДокумента) 
		И ЗначениеЗаполнено(Объект.ПервичныйДокумент) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПрослеживаемостьФормыКлиент.КодТНВЭДНачалоВыбора(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыгрузитьУведомлениеВXML(Команда)
	
	ВыгрузитьУведомлениеОбОстатках();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	Если НЕ ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило("Отпр.ув.об ост.просл.т.") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отправка уведомления в электронном виде предусмотрена законодательством с 1 июля 2021 г.'"));
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма);
	
КонецПроцедуры

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ФСС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ФСС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru='Вы уверены, что уведомление уже сдано?'"));
	
	ИнтерфейсыВзаимодействияБРОКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьТабличнуюЧастьПриИзмененииРеквизита(Элемент)
	
	Реквизит = Элемент.Заголовок;
	
	Если Объект.Товары.Количество() <> 0 Тогда
		
		ШаблонВопроса = НСтр("ru = 'При изменении %1 табличная часть будет очищена. Продолжить?'");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, Реквизит);
		
		Оповещение = Новый ОписаниеОповещения("ИзменениеРеквизитаЗаполненияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
		СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
		СтароеЗначениеТНВЭД = Объект.КодТНВЭД;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменениеРеквизитаЗаполненияЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
	
		Объект.Товары.Очистить();
		
		СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
		СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
		СтароеЗначениеТНВЭД = Объект.КодТНВЭД;

	Иначе
		
		Объект.ЕдиницаИзмерения = СтароеЗначениеЕдиницаИзмерения;
		Объект.ПервичныйДокумент = СтароеЗначениеПервичныйДокумент;
		Объект.КодТНВЭД = СтароеЗначениеТНВЭД;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура КодТНВЭДПриИзмененииНаСервере()
	
	Объект.ЕдиницаПрослеживаемости = 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.КодТНВЭД, "ЕдиницаИзмерения");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРНПТНаСервере(РНПТИзКвитанции)
	
	НайденныйРНПТ = ПрослеживаемостьПереопределяемый.ПолучитьРНПТНаСервере(РНПТИзКвитанции);
	
	Объект.РНПТ = НайденныйРНПТ;
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоТНВЭДПоДокументу();

	Если Объект.Ссылка.Пустая() Тогда
		Записать();
	ИначеЕсли Объект.Проведен Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	КонецЕсли;
	
	ТаблицаТоваров = ПрослеживаемостьВызовСервераПереопределяемый.ПолучитьТаблицуТоваровПоТНВЭД(Объект);
	
	Объект.Товары.Загрузить(ТаблицаТоваров);
	
	ЗаполнитьВременныеРеквизиты();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьУведомлениеОбОстатках()
	
	ПрослеживаемостьКлиент.ВыгрузитьУведомлениеПоПрослеживаемости(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// ТоварыЕдиницаИзмерения, ТоварыЕдиницаПрослеживаемогоТовара, ТоварыКоличествоПрослеживаемости
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЕдиницаИзмерения");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЕдиницаПрослеживаемогоТовара");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличествоПрослеживаемости");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЕдиницаИзмерения", ВидСравненияКомпоновкиДанных.Равно, Объект.ЕдиницаПрослеживаемости);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	// ТоварыКомиссионер

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКомиссионер");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Комиссионер", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Выбор комиссионера>'"));
	
	// НадписьДокументОснование

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НадписьДокументОснование");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ПервичныйДокумент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НаименованиеПервичногоДокумента", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	// ИзменитьОснование

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ИзменитьОснование");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ПервичныйДокумент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ТипУведомления = ?(Объект.НомерКорректировки = 0, 0, 1);
	
	ПрослеживаемостьБРУ.УстановитьЗаголовокФормыУведомлениеОбОстатках(ЭтотОбъект);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = ТекущаяТабличнаяЧасть();
	
	УстановитьСостояниеДокумента();
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВременныеРеквизиты()
	
	Для каждого ТекущаяСтрока Из Объект.Товары Цикл
		
		ТекущаяСтрока.ЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
		ТекущаяСтрока.ЕдиницаПрослеживаемости = Объект.ЕдиницаПрослеживаемости;
		
	КонецЦикла;
	
	СтароеЗначениеЕдиницаИзмерения = Объект.ЕдиницаИзмерения;
	СтароеЗначениеПервичныйДокумент = Объект.ПервичныйДокумент;
	СтароеЗначениеТНВЭД = Объект.КодТНВЭД;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Форма.НадписьПервичногоДокумента = ТекстПервичногоДокументаДляПредставления(
		Объект.ПервичныйДокумент,
		Объект.НаименованиеПервичногоДокумента,
		Объект.НомерПервичногоДокумента,
		Объект.ДатаПервичногоДокумента
		);
	
	Форма.Элементы.ГруппаНомерДата.Видимость = Форма.ТипУведомления = 0;
	Форма.Элементы.Организация.ТолькоПросмотр = Форма.ТипУведомления <> 0;
	Форма.Элементы.РНПТ.ТолькоПросмотр = Форма.ТипУведомления <> 0;
	
	Форма.Элементы.ИзменитьОснование.Видимость = ЗначениеЗаполнено(Объект.ПервичныйДокумент)
		И ТипЗнч(Объект.ПервичныйДокумент) <> ТИп("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров");
	
	СтруктураДополнительныхРеквизитов = Новый Структура("ПризнакУведомления,КодФормыРеорганизации",
		Объект.ПризнакУведомления, Объект.КодФормыРеорганизации);
	
	Форма.НадписьДополнительныеСведения = 
		ПрослеживаемостьФормыВызовСервера.ТекстДополнительныеСведения(СтруктураДополнительныхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	ПрослеживаемостьПереопределяемый.УстановитьСостояниеДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДополнительныеРеквизиты(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Форма = ЭтотОбъект;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",        Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("ИННДоРеорганизации",    Объект.ИННДоРеорганизации);
	ПараметрыФормы.Вставить("КППДоРеорганизации",    Объект.КППДоРеорганизации);
	ПараметрыФормы.Вставить("ФормаОткрытаИзКорректировочногоУведомления",  Ложь);
	ПараметрыФормы.Вставить("ПризнакУведомления",    Объект.ПризнакУведомления);
	ПараметрыФормы.Вставить("КодФормыРеорганизации", Объект.КодФормыРеорганизации);
	ПараметрыФормы.Вставить("Продавцы", Объект.Продавцы);
	
	ОткрытьФорму(
		"Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Форма.ФормаДополнительныхСведений",
		ПараметрыФормы, 
		Форма
	);
	
КонецПроцедуры

&НаСервере
Функция ТекущаяТабличнаяЧасть()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.ОсновныеСредства Тогда
		Возврат Элементы.ГруппаОсновныеСредства;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийУведомленияОбОстаткахПрослеживаемыхОбъектов.КомплектующиеОС 
			Тогда
		Возврат Элементы.ГруппаКомплектующиеОС;
	КонецЕсли;
	
	Возврат Элементы.ГруппаТовары;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстПервичногоДокументаДляПредставления(ПервичныйДокумент, НаименованиеПервичногоДокумента, 
		НомерПервичногоДокумента,ДатаПервичногоДокумента)
		
	Возврат 
		ПрослеживаемостьПереопределяемый.ПредставлениеПервичногоДокументаВУведомленииОбОстатках(
			ПервичныйДокумент, НаименованиеПервичногоДокумента, 
			НомерПервичногоДокумента,ДатаПервичногоДокумента);
		
КонецФункции

&НаКлиенте
Процедура ИзменитьОснование(Команда)
	
	Форма = ЭтотОбъект;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	СтруктураПараметров = 
		Новый Структура("ПервичныйДокумент,НаименованиеПервичногоДокумента,НомерПервичногоДокумента,ДатаПервичногоДокумента,ВидОперации",
			Объект.ПервичныйДокумент,
			Объект.НаименованиеПервичногоДокумента,
			Объект.НомерПервичногоДокумента,
			Объект.ДатаПервичногоДокумента,
			Объект.ВидОперации);
			
	ОткрытьФорму("Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Форма.ФормаВыбораПервичногоДокумента",
		СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДокументОснованиеНажатие(Элемент, СтандартнаяОбработка)
	
	Форма = ЭтотОбъект;
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.НаименованиеПервичногоДокумента)
		ИЛИ НЕ ЗначениеЗаполнено(Объект.ПервичныйДокумент) Тогда
		
		СтруктураПараметров = 
			Новый Структура(
				"ПервичныйДокумент, НаименованиеПервичногоДокумента, НомерПервичногоДокумента, ДатаПервичногоДокумента,ВидОперации",
				Объект.ПервичныйДокумент,Объект.НаименованиеПервичногоДокумента,
				Объект.НомерПервичногоДокумента,Объект.ДатаПервичногоДокумента,Объект.ВидОперации);
			
		ОткрытьФорму("Документ.УведомлениеОбОстаткахПрослеживаемыхТоваров.Форма.ФормаВыбораПервичногоДокумента",
			СтруктураПараметров, ЭтотОбъект);
			
	Иначе
			
		ПоказатьЗначение(,Объект.ПервичныйДокумент);
			
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОсновныеСредства

&НаКлиенте
Процедура ОсновныеСредстваОсновноеСредствоПриИзменении(Элемент)
	
	
	ТекущиеДанные = Элементы.ОсновныеСредства.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Количество = 1;
	ТекущиеДанные.КоличествоПрослеживаемости = 1;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыВСоставеОС

&НаКлиенте
Процедура ТоварыВСоставеОСКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КомплектующиеОС.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиент.ТоварыКоличествоПриИзмененииУведомления(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти
