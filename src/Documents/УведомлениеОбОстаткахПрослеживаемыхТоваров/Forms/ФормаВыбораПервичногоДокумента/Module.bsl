
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗНЧ(Параметры.ПервичныйДокумент) <> Тип("ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров") Тогда
		ПервичныйДокумент = Параметры.ПервичныйДокумент;
	КонецЕсли;
		
	НаименованиеПервичногоДокумента = Параметры.НаименованиеПервичногоДокумента;
	НомерПервичногоДокумента = Параметры.НомерПервичногоДокумента;
	ДатаПервичногоДокумента = Параметры.ДатаПервичногоДокумента;
	ВидОперации = Параметры.ВидОперации;
	
	СпособВыбораПервичногоДокумента = ?(ЗначениеЗаполнено(НаименованиеПервичногоДокумента), 1, 0);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// Первичный документ выбран из программы
	Если СпособВыбораПервичногоДокумента = 0 Тогда
		
		Если НЕ ЗначениеЗаполнено(ПервичныйДокумент) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнен первичный документ'"),
			,,, Отказ);
			
		КонецЕсли;
		
	// Первичный документ вводится вручную
	Иначе
		
		Если НЕ ЗначениеЗаполнено(НаименованиеПервичногоДокумента) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнено наименование документа'"),
				,,, Отказ);
			
		ИначеЕсли НЕ ЗначениеЗаполнено(НомерПервичногоДокумента) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнен номер документа'"),
				,,, Отказ);
			
		ИначеЕсли ДатаПервичногоДокумента = Дата("00010101") Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнена дата документа'"),
				,,, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ПервичныйДокумент = ВыбранноеЗначение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Модифицированность И ПроверитьЗаполнение()  Тогда
		
		ПараметрыПередачи = Новый Структура(
			"ПервичныйДокумент,НаименованиеПервичногоДокумента,НомерПервичногоДокумента,ДатаПервичногоДокумента", 
			ПервичныйДокумент,НаименованиеПервичногоДокумента,НомерПервичногоДокумента, ДатаПервичногоДокумента);
		
		ОповеститьОВыборе(ПараметрыПередачи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособВыбораПервичногоДокументаИзПрограммыПриИзменении(Элемент)
	
	НаименованиеПервичногоДокумента = "";
	НомерПервичногоДокумента = "";
	ДатаПервичногоДокумента = "";
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособВыбораПервичногоДокументаУказатьВручнуюПриИзменении(Элемент)

	ПервичныйДокумент = "";
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПервичныйДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПрослеживаемостьКлиентПереопределяемый.ПервичныйДокументНачалоВыбораУведомлениеОбОстатках(ВидОперации, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ПервичныйДокумент.Доступность = Форма.СпособВыбораПервичногоДокумента = 0 И НЕ Форма.ТолькоПросмотр;
	Элементы.НаименованиеДокумента.Доступность = Форма.СпособВыбораПервичногоДокумента = 1 И НЕ Форма.ТолькоПросмотр;
	Элементы.НомерДокумента.Доступность = Форма.СпособВыбораПервичногоДокумента = 1 И НЕ Форма.ТолькоПросмотр;
	Элементы.ДатаДокумента.Доступность = Форма.СпособВыбораПервичногоДокумента = 1 И НЕ Форма.ТолькоПросмотр;
	
КонецПроцедуры

#КонецОбласти
