#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	Если (Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ)) Тогда
		ВидУведомления= ОпределитьВидУведомления(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "НомерКорректировки"));
	ИначеЕсли (Параметры.Свойство("Основание") И ЗначениеЗаполнено(Параметры.Основание)) Тогда
		
		Если ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров") Тогда
			ВидУведомления = ОпределитьВидУведомления(
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Основание, "НомерКорректировки"));
		Иначе
			ВидУведомления = ПрослеживаемостьБРУ.ВидыУведомлений().Уведомление;
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") 
			И Параметры.ЗначенияЗаполнения.Свойство("ЭтоКорректировка") Тогда
		
		ВидУведомления = ?(Параметры.ЗначенияЗаполнения.ЭтоКорректировка, 
			ПрослеживаемостьБРУ.ВидыУведомлений().КорректировочноеУведомление,
			ПрослеживаемостьБРУ.ВидыУведомлений().Уведомление);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ФормыУведомлений = ПолучитьСоответствиеВидовУведомленийФормам();
	ВыбраннаяФорма = ФормыУведомлений[ВидУведомления];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокументаОсновная";
		Параметры.Вставить("ИзменитьВидОперации");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует заполненные таблицы документа
//
// Параметры:
//  Параметры - Структура: 
//						- Организация - СправочникСсылка - организация
//						- Период - Дата - дата помощника получения РНПТ
//  АдресРезультата - строка - адрес, куда будет помещен результат 
// 
Процедура ЗаполнитьДокумент(Параметры, АдресРезультата) Экспорт
	
	Если Параметры.НомерКорректировки = 0 Тогда
	
		ДанныеТаблиц = ЗаполнитьДокументДанными(
							Параметры.Дата,
							Параметры.Организация,,
							Параметры.ПервичныйДокумент,
							);
			
	Иначе
		
		ДанныеТаблиц = ПрослеживаемостьПереопределяемый.ЗаполнитьДокументДаннымиИзКоррДокументов(
							Параметры.Дата,
							Параметры.Организация,
							Параметры.ПервичноеУведомление
							);
		
	КонецЕсли;						
							
	
	ПоместитьВоВременноеХранилище(ДанныеТаблиц, АдресРезультата);
	
КонецПроцедуры

#Область ПодготовкаПараметровПроведенияДокумента

// Функция подготавливает параметры проведения документа
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	Если НЕ ПрослеживаемостьПереопределяемый.УчетнаяПолитикаСуществует(
			Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = 
		ПрослеживаемостьПереопределяемый.ТекстЗапросаТаблицаТоваровУведомлениеОПеремещении(НомераТаблиц, ДокументСсылка);
	
	ПрослеживаемостьПереопределяемый.ДополнительныеПараметрыЗапроса(НомераТаблиц, Запрос);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	
	Возврат ПараметрыПроведения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьДокументДанными(Период, Организация, ДокументОснование = Неопределено, ПервичныйДокумент = Неопределено, НомерКорректирвоки = 0) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);  
	Запрос.УстановитьПараметр("ПервичныйЛокумент", ПервичныйДокумент);
	Запрос.УстановитьПараметр("НомерКорректировки", НомерКорректирвоки);

	
	Запрос.Текст = ПрослеживаемостьПереопределяемый.ЗапросЗаполнитьДокументДаннымиУведомлениеОперемещении() + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыборкаДанных.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыборкаДанных.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыборкаДанных.Номенклатура КАК Номенклатура,
	|	ВТ_ВыборкаДанных.РНПТ КАК РНПТ,
	|	ВТ_ВыборкаДанных.Контрагент КАК Контрагент,
	|	ВТ_ВыборкаДанных.ПорядковыйНомер КАК ПорядковыйНомер,
	|	ВТ_ВыборкаДанных.КоличествоОстаток КАК Количество,
	|	ВТ_ВыборкаДанных.КоличествоПрослеживаемостиОстаток КАК КоличествоПрослеживаемости,
	|	ВТ_ВыборкаДанных.СуммаОстаток КАК Сумма,
	|	ВТ_ВыборкаДанных.СопроводительныйДокумент КАК СопроводительныйДокумент,
	|	ВТ_ВыборкаДанных.КодТНВЭД КАК КодТНВЭД,
	|	ВТ_ВыборкаДанных.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(ВТ_ВыборкаДанных.КодТНВЭД КАК Справочник.КлассификаторТНВЭД).ЕдиницаИзмерения КАК ЕдиницаПрослеживаемости
	|ИЗ
	|	ВТ_ВыборкаДанных КАК ВТ_ВыборкаДанных";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// В таблице товары СопроводительныйДокумент показывает сигнал к новому ключу
	ТаблицаКонтрагенты = РезультатЗапроса[1].Выгрузить();
	ТаблицаКонтрагенты.Колонки.Добавить("КлючСтроки", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТаблицаКонтрагенты.Колонки.Добавить("ЕдиницыПрослеживаемостиСовпадают", Новый ОписаниеТипов("Булево"));
	
	ТаблицаТовары = РезультатЗапроса[2].Выгрузить();
	ТаблицаТовары.Колонки.Добавить("КлючСтроки", Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	ФильтрОтбора = Новый Структура("Контрагент");
	
	Для каждого ТекущаСтрокаКонтрагенты Из ТаблицаКонтрагенты Цикл
		
		ЗаполнитьЗначенияСвойств(ФильтрОтбора, ТекущаСтрокаКонтрагенты);
		ТекущаСтрокаКонтрагенты.КлючСтроки = Новый УникальныйИдентификатор();
		
		ТекущаСтрокаКонтрагенты.ЕдиницыПрослеживаемостиСовпадают = Истина;
		
		НаденныйСтрокиТовары = ТаблицаТовары.НайтиСтроки(ФильтрОтбора);
		
		Для каждого ТекущаяСтрокаТовары Из НаденныйСтрокиТовары Цикл
			
			ТекущаяСтрокаТовары.КлючСтроки = ТекущаСтрокаКонтрагенты.КлючСтроки;
			
			Если ТекущаяСтрокаТовары.ЕдиницаИзмерения <> ТекущаяСтрокаТовары.ЕдиницаПрослеживаемости Тогда
				
				ТекущаСтрокаКонтрагенты.ЕдиницыПрослеживаемостиСовпадают = Ложь;
				
			КонецЕсли;
		
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый Структура("Контрагенты,Товары", ТаблицаКонтрагенты, ТаблицаТовары)
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.НомерКорректировки КАК НомерКорректировки
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.НомерКорректировки КАК НомерКорректировки
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьСоответствиеВидовУведомленийФормам() Экспорт

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	ФормыУведомленияОПеремещении = Новый Соответствие;
	ФормыУведомленияОПеремещении.Вставить(ВидыУведомлений.Уведомление, "ФормаДокументаОсновная");
	ФормыУведомленияОПеремещении.Вставить(ВидыУведомлений.КорректировочноеУведомление, "ФормаДокументаКорректировочная");
	
	Возврат ФормыУведомленияОПеремещении;

КонецФункции 

Функция ОпределитьВидУведомления(НомерКорректировки)

	ВидыУведомлений = ПрослеживаемостьБРУ.ВидыУведомлений();
	
	Если НомерКорректировки > 0 Тогда
		Возврат ВидыУведомлений.КорректировочноеУведомление;
	КонецЕсли;
	
	Возврат ВидыУведомлений.Уведомление;

КонецФункции 

#КонецОбласти

#КонецЕсли
