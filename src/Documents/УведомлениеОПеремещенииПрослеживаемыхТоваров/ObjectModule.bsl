#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
			И ДанныеЗаполнения.Свойство("Основание") Тогда
		
		ДокументОснование = ДанныеЗаполнения.Основание;
		
	Иначе 
		
		ДокументОснование = ДанныеЗаполнения;
		
	КонецЕсли;
	
	ПрослеживаемостьПереопределяемый.ОбработкаЗаполненияУведомленияОПеремещении(
		ЭтотОбъект, ДокументОснование, ТекстЗаполнения, СтандартнаяОбработка);
			
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПрослеживаемостьПереопределяемый.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = 
		Документы.УведомлениеОПеремещенииПрослеживаемыхТоваров.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ОбработкаПроведенияУведомлениеОПеремещении(ПараметрыПроведения, Движения, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПрослеживаемостьПереопределяемый.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	
	Движения.Записать();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПрослеживаемостьБРУ.ПроверитьДублированиеНомераКорректировкиВУведомленияхОПеремещении(ЭтотОбъект, РежимЗаписи);
	
	ПрослеживаемостьПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НомерКорректировки = 0 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ЕдиницаИзмеренияПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ЕдиницаПрослеживаемостиПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.РНПТПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ПорядковыйНомерПослеИзменения");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КодТНВЭДПослеИзменения");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПрослеживаемости");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Сумма");
		
	КонецЕсли;
	
	ПрослеживаемостьПереопределяемый.ОбработкаПроверкиЗаполнения(
		ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли