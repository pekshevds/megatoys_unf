#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает тип расчета в зависимости от вида операции документа
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыРасчетаДенежнымиСредствами - Тип расчета для фискализации
Функция ТипРасчетаДляФискализации() Экспорт

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	Иначе
		ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратРасходаДенежныхСредств;
	КонецЕсли;

	Возврат ТипРасчета;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	// Конец Прослеживаемость

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		ДоговорСтруктура = Справочники.ДоговорыКонтрагентов.СтруктураДляЗаполненияПоДоговоруКонтрагента(
			ДанныеЗаполнения);
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДоговорСтруктура);
	Иначе
		СтратегияЗаполнения = Новый Соответствие;
		СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
		СтратегияЗаполнения[Тип("ДокументСсылка.ПриходнаяНакладная")] = "ЗаполнитьПоПриходнойНакладной";
		СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПокупателя")] = "ЗаполнитьПоЗаказуПокупателя";
		СтратегияЗаполнения[Тип("ДокументСсылка.ЗаказПоставщику")] = "ЗаполнитьПоЗаказуПоставщику";
		СтратегияЗаполнения[Тип("ДокументСсылка.СчетНаОплату")] = "ЗаполнитьПоСчетуНаОплату";
		СтратегияЗаполнения[Тип("ДокументСсылка.ПеремещениеЗапасов")] = "ЗаполнитьПоПеремещениюЗапасов";
		СтратегияЗаполнения[Тип("ДокументСсылка.СборкаЗапасов")] = "ЗаполнитьПоСборкеЗапасов";
		СтратегияЗаполнения[Тип("ДокументСсылка.ПриемИПередачаВРемонт")] = "ЗаполнитьПоПриемуВРемонт";
		СтратегияЗаполнения[Тип(
			"ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС")] = "ЗаполнитьПоИсходящейТранспортнойОперации";
		СтратегияЗаполнения[Тип("ДокументСсылка.РасходныйОрдер")] = "ЗаполнитьПоРасходномуОрдеру";

		ИсключаяСвойства = "Грузоотправитель, Грузополучатель";
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СборкаЗапасов") Тогда
			ИсключаяСвойства = ИсключаяСвойства + ", СуммаВключаетНДС";
		КонецЕсли;
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения, ИсключаяСвойства);
	КонецЕсли;

	ПравилоНомераСчетаНаОплату = Константы.ПравилоЗаполненияНомераСчетаНаОплату.Получить();
	Если ЗначениеЗаполнено(ПравилоНомераСчетаНаОплату) Тогда

		НомерСчетаНаОплату = ПравилоНомераСчетаНаОплату;

	КонецЕсли;

	Если ЗначениеЗаполнено(Контрагент) И Не ЗначениеЗаполнено(БанковскийСчетКонтрагента) Тогда

		БанковскийСчетКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "БанковскийСчетПоУмолчанию");

	КонецЕсли;

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		НоменклатураВДокументахСервер.ПроверитьПереопределитьПоложениеСклада(ЭтотОбъект);
	КонецЕсли;
	
	// Прослеживаемость
	Если Перечисления.ВидыОперацийРасходнаяНакладная.ПрослеживаемаяОперация(ВидОперации) Тогда
		ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	Иначе
		ПрослеживаемостьУНФ.ОчиститьДанныеПрослеживаемости(Запасы, СведенияПрослеживаемости);
	КонецЕсли;
	// Конец Прослеживаемость

	ЗаполнитьРеквизитыПечати();

	ПараметрыДоговора = Справочники.ДоговорыКонтрагентов.ПараметрыДоговора(Договор);
	ЭтоУниверсальныйДокумент = ПараметрыДоговора.ЭтоУниверсальныйДокумент;

	УчитыватьВНУ = Ложь;

КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНовый() Тогда
		ОсвобождатьАвансАвтоматически = Истина;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("СебестоимостьБезНДС") Тогда
		НДСВключатьВСтоимость = Истина;
	КонецЕсли;
	
	СуммаДокумента = Запасы.Итог("Всего") + СтоимостьДоставки + ?(СуммаВключаетНДС, 0, СуммаНДСДоставки);

	ОплатыБезБонусов = Предоплата.НайтиСтроки(Новый Структура("ОплатаБонусами", Ложь)); // Возврат оплаченного бонусами
	СуммаОплаты = Предоплата.Выгрузить(ОплатыБезБонусов, "СуммаРасчетов").Итог("СуммаРасчетов");	
	РасчетыПроведениеДокументов.ВыполнитьКонтрольСуммыОплаты(СуммаОплаты, СуммаДокумента, Отказ);

	ЗаполнениеОбъектовУНФ.ЗаполнитьПоляПередЗаписью(ЭтотОбъект);
	
	// Положение склада
	Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
			СтрокаТабличнойЧасти.Ячейка = Ячейка;
		КонецЦикла;
	Иначе
		СтруктураПолей = ЗаполнениеОбъектовУНФ.СтруктурнаяЕдиницаИЯчейкаДляШапки(Запасы);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПолей);
	КонецЕсли;
	// Положение заказа покупателя
	Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.Заказ = Заказ;
		КонецЦикла;
	Иначе
		Заказ = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "Заказ");
	КонецЕсли;
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() И Не ПометкаУдаления Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	// ИнтеграцияГИСМ
	ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМУНФ.ЕстьМаркируемаяПродукцияГИСМ(Запасы);
	// Конец ИнтеграцияГИСМ
	
	// Интеграция ВЕТИС
	ЕстьПодконтрольнаяПродукцияВЕТИС = ИнтеграцияВЕТИСУНФ.ЕстьПодконтрольнаяПродукцияВЕТИС(Запасы);
	// Конец ИнтеграцияВЕТИС
	
	// ИнтеграцияГосИС
	Если ИнтеграцияИСМПВызовСервера.ВестиУчетМаркируемойПродукции() И ЗначениеЗаполнено(Контрагент)
		И Не ЕстьПодконтрольнаяПродукцияВЕТИС Тогда

		СтранаРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
		ВидыПродукцииВЗапасах = ИнтеграцияИСУНФ.ВидыПродукцииВЗапасах(Запасы);

		Если (СтранаРегистрации = Справочники.СтраныМира.Россия Или Не ЗначениеЗаполнено(СтранаРегистрации))
			И ОбменСКонтрагентами.ДокументооборотНастроен(ЭтотОбъект) И ВидыПродукцииВЗапасах.НайтиПоЗначению(
			Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха) = Неопределено
			И ВидыПродукцииВЗапасах.НайтиПоЗначению(Перечисления.ВидыПродукцииИС.Алкогольная) = Неопределено
			И ВидыПродукцииВЗапасах.НайтиПоЗначению(Перечисления.ВидыПродукцииИС.Зерно) = Неопределено
			И ВидыПродукцииВЗапасах.НайтиПоЗначению(Перечисления.ВидыПродукцииИС.ПродуктыПереработкиЗерна)
			= Неопределено Тогда
			ДополнительныеСвойства.Вставить("НеВыполнятьРасчетСтатусаГосИС");
		КонецЕсли;

	КонецЕсли;
	// Конец ИнтеграцияГосИС

	РасчетыПроведениеДокументов.ПередЗаписьюНакладной(ЭтотОбъект);
	
	// До включения автоматических скидок будем считать, что скидки рассчитаны.
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки") Тогда
		СкидкиРассчитаны = Истина;
	КонецЕсли;

	Если ЭтоНовый() И Не ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ИдентификаторПлатежа) Тогда
		ИдентификаторПлатежа = РасчетыСлужебный.ПолучитьУникальныйИдентификаторПлатежа(ЭтотОбъект);
	КонецЕсли;
	
	// Прослеживаемость
	// Для реализации не в страны ЕАЭС очистим колонку "Код ТН ВЭД"
	РеализацияВЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);

	Если Не РеализацияВЕАЭС Тогда
		Для Каждого СтрокаТаблицы Из Запасы Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.КодТНВЭД) Тогда
				СтрокаТаблицы.КодТНВЭД = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если Перечисления.ВидыОперацийРасходнаяНакладная.ПрослеживаемаяОперация(ВидОперации) Тогда
		Для Каждого СтрокаТаблицы Из Запасы Цикл
			Если СтрокаТаблицы.ПрослеживаемыйТовар Тогда
				СтрокаТаблицы.НомерГТД = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Автоматический подбор РНПТ и очистка признака прослеживаемого товара.
	ПрослеживаемыйТовар = Запасы.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
		И Перечисления.ВидыОперацийРасходнаяНакладная.ПрослеживаемаяОперация(ВидОперации);
	Если ЕстьПрослеживаемыйТовар Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Или ВидОперации
				= Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию Тогда
				ПрослеживаемостьУНФ.ПодобратьРНПТОстатки(ЭтотОбъект, Отказ);
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Или ВидОперации
				= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту Тогда
				ПрослеживаемостьУНФ.ПодобратьРНПТВозврат(ЭтотОбъект, Отказ);
			КонецЕсли;
		КонецЕсли;
	Иначе
		СведенияПрослеживаемости.Очистить();
		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
			СтрокаТабличнойЧасти.ПрослеживаемыйТовар = Ложь;
		КонецЦикла;
	КонецЕсли;
	// Конец Прослеживаемость

КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ИсключаемыеРеквизиты = Новый Массив;

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
		ПроверяемыеРеквизиты.Добавить("Подразделение");
	КонецЕсли;

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратИзПереработки Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратСОтветХранения Тогда
		ПроверяемыеРеквизиты.Добавить("Запасы.Партия");
	КонецЕсли;

	ЗаказВШапке = ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;

	ТаблицаЗапасы = Запасы.Выгрузить( , "Заказ, Всего");
	ТаблицаЗапасы.Свернуть("Заказ", "Всего");

	ТаблицаПредоплата = Предоплата.Выгрузить( , "Заказ, СуммаПлатежа");
	ТаблицаПредоплата.Свернуть("Заказ", "СуммаПлатежа");

	СвойстваКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент,
		"ВестиРасчетыПоЗаказам, ВестиРасчетыПоДоговорам");

	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		СвойстваКонтрагента.ВестиРасчетыПоЗаказам = Ложь;
		СвойстваКонтрагента.ВестиРасчетыПоДоговорам = Ложь;
	КонецЕсли;

	Если ЗаказВШапке Тогда
		Для Каждого СтрокаЗапасы Из ТаблицаЗапасы Цикл
			СтрокаЗапасы.Заказ = Заказ;
		КонецЦикла;
		Если СвойстваКонтрагента.ВестиРасчетыПоЗаказам Тогда
			Для Каждого СтрокаПредоплата Из ТаблицаПредоплата Цикл
				СтрокаПредоплата.Заказ = Заказ;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	ПроверитьЗаполненностьСтруктурнойЕдиницы(Отказ, ПроверяемыеРеквизиты);

	КоличествоЗапасы = Запасы.Количество();
	Для Каждого Строка Из ТаблицаПредоплата Цикл

		НайденнаяСтрокаЗапасы = Неопределено;

		Если СвойстваКонтрагента.ВестиРасчетыПоЗаказам И Строка.Заказ <> Неопределено И Строка.Заказ
			<> Документы.ЗаказПокупателя.ПустаяСсылка() И Строка.Заказ <> Документы.ЗаказПоставщику.ПустаяСсылка() Тогда
			НайденнаяСтрокаЗапасы = ТаблицаЗапасы.Найти(Строка.Заказ, "Заказ");
		ИначеЕсли СвойстваКонтрагента.ВестиРасчетыПоЗаказам Тогда
			НайденнаяСтрокаЗапасы = ТаблицаЗапасы.Найти(Неопределено, "Заказ");
			НайденнаяСтрокаЗапасы = ?(НайденнаяСтрокаЗапасы = Неопределено, ТаблицаЗапасы.Найти(
				Документы.ЗаказПокупателя.ПустаяСсылка(), "Заказ"), НайденнаяСтрокаЗапасы);
			НайденнаяСтрокаЗапасы = ?(НайденнаяСтрокаЗапасы = Неопределено, ТаблицаЗапасы.Найти(
				Документы.ЗаказПоставщику.ПустаяСсылка(), "Заказ"), НайденнаяСтрокаЗапасы);
		КонецЕсли;

		Если ЗначениеЗаполнено(Строка.Заказ) И НайденнаяСтрокаЗапасы = Неопределено И КоличествоЗапасы > 0
			И СвойстваКонтрагента.ВестиРасчетыПоЗаказам Тогда
			ТекстСообщения = НСтр(
				"ru = 'Нельзя зачесть аванс по заказу отличному от указанных в табличных частях ""Запасы"" или ""Расходы"".'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		КонецЕсли;
	КонецЦикла;

	РасчетыРаботаСФормамиВызовСервера.ПроверитьЗаполнениеДокументаПредоплаты(Контрагент, ПроверяемыеРеквизиты);

	Если Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() И (ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию) Тогда

		Для Каждого СтрокаЗапасы Из Запасы Цикл

			Если СтрокаЗапасы.Резерв > СтрокаЗапасы.Количество Тогда

				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'В строке №%1 табл. части ""Запасы и услуги"" количество отгружаемых позиций из резерва превышает общее количество запасов.'"),
					СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Резерв");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;
	
	// Скидка 100%.
	ЕстьРучныеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиНаценкиПродажи");
	ЕстьАвтоматическиеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки"); // АвтоматическиеСкидки
	ЕстьОплатаБонусами= ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммы"); // Бонусные программы
	Если ЕстьРучныеСкидки Или ЕстьАвтоматическиеСкидки Или ЕстьОплатаБонусами Тогда
		Для Каждого СтрокаЗапасы Из Запасы Цикл
			// АвтоматическиеСкидки
			ТекСумма = Окр(СтрокаЗапасы.Цена * СтрокаЗапасы.Количество, 2);
			ТекСуммаРучнойСкидки = ?(ЕстьРучныеСкидки, СтрокаЗапасы.СуммаСкидкиНаценки, 0);
			ТекСуммаАвтоматическойСкидки = ?(ЕстьАвтоматическиеСкидки, СтрокаЗапасы.СуммаАвтоматическойСкидки, 0);
			ТекСуммаОплатаБонусами = ?(ЕстьОплатаБонусами, СтрокаЗапасы.СуммаСкидкиОплатыБонусом, 0);
			ТекСуммаСкидки = ТекСуммаРучнойСкидки + ТекСуммаАвтоматическойСкидки + ТекСуммаОплатаБонусами;
			Если СтрокаЗапасы.ПроцентСкидкиНаценки <> 100 И ТекСуммаСкидки < ТекСумма И Не ЗначениеЗаполнено(
				СтрокаЗапасы.Сумма) Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Не заполнена колонка ""Сумма"" в строке %1 списка ""Запасы"".'"), СтрокаЗапасы.НомерСтроки);
				КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"Сумма");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если Не СвойстваКонтрагента.ВестиРасчетыПоДоговорам Тогда
		ЗаполнениеОбъектовУНФ.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Договор");
	КонецЕсли;
	
	// Серии номенклатуры
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	СерииНоменклатурыУНФ.ПроверитьЗаполнениеСерийНоменклатуры(Отказ, Запасы, СерииНоменклатуры, ДополнительныеПараметры);

	ПроверитьЗаполнениеНоменклатурыПоДоговоруОбслуживания(Отказ);
	
	// Доставка
	Если СтоимостьДоставки > 0 Тогда
		ПроверяемыеРеквизиты.Добавить("НоменклатураДоставки");
	КонецЕсли;
	// Конец Доставка
	
	// Наборы
	НаборыСервер.ПроверитьТабличнуюЧасть(ЭтотОбъект, "Запасы", Отказ);
	// КонецНаборы

	НоменклатураВДокументахСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, Отказ, Истина);

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию Тогда

		ГрузовыеТаможенныеДекларацииСервер.ПриОбработкеПроверкиЗаполнения(Отказ, ЭтотОбъект);

	КонецЕсли;
	
	// ПодарочныеСертификаты
	Если Константы.ФункциональнаяОпцияИспользоватьПодарочныеСертификаты.Получить() Тогда

		Если Не РаботаСПодарочнымиСертификатами.УказанКонтрагентДляПредоплаты() Тогда
			ТекстСообщения = НСтр(
				"ru = 'Не заполнена константа ""Служебный контрагент для подарочных сертификатов"".
				|Необходимо указать контрагента: Продажи - Еще больше возможностей.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
		КонецЕсли;
		
		// Проверка срока и области действия подарочных сертификатов
		ВыполнитьПроверкуОграниченийСертификатов(Отказ);

	КонецЕсли;
	// Конец ПодарочныеСертификаты
	
	// Прослеживаемость
	РеализацияВЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);
	// Исключаем из проверки те реквизиты табличных частей, обязательность которых
	//  зависит от значений других рекивизитов в строках табличных частей:
	ИсключаемыеРеквизиты.Добавить("Запасы.КодТНВЭД");
	
	// Проверяем табличную часть "Товары":
	Если Запасы.Количество() > 0 Тогда
		ИмяСписка = НСтр("ru = 'Запасы'");

		СтруктураПоиска = Новый Структура("ИдентификаторСтроки");
		Для Каждого СтрокаЗапасы Из Запасы Цикл

			Если Не СтрокаЗапасы.ПрослеживаемыйТовар Тогда
				Продолжить;
			КонецЕсли;
			
			// Проверка кода ТН ВЭД.
			Если РеализацияВЕАЭС И СтрокаЗапасы.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль()
				И Не ЗначениеЗаполнено(СтрокаЗапасы.КодТНВЭД) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
					"ru = 'Код ТН ВЭД'"), СтрокаЗапасы.НомерСтроки, ИмяСписка);

				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки, "КодТНВЭД");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			// Для прослеживаемого товара обязательно указывается страна происхождения.
			Если СтрокаЗапасы.ПрослеживаемыйТовар И Не ЗначениеЗаполнено(СтрокаЗапасы.СтранаПроисхождения) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
					"ru = 'Страна происхождения'"), СтрокаЗапасы.НомерСтроки, ИмяСписка);

				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
					"СтранаПроисхождения");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;

			Если СтрокаЗапасы.ПрослеживаемыйКомплект Тогда

				СтруктураПоиска.ИдентификаторСтроки = СтрокаЗапасы.ИдентификаторСтроки;
				НайденныеСтроки = СведенияПрослеживаемости.НайтиСтроки(СтруктураПоиска);

				Если Не НайденныеСтроки.Количество() Тогда
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
					"ru = 'Номер РНПТ'"), СтрокаЗапасы.НомерСтроки, ИмяСписка);

					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
						"ЗапасыРНПТ");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;
	// Конец Прослеживаемость

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
		Для Каждого СтрокаЗапасы Из Запасы Цикл
			Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
				ЗаказСтроки = СтрокаЗапасы.Заказ;
			Иначе
				ЗаказСтроки = Заказ;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЗаказСтроки) И СтрокаЗапасы.Резерв <> 0 Тогда
				ТекстСообщения = СтрШаблон(НСтр(
					"ru = 'Заполнена колонка ""Из резерва"" в строке %1 списка ""Запасы"" при не указанном заказе покупателя.'"),
					СтрокаЗапасы.НомерСтроки);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки, "Резерв");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда

		Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда

			УчетОстатковПоСкладскимОрдерам = СкладскойУчетСервер.УчетОстатковВРазрезеСкладскихОрдеров(
				СтруктурнаяЕдиница, Ложь);

			Если УчетОстатковПоСкладскимОрдерам Тогда

				Если Не ЗначениеЗаполнено(ПоложениеДокументаПоступления) Или ПоложениеДокументаПоступления
					= Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
					ПроверяемыеРеквизиты.Добавить("ДокументПоступления");
				Иначе
					Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
						Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументПоступления) Тогда
							ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
								"ru = 'Расходный ордер'"), СтрокаТабличнойЧасти.НомерСтроки, "Запасы");
							Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы",
								СтрокаТабличнойЧасти.НомерСтроки, "ДокументПоступления");
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;

		Иначе

			ТаблицаОрдерныхСкладовПоВидамУчета = СкладскойУчетСервер.ТаблицаОрдерныхСкладовПоВидамУчета();

			Если ТаблицаОрдерныхСкладовПоВидамУчета.Количество() Тогда
				ПараметрыПоиска = Новый Структура("СтруктурнаяЕдиница, Расход");

				Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл

					ПараметрыПоиска.СтруктурнаяЕдиница = СтрокаТабличнойЧасти.СтруктурнаяЕдиница;
					ПараметрыПоиска.Расход = Истина;

					НайденныеСтроки = ТаблицаОрдерныхСкладовПоВидамУчета.НайтиСтроки(ПараметрыПоиска);
					УчетОстатковПоСкладскимОрдерам = НайденныеСтроки.Количество();

					Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДокументПоступления)
						И УчетОстатковПоСкладскимОрдерам Тогда
						ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр(
							"ru = 'Расходный ордер'"), СтрокаТабличнойЧасти.НомерСтроки, "Запасы");
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы",
							СтрокаТабличнойЧасти.НомерСтроки, "ДокументПоступления");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ИсключаемыеРеквизиты);

КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	// Взаиморасчеты
	СоответствиеТабличныхЧастейИРеквизитаЗаказ = Новый Соответствие;
	СоответствиеТабличныхЧастейИРеквизитаЗаказ.Вставить("Запасы", "Заказ");
	ДополнительныеСвойства.Вставить("ИмяРеквизитаЗаказ", "Заказ");
	РасчетыПроведениеДокументов.ИнициализироватьДополнительныеСвойстваДляПроведения(ЭтотОбъект, ДополнительныеСвойства,
		Отказ, Ложь, СоответствиеТабличныхЧастейИРеквизитаЗаказ);
	// Конец Взаиморасчеты

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = СкладскойУчетСервер.МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам(
			Запасы.Выгрузить( , "СтруктурнаяЕдиница"));
	Иначе
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = Новый Массив;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам",
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам);
	
	// Инициализация данных документа.
	Документы.РасходнаяНакладная.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Взаиморасчеты
	// Проверим, можно ли продолжать и не было ли отказа в процедурах
	// формирования движений по взаиморасчетам.
	Если ДополнительныеСвойства.Отказ = Истина Тогда
		Отказ = Истина;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// Конец Взаиморасчеты
	
	// Подготовка наборов записей.
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("Запасы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыПереданныеВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыПринятыеВРазрезеГТД", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Закупки", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("Продажи", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ВыпускПродукции", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыНаСкладах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыКРасходуСоСкладов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыИАгентскиеУслугиПринятые", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗапасыПереданные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыПокупателей", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗаказыПоставщикам", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыКассовыйМетод", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыНераспределенные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыОтложенные", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПотребностьВЗапасах", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РазмещениеЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПоставщиками", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыСПокупателями", ТаблицыДляДвижений, Движения, Отказ);
	
	// Бонусы
	ПроведениеДокументовУНФ.ОтразитьДвижения("БонусныеБаллы", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("НачисленияБонусныхБаллов", ТаблицыДляДвижений, Движения, Отказ);
	//Промокоды
	ПроведениеДокументовУНФ.ОтразитьДвижения("Промокоды", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоступностьПромокодов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПрименениеПромокодов", ТаблицыДляДвижений, Движения, Отказ);

	// СерииНоменклатуры
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыГарантии", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДвиженияСерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатуры", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("СерииНоменклатурыКРасходу", ТаблицыДляДвижений, Движения, Отказ);
	
	// ПодарочныеСертификаты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПодарочныеСертификаты", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаПодарочнымиСертификатами", ТаблицыДляДвижений, Движения, Отказ);
	
	// ДисконтныеКарты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПродажиПоДисконтнымКартам", ТаблицыДляДвижений, Движения, Отказ);
	// АвтоматическиеСкидки
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПредоставленныеСкидки", ТаблицыДляДвижений, Движения, Отказ);
	// Эквайринг
	ПроведениеДокументовУНФ.ОтразитьДвижения("ДоходыИРасходыКассовыйМетодЭквайринг", ТаблицыДляДвижений, Движения,
		Отказ);
		
	ПроведениеДокументовУНФ.ОтразитьДвижения("РасчетыПоНалогам", ТаблицыДляДвижений, Движения, Отказ);
		
	ПроведениеДокументовУНФ.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Биллинг
	Если ТаблицыДляДвижений.Свойство("ТаблицаВыполнениеДоговоровОбслуживания") Тогда
		ПроведениеДокументовУНФ.ОтразитьДвижения("ВыполнениеДоговоровОбслуживания", ТаблицыДляДвижений, Движения, Отказ);
	КонецЕсли;
	
	// Суммы документов для регламентированного учета
	ПроведениеДокументовУНФ.ОтразитьДвижения("СуммыДокументовРегламентированныйУчет", ТаблицыДляДвижений, Движения,
		Отказ);
	
	// Взаиморасчеты
	ПроведениеДокументовУНФ.ОтразитьДвижения("ЗакупкиДляКУДиР", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаДокументов", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОплатаСчетовИЗаказов", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Взаиморасчеты
	
	// Интеркампани
	ПроведениеДокументовУНФ.ОтразитьДвижения("РезервыТоваровОрганизаций", ТаблицыДляДвижений, Движения, Отказ);
	// Конец Интеркампани
	
	// Прослеживаемость
	ПроведениеДокументовУНФ.ОтразитьДвижения("ПрослеживаемыеТовары", ТаблицыДляДвижений, Движения, Отказ);
	ПроведениеДокументовУНФ.ОтразитьДвижения("ОперацииСПрослеживаемымиТоварами", ТаблицыДляДвижений, Движения, Отказ);

	ПрослеживаемостьУНФ.СформироватьДвиженияПрослеживаемыхТоваровВЕАЭС(Движения, ДополнительныеСвойства);
	
	// Запись наборов записей.
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	Если ДополнительныеСвойства.Свойство("ТаблицаДокументовДляИзменения")
		И ДополнительныеСвойства.ТаблицаДокументовДляИзменения.Количество() > 0 Тогда
		РасчетыПроведениеДокументов.ОбработатьТаблицуДокументовДляИзмененияПриОтгрузке(ДополнительныеСвойства, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Контроль возникновения отрицательного остатка.
	//+mega
	Документы.РасходнаяНакладная.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);	
	//Запрос = Новый Запрос;
	//Запрос.Текст = 
	//	"ВЫБРАТЬ
	//	|	ДвиженияЗапасыНаСкладахИзменение.НомерСтроки КАК НомерСтроки,
	//	|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
	//	|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК НоменклатураПредставление,
	//	|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК ХарактеристикаПредставление,
	//	|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК ПартияПредставление,
	//	|	ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК ЯчейкаПредставление,
	//	|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
	//	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
	//	|	ЕСТЬNULL(ДвиженияЗапасыНаСкладахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыНаСкладах,
	//	|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыНаСкладах
	//	|ИЗ
	//	|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
	//	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыНаСкладах.Остатки(
	//	|				&МоментКонтроля,
	//	|						(СтруктурнаяЕдиница, Номенклатура, Характеристика, Партия, Ячейка) В
	//	|						(ВЫБРАТЬ
	//	|							ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	//	|							ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК Номенклатура,
	//	|							ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК Характеристика,
	//	|							ДвиженияЗапасыНаСкладахИзменение.Партия КАК Партия,
	//	|							ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК Ячейка
	//	|						ИЗ
	//	|							ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение)) КАК ЗапасыНаСкладахОстатки
	//	|		ПО ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
	//	|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыНаСкладахОстатки.Номенклатура
	//	|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыНаСкладахОстатки.Характеристика
	//	|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыНаСкладахОстатки.Партия
	//	|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = ЗапасыНаСкладахОстатки.Ячейка
	//	|ГДЕ
	//	|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) < 0
	//	|
	//	|УПОРЯДОЧИТЬ ПО
	//	|	НомерСтроки";
	//
	//СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	//Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	//Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	//
	//РезультатЗапроса = Запрос.Выполнить();
	//
	//ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();	
	//// Отрицательный остаток запасов на складе.
	//Если НЕ РезультатЗапроса.Пустой() Тогда
	//	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	//	КонтрольОстатковУНФ.ЗапасыНаСкладахСписком(ЭтотОбъект, ВыборкаИзРезультатаЗапроса, Отказ);
	//КонецЕсли;
	//-mega	
		
	ПроведениеДокументовУНФ.ЗакрытьМенеджерВременныхТаблиц(ЭтотОбъект);

КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	// Взаиморасчеты
	СоответствиеТабличныхЧастейИРеквизитаЗаказ = Новый Соответствие;
	СоответствиеТабличныхЧастейИРеквизитаЗаказ.Вставить("Запасы", "Заказ");
	РасчетыПроведениеДокументов.ИнициализироватьДополнительныеСвойстваДляПроведения(ЭтотОбъект, ДополнительныеСвойства,
		Отказ, Ложь, СоответствиеТабличныхЧастейИРеквизитаЗаказ);
	// Конец Взаиморасчеты
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеДокументовУНФ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	Если ДополнительныеСвойства.Свойство("ТаблицаДокументовДляИзменения")
		И ДополнительныеСвойства.ТаблицаДокументовДляИзменения.Количество() > 0 Тогда
		РасчетыПроведениеДокументов.ОбработатьТаблицуДокументовДляИзмененияПриОтгрузке(ДополнительныеСвойства, Отказ);
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныйСклад") Тогда
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = СкладскойУчетСервер.МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам(
			Запасы.Выгрузить( , "СтруктурнаяЕдиница"));
	Иначе
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам = Новый Массив;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам",
		МассивКонтроляПоЗапасамКОтгрузкеПоСкладскимОрдерам);
	
	// Контроль возникновения отрицательного остатка.
	Документы.РасходнаяНакладная.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
	// Подчиненный счет-фактура
	Если Не Отказ Тогда
		СчетаФактурыУНФ.ОтменитьПроведениеПодчиненногоСчетаФактуры(Ссылка, Номер, Дата, ДополнительныеСвойства, Ложь);
	КонецЕсли;

КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ПриКопировании объекта.
//
Процедура ПриКопировании(ОбъектКопирования)

	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.ОчиститьДатуНомерВходящегоДокумента(ЭтотОбъект);
	Предоплата.Очистить();
	ШтрихкодыУпаковок.Очистить();

	ПереданВЕГАИС = Неопределено;
	СпособПродажиГИСМ = "";

	НомерЧекаККМ = 0;
	ПодписьКассира = Неопределено;
	
	// Автоматические скидки
	СкидкиНаценки.Очистить();
	ПримененныеПромокоды.Очистить();
	ВыбранныеУправляемыеСкидки.Очистить();
	СкидкиРассчитаны = Ложь;
    // Конец Автоматические скидки
	
	// Прослеживаемость
	СведенияПрослеживаемости.Очистить();
	Если Перечисления.ВидыОперацийРасходнаяНакладная.ПрослеживаемаяОперация(ВидОперации) Тогда
		ПрослеживаемостьУНФ.ОбновитьПризнакПрослеживаемости(Запасы, Дата);
	Иначе
		ПрослеживаемостьУНФ.ОчиститьДанныеПрослеживаемости(Запасы, СведенияПрослеживаемости);
	КонецЕсли;
	// Конец Прослеживаемость

КонецПроцедуры // ПриКопировании()

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда

		Возврат;

	КонецЕсли;

	СчетаФактурыУНФ.ПриЗаписиДокументаОснованияСчетаФактуры(Ссылка, ДополнительныеСвойства, Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроцедурыЗаполнения

// Процедура заполняет авансы.
//
Процедура ЗаполнитьПредоплату() Экспорт

	Предоплата.Очистить();

	ТаблицаЗаказов = ТаблицаЗаказовДляЗаполненияПредоплаты();

	Запрос = НовыйЗапросРасшифровкиПредоплаты(ТаблицаЗаказов);

	ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();

	Пока ВыборкаРезультатаЗапроса.Следующий() Цикл

		НайденнаяСтрока = ТаблицаЗаказов.Найти(ВыборкаРезультатаЗапроса.Заказ, "Заказ");

		Если НайденнаяСтрока.ВсегоРасч = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если ВыборкаРезультатаЗапроса.СуммаРасчетов <= НайденнаяСтрока.ВсегоРасч Тогда // сумма остатка меньше или равна чем осталось распределить

			НоваяСтрока = Предоплата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРезультатаЗапроса);
			НайденнаяСтрока.ВсегоРасч = НайденнаяСтрока.ВсегоРасч - ВыборкаРезультатаЗапроса.СуммаРасчетов;

		Иначе // сумма остатка больше чем нужно распределить

			НоваяСтрока = Предоплата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРезультатаЗапроса);
			НоваяСтрока.СуммаРасчетов = НайденнаяСтрока.ВсегоРасч;
			НоваяСтрока.СуммаПлатежа = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(
				НоваяСтрока.СуммаРасчетов, ВыборкаРезультатаЗапроса.Курс,
				ВыборкаРезультатаЗапроса.КурсыВалютыДокументаКурс, ВыборкаРезультатаЗапроса.Кратность,
				ВыборкаРезультатаЗапроса.КурсыВалютыДокументаКратность);
			НайденнаяСтрока.ВсегоРасч = 0;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Обработчик заполнения документа по структуре
//
// Параметры:
// 	ДанныеЗаполнения - Структура - данные заполнения:
//    * Основание - ДокументСсылка.ЗаказПокупателя 
//    * МассивЗаказовПокупателей - Массив из ДокументСсылка.ЗаказПокупателя
//    * ДокументОснованиеПродажа - ДокументСсылка.ПриходнаяНакладная
//    * ДокументОснованиеВозврат - ДокументСсылка.ЗаказПоставщику, ДокументСсылка.ПриходнаяНакладная - основание возврата  
Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт

	Если ДанныеЗаполнения.Свойство("Основание") И ТипЗнч(ДанныеЗаполнения.Основание) = Тип(
		"ДокументСсылка.ЗаказПокупателя") И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Основание,
		"ВидОперации") = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя ввести Расходную накладную на основании заказ-наряда.'");
	КонецЕсли;

	Если ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения);
	КонецЕсли;

	Если ДанныеЗаполнения.Свойство("ДокументОснованиеПродажа") Тогда
		ЗаполнитьПоПриходнойНакладной(ДанныеЗаполнения.ДокументОснованиеПродажа, "Продажа");
	КонецЕсли;

	Если ДанныеЗаполнения.Свойство("ДокументОснованиеВозврат") Тогда
		Если ТипЗнч(ДанныеЗаполнения.ДокументОснованиеВозврат) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ВидОперацииОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.ДокументОснованиеВозврат,
				"ВидОперации");
			Если ВидОперацииОснования = Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаЗакупку Тогда
				ЗаполнитьПоЗаказуПоставщику(ДанныеЗаполнения.ДокументОснованиеВозврат);
			Иначе
				ВызватьИсключение НСтр("ru = 'Возврат можно ввести только на основании заказа на закупку.'");
			КонецЕсли;
		ИначеЕсли ТипЗнч(ДанныеЗаполнения.ДокументОснованиеВозврат) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ЗаполнитьПоПриходнойНакладной(ДанныеЗаполнения.ДокументОснованиеВозврат, "Возврат");
		Иначе
			ТекстИсключения = СтрШаблон(НСтр("ru = 'Нельзя ввести Расходную накладную на основании ""%1"".'"),
				ДанныеЗаполнения.ДокументОснованиеВозврат);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;

	Если ДанныеЗаполнения.Свойство("МассивДокументовКОтгрузке") Тогда
		ЗаполнитьПоМассивуРасходныхОрдеров(ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

// Процедура заполнения документа на основании массива расходных ордеров.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоМассивуРасходныхОрдеров(ДанныеЗаполнения) Экспорт
	
	// Заполнение шапки документа.

	МассивДокументов = ДанныеЗаполнения.МассивДокументовКОтгрузке;
	ДокументСсылка = МассивДокументов[0];

	РазрешитьСкладыВТабличныхЧастях = ПолучитьФункциональнуюОпцию("РазрешитьСкладыВТабличныхЧастях");

	Если Не РазрешитьСкладыВТабличныхЧастях Тогда
		ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	КонецЕсли;
	
	ПоложениеДокументаПоступления = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;

	ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	Организация = ДокументСсылка.Организация;
	СтруктурнаяЕдиница = ДокументСсылка.СтруктурнаяЕдиница;
	Ячейка = ДокументСсылка.Ячейка;
	Контрагент = ДокументСсылка.Контрагент;
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	ВидУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ВидУчетаОрдерныхСкладов");

	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоПрочимДокументам Тогда
		ТекстИсключения = НСтр("ru = 'Вид учета остатков на ордерном складе - по Расходным накладным.
							   |Ввод Расходной накладной на основании Расходного ордера недоступен.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

	Контрагент = ДокументСсылка.Контрагент;
	Договор = Справочники.ДоговорыКонтрагентов.ПоДокументуКонтрагентуОрганизацииИВидуОперации(Ссылка, Контрагент,
		Организация, ВидОперации);

	Если ЗначениеЗаполнено(Договор) Или Не ЗначениеЗаполнено(Контрагент) Тогда

		ДоговорСтруктура = Справочники.ДоговорыКонтрагентов.СтруктураДляЗаполненияПоДоговоруКонтрагента(Договор, Истина);
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДоговорСтруктура, ,
			"ВидОперации,СтруктурнаяЕдиница,Организация");

		СкладскойУчетСервер.ЗаполнитьТабличнуюЧастьДокументаСУчетомОстатковКлючевыеРеквизитыИдентичны(ЭтотОбъект,
			МассивДокументов, "РасходныйОрдер");

		СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

	КонецЕсли;

КонецПроцедуры 

// Обработчик заполнения документа на основании Приходной накладной
// 
// Параметры:
//  ДанныеЗаполнения - Структура - Основание для заполнения документа:
//    * Ссылка - ДокументСсылка.ПриходнаяНакладная
//  Операция - Строка - Операция
Процедура ЗаполнитьПоПриходнойНакладной(ДанныеЗаполнения, Операция = "") Экспорт
	
	// Заполнение шапки документа.
	Если Операция = "Продажа" Тогда
		Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика
			Или ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию Тогда
			ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
		Иначе
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Невозможен ввод операции ""Продажа покупателю"" на основании операции - ""%1"".'"),
				ДанныеЗаполнения.ВидОперации);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		Если НесколькоЗаказовПокупателей(ДанныеЗаполнения) Или Не УправлениеНебольшойФирмойПовтИсп.РеквизитВШапке(
			"ПоложениеЗаказаПокупателяВДокументахОтгрузки") Тогда
			ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
			ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		Иначе
			ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
			ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
	ИначеЕсли Операция = "Возврат" Тогда
		Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика Тогда
			ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику;
		ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию Тогда
			ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту;
		ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемВПереработку Тогда
			ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратИзПереработки;
		ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаОтветХранение Тогда
			ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратСОтветХранения;
		Иначе
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Невозможен ввод операции ""Возврат"" на основании операции - ""%1"".'"),
				ДанныеЗаполнения.ВидОперации);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		ПоложениеЗаказаПокупателя = ДанныеЗаполнения.ПоложениеЗаказаПоставщику;
	Иначе
		ПоложениеЗаказаПокупателя = ?(УправлениеНебольшойФирмойПовтИсп.РеквизитВШапке(
			"ПоложениеЗаказаПокупателяВДокументахОтгрузки"), Перечисления.ПоложениеРеквизитаНаФорме.ВШапке,
			Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	КонецЕсли;

	Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		Заказ = ДанныеЗаполнения.Заказ;
	Иначе
		Заказ = Неопределено;
	КонецЕсли;

	Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
		Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Ссылка, "Проект");
	КонецЕсли;

	ДокументОснование = ДанныеЗаполнения.Ссылка;
	Организация = ДанныеЗаполнения.Организация;

	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратИзПереработки Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратСОтветХранения Тогда
		Контрагент = ДанныеЗаполнения.Контрагент;
		Договор = ДанныеЗаполнения.Договор;
		НалогообложениеНДС = ДанныеЗаполнения.НалогообложениеНДС;
	Иначе
		НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , Дата);
	КонецЕсли;

	РеквизитыЗаказа = Неопределено;
	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику И ТипЗнч(ДанныеЗаполнения.Заказ)
		= Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.Заказ,
			"ЗаказПокупателя, ПоложениеЗаказаПокупателя");
	КонецЕсли;

	СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
	Ячейка = ДанныеЗаполнения.Ячейка;
	ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	НДСВключатьВСтоимость = ДанныеЗаполнения.НДСВключатьВСтоимость;

	Курс = ДанныеЗаполнения.Курс;
	Кратность = ДанныеЗаполнения.Кратность;

	ПоложениеСклада = ДанныеЗаполнения.ПоложениеСклада;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", ДанныеЗаполнения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РазмещениеЗаказов.ИсточникОбеспечения КАК ЗаказПоставщику,
	|	РазмещениеЗаказов.ЗаказПокупателя КАК ЗаказПокупателя,
	|	РазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|	РазмещениеЗаказов.Характеристика КАК Характеристика,
	|	СУММА(РазмещениеЗаказов.Количество) КАК Количество
	|ИЗ
	|	РегистрНакопления.РазмещениеЗаказов КАК РазмещениеЗаказов
	|ГДЕ
	|	РазмещениеЗаказов.Регистратор = &Регистратор
	|	И РазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|СГРУППИРОВАТЬ ПО
	|	РазмещениеЗаказов.ИсточникОбеспечения,
	|	РазмещениеЗаказов.ЗаказПокупателя,
	|	РазмещениеЗаказов.Номенклатура,
	|	РазмещениеЗаказов.Характеристика";
	ТаблицаОстаткиРазмещения = Запрос.Выполнить().Выгрузить();
	
	// Заполнение табличной части документа.
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл

		НоваяСтрока = Запасы.Добавить();

		Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти, , "Цена, Сумма, СуммаНДС, Всего");
		Иначе
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		КонецЕсли;

		Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику И ЗначениеЗаполнено(
			НоваяСтрока.ЗаказПокупателя) Тогда
			НоваяСтрока.Резерв = НоваяСтрока.Количество;
		КонецЕсли;

		Если ПолучитьФункциональнуюОпцию("УчетГТД") Тогда

			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти, "СтранаПроисхождения, НомерГТД");

		КонецЕсли;

		НоваяСтрока.ТипНоменклатурыЗапас = ЭтоТипНоменклатурыЗапас(НоваяСтрока.Номенклатура);

		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			НоваяСтрока.Заказ = Заказ;
		КонецЕсли;
		Если ВидОперации <> Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику
			Или Не ПолучитьФункциональнуюОпцию("РезервированиеЗапасов") Тогда
			НоваяСтрока.ЗаказПокупателя = Неопределено;
		ИначеЕсли РеквизитыЗаказа <> Неопределено И РеквизитыЗаказа.ПоложениеЗаказаПокупателя
			<> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			НоваяСтрока.ЗаказПокупателя = РеквизитыЗаказа.ЗаказПокупателя;
		КонецЕсли;

		Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Или ВидОперации
			<> Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
			Продолжить;
		КонецЕсли;

		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗаказПокупателя) Тогда
			НоваяСтрока.Заказ = СтрокаТабличнойЧасти.ЗаказПокупателя;
		ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Заказ) И ТипЗнч(НоваяСтрока.Заказ) = Тип(
			"ДокументСсылка.ЗаказПоставщику") Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ЗаказПоставщику", НоваяСтрока.Заказ);
			СтруктураОтбора.Вставить("Номенклатура", НоваяСтрока.Номенклатура);
			СтруктураОтбора.Вставить("Характеристика", НоваяСтрока.Характеристика);
			СтрокиОстатки = ТаблицаОстаткиРазмещения.НайтиСтроки(СтруктураОтбора);
			НоваяСтрока.Заказ = Неопределено;
			Если СтрокиОстатки.Количество() = 0 Тогда
				Продолжить;
			Иначе
				Коэффициент = КоэффициентЕдиницыИзмерения(НоваяСтрока.ЕдиницаИзмерения);
				КСписанию = НоваяСтрока.Количество * Коэффициент;
				Для Каждого СтрокаОстатков Из СтрокиОстатки Цикл
					Списать = Мин(СтрокаОстатков.Количество, КСписанию);
					Если Списать <= 0 Тогда
						Продолжить;
					КонецЕсли;
					СтрокаОстатков.Количество = СтрокаОстатков.Количество - Списать;
					КСписанию = КСписанию - Списать;
					Если КСписанию = 0 Тогда
						НоваяСтрока.Заказ = СтрокаОстатков.ЗаказПокупателя;
						Прервать;
					КонецЕсли;
					СтрокаЗаказ = Запасы.Вставить(Запасы.Индекс(НоваяСтрока));
					ЗаполнитьЗначенияСвойств(СтрокаЗаказ, НоваяСтрока);
					СтрокаЗаказ.Заказ = СтрокаОстатков.ЗаказПокупателя;
					СтрокаЗаказ.Количество = Списать / Коэффициент;
					НоваяСтрока.Количество = НоваяСтрока.Количество - СтрокаЗаказ.Количество;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;

		Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти И ЗначениеЗаполнено(
			НоваяСтрока.Заказ) Тогда
			НоваяСтрока.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Заказ, "Проект");
		КонецЕсли;

	КонецЦикла;
	
	// Прослеживаемость
	Если ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Или ВидОперации
		= Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту И ПолучитьФункциональнуюОпцию(
		"ВестиУчетПрослеживаемыхТоваров") Тогда
		СведенияПрослеживаемости.Загрузить(ДанныеЗаполнения.СведенияПрослеживаемости.Выгрузить());
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	// Конец Прослеживаемость

	Если НалогообложениеНДС <> ДанныеЗаполнения.НалогообложениеНДС Тогда

		Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда

			ПериодСтавкиНДС = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
			ОрганизацияВидСтавкиНДСПоУмолчанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация,
				"ВидСтавкиНДСПоУмолчанию");
			Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл

				НоменклатураВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТабличнойЧасти.Номенклатура,
					"ВидСтавкиНДС");
				Если ЗначениеЗаполнено(НоменклатураВидСтавкиНДС) Тогда
					СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(НоменклатураВидСтавкиНДС,
						ПериодСтавкиНДС);
				Иначе
					СтрокаТабличнойЧасти.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(ОрганизацияВидСтавкиНДСПоУмолчанию);
				КонецЕсли;

				СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
				СтрокаТабличнойЧасти.СуммаНДС = ?(СуммаВключаетНДС, СтрокаТабличнойЧасти.Сумма
					- (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100), СтрокаТабличнойЧасти.Сумма * СтавкаНДС
					/ 100);
				СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0,
					СтрокаТабличнойЧасти.СуммаНДС);

			КонецЦикла;

		Иначе

			Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
				СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
			Иначе
				СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
			КонецЕсли;

			Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл

				СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
				СтрокаТабличнойЧасти.СуммаНДС = 0;
				СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма;

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;

	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

КонецПроцедуры // ЗаполнитьПоПриходнаяНакладная()

//+mega
Процедура СкопироватьИзЗаказаПокупателя(ДанныеЗаполнения) Экспорт
	
	Если ЭтоВводНаОснованииЗаказНаряда(ДанныеЗаполнения) Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя ввести Расходную накладную на основании заказ-наряда.'");
	КонецЕсли;
	
	// Основание и настройка документа.
	МассивЗаказов = Новый Массив;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		МассивЗаказов = ДанныеЗаполнения.МассивЗаказовПокупателей;
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	Иначе
		МассивЗаказов.Добавить(ДанныеЗаполнения.Ссылка);
		ПоложениеЗаказаПокупателя = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки(
			"ПоложениеЗаказаПокупателяВДокументахОтгрузки");
		Если Не ЗначениеЗаполнено(ПоложениеЗаказаПокупателя) Тогда
			ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			Заказ = ДанныеЗаполнения;
		КонецЕсли;

		ПоложениеПроекта = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ПоложениеПроекта");
		Если Не ЗначениеЗаполнено(ПоложениеПроекта) Тогда
			ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;

	КонецЕсли;
	
	// Заполнение шапки.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателя.Ссылка КАК ОснованиеСсылка,
	|	ЗаказПокупателя.Проведен КАК ОснованиеПроведен,
	|	ЗаказПокупателя.СостояниеЗаказа КАК СостояниеЗаказа,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
	|			ТОГДА ЗаказПокупателя.Организация.БанковскийСчетПоУмолчанию
	|		ИНАЧЕ ЗаказПокупателя.БанковскийСчет
	|	КОНЕЦ КАК БанковскийСчет,
	|	ЗаказПокупателя.ПоложениеСклада КАК ПоложениеСклада,
	|	ВЫБОР
	|		КОГДА РезервированиеЗапасов.Значение
	|				ИЛИ ЗаказПокупателя.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателя.СтруктурнаяЕдиницаПродажи КАК Подразделение,
	|	ЗаказПокупателя.Контрагент КАК Контрагент,
	|	ЗаказПокупателя.Контрагент.КонтактноеЛицоПодписант КАК КонтактноеЛицоПодписант,
	|	ЗаказПокупателя.Договор КАК Договор,
	|	ЗаказПокупателя.ОснованиеПечатиСсылка КАК ОснованиеПечатиСсылка,
	|	ЗаказПокупателя.ОснованиеПечати КАК ОснованиеПечати,
	|	ЗаказПокупателя.ВидЦен КАК ВидЦен,
	|	ЗаказПокупателя.ВидСкидкиНаценки КАК ВидСкидкиНаценки,
	|	ЗаказПокупателя.ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	ЗаказПокупателя.ПроцентСкидкиПоДисконтнойКарте КАК ПроцентСкидкиПоДисконтнойКарте,
	|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказПокупателя.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ЗаказПокупателя.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ЗаказПокупателя.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ЗаказПокупателя.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
	|			ТОГДА ЗаказПокупателя.Курс
	|		ИНАЧЕ КурсыВалютСрезПоследних.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
	|			ТОГДА ЗаказПокупателя.Кратность
	|		ИНАЧЕ КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Кратность,
	|	ЗаказПокупателя.Вес КАК Вес,
	|	ЗаказПокупателя.Объем КАК Объем,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СпособДоставки В (ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.Самовывоз))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИНАЧЕ ЗаказПокупателя.НоменклатураДоставки
	|	КОНЕЦ КАК НоменклатураДоставки,
	|	ЗаказПокупателя.СтоимостьДоставки КАК СтоимостьДоставки,
	|	ЗаказПокупателя.СтавкаНДСДоставки КАК СтавкаНДСДоставки,
	|	ЗаказПокупателя.СуммаНДСДоставки КАК СуммаНДСДоставки,
	|	ЗаказПокупателя.СпособЗачетаПредоплаты КАК СпособЗачетаПредоплаты,
	|	ЗаказПокупателя.Грузоотправитель КАК Грузоотправитель,
	|	ЗаказПокупателя.Грузополучатель КАК Грузополучатель,
	|	ЗаказПокупателя.АдресДоставки КАК АдресДоставки,
	|	ЗаказПокупателя.Проект КАК Проект,
	|	ЗаказПокупателя.УсловияСчетаЗаказа КАК УсловияСчетаЗаказа,
	|	ЗаказПокупателя.ТекстУсловийСчетаЗаказа КАК ТекстУсловийСчетаЗаказа,
	|	ЗаказПокупателя.УсловияГарантийногоТалона КАК УсловияГарантийногоТалона,
	|	ЗаказПокупателя.ТекстУсловийГарантийногоТалона КАК ТекстУсловийГарантийногоТалона,
	|	ЗаказПокупателя.СпециальныйНалоговыйРежим КАК СпециальныйНалоговыйРежим
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследних
	|		ПО ЗаказПокупателя.Договор.ВалютаРасчетов = КурсыВалютСрезПоследних.Валюта},
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК РезервированиеЗапасов
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&МассивЗаказов)";

	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ДатаДокумента", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));

	СтруктураИтогов = Новый Структура;
	СтруктураИтогов.Вставить("НоменклатураДоставки");
	СтруктураИтогов.Вставить("СтоимостьДоставки", 0);
	СтруктураИтогов.Вставить("СтавкаНДСДоставки");
	СтруктураИтогов.Вставить("СуммаНДСДоставки", 0);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияПроверяемыхРеквизитов = Новый Структура("СостояниеЗаказа, Проведен, ОжидаетсяВыборВариантаКП",
			Выборка.СостояниеЗаказа, Выборка.ОснованиеПроведен, Выборка.ОжидаетсяВыборВариантаКП);
		Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(Выборка.ОснованиеСсылка,
			ЗначенияПроверяемыхРеквизитов);
		Если ЗначениеЗаполнено(Выборка.НоменклатураДоставки) Тогда
			ЗаполнитьЗначенияСвойств(СтруктураИтогов, Выборка, "НоменклатураДоставки, СтавкаНДСДоставки");
			СтруктураИтогов.СтоимостьДоставки = СтруктураИтогов.СтоимостьДоставки + Выборка.СтоимостьДоставки;
			СтруктураИтогов.СуммаНДСДоставки = СтруктураИтогов.СуммаНДСДоставки + Выборка.СуммаНДСДоставки;
		КонецЕсли;
	КонецЦикла;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураИтогов);

	Если Не ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойСклад");
		Если Не ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
		КонецЕсли;
	КонецЕсли;
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|				ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТипНоменклатурыЗапас,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ЗаказПокупателяЗапасы.КоличествоСобрано КАК КоличествоСобрано,
	|	ЗаказПокупателяЗапасы.ЕстьСборка КАК ЕстьСборка,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Цена КАК Цена,
	|	ЗаказПокупателяЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.Сумма КАК Сумма,
	|	ЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПокупателяЗапасы.СуммаНДС КАК СуммаНДС,
	|	ЗаказПокупателяЗапасы.Всего КАК Всего,
	|	ЗаказПокупателяЗапасы.Ссылка КАК Заказ,
	|	ЗаказПокупателяЗапасы.Ссылка.Проект КАК Проект,
	|	ЗаказПокупателяЗапасы.Содержание КАК Содержание,
	|	ЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиОплатыБонусом КАК СуммаСкидкиОплатыБонусом,
	|	ЗаказПокупателяЗапасы.ПродажаПодарка КАК ПродажаПодарка,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ЗаказПокупателяЗапасы.Вес КАК Вес,
	|	ЗаказПокупателяЗапасы.Объем КАК Объем,
	|	ЗаказПокупателяЗапасы.Номенклатура.Вес КАК ВесЕдиницыТовара,
	|	ЗаказПокупателяЗапасы.Номенклатура.Объем КАК ОбъемЕдиницыТовара,
	|	ВЫБОР
	|		КОГДА &РезервированиеЗапасов
	|					И ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				ИЛИ ЗаказПокупателяЗапасы.Ссылка.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Номенклатура.Склад
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателяЗапасы.Ссылка.УчетПотребностиПоСкладам КАК УчетПотребностиПоСкладам,
	|	ЗаказПокупателяЗапасы.Номенклатура.Ячейка КАК Ячейка,
	|	ЗаказПокупателяЗапасы.Номенклатура.Ячейка.Владелец КАК СтруктурнаяЕдиницаЯчейки,
	|	ЗаказПокупателяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ЗаказПокупателяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЗаказПокупателяЗапасы.ДоляСтоимости КАК ДоляСтоимости,
	|	ЗаказПокупателяЗапасы.Номенклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ЗаказПокупателяЗапасы.Номенклатура.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	(ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ИЛИ ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И НЕ ЗаказПокупателяЗапасы.ПродажаПодарка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(МассивЗаказов,
		Запрос.МенеджерВременныхТаблиц, Ложь);

	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("РезервированиеЗапасов", Ложь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Запасы.Добавить(), ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Вес = Запасы.Итог("Вес");
	Объем = Запасы.Итог("Объем");
	
	ДоговорВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора");
	Если ЗначениеЗаполнено(Договор) И ДоговорВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию;
		
		Для Каждого СтрокаЗапасы Из Запасы Цикл
			Если НЕ СтрокаЗапасы.Партия.Статус = Перечисления.СтатусыПартий.СобственныеЗапасы Тогда
				СтрокаЗапасы.Партия = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры 

Функция ЗаполнитьСтруктурныеЕдиницыТабличнойЧастиЗапасы()
	
	ТаблицаЗначений = Запасы.Выгрузить();
	ТаблицаЗначений.Свернуть("Номенклатура");	
	Номенклатура = ТаблицаЗначений.ВыгрузитьКолонку("Номенклатура"); 
	
	ДатаСреза = ?(Ссылка.Пустая(), ТекущаяДатаСеанса(), Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
		|	МИНИМУМ(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗапасыНаСкладах.Остатки(&ДатаСреза, Номенклатура В (&Номенклатура)) КАК ЗапасыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыОстатки.СтруктурнаяЕдиница,
		|	ЗапасыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	КоличествоОстаток УБЫВ,
		|	СтруктурнаяЕдиница";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	БылиИзменения = Ложь;
	Для Каждого ТекСтрока Из Запасы Цикл 
		
		ВыборкаДетальныеЗаписи.Сбросить();
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Новый Структура("Номенклатура", ТекСтрока.Номенклатура)) Тогда 
			
			ТекСтрока.СтруктурнаяЕдиница = ВыборкаДетальныеЗаписи.СтруктурнаяЕдиница;
			БылиИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат БылиИзменения;
КонецФункции

//-mega

// Обработчик заполнения на основании Заказа покупателя.
//
// Параметры:
//  ДанныеЗаполнения - Структура, ДокументСсылка.ПриходнаяНакладная - Основание для заполнения документа.
//
Процедура ЗаполнитьПоЗаказуПокупателя(ДанныеЗаполнения) Экспорт
	
	//+mega
	СкопироватьИзЗаказаПокупателя(ДанныеЗаполнения);
	ЗаполнитьСтруктурныеЕдиницыТабличнойЧастиЗапасы();
	Возврат;
	//-mega
	
	Если ЭтоВводНаОснованииЗаказНаряда(ДанныеЗаполнения) Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя ввести Расходную накладную на основании заказ-наряда.'");
	КонецЕсли;
	
	// Основание и настройка документа.
	МассивЗаказов = Новый Массив;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("МассивЗаказовПокупателей") Тогда
		МассивЗаказов = ДанныеЗаполнения.МассивЗаказовПокупателей;
		ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
		ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
	Иначе
		МассивЗаказов.Добавить(ДанныеЗаполнения.Ссылка);
		ПоложениеЗаказаПокупателя = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки(
			"ПоложениеЗаказаПокупателяВДокументахОтгрузки");
		Если Не ЗначениеЗаполнено(ПоложениеЗаказаПокупателя) Тогда
			ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			Заказ = ДанныеЗаполнения;
		КонецЕсли;

		ПоложениеПроекта = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ПоложениеПроекта");
		Если Не ЗначениеЗаполнено(ПоложениеПроекта) Тогда
			ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
		КонецЕсли;
		Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;

	КонецЕсли;
	
	// Заполнение шапки.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателя.Ссылка КАК ОснованиеСсылка,
	|	ЗаказПокупателя.Проведен КАК ОснованиеПроведен,
	|	ЗаказПокупателя.СостояниеЗаказа КАК СостояниеЗаказа,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
	|			ТОГДА ЗаказПокупателя.Организация.БанковскийСчетПоУмолчанию
	|		ИНАЧЕ ЗаказПокупателя.БанковскийСчет
	|	КОНЕЦ КАК БанковскийСчет,
	|	ЗаказПокупателя.ПоложениеСклада КАК ПоложениеСклада,
	|	ВЫБОР
	|		КОГДА РезервированиеЗапасов.Значение
	|				ИЛИ ЗаказПокупателя.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателя.СтруктурнаяЕдиницаПродажи КАК Подразделение,
	|	ЗаказПокупателя.Контрагент КАК Контрагент,
	|	ЗаказПокупателя.Контрагент.КонтактноеЛицоПодписант КАК КонтактноеЛицоПодписант,
	|	ЗаказПокупателя.Договор КАК Договор,
	|	ЗаказПокупателя.ОснованиеПечатиСсылка КАК ОснованиеПечатиСсылка,
	|	ЗаказПокупателя.ОснованиеПечати КАК ОснованиеПечати,
	|	ЗаказПокупателя.ВидЦен КАК ВидЦен,
	|	ЗаказПокупателя.ВидСкидкиНаценки КАК ВидСкидкиНаценки,
	|	ЗаказПокупателя.ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	ЗаказПокупателя.ПроцентСкидкиПоДисконтнойКарте КАК ПроцентСкидкиПоДисконтнойКарте,
	|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказПокупателя.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ЗаказПокупателя.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ЗаказПокупателя.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ЗаказПокупателя.ОжидаетсяВыборВариантаКП КАК ОжидаетсяВыборВариантаКП,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
	|			ТОГДА ЗаказПокупателя.Курс
	|		ИНАЧЕ КурсыВалютСрезПоследних.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
	|			ТОГДА ЗаказПокупателя.Кратность
	|		ИНАЧЕ КурсыВалютСрезПоследних.Кратность
	|	КОНЕЦ КАК Кратность,
	|	ЗаказПокупателя.Вес КАК Вес,
	|	ЗаказПокупателя.Объем КАК Объем,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателя.СпособДоставки В (ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.Самовывоз))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИНАЧЕ ЗаказПокупателя.НоменклатураДоставки
	|	КОНЕЦ КАК НоменклатураДоставки,
	|	ЗаказПокупателя.СтоимостьДоставки КАК СтоимостьДоставки,
	|	ЗаказПокупателя.СтавкаНДСДоставки КАК СтавкаНДСДоставки,
	|	ЗаказПокупателя.СуммаНДСДоставки КАК СуммаНДСДоставки,
	|	ЗаказПокупателя.СпособЗачетаПредоплаты КАК СпособЗачетаПредоплаты,
	|	ЗаказПокупателя.Грузоотправитель КАК Грузоотправитель,
	|	ЗаказПокупателя.Грузополучатель КАК Грузополучатель,
	|	ЗаказПокупателя.АдресДоставки КАК АдресДоставки,
	|	ЗаказПокупателя.Проект КАК Проект,
	|	ЗаказПокупателя.УсловияСчетаЗаказа КАК УсловияСчетаЗаказа,
	|	ЗаказПокупателя.ТекстУсловийСчетаЗаказа КАК ТекстУсловийСчетаЗаказа,
	|	ЗаказПокупателя.УсловияГарантийногоТалона КАК УсловияГарантийногоТалона,
	|	ЗаказПокупателя.ТекстУсловийГарантийногоТалона КАК ТекстУсловийГарантийногоТалона,
	|	ЗаказПокупателя.СпециальныйНалоговыйРежим КАК СпециальныйНалоговыйРежим
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследних
	|		ПО ЗаказПокупателя.Договор.ВалютаРасчетов = КурсыВалютСрезПоследних.Валюта},
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК РезервированиеЗапасов
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&МассивЗаказов)";

	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ДатаДокумента", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));

	СтруктураИтогов = Новый Структура;
	СтруктураИтогов.Вставить("НоменклатураДоставки");
	СтруктураИтогов.Вставить("СтоимостьДоставки", 0);
	СтруктураИтогов.Вставить("СтавкаНДСДоставки");
	СтруктураИтогов.Вставить("СуммаНДСДоставки", 0);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияПроверяемыхРеквизитов = Новый Структура("СостояниеЗаказа, Проведен, ОжидаетсяВыборВариантаКП",
			Выборка.СостояниеЗаказа, Выборка.ОснованиеПроведен, Выборка.ОжидаетсяВыборВариантаКП);
		Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(Выборка.ОснованиеСсылка,
			ЗначенияПроверяемыхРеквизитов);
		Если ЗначениеЗаполнено(Выборка.НоменклатураДоставки) Тогда
			ЗаполнитьЗначенияСвойств(СтруктураИтогов, Выборка, "НоменклатураДоставки, СтавкаНДСДоставки");
			СтруктураИтогов.СтоимостьДоставки = СтруктураИтогов.СтоимостьДоставки + Выборка.СтоимостьДоставки;
			СтруктураИтогов.СуммаНДСДоставки = СтруктураИтогов.СуммаНДСДоставки + Выборка.СуммаНДСДоставки;
		КонецЕсли;
	КонецЦикла;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураИтогов);

	Если Не ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойСклад");
		Если Не ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнение табличной части.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.Склад КАК Склад,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ	
	|	(ВЫБРАТЬ
	|		ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|		ЗаказыОстатки.Характеристика КАК Характеристика,
	|		ЗаказыОстатки.Склад КАК Склад,
	|		ЗаказыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей.Остатки(
	|				,
	|				ЗаказПокупателя В (&МассивЗаказов)
	|					И (Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|						ИЛИ Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|						ИЛИ Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))) КАК ЗаказыОстатки	
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗаказыПокупателей.ЗаказПокупателя,
	|		ДвиженияДокументаЗаказыПокупателей.Номенклатура,
	|		ДвиженияДокументаЗаказыПокупателей.Характеристика,
	|		ДвиженияДокументаЗаказыПокупателей.Склад,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗаказыПокупателей.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗаказыПокупателей.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыПокупателей КАК ДвиженияДокументаЗаказыПокупателей
	|	ГДЕ
	|		ДвиженияДокументаЗаказыПокупателей.Регистратор = &Ссылка) КАК ЗаказыОстатки	
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.Склад
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|				ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТипНоменклатурыЗапас,
	|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПокупателяЗапасы.Количество КАК Количество,
	|	ЗаказПокупателяЗапасы.КоличествоСобрано КАК КоличествоСобрано,
	|	ЗаказПокупателяЗапасы.ЕстьСборка КАК ЕстьСборка,
	|	ЗаказПокупателяЗапасы.Партия КАК Партия,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяЗапасы.Цена КАК Цена,
	|	ЗаказПокупателяЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	ЗаказПокупателяЗапасы.Сумма КАК Сумма,
	|	ЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПокупателяЗапасы.СуммаНДС КАК СуммаНДС,
	|	ЗаказПокупателяЗапасы.Всего КАК Всего,
	|	ЗаказПокупателяЗапасы.Ссылка КАК Заказ,
	|	ЗаказПокупателяЗапасы.Ссылка.Проект КАК Проект,
	|	ЗаказПокупателяЗапасы.Содержание КАК Содержание,
	|	ЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ЗаказПокупателяЗапасы.СуммаСкидкиОплатыБонусом КАК СуммаСкидкиОплатыБонусом,
	|	ЗаказПокупателяЗапасы.ПродажаПодарка КАК ПродажаПодарка,
	|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
	|	ЗаказПокупателяЗапасы.Вес КАК Вес,
	|	ЗаказПокупателяЗапасы.Объем КАК Объем,
	|	ЗаказПокупателяЗапасы.Номенклатура.Вес КАК ВесЕдиницыТовара,
	|	ЗаказПокупателяЗапасы.Номенклатура.Объем КАК ОбъемЕдиницыТовара,
	|	ВЫБОР
	|		КОГДА &РезервированиеЗапасов
	|					И ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|				ИЛИ ЗаказПокупателяЗапасы.Ссылка.УчетПотребностиПоСкладам
	|			ТОГДА ЗаказПокупателяЗапасы.СтруктурнаяЕдиницаРезерв
	|		ИНАЧЕ ЗаказПокупателяЗапасы.Номенклатура.Склад
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ЗаказПокупателяЗапасы.Ссылка.УчетПотребностиПоСкладам КАК УчетПотребностиПоСкладам,
	|	ЗаказПокупателяЗапасы.Номенклатура.Ячейка КАК Ячейка,
	|	ЗаказПокупателяЗапасы.Номенклатура.Ячейка.Владелец КАК СтруктурнаяЕдиницаЯчейки,
	|	ЗаказПокупателяЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	ЗаказПокупателяЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ЗаказПокупателяЗапасы.ДоляСтоимости КАК ДоляСтоимости,
	|	ЗаказПокупателяЗапасы.Номенклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ЗаказПокупателяЗапасы.Номенклатура.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|ГДЕ
	|	(ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ИЛИ ЗаказПокупателяЗапасы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И НЕ ЗаказПокупателяЗапасы.ПродажаПодарка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкидкиНаценки.Ссылка КАК Заказ,
	|	СкидкиНаценки.КлючСвязи КАК КлючСвязи,
	|	СкидкиНаценки.СкидкаНаценка КАК СкидкаНаценка,
	|	СкидкиНаценки.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаказПокупателя.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	СкидкиНаценки.Ссылка В(&МассивЗаказов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.Ссылка КАК Заказ,
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ПОМЕСТИТЬ ВТДобавленныеНаборы
	|ИЗ
	|	Документ.ЗаказПокупателя.ДобавленныеНаборы КАК ДобавленныеНаборы
	|ГДЕ
	|	ДобавленныеНаборы.Ссылка В(&МассивЗаказов)
	|	И ДобавленныеНаборы.НомерВариантаКП = ДобавленныеНаборы.Ссылка.ОсновнойВариантКП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.Заказ КАК Заказ,
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ИЗ
	|	ВТДобавленныеНаборы КАК ДобавленныеНаборы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяПредоплата.Ссылка КАК Ссылка,
	|	ЗаказПокупателяПредоплата.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяПредоплата.Документ КАК Документ,
	|	ЗаказПокупателяПредоплата.НомерСертификата КАК НомерСертификата,
	|	ЗаказПокупателяПредоплата.СуммаРасчетов КАК СуммаРасчетов,
	|	ЗаказПокупателяПредоплата.Курс КАК Курс,
	|	ЗаказПокупателяПредоплата.Кратность КАК Кратность,
	|	ЗаказПокупателяПредоплата.СуммаПлатежа КАК СуммаПлатежа,
	|	ЗаказПокупателяПредоплата.ОплатаБонусами КАК ОплатаБонусами,
	|	ЗаказПокупателяПредоплата.ОплатаСертификатом КАК ОплатаСертификатом,
	|	ЗаказПокупателяПредоплата.Ссылка КАК Заказ
	|ИЗ
	|	Документ.ЗаказПокупателя.Предоплата КАК ЗаказПокупателяПредоплата
	|ГДЕ
	|	ЗаказПокупателяПредоплата.Ссылка В(&МассивЗаказов)
	|	И НЕ ЗаказПокупателяПредоплата.ОплатаБонусами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателяПредоплата.Ссылка КАК Ссылка,
	|	ЗаказПокупателяПредоплата.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяПредоплата.Документ КАК Документ,
	|	ЗаказПокупателяПредоплата.НомерСертификата КАК НомерСертификата,
	|	ЗаказПокупателяПредоплата.СуммаРасчетов КАК СуммаРасчетов,
	|	ЗаказПокупателяПредоплата.Курс КАК Курс,
	|	ЗаказПокупателяПредоплата.Кратность КАК Кратность,
	|	ЗаказПокупателяПредоплата.СуммаПлатежа КАК СуммаПлатежа,
	|	ЗаказПокупателяПредоплата.ОплатаБонусами КАК ОплатаБонусами,
	|	ЗаказПокупателяПредоплата.ОплатаСертификатом КАК ОплатаСертификатом,
	|	ЗаказПокупателяПредоплата.Ссылка КАК Заказ
	|ИЗ
	|	Документ.ЗаказПокупателя.Предоплата КАК ЗаказПокупателяПредоплата
	|ГДЕ
	|	ЗаказПокупателяПредоплата.Ссылка В(&МассивЗаказов)
	|	И ЗаказПокупателяПредоплата.ОплатаБонусами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.Заказ КАК Заказ,
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ИЗ
	|	ВТДобавленныеНаборы КАК ДобавленныеНаборы
	|ГДЕ
	|	ДобавленныеНаборы.НоменклатураНабора.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПримененныеПромокоды.Промокод КАК Промокод,
	|	ПримененныеПромокоды.УсловиеПредоставленияСкидки КАК УсловиеПредоставленияСкидки,
	|	ПримененныеПромокоды.СкидкаПредоставлена КАК СкидкаПредоставлена,
	|	ПримененныеПромокоды.ЭтоАктивацияПромокода КАК ЭтоАктивацияПромокода
	|ИЗ
	|	Документ.ЗаказПокупателя.ПримененныеПромокоды КАК ПримененныеПромокоды
	|ГДЕ
	|	ПримененныеПромокоды.Ссылка В(&МассивЗаказов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыбранныеУправляемыеСкидки.СкидкаНаценка КАК СкидкаНаценка,
	|	ВыбранныеУправляемыеСкидки.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ЗаказПокупателя.ВыбранныеУправляемыеСкидки КАК ВыбранныеУправляемыеСкидки
	|ГДЕ
	|	ВыбранныеУправляемыеСкидки.Ссылка В(&МассивЗаказов)
	|		И ВыбранныеУправляемыеСкидки.НомерВариантаКП = ВыбранныеУправляемыеСкидки.Ссылка.ОсновнойВариантКП";

	Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(МассивЗаказов,
		Запрос.МенеджерВременныхТаблиц, Ложь);

	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("РезервированиеЗапасов", ПолучитьФункциональнуюОпцию("РезервированиеЗапасов"));

	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[0].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя,Номенклатура,Характеристика");
	
	// АвтоматическиеСкидки.
	ИспользоватьАвтоматическиеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки");
	Если ИспользоватьАвтоматическиеСкидки Тогда
		СкидкиНаценки.Очистить();
	КонецЕсли;
	// Конец АвтоматическиеСкидки.
	
	// Доставка
	Если ЗначениеЗаполнено(НоменклатураДоставки) И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Номенклатура", НоменклатураДоставки);
		СтруктураДляПоиска.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", ДанныеЗаполнения);
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
		Если МассивСтрокОстатков.Количество() = 0 Тогда
			НоменклатураДоставки = Неопределено;
			СтоимостьДоставки = 0;
			СуммаНДСДоставки = 0;
		КонецЕсли;
	КонецЕсли; 
	// Конец Доставка
	
	// Наборы
	ПропускаемыеНаборы = Новый Массив;
	ВыборкаИсключаемыеНаборы = МассивРезультатов[7].Выбрать();
	Пока ВыборкаИсключаемыеНаборы.Следующий() Цикл
		ДобавитьОписаниеНабора(ПропускаемыеНаборы, ВыборкаИсключаемыеНаборы.Заказ,
			ВыборкаИсключаемыеНаборы.НоменклатураНабора, ВыборкаИсключаемыеНаборы.ХарактеристикаНабора);
	КонецЦикла; 
	// Конец Наборы

	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	// Ключ связи нужно поменять, т.к. накладную можно вводить на основании сразу двух заказов.
	// В этом случае ключи связи могут дублироваться.
	ТекКлючСвязи = 0;

	Если ТаблицаОстатков.Количество() > 0 Тогда

		Выборка = МассивРезультатов[1].Выбрать();
		Пока Выборка.Следующий() Цикл

			СтруктураДляПоиска = Новый Структура;
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.Заказ);
			СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
			СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
			Если Выборка.УчетПотребностиПоСкладам И ЗначениеЗаполнено(Выборка.СтруктурнаяЕдиница) Тогда
				СтруктураДляПоиска.Вставить("Склад", Выборка.СтруктурнаяЕдиница);
			КонецЕсли;

			МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			Если МассивСтрокОстатков.Количество() = 0 И Выборка.УчетПотребностиПоСкладам Тогда
				// Попытка поиска остатка по другому складу
				СтруктураДляПоиска.Удалить("Склад");
				МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
			КонецЕсли;
			Если МассивСтрокОстатков.Количество() = 0 Тогда
				// Наборы
				Если ЗначениеЗаполнено(Выборка.НоменклатураНабора) Тогда
					ДобавитьОписаниеНабора(ПропускаемыеНаборы, Выборка.Заказ, Выборка.НоменклатураНабора,
						Выборка.ХарактеристикаНабора);
				КонецЕсли;
				// Конец Наборы
				Продолжить;
			КонецЕсли;

			ОстатокКОтгрузке = Выборка.Количество;
			ОстатокСобрано = Выборка.КоличествоСобрано;

			Для Каждого СтрокаОстаток Из МассивСтрокОстатков Цикл

				Если СтрокаОстаток.КоличествоОстаток = 0 Тогда
					Продолжить;
				КонецЕсли;

				Если ПолучитьФункциональнуюОпцию("ИспользоватьСборкуЗаказов") И Выборка.ЕстьСборка Тогда
					Если Выборка.СтруктурнаяЕдиница = СтрокаОстаток.Склад Тогда
						УжеОтгружено = ОстатокКОтгрузке - СтрокаОстаток.КоличествоОстаток;
					Иначе
						УжеОтгружено = 0;
					КонецЕсли;
					СобраноНеОтгружено = ОстатокСобрано - ?(УжеОтгружено > 0, УжеОтгружено, 0);
					Если СобраноНеОтгружено > 0 Тогда
						ВыборкаКоличество = СобраноНеОтгружено;
					Иначе
						ВыборкаКоличество = 0;
						// Наборы
						Если ЗначениеЗаполнено(Выборка.НоменклатураНабора) Тогда
							ДобавитьОписаниеНабора(ПропускаемыеНаборы, Выборка.Заказ, Выборка.НоменклатураНабора,
								Выборка.ХарактеристикаНабора);
						КонецЕсли;
						// Конец Наборы
						Продолжить;
					КонецЕсли;
				Иначе
					ВыборкаКоличество = ОстатокКОтгрузке;
				КонецЕсли;

				НоваяСтрока = Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				Если Выборка.УчетПотребностиПоСкладам Тогда
					НоваяСтрока.СтруктурнаяЕдиница = СтрокаОстаток.Склад;
				КонецЕсли;

				Если ПоложениеПроекта <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
					НоваяСтрока.Проект = Проект;
				КонецЕсли;
				
				// Ключ связи нужно поменять, т.к. накладную можно вводить на основании сразу двух заказов.
				// В этом случае ключи связи могут дублироваться.
				ТекКлючСвязи = ТекКлючСвязи + 1;
				НоваяСтрока.КлючСвязи = ТекКлючСвязи;

				КоличествоКСписанию = Окр(ВыборкаКоличество * Выборка.Коэффициент, 3);
				СтрокаОстаток.КоличествоОстаток = СтрокаОстаток.КоличествоОстаток - КоличествоКСписанию;

				Если СтрокаОстаток.КоличествоОстаток < 0 Или ВыборкаКоличество <> ОстатокКОтгрузке Тогда
					
					// Наборы
					Если ЗначениеЗаполнено(Выборка.НоменклатураНабора) Тогда
						ДобавитьОписаниеНабора(ПропускаемыеНаборы, Выборка.Заказ, Выборка.НоменклатураНабора,
							Выборка.ХарактеристикаНабора);
					КонецЕсли;
					// Конец Наборы

					Если СтрокаОстаток.КоличествоОстаток < 0 Тогда
						КоличествоКСписанию = Окр((КоличествоКСписанию + СтрокаОстаток.КоличествоОстаток)
							/ Выборка.Коэффициент, 3);
						СтрокаОстаток.КоличествоОстаток = 0;
					Иначе
						КоличествоКСписанию = Окр(КоличествоКСписанию / Выборка.Коэффициент, 3);
					КонецЕсли;

					ДанныеСтроки = Новый Структура;
					ДанныеСтроки.Вставить("Количество", КоличествоКСписанию);
					ДанныеСтроки.Вставить("Цена", Выборка.Цена);
					ДанныеСтроки.Вставить("Сумма", 0);
					ДанныеСтроки.Вставить("ПроцентСкидкиНаценки", Выборка.ПроцентСкидкиНаценки);
					ДанныеСтроки.Вставить("СуммаСкидкиНаценки", 0);
					ДанныеСтроки.Вставить("СтавкаНДС", Выборка.СтавкаНДС);
					ДанныеСтроки.Вставить("СуммаНДС", 0);
					ДанныеСтроки.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
					ДанныеСтроки.Вставить("Всего", 0);
					ДанныеСтроки.Вставить("Вес", Выборка.ВесЕдиницыТовара * КоличествоКСписанию);
					ДанныеСтроки.Вставить("Объем", Выборка.ОбъемЕдиницыТовара * КоличествоКСписанию);

					ЗаполнениеОбъектовУНФ.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);

					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);

				КонецЕсли;

				Если Не ЗначениеЗаполнено(НоваяСтрока.СтруктурнаяЕдиница) Тогда
					ЗначениеПоУмолчанию = Справочники.СтруктурныеЕдиницы.ОсновнойСклад();
					НоваяСтрока.СтруктурнаяЕдиница = ?(ЗначениеЗаполнено(СтруктурнаяЕдиница), СтруктурнаяЕдиница,
						ЗначениеПоУмолчанию);
				КонецЕсли;

				Если Выборка.СтруктурнаяЕдиницаЯчейки <> НоваяСтрока.СтруктурнаяЕдиница Тогда
					НоваяСтрока.Ячейка = Неопределено;
				КонецЕсли;

				ОстатокКОтгрузке = ОстатокКОтгрузке - КоличествоКСписанию;
				ОстатокСобрано = ОстатокСобрано - КоличествоКСписанию;
				Если ОстатокКОтгрузке <= 0 Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;

		КонецЦикла;

	КонецЕсли;

	Вес = Запасы.Итог("Вес");
	Объем = Запасы.Итог("Объем");
	
	// Наборы
	// Удаление неполных и неподходящих наборов
	Для Каждого ОписаниеНабора Из ПропускаемыеНаборы Цикл
		УдаляемыеСтроки = Запасы.НайтиСтроки(ОписаниеНабора);
		Для Каждого СтрокаЗапаса Из УдаляемыеСтроки Цикл
			Для Каждого СтрокаСкидки Из СкидкиНаценки.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаЗапаса.КлючСвязи)) Цикл
				СкидкиНаценки.Удалить(СтрокаСкидки);
			КонецЦикла;
			Запасы.Удалить(СтрокаЗапаса);
		КонецЦикла;
	КонецЦикла;

	ДобавленныеНаборы.Очистить();
	ВыборкаНаборы = МассивРезультатов[4].Выбрать();
	Пока ВыборкаНаборы.Следующий() Цикл
		Если ПропуститьНабор(ПропускаемыеНаборы, ВыборкаНаборы) Тогда
			Продолжить;
		КонецЕсли;
		ДобавитьНаборы(ВыборкаНаборы);
	КонецЦикла; 
	// Конец Наборы
	
	// Заполнение резервов.
	Если Запасы.Количество() > 0 И Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() И ЗначениеЗаполнено(
		СтруктурнаяЕдиница) Тогда
		ЗаполнитьКолонкуРезервПоРезервам();
	КонецЕсли;
	
	// Взаиморасчеты
	Предоплата.Очистить();
	Предоплата.Загрузить(МассивРезультатов[5].Выгрузить());

	// АвтоматическиеСкидки.
	Если ИспользоватьАвтоматическиеСкидки Тогда
		// Управляемые скидки
		ТаблицаВыбранныеУправляемыеСкидки = МассивРезультатов[9].Выгрузить();
		КлючиСвязиДокумента = Запасы.ВыгрузитьКолонку("КлючСвязи");
		КлючиСвязиДокумента.Добавить(0); // Скидки, действующие на документ целиком
		СтрокиПеренести = Новый Массив;
		Для Каждого СтрокаУправляемойСкидки Из ТаблицаВыбранныеУправляемыеСкидки Цикл
			Если Не КлючиСвязиДокумента.Найти(СтрокаУправляемойСкидки.КлючСвязи) = Неопределено Тогда
				СтрокиПеренести.Добавить(СтрокаУправляемойСкидки);
			КонецЕсли;
		КонецЦикла;
		ВыбранныеУправляемыеСкидки.Загрузить(ТаблицаВыбранныеУправляемыеСкидки.Скопировать(СтрокиПеренести));
		
		// Промокоды
		ТаблицаПримененныеПромокоды = МассивРезультатов[8].Выгрузить();
		// Если в заказе выдается промокод, но заказ еще не отгружен полностью,
		// Скидка с выдачей промокода не применится (она применяется только для последней отгрузки по заказу)
		// В таком случае, промокод при расчете скидок будет удален из ТЧ.
		ПримененныеПромокоды.Загрузить(ТаблицаПримененныеПромокоды);
		РасчетныеСкидки = СкидкиНаценкиСерверПереопределяемый.ПолучитьСкидкиНаценкиПоЗаказу(ЭтотОбъект);
		СуммаОплатыБонусами = 0;
		Если РасчетныеСкидки.Свойство("СуммаОплатыБонусами", СуммаОплатыБонусами) Тогда
			ВыборкаОплатаБонусами = МассивРезультатов[6].Выбрать();
			Пока ВыборкаОплатаБонусами.Следующий() Цикл
				СтрокаОплатыБонусами = Предоплата.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОплатыБонусами, ВыборкаОплатаБонусами);
				СтрокаОплатыБонусами.СуммаРасчетов = Мин(СуммаОплатыБонусами, СтрокаОплатыБонусами.СуммаРасчетов);
				СуммаОплатыБонусами = СуммаОплатыБонусами - СтрокаОплатыБонусами.СуммаРасчетов;
				Если СуммаОплатыБонусами <= 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		СкидкиНаценкиСервер.ПрименитьРассчитанныеСкидкиКОбъекту(ЭтотОбъект, РасчетныеСкидки);

	КонецЕсли;
	// Конец АвтоматическиеСкидки.

	Ячейка = ЗаполнениеОбъектовУНФ.ЗначениеДляШапки(Запасы, "Ячейка");
	Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти И ПолучитьФункциональнуюОпцию(
		"РазрешитьСкладыВТабличныхЧастях") Тогда
		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл
			Если СтрокаТабличнойЧасти.СтруктурнаяЕдиница <> СтруктурнаяЕдиница Или СтрокаТабличнойЧасти.Ячейка
				<> Ячейка Тогда
				ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ДоговорВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора");
	Если ЗначениеЗаполнено(Договор) И ДоговорВидДоговора = Перечисления.ВидыДоговоров.СКомиссионером Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаНаКомиссию;
		
		Для Каждого СтрокаЗапасы Из Запасы Цикл
			Если НЕ СтрокаЗапасы.Партия.Статус = Перечисления.СтатусыПартий.СобственныеЗапасы Тогда
				СтрокаЗапасы.Партия = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Учет по УСН
	СистемаНалогообложенияСтруктура = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(
		Организация, ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	Если СистемаНалогообложенияСтруктура.ПлательщикУСН Тогда
		Если СистемаНалогообложенияСтруктура.ПлательщикЕНВД Тогда
			ОрганизацияВидУчетаСтраховыхВзносов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация,
				"ВидУчетаСтраховыхВзносов");
			УчитыватьВНУ = Не (ОрганизацияВидУчетаСтраховыхВзносов
				= Перечисления.ВидыУчетаСтраховыхВзносов.УчитыватьВЕНВД);
		Иначе
			УчитыватьВНУ = Истина;
		КонецЕсли;
	Иначе
		УчитыватьВНУ = Ложь;
	КонецЕсли;

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		//@skip-check reading-attribute-from-database
		ПеренестиСерииНоменклатуры(ДанныеЗаполнения.СерииНоменклатуры, ДанныеЗаполнения.Запасы);
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказПокупателя()

// Обработчик заполнения документа на основании Заказа поставщику.
//
// Параметры:
//  ЗаказПоставщику - ДокументСсылка.ЗаказПоставщику.
//
Процедура ЗаполнитьПоЗаказуПоставщику(ЗаказПоставщику) Экспорт

	Если УправлениеНебольшойФирмойПовтИсп.РеквизитВШапке("ПоложениеЗаказаПокупателяВДокументахОтгрузки") Тогда
		Заказ = ЗаказПоставщику;
	Иначе
		Заказ = Неопределено;
	КонецЕсли;

	РеквизитыШапкиЗаказаПоставщика = Новый Массив;
	РеквизитыШапкиЗаказаПоставщика.Добавить("Организация");
	РеквизитыШапкиЗаказаПоставщика.Добавить("ВидОперации");
	РеквизитыШапкиЗаказаПоставщика.Добавить("СтруктурнаяЕдиницаРезерв");
	РеквизитыШапкиЗаказаПоставщика.Добавить("Контрагент");
	РеквизитыШапкиЗаказаПоставщика.Добавить("Договор");
	РеквизитыШапкиЗаказаПоставщика.Добавить("ВалютаДокумента");
	РеквизитыШапкиЗаказаПоставщика.Добавить("НалогообложениеНДС");
	РеквизитыШапкиЗаказаПоставщика.Добавить("СуммаВключаетНДС");
	РеквизитыШапкиЗаказаПоставщика.Добавить("НДСВключатьВСтоимость");
	РеквизитыШапкиЗаказаПоставщика.Добавить("Курс");
	РеквизитыШапкиЗаказаПоставщика.Добавить("Кратность");
	РеквизитыШапкиЗаказаПоставщика.Добавить("СостояниеЗаказа");
	РеквизитыШапкиЗаказаПоставщика.Добавить("Проведен");
	РеквизитыЗаказаПоставщика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказПоставщику,
		РеквизитыШапкиЗаказаПоставщика);

	Документы.ЗаказПоставщику.ПроверитьВозможностьВводаНаОснованииЗаказаПоставщику(ЗаказПоставщику,
		РеквизитыЗаказаПоставщика);

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказаПоставщика,
		"Организация, Контрагент, Договор, ВалютаДокумента, НалогообложениеНДС, СуммаВключаетНДС, НДСВключатьВСтоимость, Курс, Кратность");
	ДокументОснование = ЗаказПоставщику;

	ПериодКурсовВалют = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	Если Не ВалютаДокумента = Константы.НациональнаяВалюта.Получить() Тогда
		ДоговорВалютаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВалютаРасчетов");
		ОтборКурсовВалют = Новый Структура("Валюта", ДоговорВалютаРасчетов);
		СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ПериодКурсовВалют, ОтборКурсовВалют);
		Курс = СтруктураПоВалюте.Курс;
		Кратность = СтруктураПоВалюте.Кратность;
	КонецЕсли;
	
	// Заполнение табличной части.
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	Если РеквизитыЗаказаПоставщика.ВидОперации = Перечисления.ВидыОперацийЗаказПоставщику.ЗаказНаПереработку Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаВПереработку;
		СтруктурнаяЕдиница = РеквизитыЗаказаПоставщика.СтруктурнаяЕдиницаРезерв;
		Если Не ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
			СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ОсновнойСклад");
		КонецЕсли;
		НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , ПериодКурсовВалют);
		ЗаполнитьПоЗаказПоставщикуНаПереработку(ЗаказПоставщику);
	Иначе
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику;
		ЗаполнитьПоЗаказПоставщикуНаЗакупку(ЗаказПоставщику);
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказПоставщику()

// Процедура заполнения документа на основании приходной накладной.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПриходнаяНакладная - приходная накладная
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоСчетуНаОплату(ДанныеЗаполнения) Экспорт

	ВидОперации			 = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	ЗаказВТабличнойЧасти = Не УправлениеНебольшойФирмойПовтИсп.РеквизитВШапке(
		"ПоложениеЗаказаПокупателяВДокументахОтгрузки");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату", ДанныеЗаполнения);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетНаОплату.Ссылка КАК ДокументОснование,
	|	СчетНаОплату.Организация КАК Организация,
	|	СчетНаОплату.БанковскийСчет КАК БанковскийСчет,
	|	СчетНаОплату.Контрагент КАК Контрагент,
	|	СчетНаОплату.Договор КАК Договор,
	|	СчетНаОплату.ВидЦен КАК ВидЦен,
	|	СчетНаОплату.ВидСкидкиНаценки КАК ВидСкидкиНаценки,
	|	СчетНаОплату.ВалютаДокумента КАК ВалютаДокумента,
	|	СчетНаОплату.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	СчетНаОплату.НалогообложениеНДС КАК НалогообложениеНДС,
	|	СчетНаОплату.ОснованиеПечатиСсылка КАК ОснованиеПечатиСсылка,
	|	СчетНаОплату.ОснованиеПечати КАК ОснованиеПечати,
	|	СчетНаОплату.Курс КАК Курс,
	|	СчетНаОплату.Кратность КАК Кратность,
	|	СчетНаОплату.ДисконтнаяКарта КАК ДисконтнаяКарта,
	|	СчетНаОплату.ПроцентСкидкиПоДисконтнойКарте КАК ПроцентСкидкиПоДисконтнойКарте,
	|	СчетНаОплату.НоменклатураДоставки КАК НоменклатураДоставки,
	|	СчетНаОплату.СтоимостьДоставки КАК СтоимостьДоставки,
	|	СчетНаОплату.СтавкаНДСДоставки КАК СтавкаНДСДоставки,
	|	СчетНаОплату.СуммаНДСДоставки КАК СуммаНДСДоставки,
	|	СчетНаОплату.Вес КАК Вес,
	|	СчетНаОплату.Объем КАК Объем,
	|	ВЫБОР
	|		КОГДА СчетНаОплату.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА СчетНаОплату.ДокументОснование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	СчетНаОплату.УсловияСчетаЗаказа КАК УсловияСчетаЗаказа,
	|	СчетНаОплату.ТекстУсловийСчетаЗаказа КАК ТекстУсловийСчетаЗаказа
	|ИЗ
	|	Документ.СчетНаОплату КАК СчетНаОплату
	|ГДЕ
	|	СчетНаОплату.Ссылка = &СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетНаОплатуЗапасы.НомерСтроки КАК НомерСтроки,
	|	СчетНаОплатуЗапасы.Номенклатура КАК Номенклатура,
	|	СчетНаОплатуЗапасы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СчетНаОплатуЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СчетНаОплатуЗапасы.ТипНоменклатурыЗапас КАК ТипНоменклатурыЗапас,
	|	СчетНаОплатуЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СчетНаОплатуЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ СчетНаОплатуЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	СчетНаОплатуЗапасы.Количество КАК Количество,
	|	СчетНаОплатуЗапасы.Партия КАК Партия,
	|	СчетНаОплатуЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СчетНаОплатуЗапасы.Цена КАК Цена,
	|	СчетНаОплатуЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	СчетНаОплатуЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
	|	СчетНаОплатуЗапасы.Сумма КАК Сумма,
	|	СчетНаОплатуЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СчетНаОплатуЗапасы.СуммаНДС КАК СуммаНДС,
	|	СчетНаОплатуЗапасы.Всего КАК Всего,
	|	СчетНаОплатуЗапасы.Содержание КАК Содержание,
	|	СчетНаОплатуЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	СчетНаОплатуЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	СчетНаОплатуЗапасы.КлючСвязи КАК КлючСвязи,
	|	СчетНаОплатуЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки1,
	|	СчетНаОплатуЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки1,
	|	СчетНаОплатуЗапасы.Вес КАК Вес,
	|	СчетНаОплатуЗапасы.Объем КАК Объем,
	|	СчетНаОплатуЗапасы.Номенклатура.Склад КАК СтруктурнаяЕдиница,
	|	СчетНаОплатуЗапасы.Номенклатура.Ячейка КАК Ячейка,
	|	СчетНаОплатуЗапасы.НоменклатураНабора КАК НоменклатураНабора,
	|	СчетНаОплатуЗапасы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	СчетНаОплатуЗапасы.ДоляСтоимости КАК ДоляСтоимости
	|ИЗ
	|	ВТСчетНаОплатуЗапасы КАК СчетНаОплатуЗапасы
	|ГДЕ
	|	(СчетНаОплатуЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ИЛИ СчетНаОплатуЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ИЛИ СчетНаОплатуЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкидкиНаценки.Ссылка КАК Заказ,
	|	СкидкиНаценки.КлючСвязи КАК КлючСвязи,
	|	СкидкиНаценки.СкидкаНаценка КАК СкидкаНаценка,
	|	СкидкиНаценки.Сумма КАК Сумма
	|ИЗ
	|	Документ.СчетНаОплату.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	СкидкиНаценки.Ссылка = &СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ПОМЕСТИТЬ ВТДобавленныеНаборы
	|ИЗ
	|	Документ.СчетНаОплату.ДобавленныеНаборы КАК ДобавленныеНаборы
	|ГДЕ
	|	ДобавленныеНаборы.Ссылка = &СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ИЗ
	|	ВТДобавленныеНаборы КАК ДобавленныеНаборы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДобавленныеНаборы.НоменклатураНабора КАК НоменклатураНабора,
	|	ДобавленныеНаборы.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ДобавленныеНаборы.Количество КАК Количество
	|ИЗ
	|	ВТДобавленныеНаборы КАК ДобавленныеНаборы
	|ГДЕ
	|	ДобавленныеНаборы.НоменклатураНабора.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Подарки.Ссылка КАК Ссылка,
	|	Подарки.НомерСтроки КАК НомерСтроки,
	|	Подарки.КлючСвязи КАК КлючСвязи,
	|	Подарки.СкидкаНаценка КАК СкидкаНаценка,
	|	Подарки.Номенклатура КАК Номенклатура,
	|	Подарки.Характеристика КАК Характеристика,
	|	Подарки.Количество КАК Количество,
	|	Подарки.Цена КАК Цена,
	|	Подарки.Сумма КАК Сумма,
	|	Подарки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Подарки.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.СчетНаОплату.Подарки КАК Подарки
	|ГДЕ
	|	Подарки.Ссылка = &СчетНаОплату";

	Документы.СчетНаОплату.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(ДанныеЗаполнения,
		Запрос.МенеджерВременныхТаблиц, Ложь);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Заполнение шапки	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ВыборкаШапка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) И ЗначениеЗаполнено(Организация) Тогда
		БанковскийСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "БанковскийСчетПоУмолчанию");
	КонецЕсли;

	Если ВалютаДокумента <> Константы.НациональнаяВалюта.Получить() Тогда
		ДоговорВалютаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВалютаРасчетов");
		ВалютаКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДоговорВалютаРасчетов, Дата);
		Курс = ВалютаКурсКратность.Курс;
		Кратность = ВалютаКурсКратность.Кратность;
	КонецЕсли;

	ЗаказПокупателяДляЗаполнения = ВыборкаШапка.ЗаказПокупателя;
	Если Не ЗаказВТабличнойЧасти Тогда
		Заказ = ЗаказПокупателяДляЗаполнения;
	КонецЕсли;
	
	// Наборы
	ПропускаемыеНаборы = Новый Массив;
	ВыборкаИсключаемыеНаборы = МассивРезультатов[5].Выбрать();
	Пока ВыборкаИсключаемыеНаборы.Следующий() Цикл
		ДобавитьОписаниеНабора(ПропускаемыеНаборы, , ВыборкаИсключаемыеНаборы.НоменклатураНабора,
			ВыборкаИсключаемыеНаборы.ХарактеристикаНабора);
	КонецЦикла; 
	// Конец Наборы
	
	// Заполнение табличной части документа.
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	ВыборкаЗапасы = МассивРезультатов[1].Выбрать();
	Пока ВыборкаЗапасы.Следующий() Цикл

		Если ЗначениеЗаполнено(ВыборкаЗапасы.НоменклатураНабора) И ПропуститьНабор(ПропускаемыеНаборы, ВыборкаЗапасы) Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы, , "СтранаПроисхождения, СтруктурнаяЕдиница, Ячейка");

		Если ЗаказВТабличнойЧасти Тогда
			НоваяСтрока.Заказ = ЗаказПокупателяДляЗаполнения;
		КонецЕсли;

		Если ВыборкаЗапасы.ТипНоменклатурыЗапас Тогда

			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЗапасы, "СтранаПроисхождения, СтруктурнаяЕдиница, Ячейка");

		КонецЕсли;

	КонецЦикла;
	
	// АвтоматическиеСкидки
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки") Тогда
		СкидкиРассчитаны = Истина;
		СкидкиНаценки.Очистить();
		ВыборкаСкидки = МассивРезультатов[2].Выбрать();
		Пока ВыборкаСкидки.Следующий() Цикл
			Если Запасы.Найти(ВыборкаСкидки.КлючСвязи, "КлючСвязи") <> Неопределено Тогда
				НоваяСтрокаСкидкиНаценки = СкидкиНаценки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСкидкиНаценки, ВыборкаСкидки);
			КонецЕсли;
		КонецЦикла;
		// Подарки
		Подарки.Очистить();
		ВыборкаПодарки = МассивРезультатов[6].Выбрать();
		Пока ВыборкаПодарки.Следующий() Цикл
			НоваяСтрокаПодарка = Подарки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПодарка, ВыборкаПодарки);
		КонецЦикла;
	КонецЕсли;
	// Конец АвтоматическиеСкидки
	
	// Наборы
	ДобавленныеНаборы.Очистить();
	ВыборкаНаборы = МассивРезультатов[4].Выбрать();
	Пока ВыборкаНаборы.Следующий() Цикл
		Если ПропуститьНабор(ПропускаемыеНаборы, ВыборкаНаборы) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДобавленныеНаборы.Добавить(), ВыборкаНаборы);
	КонецЦикла; 
	// Конец Наборы

КонецПроцедуры // ЗаполнитьПоСчетНаОплату()

// Обработчик заполнения на основании ПеремещениеЗапасов.
//
// Параметры:
//  ДанныеЗаполнения - Структура, ДокументСсылка.ПеремещениеЗапасов - Основание для заполнения документа.
//
Процедура ЗаполнитьПоПеремещениюЗапасов(ДанныеЗаполнения) Экспорт

	Если ТипЗнч(ДанныеЗаполнения) <> Тип("ДокументСсылка.ПеремещениеЗапасов") Тогда
		Возврат;
	КонецЕсли;
	
	// Если перемещение введено на основании заказа покупателя, заполняем расходную накладную на основании этого заказа, но
	// со складом-получателем перемещения
	ТабЗаказов = ДанныеЗаполнения.Запасы.Выгрузить( , "ЗаказПокупателя");
	ТабЗаказов.Свернуть("ЗаказПокупателя");
	Если ТабЗаказов.Количество() = 1 И ЗначениеЗаполнено(ТабЗаказов[0].ЗаказПокупателя) Тогда
		ОдинЗаказПокупателяВСтроках = Истина;
	Иначе
		ОдинЗаказПокупателяВСтроках = Ложь;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ЗаказПокупателя) Или (Не ОдинЗаказПокупателяВСтроках) Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиница,
		|	ТаблицаДокумента.Запасы.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		ВЫБОР
		|			КОГДА ТаблицаДокумента.Запасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ТипНоменклатурыЗапас,
		|		Характеристика КАК Характеристика,
		|		Партия КАК Партия,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Количество КАК Количество,
		|		Резерв КАК Резерв,
		|		ЗаказПокупателя КАК ЗаказПокупателя,
		|		СерииНоменклатуры КАК СерииНоменклатуры,
		|		КлючСвязи КАК КлючСвязи,
		|		Проект КАК Проект,
		|		ВЫБОР
		|			КОГДА ТаблицаДокумента.Запасы.ЯчейкаПолучатель = ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка)
		|				ТОГДА ТаблицаДокумента.ЯчейкаПолучатель
		|			ИНАЧЕ ТаблицаДокумента.Запасы.ЯчейкаПолучатель
		|		КОНЕЦ КАК Ячейка,
		|		ТаблицаДокумента.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиница
		|	) КАК Запасы,
		|	ТаблицаДокумента.ЯчейкаПолучатель КАК Ячейка
		|ИЗ
		|	Документ.ПеремещениеЗапасов КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		РезультатЗапроса = Запрос.Выполнить();

		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
		
		Запасы.Загрузить(Выборка.Запасы.Выгрузить());

		СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);

		ПоложениеСклада = ДанныеЗаполнения.ПоложениеЯчейкиПолучателя;

	Иначе
		
		// Заполнение шапки.
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПокупателя.Ссылка КАК Заказ,
		|	ЗаказПокупателя.Ссылка КАК ОснованиеСсылка,
		|	&ДокументСсылкаПеремещениеЗапасов КАК ДокументОснование,
		|	ЗаказПокупателя.Проведен КАК ОснованиеПроведен,
		|	ЗаказПокупателя.СостояниеЗаказа КАК СостояниеЗаказа,
		|	ЗаказПокупателя.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ЗаказПокупателя.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
		|			ТОГДА ЗаказПокупателя.Организация.БанковскийСчетПоУмолчанию
		|		ИНАЧЕ ЗаказПокупателя.БанковскийСчет
		|	КОНЕЦ КАК БанковскийСчет,
		|	ЗаказПокупателя.ПоложениеСклада КАК ПоложениеСклада,
		|	ВЫБОР
		|		КОГДА РезервированиеЗапасов.Значение
		|			ТОГДА ЗаказПокупателя.СтруктурнаяЕдиницаРезерв
		|	КОНЕЦ КАК СтруктурнаяЕдиницаОтправитель,
		|	&СкладПолучатель КАК СтруктурнаяЕдиница,
		|	ЗаказПокупателя.Контрагент КАК Контрагент,
		|	ЗаказПокупателя.Договор КАК Договор,
		|	ЗаказПокупателя.ВидЦен КАК ВидЦен,
		|	ЗаказПокупателя.ВидСкидкиНаценки КАК ВидСкидкиНаценки,
		|	ЗаказПокупателя.ДисконтнаяКарта КАК ДисконтнаяКарта,
		|	ЗаказПокупателя.ПроцентСкидкиПоДисконтнойКарте КАК ПроцентСкидкиПоДисконтнойКарте,
		|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
		|	ЗаказПокупателя.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ЗаказПокупателя.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ЗаказПокупателя.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
		|	ВЫБОР
		|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
		|			ТОГДА ЗаказПокупателя.Курс
		|		ИНАЧЕ КурсыВалютСрезПоследних.Курс
		|	КОНЕЦ КАК Курс,
		|	ВЫБОР
		|		КОГДА ЗаказПокупателя.ВалютаДокумента = НациональнаяВалюта.Значение
		|			ТОГДА ЗаказПокупателя.Кратность
		|		ИНАЧЕ КурсыВалютСрезПоследних.Кратность
		|	КОНЕЦ КАК Кратность
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДокумента, ) КАК КурсыВалютСрезПоследних
		|		ПО ЗаказПокупателя.Договор.ВалютаРасчетов = КурсыВалютСрезПоследних.Валюта},
		|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
		|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК РезервированиеЗапасов
		|ГДЕ
		|	ЗаказПокупателя.Ссылка = &ЗаказВПеремещенииЗапасов";

		Запрос.УстановитьПараметр("ЗаказВПеремещенииЗапасов", ДанныеЗаполнения.ЗаказПокупателя);
		Запрос.УстановитьПараметр("СкладПолучатель", ДанныеЗаполнения.СтруктурнаяЕдиницаПолучатель);
		Запрос.УстановитьПараметр("ДокументСсылкаПеремещениеЗапасов", ДанныеЗаполнения);
		Запрос.УстановитьПараметр("ДатаДокумента", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));

		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗначенияПроверяемыхРеквизитов = Новый Структура("СостояниеЗаказа, Проведен", Выборка.СостояниеЗаказа,
				Выборка.ОснованиеПроведен);
			Документы.ЗаказПокупателя.ПроверитьВозможностьВводаНаОснованииЗаказаПокупателя(Выборка.ОснованиеСсылка,
				ЗначенияПроверяемыхРеквизитов);
		КонецЦикла;

		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

		Если Не ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
			ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойСклад");
			Если Не ЗначениеЗаполнено(ЗначениеНастройки) Тогда
				СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
			КонецЕсли;
		КонецЕсли;
		
		// Заполнение табличной части.
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыОстатки.Характеристика КАК Характеристика,
		|	СУММА(ВЫБОР
		|			КОГДА ЗаказыОстатки.КоличествоОстаток - ПеремещениеКоличество.Количество >= 0
		|				ТОГДА ПеремещениеКоличество.Количество
		|			ИНАЧЕ ЗаказыОстатки.КоличествоОстаток
		|		КОНЕЦ) КАК КоличествоОстаток,
		|	ЗаказыОстатки.КоличествоОстаток КАК ЗаказыОстаток,
		|	ПеремещениеКоличество.Количество КАК ПеремещениеОстаток
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПеремещениеЗапасовЗапасы.Номенклатура КАК Номенклатура,
		|		ПеремещениеЗапасовЗапасы.Характеристика КАК Характеристика,
		|		СУММА(ПеремещениеЗапасовЗапасы.Количество) КАК Количество,
		|		ПеремещениеЗапасовЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
		|	ИЗ
		|		Документ.ПеремещениеЗапасов.Запасы КАК ПеремещениеЗапасовЗапасы
		|	ГДЕ
		|		ПеремещениеЗапасовЗапасы.Ссылка = &Перемещение
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПеремещениеЗапасовЗапасы.Номенклатура,
		|		ПеремещениеЗапасовЗапасы.Характеристика,
		|		ПеремещениеЗапасовЗапасы.ЗаказПокупателя) КАК ПеремещениеКоличество
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
		|			ЗаказыОстатки.Номенклатура КАК Номенклатура,
		|			ЗаказыОстатки.Характеристика КАК Характеристика,
		|			ЗаказыОстатки.КоличествоОстаток КАК КоличествоОстаток
		|		ИЗ
		|			РегистрНакопления.ЗаказыПокупателей.Остатки(
		|					,
		|					ЗаказПокупателя = &ЗаказПокупателя
		|						И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК ЗаказыОстатки
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ДвиженияДокументаЗаказыПокупателей.ЗаказПокупателя,
		|			ДвиженияДокументаЗаказыПокупателей.Номенклатура,
		|			ДвиженияДокументаЗаказыПокупателей.Характеристика,
		|			ВЫБОР
		|				КОГДА ДвиженияДокументаЗаказыПокупателей.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|					ТОГДА ЕСТЬNULL(ДвиженияДокументаЗаказыПокупателей.Количество, 0)
		|				ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗаказыПокупателей.Количество, 0)
		|			КОНЕЦ
		|		ИЗ
		|			РегистрНакопления.ЗаказыПокупателей КАК ДвиженияДокументаЗаказыПокупателей
		|		ГДЕ
		|			ДвиженияДокументаЗаказыПокупателей.Регистратор = &Ссылка) КАК ЗаказыОстатки
		|		ПО ПеремещениеКоличество.Номенклатура = ЗаказыОстатки.Номенклатура
		|			И ПеремещениеКоличество.Характеристика = ЗаказыОстатки.Характеристика
		|			И ПеремещениеКоличество.ЗаказПокупателя = ЗаказыОстатки.ЗаказПокупателя
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыОстатки.ЗаказПокупателя,
		|	ЗаказыОстатки.Номенклатура,
		|	ЗаказыОстатки.Характеристика,
		|	ЗаказыОстатки.КоличествоОстаток,
		|	ПеремещениеКоличество.Количество
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
		|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
		|	ЗаказПокупателяЗапасы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ВЫБОР
		|		КОГДА ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТипНоменклатурыЗапас,
		|	ЗаказПокупателяЗапасы.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПокупателяЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
		|			ТОГДА 1
		|		ИНАЧЕ ЕСТЬNULL(ЗаказПокупателяЗапасы.ЕдиницаИзмерения.Коэффициент, 1)
		|	КОНЕЦ КАК Коэффициент,
		|	ЗаказПокупателяЗапасы.Количество КАК Количество,
		|	ЗаказПокупателяЗапасы.Партия КАК Партия,
		|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказПокупателяЗапасы.Цена КАК Цена,
		|	ЗаказПокупателяЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
		|	ЗаказПокупателяЗапасы.СуммаСкидкиНаценки КАК СуммаСкидкиНаценки,
		|	ЗаказПокупателяЗапасы.Сумма КАК Сумма,
		|	ЗаказПокупателяЗапасы.СтавкаНДС КАК СтавкаНДС,
		|	ЗаказПокупателяЗапасы.СуммаНДС КАК СуммаНДС,
		|	ЗаказПокупателяЗапасы.Всего КАК Всего,
		|	ЗаказПокупателяЗапасы.Ссылка КАК Заказ,
		|	ЗаказПокупателяЗапасы.Содержание КАК Содержание,
		|	ЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
		|	ЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
		|	ЗаказПокупателяЗапасы.КлючСвязи КАК КлючСвязи,
		|	ЗаказПокупателяЗапасы.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки1,
		|	ЗаказПокупателяЗапасы.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки1
		|ИЗ
		|	ВТЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
		|ГДЕ
		|	(ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
		|			ИЛИ ЗаказПокупателяЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СкидкиНаценки.Ссылка КАК Заказ,
		|	СкидкиНаценки.КлючСвязи КАК КлючСвязи,
		|	СкидкиНаценки.СкидкаНаценка КАК СкидкаНаценка,
		|	СкидкиНаценки.Сумма КАК Сумма
		|ИЗ
		|	Документ.ЗаказПокупателя.СкидкиНаценки КАК СкидкиНаценки
		|ГДЕ
		|	СкидкиНаценки.Ссылка = &ЗаказПокупателя";

		Документы.ЗаказПокупателя.ДобавитьТаблицуЗапасыВМенеджерВременныхТаблиц(ДанныеЗаполнения.ЗаказПокупателя,
			Запрос.МенеджерВременныхТаблиц, Ложь);
		Запрос.УстановитьПараметр("ЗаказПокупателя", ДанныеЗаполнения.ЗаказПокупателя);
		Запрос.УстановитьПараметр("Перемещение", ДанныеЗаполнения);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		МассивРезультатов = Запрос.ВыполнитьПакет();
		ТаблицаОстатков = МассивРезультатов[0].Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("ЗаказПокупателя,Номенклатура,Характеристика");
		
		// АвтоматическиеСкидки.
		ИспользоватьАвтоматическиеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки");
		Если ИспользоватьАвтоматическиеСкидки Тогда
			СкидкиНаценкиЗаказа = МассивРезультатов[2].Выгрузить();
			СкидкиНаценки.Очистить();
		КонецЕсли;
		// Конец АвтоматическиеСкидки.

		Запасы.Очистить();
		СерииНоменклатуры.Очистить();
		Если ТаблицаОстатков.Количество() > 0 Тогда

			Выборка = МассивРезультатов[1].Выбрать();
			Пока Выборка.Следующий() Цикл

				СтруктураДляПоиска = Новый Структура;
				СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.Заказ);
				СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
				СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);

				МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
				Если МассивСтрокОстатков.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;

				НоваяСтрока = Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

				КоличествоКСписанию = Выборка.Количество * Выборка.Коэффициент;
				МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток
					- КоличествоКСписанию;
				Если МассивСтрокОстатков[0].КоличествоОстаток < 0 Тогда

					КоличествоКСписанию = (КоличествоКСписанию + МассивСтрокОстатков[0].КоличествоОстаток)
						/ Выборка.Коэффициент;

					ДанныеСтроки = Новый Структура;
					ДанныеСтроки.Вставить("Количество", КоличествоКСписанию);
					ДанныеСтроки.Вставить("Цена", Выборка.Цена);
					ДанныеСтроки.Вставить("Сумма", 0);
					ДанныеСтроки.Вставить("ПроцентСкидкиНаценки", Выборка.ПроцентСкидкиНаценки);
					ДанныеСтроки.Вставить("СуммаСкидкиНаценки", 0);
					ДанныеСтроки.Вставить("СтавкаНДС", Выборка.СтавкаНДС);
					ДанныеСтроки.Вставить("СуммаНДС", 0);
					ДанныеСтроки.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
					ДанныеСтроки.Вставить("Всего", 0);
					ЗаполнениеОбъектовУНФ.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);

					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);

				КонецЕсли;
				
				// АвтоматическиеСкидки
				Если ИспользоватьАвтоматическиеСкидки Тогда
					КоличествоВДокументе = Выборка.Количество * Выборка.Коэффициент;
					ПересчитатьСуммы = КоличествоВДокументе <> КоличествоКСписанию;
					КоэффициентПересчетаСкидки = ?(ПересчитатьСуммы, КоличествоКСписанию / КоличествоВДокументе, 1);
					Если КоэффициентПересчетаСкидки <> 1 Тогда
						НоваяСтрока.СуммаАвтоматическойСкидки = Окр(Выборка.СуммаАвтоматическойСкидки
							* КоэффициентПересчетаСкидки, 2);
					КонецЕсли;
					
					// Формирование табличной части скидок
					СуммаКРаспределению = НоваяСтрока.СуммаАвтоматическойСкидки;

					ЕстьСтрокаСкидки = Ложь;
					ОтборСкидкиНаценкиЗаказа = Новый Структура("Заказ,КлючСвязи");
					Если Выборка.КлючСвязи <> 0 Тогда
						ЗаполнитьЗначенияСвойств(ОтборСкидкиНаценкиЗаказа, Выборка);
						Для Каждого СтрокаСкидкиЗаказа Из СкидкиНаценкиЗаказа.НайтиСтроки(ОтборСкидкиНаценкиЗаказа) Цикл

							СтрокаСкидки = СкидкиНаценки.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаСкидки, СтрокаСкидкиЗаказа);
							СтрокаСкидки.Сумма = КоэффициентПересчетаСкидки * СтрокаСкидки.Сумма;
							СуммаКРаспределению = СуммаКРаспределению - СтрокаСкидки.Сумма;
							ЕстьСтрокаСкидки = Истина;

						КонецЦикла;
					КонецЕсли;

					Если ЕстьСтрокаСкидки И СуммаКРаспределению <> 0 Тогда
						СтрокаСкидки.Сумма = СтрокаСкидки.Сумма + СуммаКРаспределению;
					КонецЕсли;
				КонецЕсли;
				// Конец АвтоматическиеСкидки

				Если МассивСтрокОстатков[0].КоличествоОстаток <= 0 Тогда
					ТаблицаОстатков.Удалить(МассивСтрокОстатков[0]);
				КонецЕсли;

			КонецЦикла;

		КонецЕсли;
		
		// Заполнение резервов.
		Если Запасы.Количество() > 0 И Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить()
			И ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
			ЗаполнитьКолонкуРезервПоРезервам();
		КонецЕсли;
		
		// АвтоматическиеСкидки.
		Если ИспользоватьАвтоматическиеСкидки Тогда
			РезультатРасчетаСкидокНаценок = СкидкиНаценки.Выгрузить();
			СкидкиНаценкиСервер.ПрименитьРезультатРасчетаСкидокКОбъекту(ЭтотОбъект, "Запасы",
				РезультатРасчетаСкидокНаценок);
		КонецЕсли;

	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	КонецЕсли;

	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

КонецПроцедуры // ЗаполнитьПоПеремещениюЗапасов()

// Обработчик заполнения на основании СборкаЗапасов.
//
// Параметры:
//  ДанныеЗаполнения - ДокументСсылка.СборкаЗапасов - Основание для заполнения документа.
//
Процедура ЗаполнитьПоСборкеЗапасов(ДанныеЗаполнения) Экспорт

	Если ТипЗнч(ДанныеЗаполнения) <> Тип("ДокументСсылка.СборкаЗапасов") Тогда
		Возврат;
	КонецЕсли;

	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,
		"ВидОперации, ПоложениеЗаказаПокупателя, ЗаказПокупателя, Организация, СтруктурнаяЕдиницаПродукции, СтруктурнаяЕдиницаПродукции.ТипСтруктурнойЕдиницы, ЯчейкаПродукции");

	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ПоложениеЗаказаПокупателя) Тогда
		ПоложениеЗаказаПокупателя = ЗначенияРеквизитов.ПоложениеЗаказаПокупателя;
		Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			Заказ = ЗначенияРеквизитов.ЗаказПокупателя;
		КонецЕсли;
	ИначеЕсли УправлениеНебольшойФирмойПовтИсп.РеквизитВШапке("ПоложениеЗаказаПокупателяВДокументахОтгрузки") Тогда
		Заказ = ЗначенияРеквизитов.ЗаказПокупателя;
	Иначе
		Заказ = Неопределено;
	КонецЕсли;

	ДокументОснование = ДанныеЗаполнения;
	Организация = ЗначенияРеквизитов.Организация;
	ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , Дата);
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ЗначенияРеквизитов.СтруктурнаяЕдиницаПродукции) И ЗначенияРеквизитов.СтруктурнаяЕдиницаПродукцииТипСтруктурнойЕдиницы
		= Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		СтруктурнаяЕдиница = ЗначенияРеквизитов.СтруктурнаяЕдиницаПродукции;
		Ячейка = ЗначенияРеквизитов.ЯчейкаПродукции;
	Иначе
		СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
	КонецЕсли;

	Если ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказПокупателя) И ПоложениеЗаказаПокупателя
		<> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗначенияРеквизитов.ЗаказПокупателя,
			"Контрагент, Договор, ВидЦен, ВалютаДокумента, СуммаВключаетНДС, НДСВключатьВСтоимость, Курс, Кратность, НалогообложениеНДС");
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
	Иначе
		ВидЦен = Справочники.ВидыЦен.Оптовая;
		ВалютаДокумента = Константы.НациональнаяВалюта.Получить();
		КурсДокумента = РегистрыСведений.КурсыВалют.ПолучитьПоследнее( , Новый Структура("Валюта", ВалютаДокумента));
		Курс = КурсДокумента.Курс;
		Кратность = КурсДокумента.Кратность;
		СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦен, "ЦенаВключаетНДС", Истина);
		НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , Дата);
	КонецЕсли;

	ЗаполнятьРезерв = ЗначениеЗаполнено(ЗначенияРеквизитов.ЗаказПокупателя) И ПолучитьФункциональнуюОпцию(
		"РезервированиеЗапасов");
	
	// Заполнение табличной части документа.
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТСтавкиНДСНоменклатура.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА ВТСтавкиНДСОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНДСНоменклатура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Партия КАК Партия,
	|	ТабличнаяЧасть.СерииНоменклатуры КАК СерииНоменклатуры,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Резерв КАК Резерв,
	|	ТабличнаяЧасть.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТабличнаяЧасть.Спецификация КАК Спецификация,
	|	ТабличнаяЧасть.КлючСвязи КАК КлючСвязи,
	|	ТабличнаяЧасть.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТабличнаяЧасть.НомерГТД КАК НомерГТД,
	|	ТабличнаяЧасть.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТабличнаяЧасть.Ячейка КАК Ячейка,
	|	ТабличнаяЧасть.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТабличнаяЧасть.Ссылка.ЗаказПокупателя КАК ЗаказПокупателяШапки
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	Документ.СборкаЗапасов.Продукция КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСНоменклатура
	|		ПО (ТабличнаяЧасть.Номенклатура.ВидСтавкиНДС  = ВТСтавкиНДСНоменклатура.ВидСтавкиНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСОрганизация
	|		ПО (ТабличнаяЧасть.Ссылка.Организация.ВидСтавкиНДСПоУмолчанию  = ВТСтавкиНДСОрганизация.ВидСтавкиНДС)
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Номенклатура.ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТСтавкиНДСНоменклатура.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА ВТСтавкиНДСОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНДСНоменклатура.СтавкаНДС
	|	КОНЕЦ,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Партия,
	|	ТабличнаяЧасть.СерииНоменклатуры,
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.Резерв,
	|	ТабличнаяЧасть.ЕдиницаИзмерения,
	|	ТабличнаяЧасть.Спецификация,
	|	ТабличнаяЧасть.КлючСвязи,
	|	ТабличнаяЧасть.СтранаПроисхождения,
	|	ТабличнаяЧасть.НомерГТД,
	|	ТабличнаяЧасть.СтруктурнаяЕдиница,
	|	ТабличнаяЧасть.Ячейка,
	|	ТабличнаяЧасть.ЗаказПокупателя,
	|	ТабличнаяЧасть.Ссылка.ЗаказПокупателя
	|ИЗ
	|	Документ.СборкаЗапасов.Запасы КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСНоменклатура
	|		ПО (ТабличнаяЧасть.Номенклатура.ВидСтавкиНДС  = ВТСтавкиНДСНоменклатура.ВидСтавкиНДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСОрганизация
	|		ПО (ТабличнаяЧасть.Ссылка.Организация.ВидСтавкиНДСПоУмолчанию  = ВТСтавкиНДСОрганизация.ВидСтавкиНДС)
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.Ссылка.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСборкаЗапасов.Сборка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.ТипНоменклатуры КАК ТипНоменклатуры,
	|	Запасы.СтавкаНДС КАК СтавкаНДС,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.Партия КАК Партия,
	|	Запасы.СерииНоменклатуры КАК СерииНоменклатуры,
	|	Запасы.Количество КАК Количество,
	|	Запасы.Резерв КАК Резерв,
	|	Запасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Запасы.Спецификация КАК Спецификация,
	|	Запасы.КлючСвязи КАК КлючСвязи,
	|	Запасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Запасы.НомерГТД КАК НомерГТД,
	|	Запасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Запасы.Ячейка КАК Ячейка,
	|	Запасы.ЗаказПокупателя КАК Заказ,
	|	Запасы.ЗаказПокупателяШапки КАК ЗаказПокупателяШапки,
	|	ВЫБОР
	|		КОГДА НЕ ЦеныСХарактеристиками.Цена ЕСТЬ NULL
	|			ТОГДА ЦеныСХарактеристиками.Цена
	|		КОГДА НЕ ЦеныБезХарактеристик.Цена ЕСТЬ NULL
	|			ТОГДА ЦеныБезХарактеристик.Цена
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ ЦеныСХарактеристиками.ВидЦен.ЦенаВключаетНДС ЕСТЬ NULL
	|			ТОГДА ЦеныСХарактеристиками.ВидЦен.ЦенаВключаетНДС
	|		КОГДА НЕ ЦеныБезХарактеристик.ВидЦен.ЦенаВключаетНДС ЕСТЬ NULL
	|			ТОГДА ЦеныБезХарактеристик.ВидЦен.ЦенаВключаетНДС
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ ЦеныСХарактеристиками.ВидЦен.ВалютаЦены ЕСТЬ NULL
	|			ТОГДА ЦеныСХарактеристиками.ВидЦен.ВалютаЦены
	|		КОГДА НЕ ЦеныБезХарактеристик.ВидЦен.ВалютаЦены ЕСТЬ NULL
	|			ТОГДА ЦеныБезХарактеристик.ВидЦен.ВалютаЦены
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВалютаЦены
	|ИЗ
	|	Запасы КАК Запасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				,
	|				ВидЦен = &ВидЦен
	|					И (Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							Запасы.Номенклатура,
	|							Запасы.Характеристика
	|						ИЗ
	|							Запасы КАК Запасы)) КАК ЦеныСХарактеристиками
	|		ПО Запасы.Номенклатура = ЦеныСХарактеристиками.Номенклатура
	|			И Запасы.Характеристика = ЦеныСХарактеристиками.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				,
	|				ВидЦен = &ВидЦен
	|					И Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							Запасы.Номенклатура
	|						ИЗ
	|							Запасы КАК Запасы)) КАК ЦеныБезХарактеристик
	|		ПО Запасы.Номенклатура = ЦеныБезХарактеристик.Номенклатура";
	Запрос.Текст = Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС(?(ЗначениеЗаполнено(Дата), Дата,
		ТекущаяДатаСеанса())) + ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();

	Курсы = Новый Соответствие;

	Пока Выборка.Следующий() Цикл

		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

		НоваяСтрока.ТипНоменклатурыЗапас = (Выборка.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас)
			Или (Выборка.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат);

		Если ПоложениеЗаказаПокупателя <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			НоваяСтрока.Заказ = Выборка.ЗаказПокупателяШапки;
		КонецЕсли;

		Если ЗаполнятьРезерв Тогда
			НоваяСтрока.Резерв = НоваяСтрока.Количество;
		КонецЕсли;

		Если ВалютаДокумента <> Выборка.ВалютаЦены Тогда

			КурсЦены = Курсы.Получить(Выборка.ВалютаЦены);
			Если КурсЦены = Неопределено Тогда
				КурсЦены = ЦенообразованиеСервер.ПолучитьКурсыВалют(Выборка.ВалютаЦены, ВалютаДокумента, Дата);
				Курсы.Вставить(Выборка.ВалютаЦены, КурсЦены);
			КонецЕсли;

			НоваяСтрока.Цена = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(НоваяСтрока.Цена, КурсЦены.КурсНач,
				КурсЦены.Курс, КурсЦены.КратностьНач, КурсЦены.Кратность);

		КонецЕсли;

		Если СуммаВключаетНДС <> Выборка.ЦенаВключаетНДС Тогда

			НоваяСтрока.Цена = ЦенообразованиеСервер.ПересчитатьСуммуПриИзмененииФлаговНДС(НоваяСтрока.Цена,
				СуммаВключаетНДС, Выборка.СтавкаНДС);

		КонецЕсли;

		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.Количество;

	КонецЦикла;

	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда

		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл

			СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
			СтрокаТабличнойЧасти.СуммаНДС = ?(СуммаВключаетНДС, СтрокаТабличнойЧасти.Сумма
				- (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100), СтрокаТабличнойЧасти.Сумма * СтавкаНДС
				/ 100);
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0,
				СтрокаТабличнойЧасти.СуммаНДС);

		КонецЦикла;

	Иначе

		Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		Иначе
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
		КонецЕсли;

		Для Каждого СтрокаТабличнойЧасти Из Запасы Цикл

			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
			СтрокаТабличнойЧасти.СуммаНДС = 0;

			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма;

		КонецЦикла;

	КонецЕсли;

	Если ЗначенияРеквизитов.ВидОперации = Перечисления.ВидыОперацийСборкаЗапасов.Сборка Тогда
		СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения, "Продукция",
			"СерииНоменклатурыПродукция");
	Иначе
		СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;

	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

КонецПроцедуры // ЗаполнитьПоПеремещениюЗапасов()

// Обработчик заполнения на основании ПеремещениеЗапасов.
//
// Параметры:
//  ДанныеЗаполнения - Структура, ДокументСсылка.ПеремещениеЗапасов - Основание для заполнения документа.
//
Процедура ЗаполнитьПоПриемуВРемонт(ДанныеЗаполнения) Экспорт

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриемИПередачаВРемонт") Тогда
		ДокументСсылкаПриемВРемонт = ДанныеЗаполнения;
	Иначе
		Возврат;
	КонецЕсли;

	ДанныеШапкиДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаПриемВРемонт,
		"Ссылка, Организация, Контрагент, Договор");

	ДокументОснование = ДанныеШапкиДокумента.Ссылка;
	Организация = ДанныеШапкиДокумента.Организация;
	Контрагент = ДанныеШапкиДокумента.Контрагент;
	Договор = ДанныеШапкиДокумента.Договор;
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВидЦен, ВидСкидкиНаценки, ВалютаРасчетов");

	ВидЦен = ДанныеДоговора.ВидЦен;
	ВидСкидкиНаценки = ДанныеДоговора.ВидСкидкиНаценки;
	ВалютаДокумента = ДанныеДоговора.ВалютаРасчетов;
	Если ЗначениеЗаполнено(ДанныеДоговора.ВидЦен) Тогда
		СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦен, "ЦенаВключаетНДС");
	Иначе
		СуммаВключаетНДС = Неопределено;
	КонецЕсли;
	НалогообложениеНДС = НалогиУНФ.НалогообложениеНДС(Организация, , Дата);

	ВалютаКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДанныеДоговора.ВалютаРасчетов, Дата);
	Курс = ВалютаКурсКратность.Курс;
	Кратность = ВалютаКурсКратность.Кратность;

	ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
	Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Проект");

КонецПроцедуры

// Процедура заполнения документа на основании расходного ордера.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоРасходномуОрдеру(ДанныеЗаполнения) Экспорт
	
	// Заполнение шапки документа.
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	Организация = ДанныеЗаполнения.Организация;
	СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
	Ячейка = ДанныеЗаполнения.Ячейка;
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	Запасы.Очистить();
	СерииНоменклатуры.Очистить();
	СведенияПрослеживаемости.Очистить();
	ДобавленныеНаборы.Очистить();
	ШтрихкодыУпаковок.Очистить();
	СкидкиНаценки.Очистить();

	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД");

	ВидУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ВидУчетаОрдерныхСкладов");

	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоПрочимДокументам Тогда
		ТекстИсключения = НСтр("ru = 'Вид учета остатков на ордерном складе - по Расходным накладным.
							   |Ввод(заполнение) Расходной накладной на основании Складского ордера недоступен.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

	ПоложениеСклада = ДанныеЗаполнения.ПоложениеЯчейки;

	Контрагент = ДанныеЗаполнения.Контрагент;
	Договор = Справочники.ДоговорыКонтрагентов.ПоДокументуКонтрагентуОрганизацииИВидуОперации(Ссылка, Контрагент,
		Организация, ВидОперации);

	Компания = Константы.УчетПоКомпании.Компания(Организация);

	Если ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.ПоСкладуВЦелом Тогда
		ЗаполнитьПоСкладскомуОрдеруБезУчетаОстатков(ДанныеЗаполнения, Компания);
		Возврат;
	КонецЕсли;
	
	Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке И ВидУчета = Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоСкладскимОрдерам Тогда
		ДокументПоступления = ДокументОснование;
	КонецЕсли;

	Если ЗначениеЗаполнено(Договор) Или Не ЗначениеЗаполнено(Контрагент) Тогда

		ДоговорСтруктура = Справочники.ДоговорыКонтрагентов.СтруктураДляЗаполненияПоДоговоруКонтрагента(Договор, Истина);
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДоговорСтруктура);

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходныйОрдерЗапасы.НомерСтроки КАК НомерСтроки,
		|	РасходныйОрдерЗапасы.Номенклатура КАК Номенклатура,
		|	РасходныйОрдерЗапасы.Характеристика КАК Характеристика,
		|	РасходныйОрдерЗапасы.Партия КАК Партия,
		|	РасходныйОрдерЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	РасходныйОрдерЗапасы.СерииНоменклатуры КАК СерииНоменклатуры,
		|	РасходныйОрдерЗапасы.КлючСвязи КАК КлючСвязи,
		|	РасходныйОрдерЗапасы.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА &КонтролироватьОстаткиПоДокументам
		|			ТОГДА &ДокументОснование
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДокументПоступления,
		|	РасходныйОрдерЗапасы.Ячейка КАК Ячейка,
		|	&СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
		|ИЗ
		|	Документ.РасходныйОрдер.Запасы КАК РасходныйОрдерЗапасы
		|ГДЕ
		|	РасходныйОрдерЗапасы.Ссылка = &ДокументОснование
		|	И РасходныйОрдерЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗапасыКРасходуСоСкладовОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыКРасходуСоСкладовОстатки.Характеристика КАК Характеристика,
		|	ЗапасыКРасходуСоСкладовОстатки.Партия КАК Партия,
		|	ВЫБОР
		|		КОГДА ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток < 0
		|			ТОГДА -ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
		|		ИНАЧЕ ЗапасыКРасходуСоСкладовОстатки.КоличествоОстаток
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА &КонтролироватьОстаткиПоДокументам
		|			ТОГДА &ДокументОснование
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДокументПоступления,
		|	&СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыКРасходуСоСкладовОстатки.Ячейка КАК Ячейка
		|ПОМЕСТИТЬ КПоступлению
		|ИЗ
		|	РегистрНакопления.ЗапасыКРасходуСоСкладов.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И ДокументОснование = &ДокументОснование) КАК ЗапасыКРасходуСоСкладовОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыКРасходуСоСкладов.Номенклатура,
		|	ЗапасыКРасходуСоСкладов.Характеристика,
		|	ЗапасыКРасходуСоСкладов.Партия,
		|	ВЫБОР
		|		КОГДА ЗапасыКРасходуСоСкладов.Количество < 0
		|			ТОГДА -ЗапасыКРасходуСоСкладов.Количество
		|		ИНАЧЕ ЗапасыКРасходуСоСкладов.Количество
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &КонтролироватьОстаткиПоДокументам
		|			ТОГДА &ДокументОснование
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	&СтруктурнаяЕдиница,
		|	ЗапасыКРасходуСоСкладов.Ячейка
		|ИЗ
		|	РегистрНакопления.ЗапасыКРасходуСоСкладов КАК ЗапасыКРасходуСоСкладов
		|ГДЕ
		|	ЗапасыКРасходуСоСкладов.Регистратор = &Ссылка
		|	И ЗапасыКРасходуСоСкладов.ДокументОснование = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КПоступлению.Номенклатура КАК Номенклатура,
		|	КПоступлению.Характеристика КАК Характеристика,
		|	КПоступлению.Партия КАК Партия,
		|	СУММА(КПоступлению.Количество) КАК Количество,
		|	КПоступлению.ДокументПоступления КАК ДокументПоступления,
		|	КПоступлению.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	КПоступлению.Ячейка КАК Ячейка
		|ИЗ
		|	КПоступлению КАК КПоступлению
		|
		|СГРУППИРОВАТЬ ПО
		|	КПоступлению.ДокументПоступления,
		|	КПоступлению.Ячейка,
		|	КПоступлению.Номенклатура,
		|	КПоступлению.Партия,
		|	КПоступлению.СтруктурнаяЕдиница,
		|	КПоступлению.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасходныйОрдерЗапасы.Номенклатура КАК Номенклатура,
		|	РасходныйОрдерЗапасы.Характеристика КАК Характеристика,
		|	РасходныйОрдерЗапасы.Партия КАК Партия,
		|	РасходныйОрдерСерииНоменклатуры.Серия КАК Серия,
		|	РасходныйОрдерЗапасы.КлючСвязи КАК КлючСвязи,
		|	РасходныйОрдерСерииНоменклатуры.Количество КАК Количество
		|ИЗ
		|	Документ.РасходныйОрдер.СерииНоменклатуры КАК РасходныйОрдерСерииНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдер.Запасы КАК РасходныйОрдерЗапасы
		|		ПО (РасходныйОрдерСерииНоменклатуры.Ссылка = &ДокументОснование)
		|			И (РасходныйОрдерЗапасы.Ссылка = &ДокументОснование)
		|			И (РасходныйОрдерЗапасы.КлючСвязи = РасходныйОрдерСерииНоменклатуры.КлючСвязи)
		|ГДЕ
		|	РасходныйОрдерЗапасы.Ссылка = &ДокументОснование
		|	И РасходныйОрдерСерииНоменклатуры.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СерииНоменклатурыКРасходуОстатки.Серия КАК Серия,
		|	-СерииНоменклатурыКРасходуОстатки.КоличествоОстаток КАК Количество,
		|	СерииНоменклатурыКРасходуОстатки.Номенклатура КАК Номенклатура,
		|	СерииНоменклатурыКРасходуОстатки.Характеристика КАК Характеристика,
		|	СерииНоменклатурыКРасходуОстатки.Партия КАК Партия
		|ПОМЕСТИТЬ ОстаткиСерииИтог
		|ИЗ
		|	РегистрНакопления.СерииНоменклатурыКРасходу.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И ДокументОснование = &ДокументОснование) КАК СерииНоменклатурыКРасходуОстатки
		|ГДЕ
		|	СерииНоменклатурыКРасходуОстатки.КоличествоОстаток < 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СерииНоменклатурыКРасходу.Серия,
		|	СерииНоменклатурыКРасходу.Количество,
		|	СерииНоменклатурыКРасходу.Номенклатура,
		|	СерииНоменклатурыКРасходу.Характеристика,
		|	СерииНоменклатурыКРасходу.Партия
		|ИЗ
		|	РегистрНакопления.СерииНоменклатурыКРасходу КАК СерииНоменклатурыКРасходу
		|ГДЕ
		|	СерииНоменклатурыКРасходу.Регистратор = &Ссылка
		|	И СерииНоменклатурыКРасходу.ДокументОснование = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиСерииИтог.Серия КАК Серия,
		|	СУММА(ОстаткиСерииИтог.Количество) КАК Количество,
		|	ОстаткиСерииИтог.Номенклатура КАК Номенклатура,
		|	ОстаткиСерииИтог.Характеристика КАК Характеристика,
		|	ОстаткиСерииИтог.Партия КАК Партия
		|ИЗ
		|	ОстаткиСерииИтог КАК ОстаткиСерииИтог
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиСерииИтог.Серия,
		|	ОстаткиСерииИтог.Характеристика,
		|	ОстаткиСерииИтог.Партия,
		|	ОстаткиСерииИтог.Номенклатура";

		КонтролироватьОстаткиПоДокументам = ВидУчета
			= Перечисления.ВидыУчетаОрдерныхСкладов.УчетОстатковПоСкладскимОрдерам;
		Запрос.УстановитьПараметр("ДокументОснование", ?(КонтролироватьОстаткиПоДокументам, ДанныеЗаполнения,
			Неопределено));

		Запрос.УстановитьПараметр("Организация", Компания);
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		Запрос.УстановитьПараметр("КонтролироватьОстаткиПоДокументам", Истина);

		РезультатыЗапроса = Запрос.ВыполнитьПакет();

		Если РезультатыЗапроса[2].Пустой() Тогда
			Возврат;
		КонецЕсли;

		ТаблицаСерииНоменклатуры = РезультатыЗапроса[3].Выгрузить();
		ТаблицаОстаткиСерииНоменклатуры = РезультатыЗапроса[5].Выгрузить();
		ТаблицаОстаткиЗапасы = РезультатыЗапроса[2].Выгрузить();

		ВыборкаЗапасы = РезультатыЗапроса[0].Выбрать();

		ПараметрыПоискаСерии = Новый Структура("КлючСвязи");
		
		Если СкладскойУчетСервер.КонтролироватьОстаткиПоЯчейкамКПоступлениюОтгрзуке(СтруктурнаяЕдиница) Тогда
			ПараметрыПоискаОстаткиЗапасы = Новый Структура;
			ПараметрыПоискаОстаткиЗапасы.Вставить("Номенклатура");
			ПараметрыПоискаОстаткиЗапасы.Вставить("Характеристика");
			ПараметрыПоискаОстаткиЗапасы.Вставить("Партия");
			ПараметрыПоискаОстаткиЗапасы.Вставить("Ячейка");
		Иначе
			ПараметрыПоискаОстаткиЗапасы = Новый Структура;
			ПараметрыПоискаОстаткиЗапасы.Вставить("Номенклатура");
			ПараметрыПоискаОстаткиЗапасы.Вставить("Характеристика");
			ПараметрыПоискаОстаткиЗапасы.Вставить("Партия");
		КонецЕсли;
		
		ПараметрыПоискаОстаткиСерииНоменклатуры = Новый Структура;
		ПараметрыПоискаОстаткиСерииНоменклатуры.Вставить("Номенклатура");
		ПараметрыПоискаОстаткиСерииНоменклатуры.Вставить("Характеристика");
		ПараметрыПоискаОстаткиСерииНоменклатуры.Вставить("Партия");
		ПараметрыПоискаОстаткиСерииНоменклатуры.Вставить("Серия");

		ОблагаетсяНДС = НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;

		СтруктураЦен = Новый Структура;
		СтруктураЦен.Вставить("ВидЦен", ВидЦен);
		СтруктураЦен.Вставить("ЭтоНовыйОбъект", Истина);
		СтруктураЦен.Вставить("ДатаОбработки", ТекущаяДатаСеанса());
		СтруктураЦен.Вставить("Номенклатура", Неопределено);
		СтруктураЦен.Вставить("Коэффициент", 1);
		СтруктураЦен.Вставить("ВалютаДокумента", ВалютаДокумента);
		СтруктураЦен.Вставить("Характеристика", Неопределено);
		СтруктураЦен.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);

		ДанныеСтроки = Новый Структура;
		ДанныеСтроки.Вставить("Количество", 1);
		ДанныеСтроки.Вставить("Цена", 0);
		ДанныеСтроки.Вставить("Сумма", 0);
		ДанныеСтроки.Вставить("ПроцентСкидкиНаценки", 0);
		ДанныеСтроки.Вставить("СуммаСкидкиНаценки", 0);
		ДанныеСтроки.Вставить("СтавкаНДС", Неопределено);
		ДанныеСтроки.Вставить("СуммаНДС", 0);
		ДанныеСтроки.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
		ДанныеСтроки.Вставить("Всего", 0);

		ПериодСтавкиНДС = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
		КомпанияВидСтавкиНДСПоУмолчанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Компания, "ВидСтавкиНДСПоУмолчанию");

		Пока ВыборкаЗапасы.Следующий() Цикл

			Если Не ЗначениеЗаполнено(ВыборкаЗапасы.Количество) Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиЗапасы, ВыборкаЗапасы);
			НайденныеОстаткиЗапасы = ТаблицаОстаткиЗапасы.НайтиСтроки(ПараметрыПоискаОстаткиЗапасы);

			Если Не НайденныеОстаткиЗапасы.Количество() Или (НайденныеОстаткиЗапасы.Количество()
				И НайденныеОстаткиЗапасы[0].Количество = 0) Тогда
				Продолжить;
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(ПараметрыПоискаСерии, ВыборкаЗапасы);
			НайденныеСерии = ТаблицаСерииНоменклатуры.НайтиСтроки(ПараметрыПоискаСерии);

			КоличествоСерий = 0;
			ИспользуютсяСерииВСтроке = Ложь;
			Для Каждого СтрокаСерии Из НайденныеСерии Цикл

				ИспользуютсяСерииВСтроке = Истина;

				ЗаполнитьЗначенияСвойств(ПараметрыПоискаОстаткиСерииНоменклатуры, СтрокаСерии);
				НайденныеОстаткиСерий = ТаблицаОстаткиСерииНоменклатуры.НайтиСтроки(
					ПараметрыПоискаОстаткиСерииНоменклатуры);

				Если Не НайденныеОстаткиСерий.Количество() Или (НайденныеОстаткиСерий.Количество()
					И НайденныеОстаткиСерий[0].Количество = 0) Тогда
					Продолжить;
				КонецЕсли;

				ОстатокСерий = НайденныеОстаткиСерий[0];

				Если Не ОстатокСерий.Количество > 0 Тогда
					Продолжить;
				КонецЕсли;

				НоваяСтрокаСерии = СерииНоменклатуры.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, СтрокаСерии);

				Если НоваяСтрокаСерии.Количество >= ОстатокСерий.Количество Тогда
					НоваяСтрокаСерии.Количество = ОстатокСерий.Количество;
				КонецЕсли;

				ОстатокСерий.Количество = ОстатокСерий.Количество - НоваяСтрокаСерии.Количество;

				КоличествоСерий = КоличествоСерий + НоваяСтрокаСерии.Количество;

			КонецЦикла;

			Если ИспользуютсяСерииВСтроке И КоличествоСерий = 0 Тогда
				Продолжить;
			КонецЕсли;

			ОстатокЗапасов = НайденныеОстаткиЗапасы[0];

			НоваяСтрокаЗапасы = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ВыборкаЗапасы);
			НоваяСтрокаЗапасы.ТипНоменклатурыЗапас = Истина;

			Если ТипЗнч(НоваяСтрокаЗапасы.ЕдиницаИзмерения) = ТИП("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда

				КоличествоВСтрокеЗапасов = ?(КоличествоСерий > 0, КоличествоСерий, НоваяСтрокаЗапасы.Количество);

				Если КоличествоВСтрокеЗапасов >= ОстатокЗапасов.Количество Тогда
					КоличествоВСтрокеЗапасов = ОстатокЗапасов.Количество;
					ОстатокЗапасов.Количество = 0;
				Иначе
					ОстатокЗапасов.Количество = ОстатокЗапасов.Количество - КоличествоВСтрокеЗапасов;
				КонецЕсли;

				НоваяСтрокаЗапасы.Количество = КоличествоВСтрокеЗапасов;

			Иначе

				КоэффициентЕдиницыИзмерения = КоэффициентЕдиницыИзмерения(НоваяСтрокаЗапасы.ЕдиницаИзмерения);

				КоличествоСтрокиВЕдиницахХранения = НоваяСтрокаЗапасы.Количество * КоэффициентЕдиницыИзмерения;
				КоличествоВСтрокеЗапасов = ?(КоличествоСерий > 0, КоличествоСерий, КоличествоСтрокиВЕдиницахХранения);

				Если КоличествоВСтрокеЗапасов >= ОстатокЗапасов.Количество Тогда
					КоличествоВСтрокеЗапасов = ОстатокЗапасов.Количество;
					ОстатокЗапасов.Количество = 0;
				Иначе
					ОстатокЗапасов.Количество = ОстатокЗапасов.Количество - КоличествоВСтрокеЗапасов;
				КонецЕсли;

				НоваяСтрокаЗапасы.Количество = КоличествоВСтрокеЗапасов / КоэффициентЕдиницыИзмерения;

			КонецЕсли;

			Если СерииНоменклатурыУНФ.ИспользоватьСерииНоменклатурыОстатки() = Истина Тогда
				НоваяСтрокаЗапасы.СерииНоменклатуры = СерииНоменклатурыУНФ.ПредставлениеСерийНоменклатуры(
					СерииНоменклатуры, НоваяСтрокаЗапасы.КлючСвязи);
			КонецЕсли;

			ЗначенияРеквизитовНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяСтрокаЗапасы.Номенклатура,
				"ВидСтавкиНДС, СтранаПроисхождения");

			Если ОблагаетсяНДС Тогда
				Партия = НоваяСтрокаЗапасы.Партия;
				СвойстваПартии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Партия, "Статус, НалогообложениеНДС");
				Если ЗначениеЗаполнено(Партия) И СвойстваПартии.Статус = Перечисления.СтатусыПартий.ТоварыНаКомиссии
					И СвойстваПартии.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
					НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(Перечисления.ВидыСтавокНДС.БезНДС);
				Иначе
					Если ЗначениеЗаполнено(ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС) Тогда
						НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(
							ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС, ПериодСтавкиНДС);
					Иначе
						НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(КомпанияВидСтавкиНДСПоУмолчанию,
							ПериодСтавкиНДС);
					КонецЕсли;

				КонецЕсли;
			Иначе

				Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
					СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
				Иначе
					СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
				КонецЕсли;

				НоваяСтрокаЗапасы.СтавкаНДС = СтавкаНДСПоУмолчанию;

			КонецЕсли;

			Если УчетГТД Тогда
				НоваяСтрокаЗапасы.СтранаПроисхождения = ЗначенияРеквизитовНоменклатуры.СтранаПроисхождения;
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(СтруктураЦен, НоваяСтрокаЗапасы);
			НоваяСтрокаЗапасы.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураЦен);

			ЗаполнитьЗначенияСвойств(ДанныеСтроки, НоваяСтрокаЗапасы);
			ЗаполнениеОбъектовУНФ.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ДанныеСтроки);

		КонецЦикла;

	КонецЕсли;

	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
	КонецЕсли;

	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

КонецПроцедуры // ЗаполнитьПоПриходныйОрдер()

// Обработчик заполнения на основании складского ордера без учета остатков.
//
// Параметры:
//	ДанныеЗаполнения - Структура
//	Компания - СправочникСсылка.Организации
//
Процедура ЗаполнитьПоСкладскомуОрдеруБезУчетаОстатков(ДанныеЗаполнения, Компания) Экспорт

	УчетГТД = ПолучитьФункциональнуюОпцию("УчетГТД");
	ОблагаетсяНДС = НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
	НалогиУНФ.ЗаполнитьНДСВключенВСтоимостьВДокументе(ЭтотОбъект);
	
	СтруктураЦен = Новый Структура;
	СтруктураЦен.Вставить("ВидЦен", ВидЦен);
	СтруктураЦен.Вставить("ЭтоНовыйОбъект", Истина);
	СтруктураЦен.Вставить("ДатаОбработки", ТекущаяДатаСеанса());
	СтруктураЦен.Вставить("Номенклатура", Неопределено);
	СтруктураЦен.Вставить("Коэффициент", 1);
	СтруктураЦен.Вставить("ВалютаДокумента", ВалютаДокумента);
	СтруктураЦен.Вставить("Характеристика", Неопределено);
	СтруктураЦен.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);

	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("Количество", 1);
	ДанныеСтроки.Вставить("Цена", 0);
	ДанныеСтроки.Вставить("Сумма", 0);
	ДанныеСтроки.Вставить("ПроцентСкидкиНаценки", 0);
	ДанныеСтроки.Вставить("СуммаСкидкиНаценки", 0);
	ДанныеСтроки.Вставить("СтавкаНДС", Неопределено);
	ДанныеСтроки.Вставить("СуммаНДС", 0);
	ДанныеСтроки.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
	ДанныеСтроки.Вставить("Всего", 0);

	ПериодСтавкиНДС = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КомпанияВидСтавкиНДСПоУмолчанию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Компания, "ВидСтавкиНДСПоУмолчанию");

	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл

		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТабличнойЧасти);

		НоваяСтрокаЗапасы.СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
		НоваяСтрокаЗапасы.Ячейка = СтрокаТабличнойЧасти.Ячейка;

		ЗначенияРеквизитовНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяСтрокаЗапасы.Номенклатура,
			"ВидСтавкиНДС, СтранаПроисхождения");

		Если ОблагаетсяНДС Тогда
			Партия = НоваяСтрокаЗапасы.Партия;
			СвойстваПартии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Партия, "Статус, НалогообложениеНДС");
			Если ЗначениеЗаполнено(Партия) И СвойстваПартии.Статус = Перечисления.СтатусыПартий.ТоварыНаКомиссии
				И СвойстваПартии.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
				НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(Перечисления.ВидыСтавокНДС.БезНДС);
			Иначе
				Если ЗначениеЗаполнено(ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС) Тогда
					НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(
						ЗначенияРеквизитовНоменклатуры.ВидСтавкиНДС, ПериодСтавкиНДС);
				Иначе
					НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(КомпанияВидСтавкиНДСПоУмолчанию,
						ПериодСтавкиНДС);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
				СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
			Иначе
				СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
			КонецЕсли;

			НоваяСтрокаЗапасы.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(СтруктураЦен, НоваяСтрокаЗапасы);
		НоваяСтрокаЗапасы.Цена = ЦенообразованиеСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураЦен);

		ЗаполнитьЗначенияСвойств(ДанныеСтроки, НоваяСтрокаЗапасы);
		ЗаполнениеОбъектовУНФ.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, ДанныеСтроки);

		Если УчетГТД Тогда
			НоваяСтрокаЗапасы.СтранаПроисхождения = ЗначенияРеквизитовНоменклатуры.СтранаПроисхождения;
		КонецЕсли;

		НоваяСтрокаЗапасы.ТипНоменклатурыЗапас = Истина;

	КонецЦикла;

	СерииНоменклатурыУНФ.ЗаполнитьТЧСерииНоменклатурыПоКлючуСвязи(ЭтотОбъект, ДанныеЗаполнения);
	СерииНоменклатурыУНФ.УдалитьСерииНоменклатурыВТабличнойЧастиВЗависимостиОтПолитики(ЭтотОбъект);

КонецПроцедуры

// Процедура заполняет колонку Количество по резервам под заказ.
//
Процедура ЗаполнитьКолонкуРезервПоРезервам() Экспорт

	Запасы.ЗагрузитьКолонку(Новый Массив(Запасы.Количество()), "Резерв");

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ВЫБОР
	|		КОГДА &ЗаказВКолонкеЗаказПокупателя
	|			ТОГДА ТаблицаЗапасы.ЗаказПокупателя
	|		КОГДА &ЗаказВШапке
	|			ТОГДА &Заказ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|						И ТаблицаЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|					ТОГДА ТаблицаЗапасы.Заказ
	|				ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА &СкладВТабличнойЧасти
	|			ТОГДА ТаблицаЗапасы.СтруктурнаяЕдиница
	|		ИНАЧЕ &СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	&ТаблицаЗапасы КАК ТаблицаЗапасы
	|ГДЕ
	|	ТаблицаЗапасы.ТипНоменклатурыЗапас";

	Запрос.УстановитьПараметр("ТаблицаЗапасы", Запасы.Выгрузить());
	ЗаказВКолонкеЗаказПокупателя = ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику
		Или ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПередачаВПереработку;
	Запрос.УстановитьПараметр("ЗаказВКолонкеЗаказПокупателя", ЗаказВКолонкеЗаказПокупателя);
	Запрос.УстановитьПараметр("ЗаказВШапке", ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке);
	Запрос.УстановитьПараметр("Заказ", ?(ЗначениеЗаполнено(Заказ), Заказ, Документы.ЗаказПокупателя.ПустаяСсылка()));
	Запрос.УстановитьПараметр("СкладВТабличнойЧасти", ПоложениеСклада
		= Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);

	Запрос.Выполнить();

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				,
	|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|					(ВЫБРАТЬ
	|						&Организация,
	|						ТаблицаЗапасы.СтруктурнаяЕдиница,
	|						ТаблицаЗапасы.Номенклатура.СчетУчетаЗапасов,
	|						ТаблицаЗапасы.Номенклатура,
	|						ТаблицаЗапасы.Характеристика,
	|						ТаблицаЗапасы.Партия,
	|						ТаблицаЗапасы.ЗаказПокупателя
	|					ИЗ
	|						ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|					ГДЕ
	|						ТаблицаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))) КАК ЗапасыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.СчетУчета,
	|		ДвиженияДокументаЗапасы.ЗаказПокупателя,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗапасы.Период <= &Период
	|		И ДвиженияДокументаЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		И ДвиженияДокументаЗапасы.СтруктурнаяЕдиница ССЫЛКА Справочник.СтруктурныеЕдиницы) КАК ЗапасыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.ЗаказПокупателя,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия";

	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл

		СтруктураДляПоиска = Новый Структура;
		Если ЗаказВКолонкеЗаказПокупателя Тогда
			СтруктураДляПоиска.Вставить("ЗаказПокупателя", Выборка.ЗаказПокупателя);
		ИначеЕсли ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			СтруктураДляПоиска.Вставить("Заказ", Выборка.ЗаказПокупателя);
		КонецЕсли;
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", Выборка.Партия);
		Если ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", Выборка.СтруктурнаяЕдиница);
		КонецЕсли;

		ВсегоОстаток = Выборка.КоличествоОстаток;
		МассивСтрокЗапасы = Запасы.НайтиСтроки(СтруктураДляПоиска);
		Для Каждого СтрокаЗапасы Из МассивСтрокЗапасы Цикл

			КоэффициентЕдиницыИзмерения = КоэффициентЕдиницыИзмерения(СтрокаЗапасы.ЕдиницаИзмерения);
			ВсегоОстаток = ВсегоОстаток / КоэффициентЕдиницыИзмерения;

			Если СтрокаЗапасы.Количество >= ВсегоОстаток Тогда
				СтрокаЗапасы.Резерв = ВсегоОстаток;
				ВсегоОстаток = 0;
			Иначе
				СтрокаЗапасы.Резерв = СтрокаЗапасы.Количество;
				ВсегоОстаток = ВсегоОстаток - СтрокаЗапасы.Количество;
				ВсегоОстаток = ВсегоОстаток * КоэффициентЕдиницыИзмерения;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры // ЗаполнитьКолонкуРезервПоРезервам()

#Область ВЕТИС

// Заполнение документа на основании исходящей транспортной операции ВЕТИС.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоИсходящейТранспортнойОперации(ДанныеЗаполнения) Экспорт

	Реквизиты = ИнтеграцияВЕТИСУНФ.ДанныеПрикладныхДокументовИзИсходящейТранспортнойОперацииВЕТИС(ДанныеЗаполнения);
	Реквизиты.Следующий();

	Если Не ЗначениеЗаполнено(Ссылка) Тогда

		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);

	КонецЕсли;

	СкладВТабличнойЧасти = ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;

	Товары = Реквизиты.Товары.Выгрузить();

	Запасы.Очистить();

	ПериодСтавкиНДС = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	Для Каждого СтрокаТовары Из Товары Цикл
		НоваяСтрокаЗапасы = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗапасы, СтрокаТовары);
		НоменклатураВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрокаЗапасы.Номенклатура,
			"ВидСтавкиНДС");
		НоваяСтрокаЗапасы.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(НоменклатураВидСтавкиНДС, ПериодСтавкиНДС);
		Если СкладВТабличнойЧасти Тогда
			НоваяСтрокаЗапасы.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
		КонецЕсли;
		НоменклатураВДокументахСервер.ЗаполнитьДанныеВСтрокеТабличнойЧасти(ЭтотОбъект, "Запасы", НоваяСтрокаЗапасы);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйЗапросРасшифровкиПредоплаты(Знач ТаблицаЗаказов)

	Результат = Новый Запрос;
	Результат.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасчетыСПокупателямиОстатки.Документ КАК Документ,
	|	РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
	|	РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
	|	РасчетыСПокупателямиОстатки.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	СУММА(РасчетыСПокупателямиОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(РасчетыСПокупателямиОстатки.СуммаВалОстаток) КАК СуммаВалОстаток,
	|	СУММА(РасчетыСПокупателямиОстатки.СуммаРегОстаток) КАК СуммаРегОстаток
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателямиОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСПокупателямиОстатки.Договор КАК Договор,
	|		РасчетыСПокупателямиОстатки.Документ КАК Документ,
	|		РасчетыСПокупателямиОстатки.Документ.Дата КАК ДокументДата,
	|		РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРегОстаток, 0) КАК СуммаРегОстаток
	|	ИЗ
	|		РегистрНакопления.РасчетыСПокупателями.Остатки(
	|				&Период,
	|				Организация = &Организация
	|					И Контрагент = &Контрагент
	|					И Договор = &Договор
	|					И Заказ В (&Заказ)
	|					И ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПокупателямиОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаРасчетыСПокупателями.Договор,
	|		ДвиженияДокументаРасчетыСПокупателями.Документ,
	|		ДвиженияДокументаРасчетыСПокупателями.Документ.Дата,
	|		ДвиженияДокументаРасчетыСПокупателями.Заказ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.Сумма, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.Сумма, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаВал, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаВал, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаРег, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПокупателями.СуммаРег, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РасчетыСПокупателями КАК ДвиженияДокументаРасчетыСПокупателями
	|	ГДЕ
	|		ДвиженияДокументаРасчетыСПокупателями.Регистратор = &Ссылка
	|		И ДвиженияДокументаРасчетыСПокупателями.Период <= &Период
	|		И ДвиженияДокументаРасчетыСПокупателями.Организация = &Организация
	|		И ДвиженияДокументаРасчетыСПокупателями.Контрагент = &Контрагент
	|		И ДвиженияДокументаРасчетыСПокупателями.Договор = &Договор
	|		И ДвиженияДокументаРасчетыСПокупателями.Заказ В(&Заказ)
	|		И ДвиженияДокументаРасчетыСПокупателями.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК РасчетыСПокупателямиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПокупателямиОстатки.Документ,
	|	РасчетыСПокупателямиОстатки.Заказ,
	|	РасчетыСПокупателямиОстатки.ДокументДата,
	|	РасчетыСПокупателямиОстатки.Договор.ВалютаРасчетов
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыСПокупателямиОстатки.СуммаВалОстаток) < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасчетыСПокупателямиОстатки.Документ КАК Документ,
	|	РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
	|	РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
	|	РасчетыСПокупателямиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
	|	-СУММА(РасчетыСПокупателямиОстатки.СуммаУчета) КАК СуммаУчета,
	|	-СУММА(РасчетыСПокупателямиОстатки.СуммаРасчетов) КАК СуммаРасчетов,
	|	-СУММА(РасчетыСПокупателямиОстатки.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРег, 0) = 0
	|				ТОГДА РасчетыСПокупателямиОстатки.СуммаУчета / ВЫБОР
	|						КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРасчетов, 0) <> 0
	|							ТОГДА РасчетыСПокупателямиОстатки.СуммаРасчетов
	|						ИНАЧЕ 1
	|					КОНЕЦ * (РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКурс / РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКратность)
	|			ИНАЧЕ РасчетыСПокупателямиОстатки.СуммаРег / ВЫБОР
	|					КОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРасчетов, 0) <> 0
	|						ТОГДА РасчетыСПокупателямиОстатки.СуммаРасчетов
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		КОНЕЦ) КАК Курс,
	|	1 КАК Кратность,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКурс КАК КурсыВалютыДокументаКурс,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКратность КАК КурсыВалютыДокументаКратность
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСПокупателямиОстатки.ВалютаРасчетов КАК ВалютаРасчетов,
	|		РасчетыСПокупателямиОстатки.Документ КАК Документ,
	|		РасчетыСПокупателямиОстатки.ДокументДата КАК ДокументДата,
	|		РасчетыСПокупателямиОстатки.Заказ КАК Заказ,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаУчета,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаРасчетов,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) * КурсыВалютыУчета.Курс * &КратностьВалютыДокумента / (&КурсВалютыДокумента * КурсыВалютыУчета.Кратность) КАК СуммаПлатежа,
	|		ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаРегОстаток, 0) КАК СуммаРег,
	|		КурсыВалютыУчета.Курс КАК КурсыВалютыУчетаКурс,
	|		КурсыВалютыУчета.Кратность КАК КурсыВалютыУчетаКратность,
	|		&КурсВалютыДокумента КАК КурсыВалютыДокументаКурс,
	|		&КратностьВалютыДокумента КАК КурсыВалютыДокументаКратность
	|	ИЗ
	|		ВременнаяТаблицаРасчетыСПокупателямиОстатки КАК РасчетыСПокупателямиОстатки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &ВалютаУчета) КАК КурсыВалютыУчета
	|			ПО (ИСТИНА)) КАК РасчетыСПокупателямиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПокупателямиОстатки.Документ,
	|	РасчетыСПокупателямиОстатки.Заказ,
	|	РасчетыСПокупателямиОстатки.ДокументДата,
	|	РасчетыСПокупателямиОстатки.ВалютаРасчетов,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКурс,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыУчетаКратность,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКурс,
	|	РасчетыСПокупателямиОстатки.КурсыВалютыДокументаКратность
	|
	|ИМЕЮЩИЕ
	|	-СУММА(РасчетыСПокупателямиОстатки.СуммаРасчетов) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументДата";

	Результат.УстановитьПараметр("Заказ", ТаблицаЗаказов.ВыгрузитьКолонку("Заказ"));
	Результат.УстановитьПараметр("Организация", Константы.УчетПоКомпании.Компания(Организация));
	Результат.УстановитьПараметр("Контрагент", Контрагент);
	Результат.УстановитьПараметр("Договор", Договор);
	Результат.УстановитьПараметр("Период", Дата);
	Результат.УстановитьПараметр("ВалютаДокумента", ВалютаДокумента);
	Результат.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВалютаРасчетов") = ВалютаДокумента Тогда
		Результат.УстановитьПараметр("КурсВалютыДокумента", Курс);
		Результат.УстановитьПараметр("КратностьВалютыДокумента", Кратность);
	Иначе
		Результат.УстановитьПараметр("КурсВалютыДокумента", 1);
		Результат.УстановитьПараметр("КратностьВалютыДокумента", 1);
	КонецЕсли;
	Результат.УстановитьПараметр("Ссылка", Ссылка);

	Возврат Результат;

КонецФункции

Функция ТаблицаЗаказовДляЗаполненияПредоплаты()

	Результат = Запасы.Выгрузить( , "Заказ, Всего");

	Если ЗначениеЗаполнено(НоменклатураДоставки) И ЗначениеЗаполнено(СтоимостьДоставки) Тогда
		СтрокаСтоимостьДоставки = Результат.Добавить();
		СтрокаСтоимостьДоставки.Всего = СтоимостьДоставки;
	КонецЕсли;

	Результат.Колонки.Добавить("ВсегоРасч");
	КонтрагентВестиРасчетыПоЗаказам = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ВестиРасчетыПоЗаказам");
	ДоговорВалютаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВалютаРасчетов");
	Для Каждого ТекСтрока Из Результат Цикл
		Если Не КонтрагентВестиРасчетыПоЗаказам Тогда
			ТекСтрока.Заказ = Документы.ЗаказПокупателя.ПустаяСсылка();
		ИначеЕсли ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке Тогда
			ТекСтрока.Заказ = Заказ;
		Иначе
			ТекСтрока.Заказ = ?(ТекСтрока.Заказ = Неопределено, Документы.ЗаказПокупателя.ПустаяСсылка(),
				ТекСтрока.Заказ);
		КонецЕсли;
		КурсНач = ?(ДоговорВалютаРасчетов = ВалютаДокумента, Курс, 1);
		КратностьНач = ?(ДоговорВалютаРасчетов = ВалютаДокумента, Кратность, 1);
		ТекСтрока.ВсегоРасч = ЦенообразованиеСервер.ПересчитатьИзВалютыВВалюту(ТекСтрока.Всего, КурсНач, Курс,
			КратностьНач, Кратность);
	КонецЦикла;
	Результат.Свернуть("Заказ", "Всего, ВсегоРасч");
	Результат.Сортировать("Заказ Возр");

	Возврат Результат;

КонецФункции // ЗаполнитьПредоплату()

// Процедура заполняет реквизиты печати
Процедура ЗаполнитьРеквизитыПечати()

	Автомобиль = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойАвтомобиль", Организация);
	Водитель = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойВодитель", Организация);
	Перевозчик = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойПеревозчик", Организация);
	ОрганизацияОплачиваетПеревозку = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки(
		"ОрганизацияОплачиваетПеревозку", Организация);

	Если Не ЗначениеЗаполнено(УсловияСчетаЗаказа) Тогда
		УсловияСчетаЗаказа = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("УсловияСчетаЗаказа",
			Организация);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(УсловияГарантийногоТалона) Тогда
		УсловияГарантийногоТалона = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("УсловияГарантии",
			Организация);
	КонецЕсли;

	НастройкаКладовщик = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ПодписьМОЛ", Организация);
	Если ЗначениеЗаполнено(НастройкаКладовщик) Тогда
		ПодписьКладовщика = НастройкаКладовщик;
	КонецЕсли;

КонецПроцедуры

// Процедура заполнения документа на основании заказа поставщику.
//
// Параметры:
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоЗаказПоставщикуНаПереработку(ДанныеЗаполнения)

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ЗаказыОстатки.НомерСтроки) КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЗаказыОстатки.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТипНоменклатурыЗапас,
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТСтавкиНДСНоменклатура.СтавкаНДС,
	|			ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА ВТСтавкиНДСОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНДСНоменклатура.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	ЗаказыОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказыОстатки.Заказ КАК Заказ,
	|	ЗаказыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(ЗаказыОстатки.Количество) КАК Количество,
	|	СУММА(ЗаказыОстатки.Резерв) КАК Резерв
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказПоставщикуМатериалы.НомерСтроки КАК НомерСтроки,
	|		ЗаказПоставщикуМатериалы.Номенклатура КАК Номенклатура,
	|		ЗаказПоставщикуМатериалы.Характеристика КАК Характеристика,
	|		ЗаказПоставщикуМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказПоставщикуМатериалы.Ссылка КАК Заказ,
	|		ЗаказПоставщикуМатериалы.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ЗаказПоставщикуМатериалы.Количество КАК Количество,
	|		ЗаказПоставщикуМатериалы.Резерв КАК Резерв
	|	ИЗ
	|		Документ.ЗаказПоставщику.Материалы КАК ЗаказПоставщикуМатериалы
	|	ГДЕ
	|		ЗаказПоставщикуМатериалы.Ссылка = &ДокументОснование
	|		И ЗаказПоставщикуМатериалы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПриходнаяНакладнаяЗапасы.НомерСтроки,
	|		ПриходнаяНакладнаяЗапасы.Номенклатура,
	|		ПриходнаяНакладнаяЗапасы.Характеристика,
	|		ПриходнаяНакладнаяЗапасы.ЕдиницаИзмерения,
	|		ПриходнаяНакладнаяЗапасы.Заказ,
	|		ПриходнаяНакладнаяЗапасы.ЗаказПокупателя,
	|		ПриходнаяНакладнаяЗапасы.Количество,
	|		ВЫБОР
	|			КОГДА ПриходнаяНакладнаяЗапасы.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ТОГДА ПриходнаяНакладнаяЗапасы.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
	|	ГДЕ
	|		ПриходнаяНакладнаяЗапасы.Ссылка.Проведен
	|		И
	|			ПриходнаяНакладнаяЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПриходнаяНакладная.ВозвратОтПереработчика)
	|		И ПриходнаяНакладнаяЗапасы.Заказ = &ДокументОснование
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РасходнаяНакладнаяЗапасы.НомерСтроки,
	|		РасходнаяНакладнаяЗапасы.Номенклатура,
	|		РасходнаяНакладнаяЗапасы.Характеристика,
	|		РасходнаяНакладнаяЗапасы.ЕдиницаИзмерения,
	|		РасходнаяНакладнаяЗапасы.Заказ,
	|		РасходнаяНакладнаяЗапасы.ЗаказПокупателя,
	|		-РасходнаяНакладнаяЗапасы.Количество,
	|		-РасходнаяНакладнаяЗапасы.Резерв
	|	ИЗ
	|		Документ.РасходнаяНакладная.Запасы КАК РасходнаяНакладнаяЗапасы
	|	ГДЕ
	|		РасходнаяНакладнаяЗапасы.Ссылка.Проведен
	|		И
	|			РасходнаяНакладнаяЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходнаяНакладная.ПередачаВПереработку)
	|		И РасходнаяНакладнаяЗапасы.Заказ = &ДокументОснование
	|		И НЕ РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка) КАК ЗаказыОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСНоменклатура
	|		ПО ЗаказыОстатки.Номенклатура.ВидСтавкиНДС = ВТСтавкиНДСНоменклатура.ВидСтавкиНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНДС КАК ВТСтавкиНДСОрганизация
	|		ПО ЗаказыОстатки.Заказ.Организация.ВидСтавкиНДСПоУмолчанию = ВТСтавкиНДСОрганизация.ВидСтавкиНДС
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ЗаказыОстатки.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЗаказыОстатки.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТСтавкиНДСНоменклатура.СтавкаНДС,
	|			ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА ВТСтавкиНДСОрганизация.СтавкаНДС
	|		ИНАЧЕ ВТСтавкиНДСНоменклатура.СтавкаНДС
	|	КОНЕЦ,
	|	ЗаказыОстатки.Характеристика,
	|	ЗаказыОстатки.ЕдиницаИзмерения,
	|	ЗаказыОстатки.Заказ,
	|	ЗаказыОстатки.ЗаказПокупателя,
	|	ЗаказыОстатки.Номенклатура.СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ЗаказыОстатки.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.Количество) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Запрос = Новый Запрос;
	ПериодЗапроса = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	Запрос.Текст = Справочники.СтавкиНДС.ПолучитьТекстЗапросаСозданияВТСтавкиНДС(ПериодЗапроса) + ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл

			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Если НоваяСтрока.Резерв < 0 Или Не ЗначениеЗаполнено(НоваяСтрока.ЗаказПокупателя) Тогда
				НоваяСтрока.Резерв = 0;
			КонецЕсли;
			НоваяСтрока.СтруктурнаяЕдиница = СтруктурнаяЕдиница;

			Если Не НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда

				Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
					НоваяСтрока.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
				Иначе
					НоваяСтрока.СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
				КонецЕсли;

			КонецЕсли;

			Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
				ВыборкаЗаказПокупателя = Выборка.ЗаказПокупателя; // ДокументСсылка.ЗаказПокупателя
				НоваяСтрока.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаЗаказПокупателя, "Проект");
			КонецЕсли;

		КонецЦикла;
	КонецЕсли;
		
	// Заполнение резервов.
	Если Запасы.Количество() > 0 И Константы.ФункциональнаяОпцияРезервированиеЗапасов.Получить() И ЗначениеЗаполнено(
		СтруктурнаяЕдиница) Тогда
		ЗаполнитьКолонкуРезервПоРезервам();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПоЗаказПоставщикуНаПереработку()

// Процедура заполнения документа на основании заказа поставщику.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//	
Процедура ЗаполнитьПоЗаказПоставщикуНаЗакупку(ДанныеЗаполнения)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыОстатки.Характеристика КАК Характеристика,
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыОстатки.Номенклатура КАК Номенклатура,
	|		ЗаказыОстатки.Характеристика КАК Характеристика,
	|		ЗаказыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам.Остатки(, ЗаказПоставщику = &ДокументОснование
	|		И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)) КАК ЗаказыОстатки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗаказыПоставщикам.Номенклатура,
	|		ДвиженияДокументаЗаказыПоставщикам.Характеристика,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗаказыПоставщикам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗаказыПоставщикам.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗаказыПоставщикам.Количество, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыПоставщикам КАК ДвиженияДокументаЗаказыПоставщикам
	|	ГДЕ
	|		ДвиженияДокументаЗаказыПоставщикам.Регистратор = &Ссылка
	|		И ДвиженияДокументаЗаказыПоставщикам.ЗаказПоставщику = &ДокументОснование) КАК ЗаказыОстатки
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыОстатки.Номенклатура,
	|	ЗаказыОстатки.Характеристика
	|ИМЕЮЩИЕ
	|	СУММА(ЗаказыОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПоставщикуЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПоставщикуЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуЗапасы.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуЗапасы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТипНоменклатурыЗапас,
	|	ЗаказПоставщикуЗапасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщикуЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПоставщикуЗапасы.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ЗаказПоставщикуЗапасы.Количество КАК Количество,
	|	ЗаказПоставщикуЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПоставщикуЗапасы.Цена КАК Цена,
	|	ЗаказПоставщикуЗапасы.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ЗаказПоставщикуЗапасы.Сумма КАК Сумма,
	|	ЗаказПоставщикуЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПоставщикуЗапасы.СуммаНДС КАК СуммаНДС,
	|	ЗаказПоставщикуЗапасы.Всего КАК Всего,
	|	ЗаказПоставщикуЗапасы.Содержание КАК Содержание,
	|	ЗаказПоставщикуЗапасы.Ссылка КАК Заказ,
	|	ВЫБОР
	|		КОГДА НЕ РезервированиеЗапасов.Значение
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА
	|			ЗаказПоставщикуЗапасы.Ссылка.ПоложениеЗаказаПокупателя = ЗНАЧЕНИЕ(Перечисление.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти)
	|			ТОГДА ЗаказПоставщикуЗапасы.ЗаказПокупателя
	|		ИНАЧЕ ЗаказПоставщикуЗапасы.Ссылка.ЗаказПокупателя
	|	КОНЕЦ КАК ЗаказПокупателя
	|ИЗ
	|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК РезервированиеЗапасов
	|ГДЕ
	|	ЗаказПоставщикуЗапасы.Ссылка = &ДокументОснование
	|	И ЗаказПоставщикуЗапасы.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаОстатков = МассивРезультатов[0].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("Номенклатура,Характеристика");

	Выборка = МассивРезультатов[1].Выбрать();
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл

		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Номенклатура", Выборка.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", Выборка.Характеристика);

		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляПоиска);
		Если МассивСтрокОстатков.Количество() = 0 Тогда

			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

		ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) = Выборка.Количество Тогда

			МассивСтрокОстатков[0].КоличествоОстаток = 0;
			Продолжить;

		ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) > Выборка.Количество Тогда

			МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - Выборка.Количество
				* Выборка.Коэффициент;
			Продолжить;

		ИначеЕсли (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент) < Выборка.Количество Тогда

			КоличествоКСписанию = -1 * (МассивСтрокОстатков[0].КоличествоОстаток / Выборка.Коэффициент
				- Выборка.Количество);
			МассивСтрокОстатков[0].КоличествоОстаток = 0;

			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

			ДанныеСтроки = Новый Структура;
			ДанныеСтроки.Вставить("Количество", КоличествоКСписанию);
			ДанныеСтроки.Вставить("Цена", Выборка.Цена);
			ДанныеСтроки.Вставить("Сумма", 0);
			ДанныеСтроки.Вставить("СтавкаНДС", Выборка.СтавкаНДС);
			ДанныеСтроки.Вставить("СуммаНДС", 0);
			ДанныеСтроки.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
			ДанныеСтроки.Вставить("Всего", 0);
			ДанныеСтроки.Вставить("ПроцентСкидкиНаценки", Выборка.ПроцентСкидкиНаценки);
			ДанныеСтроки.Вставить("СуммаСкидкиНаценки", 0);

			ЗаполнениеОбъектовУНФ.РассчитатьСуммыВСтрокеТабличнойЧасти(ДанныеСтроки);

			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);

		КонецЕсли;

		Если Не ЗначениеЗаполнено(НоваяСтрока.СтруктурнаяЕдиница) Тогда
			НоваяСтрока.СтруктурнаяЕдиница = ?(ЗначениеЗаполнено(СтруктурнаяЕдиница), СтруктурнаяЕдиница,
				ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ОсновнойСклад"));
		КонецЕсли;

		Если ПоложениеПроекта = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
			ВыборкаЗаказПокупателя = Выборка.ЗаказПокупателя; // ДокументСсылка.ЗаказПокупателя
			НоваяСтрока.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаЗаказПокупателя, "Проект");
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ЗаполнитьПоЗаказПоставщикуНаЗакупку()

Функция ЭтоТипНоменклатурыЗапас(Номенклатура)
	ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТипНоменклатуры");
	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас Тогда
		Возврат Истина;
	КонецЕсли;
	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
КонецФункции

Функция КоэффициентЕдиницыИзмерения(ЕдиницаИзмерения)

	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		Возврат 1;
	КонецЕсли;

	Если ТипЗнч(ЕдиницаИзмерения) <> Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
		Возврат 1;
	КонецЕсли;

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЕдиницаИзмерения, "Коэффициент");

КонецФункции

Функция ЭтоВводНаОснованииЗаказНаряда(ДанныеЗаполнения)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ВидОперацииЗаказаПокупателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "ВидОперации");
	КонецЕсли;

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") И ТипЗнч(
		ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ВидОперацииЗаказаПокупателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Основание,
			"ВидОперации");
	КонецЕсли;

	Возврат ВидОперацииЗаказаПокупателя = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд;

КонецФункции

Процедура ПроверитьЗаполненностьСтруктурнойЕдиницы(Отказ, ПроверяемыеРеквизиты)

	Если ПоложениеСклада <> Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить("СтруктурнаяЕдиница");
		Возврат;
	КонецЕсли;

	Для Каждого ТекСтрокаТЧ Из Запасы Цикл
		Если Не ТекСтрокаТЧ.ТипНоменклатурыЗапас Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекСтрокаТЧ.СтруктурнаяЕдиница) Тогда
			Продолжить;
		КонецЕсли;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена колонка ""Склад""  в строке %1 списка ""Запасы""'"),
			ТекСтрокаТЧ.НомерСтроки);
		КонтекстноеПоле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", ТекСтрокаТЧ.НомерСтроки,
			"СтруктурнаяЕдиница");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, КонтекстноеПоле, , Отказ);
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьПроверкуОграниченийСертификатов(Отказ)
	
	// Проверка срока действия
	РаботаСПодарочнымиСертификатами.ПроверитьСрокДействия(ЭтотОбъект, "Запасы", Отказ);
	РаботаСПодарочнымиСертификатами.ПроверитьСрокДействия(ЭтотОбъект, "Предоплата", Отказ);
	
	// Проверка области действия
	СтруктураДляПроверки = Новый Структура;
	СтруктураДляПроверки.Вставить("Запасы", Запасы.Выгрузить( , "Номенклатура, Характеристика, Сумма"));
	Сертификаты = Предоплата.Выгрузить(
		Новый Структура("ОплатаСертификатом", Истина), "Документ, СуммаРасчетов");
	Сертификаты.Колонки.Документ.Имя = "ПодарочныйСертификат";
	Сертификаты.Колонки.СуммаРасчетов.Имя = "Сумма";
	СтруктураДляПроверки.Вставить("Сертификаты", Сертификаты);
	РаботаСПодарочнымиСертификатами.ПроверитьОбластьДействияСертификатов(СтруктураДляПроверки, Отказ);

КонецПроцедуры

// Наборы
Процедура ДобавитьОписаниеНабора(ПропускаемыеНаборы, Заказ = Неопределено, НоменклатураНабора, ХарактеристикаНабора)

	Для Каждого ОписаниеНабора Из ПропускаемыеНаборы Цикл
		Если ОписаниеНабора.НоменклатураНабора = НоменклатураНабора И ОписаниеНабора.ХарактеристикаНабора = ХарактеристикаНабора
			И (Заказ = Неопределено Или Не ОписаниеНабора.Свойство("Заказ") Или ОписаниеНабора.Заказ = Заказ) Тогда
			// Уже добавлено
			Возврат;
		КонецЕсли;
	КонецЦикла;
	СтруктураНабора = Новый Структура;
	Если Заказ <> Неопределено Тогда
		СтруктураНабора.Вставить("Заказ", Заказ);
	КонецЕсли;
	СтруктураНабора.Вставить("НоменклатураНабора", НоменклатураНабора);
	СтруктураНабора.Вставить("ХарактеристикаНабора", ХарактеристикаНабора);
	ПропускаемыеНаборы.Добавить(СтруктураНабора);

КонецПроцедуры

Функция ПропуститьНабор(ПропускаемыеНаборы, СтрокаНабора)

	Для Каждого ОписаниеНабора Из ПропускаемыеНаборы Цикл
		Если ОписаниеНабора.НоменклатураНабора = СтрокаНабора.НоменклатураНабора И ОписаниеНабора.ХарактеристикаНабора
			= СтрокаНабора.ХарактеристикаНабора И (Не ОписаниеНабора.Свойство("Заказ") Или ОписаниеНабора.Заказ
			= СтрокаНабора.Заказ) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;

КонецФункции

Процедура ДобавитьНаборы(ВыборкаНаборы)

	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НоменклатураНабора", ВыборкаНаборы.НоменклатураНабора);
	СтруктураОтбора.Вставить("ХарактеристикаНабора", ВыборкаНаборы.ХарактеристикаНабора);
	СтрокиНаборы = ДобавленныеНаборы.НайтиСтроки(СтруктураОтбора);
	Если СтрокиНаборы.Количество() = 0 Тогда
		ЗаполнитьЗначенияСвойств(ДобавленныеНаборы.Добавить(), ВыборкаНаборы);
	Иначе
		СтрокиНаборы[0].Количество = СтрокиНаборы[0].Количество + ВыборкаНаборы.Количество;
	КонецЕсли;

КонецПроцедуры

Функция НесколькоЗаказовПокупателей(Документ)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщикуЗапасы.ЗаказПокупателя) КАК КоличествоЗаказов
		|ИЗ
		|	Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
		|ГДЕ
		|	ЗаказПоставщикуЗапасы.Ссылка = &Ссылка";
	Иначе
		Запрос.УстановитьПараметр("Ссылка", Документ);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВложенныйЗапрос.ЗаказПокупателя) КАК КоличествоЗаказов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПриходнаяНакладнаяЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
		|	ИЗ
		|		Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
		|	ГДЕ
		|		ПриходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|		И ПриходнаяНакладнаяЗапасы.Заказ В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПриходнаяНакладнаяЗапасы.Заказ
		|	ИЗ
		|		Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
		|	ГДЕ
		|		ПриходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|		И ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
		|		И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗаказПоставщикуЗапасы.ЗаказПокупателя
		|	ИЗ
		|		Документ.ПриходнаяНакладная.Запасы КАК ПриходнаяНакладнаяЗапасы
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Запасы КАК ЗаказПоставщикуЗапасы
		|			ПО ПриходнаяНакладнаяЗапасы.Заказ = ЗаказПоставщикуЗапасы.Ссылка
		|	ГДЕ
		|		ПриходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|		И ПриходнаяНакладнаяЗапасы.Заказ ССЫЛКА Документ.ЗаказПоставщику
		|		И ПриходнаяНакладнаяЗапасы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)) КАК ВложенныйЗапрос";
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	Выборка.Следующий();
	Возврат Выборка.КоличествоЗаказов > 1;

КонецФункции

Процедура ПроверитьЗаполнениеНоменклатурыПоДоговоруОбслуживания(Отказ)

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБиллинг") Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Договор) Тогда
		Возврат;
	КонецЕсли;

	СвойстваДоговора = Справочники.ДоговорыКонтрагентов.СвойстваДоговораОбслуживания(Договор);

	Если Не СвойстваДоговора.ЭтоДоговорОбслуживания Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаЗапасы Из Запасы Цикл
		РазрешенаПродажаПозиции = Справочники.ДоговорыКонтрагентов.РазрешенаПродажаНоменклатурыПоДоговоруОбслуживания(
			Договор, СтрокаЗапасы.Номенклатура, СтрокаЗапасы.Характеристика);
		Если РазрешенаПродажаПозиции Тогда
			Продолжить;
		КонецЕсли;
		ТекстСообщения = НСтр(
			"ru = 'Запрещено проводить незапланированные товары/услуги по текущему договору обслуживания.'");
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Запасы", СтрокаЗапасы.НомерСтроки,
			"Номенклатура");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СвойстваДоговора.ДоговорОбслуживанияТарифныйПлан,
			ПутьКДанным, , Отказ);
	КонецЦикла;

КонецПроцедуры

Процедура ПеренестиСерииНоменклатуры(ТаблицаСерииНоменклатурыЗаказа, ТаблицаЗапасыЗаказа)

	Если Не ТаблицаСерииНоменклатурыЗаказа.Количество() Тогда
		Возврат;
	КонецЕсли;

	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Партия");
	СтруктураПоискаКлючСвязи = Новый Структура("КлючСвязи");

	ТаблицаСтрокЗапасовССериями = Новый ТаблицаЗначений;
	ТаблицаСтрокЗапасовССериями.Колонки.Добавить("КлючСвязи");

	СтруктураДанные = Новый Структура;
	Для Каждого СтрокаСерийЗаказа Из ТаблицаСерииНоменклатурыЗаказа Цикл

		СтруктураПоискаКлючСвязи.КлючСвязи = СтрокаСерийЗаказа.КлючСвязи;

		НайденныеСтрокиЗапасыЗаказПокупателя = ТаблицаЗапасыЗаказа.НайтиСтроки(СтруктураПоискаКлючСвязи);

		Если Не НайденныеСтрокиЗапасыЗаказПокупателя.Количество() Тогда
			Продолжить;
		КонецЕсли;

		НайденнаяСтрока = НайденныеСтрокиЗапасыЗаказПокупателя[0];

		СтруктураДанные.Вставить("Номенклатура", НайденнаяСтрока.Номенклатура);
		СерииНоменклатурыУНФКлиентСервер.ДополнитьСтруктуруДаннымиДляПолученияСерийНоменклатуры(ЭтотОбъект,
			СтруктураДанные);
		СтруктураДанные.ПараметрыПодбораСтатуса.Вставить("ВидОперации",
			Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю);
		СтатусПодбораСерий = СерииНоменклатурыУНФ.СтатусСерийНоменклатурыПриИзменении(СтруктураДанные);
		СтруктураДанные.Очистить();

		Если СтатусПодбораСерий = 0 Тогда
			Продолжить;
		КонецЕсли;

		СтруктураПоиска.Номенклатура = НайденнаяСтрока.Номенклатура;
		СтруктураПоиска.Характеристика = НайденнаяСтрока.Характеристика;
		СтруктураПоиска.Партия = НайденнаяСтрока.Партия;

		НайденныеСтрокиЗапасы = Запасы.НайтиСтроки(СтруктураПоиска);

		Если Не НайденныеСтрокиЗапасы.Количество() Тогда
			Продолжить;
		КонецЕсли;

		СтрокаЗапасовССериями = ТаблицаСтрокЗапасовССериями.Добавить();
		СтрокаЗапасовССериями.КлючСвязи = НайденныеСтрокиЗапасы[0].КлючСвязи;

		НоваяСтрокаСерии = СерииНоменклатуры.Добавить();
		НоваяСтрокаСерии.КлючСвязи = НайденныеСтрокиЗапасы[0].КлючСвязи;
		НоваяСтрокаСерии.Количество = СтрокаСерийЗаказа.Количество;
		НоваяСтрокаСерии.Серия = СтрокаСерийЗаказа.Серия;

	КонецЦикла;

	ТаблицаСтрокЗапасовССериями.Свернуть("КлючСвязи");
	
	// Заполним строковое представление серий

	Для Каждого СтрокаТаблицыЗапасовССериями Из ТаблицаСтрокЗапасовССериями Цикл

		СтроковоеПредставлениеСерийНоменклатуры = "";
		СтруктураПоискаКлючСвязи.КлючСвязи = СтрокаТаблицыЗапасовССериями.КлючСвязи;

		НайденныеСтрокиСерии = СерииНоменклатуры.НайтиСтроки(СтруктураПоискаКлючСвязи);

		Если Не НайденныеСтрокиСерии.Количество() Тогда
			Продолжить;
		КонецЕсли;

		Для Каждого СтрокаСерий Из НайденныеСтрокиСерии Цикл

			СтроковоеПредставлениеСерийНоменклатуры = СтроковоеПредставлениеСерийНоменклатуры + СтрокаСерий.Серия + "; ";

		КонецЦикла;

		СтроковоеПредставлениеСерийНоменклатуры = Лев(СтроковоеПредставлениеСерийНоменклатуры, Мин(СтрДлина(
			СтроковоеПредставлениеСерийНоменклатуры) - 2, 150));

		НайденныеСтрокиЗапасы = Запасы.НайтиСтроки(СтруктураПоискаКлючСвязи);

		Если Не НайденныеСтрокиЗапасы.Количество() Тогда
			Продолжить;
		КонецЕсли;

		НайденныеСтрокиЗапасы[0].СерииНоменклатуры = СтроковоеПредставлениеСерийНоменклатуры;

	КонецЦикла;
КонецПроцедуры

#Область Прослеживаемость

#КонецОбласти

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли