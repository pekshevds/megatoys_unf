#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиУНФ.ЗаполнитьДополнительныеПараметры(Объект,
		"ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПечатьДокументовУНФ.УстановитьОтображениеПодменюПечати(Элементы.ПодменюПечать);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.ВариантИспользованияПоложенияДляМагазина = Перечисления.ВариантыИспользованияПоложенийДляРасчетаПремий.ИндивидуальноеДляМагазина;
		
	КонецЕсли;
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ОдноПоложениеДляВсехМагазинов", Перечисления.ВариантыИспользованияПоложенийДляРасчетаПремий.ОдноПоложениеДляВсехМагазинов);
	КэшЗначений.Вставить("ИндивидуальноеДляМагазина", Перечисления.ВариантыИспользованияПоложенийДляРасчетаПремий.ИндивидуальноеДляМагазина);
	КэшЗначений.Вставить("ОдноДляВыбранныхМагазинов", Перечисления.ВариантыИспользованияПоложенийДляРасчетаПремий.ОдноДляВыбранныхМагазинов);
	
	НаборСвойств_Документ_ПоложениеОПремировании = УправлениеСвойствами.НаборСвойствПоИмени(
		"Документ_ПоложениеОПремировании");
	КэшЗначений.Вставить("НаборСвойств_Документ_ПоложениеОПремировании", НаборСвойств_Документ_ПоложениеОПремировании);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьЗаголовокГруппыПравил();
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Обсуждения
	ОбсужденияУНФКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	//Обсуждения
	ОбсужденияУНФ.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантИспользованияПоложенияДляМагазинаПриИзменении(Элемент)
	
	Если Объект.ВариантИспользованияПоложенияДляМагазина 
		= КэшЗначений.ОдноПоложениеДляВсехМагазинов Тогда
		
		Объект.Магазины.Очистить();
		
	КонецЕсли;
	
	Объект.ПравилаРасчетаПремий.Очистить(); 
	
	УстановитьВидимостьДоступностьЭлементов();
	УстановитьЗаголовокГруппыПравил();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект,
		"Объект.Комментарий");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМагазины

&НаКлиенте
Процедура МагазиныПриИзменении(Элемент)
	
	УстановитьОтборТаблицыПравилРасчетаПоТекущимДанным();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазиныПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборТаблицыПравилРасчетаПоТекущимДанным();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазиныМагазинПриИзменении(Элемент)
	
	УстановитьОтборТаблицыПравилРасчетаПоТекущимДанным();
	
КонецПроцедуры

&НаКлиенте
Процедура МагазиныПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановитьЗаголовокГруппыПравил();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПравилаРасчетаПремийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрокаПравилаРасчетаПремий = Элементы.ПравилаРасчетаПремий.ТекущиеДанные;
	
	Если Объект.ВариантИспользованияПоложенияДляМагазина = 
		КэшЗначений.ИндивидуальноеДляМагазина Тогда
		
		Если Элементы.ПравилаРасчетаПремий.ОтборСтрок <> Неопределено Тогда
			
			ТекущаяСтрокаПравилаРасчетаПремий.Магазин =  Элементы.ПравилаРасчетаПремий.ОтборСтрок.Магазин;
			
		КонецЕсли;
		
	Иначе
		
		ТекущаяСтрокаПравилаРасчетаПремий.Магазин = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьДополнительныйРеквизит(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", КэшЗначений.НаборСвойств_Документ_ПоложениеОПремировании);
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Ложь);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЗаголовокГруппыПравил()

	ТекстЗаголовка = "";
	
	Если Объект.ВариантИспользованияПоложенияДляМагазина 
		= КэшЗначений.ОдноПоложениеДляВсехМагазинов Тогда
		
		ТекстЗаголовка = НСтр("ru = 'Использование для всех подразделений'");
		
	ИначеЕсли Объект.ВариантИспользованияПоложенияДляМагазина 
		= КэшЗначений.ОдноДляВыбранныхМагазинов Тогда
		
		ТекстЗаголовка = НСтр("ru = 'Использование для выбранных подразделений'");
		
	Иначе
		
		ТекущаяСтрокаМагазины = Элементы.Магазины.ТекущиеДанные;
		ТекущийМагазин = "";
		Если ТекущаяСтрокаМагазины <> Неопределено Тогда
			ТекущийМагазин = ТекущаяСтрокаМагазины.Магазин;
		КонецЕсли;
		
		ТекстЗаголовка = НСтр("ru = 'Использование для подразделения %1'");
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, ТекущийМагазин);
		
	КонецЕсли;
	
	Элементы.ГруппаПравила.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	ВидимостьГруппыМагазинов = НЕ Объект.ВариантИспользованияПоложенияДляМагазина 
		= КэшЗначений.ОдноПоложениеДляВсехМагазинов;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаМагазины", "Видимость", ВидимостьГруппыМагазинов);
	
	УстановитьДоступностьТаблицыПравилРасчета(); 
	
	УстановитьОтборТаблицыПравилРасчетаПоТекущимДанным();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьТаблицыПравилРасчета()

	ДоступностьТаблицыПравил = (Объект.ВариантИспользованияПоложенияДляМагазина 
			= КэшЗначений.ОдноПоложениеДляВсехМагазинов)
			ИЛИ Объект.Магазины.Количество() > 0;
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПравилаРасчетаПремий", "Доступность", ДоступностьТаблицыПравил);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПравилаРасчетаПремий", "ТолькоПросмотр", НЕ ДоступностьТаблицыПравил);
			
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТаблицыПравилРасчетаПремий(ЗначениеОтбора)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПравилаРасчетаПремий", "ОтборСтрок", ЗначениеОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТаблицыПравилРасчетаПоТекущимДанным()

	Если Объект.ВариантИспользованияПоложенияДляМагазина
		= КэшЗначений.ОдноПоложениеДляВсехМагазинов Тогда
		
		УстановитьОтборТаблицыПравилРасчетаПремий(Неопределено);
		
	Иначе
		
		ТекущаяСтрокаМагазины = Элементы.Магазины.ТекущиеДанные;
		Если ТекущаяСтрокаМагазины <> Неопределено Тогда
			
			Если Объект.ВариантИспользованияПоложенияДляМагазина =
				КэшЗначений.ОдноДляВыбранныхМагазинов Тогда
				
				ЗначениеОтбора = Неопределено;
				
			Иначе
				
				ЗначениеОтбора = Новый ФиксированнаяСтруктура("Магазин", ТекущаяСтрокаМагазины.Магазин);
				
			КонецЕсли;
			
			УстановитьОтборТаблицыПравилРасчетаПремий(ЗначениеОтбора); 
			УстановитьЗаголовокГруппыПравил();
			
		КонецЕсли;
		
	КонецЕсли; 
	
	УстановитьДоступностьТаблицыПравилРасчета();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти






















