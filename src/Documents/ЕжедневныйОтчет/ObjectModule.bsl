#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


// Процедура - Заполнить документ по справочнику рабочая смена
//
// Параметры:
//  ДанныеЗаполнения - СправочникСсылка.РабочиеСменыСтруктурныхЕдиниц - основание документа
//
Процедура ЗаполнитьПоРабочейСмене(ДанныеЗаполнения) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		СправочникОснование = ДанныеЗаполнения;
		БлокировкаДанных  = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.РабочиеСменыСтруктурныхЕдиниц");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СправочникОснование);
		БлокировкаДанных.Заблокировать();
		
		ДатаОтчета = НачалоДня(ТекущаяДатаСеанса());
		Склад = ДанныеЗаполнения.Склад;
		РабочаяСмена = ДанныеЗаполнения;
		
		Документы.ЕжедневныйОтчет.ЗаполнитьТабличнуюЧастьСотрудникиСмены(ЭтотОбъект);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = НСтр("ru = 'Ошибка заполнения документа по основанию'", ОбщегоНазначения.КодОсновногоЯзыка());
		ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ТекстОшибки, УровеньЖурналаРегистрации.Ошибка, , , ПредставлениеОшибки);
		Отказ = Истина;
	
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("СправочникСсылка.РабочиеСменыСтруктурныхЕдиниц")] = "ЗаполнитьПоРабочейСмене";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеДокументовУНФ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ЕжедневныйОтчет.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеДокументовУНФ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ПроведениеДокументовУНФ.ОтразитьДвижения("ФактическоеРабочееВремяСотрудников", ТаблицыДляДвижений, Движения, Отказ);
	
	ПроведениеДокументовУНФ.ЗакрытьМенеджерВременныхТаблиц(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли