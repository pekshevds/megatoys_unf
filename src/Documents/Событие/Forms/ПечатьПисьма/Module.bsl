
#Область ОписаниеПеременных

&НаКлиенте
Перем ПервоеОткрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Письмо") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПечататьНемедленно") Тогда
		ПечататьНемедленно = Параметры.ПечататьНемедленно;
	КонецЕсли;
	
	Письмо = Параметры.Письмо;
	
	ВложенияПисьма = Документы.Событие.ВложенияСобытия(Письмо, УникальныйИдентификатор);
	ТекстHTML = Гипертекст.ОбработатьТекстHTML(
		Письмо.СодержаниеHTML, ,
		ВложенияПисьма);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Элементы.ТекстHTML.Документ.execCommand("Print"); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТекстHTMLДокументСформирован(Элемент)
	
	ГипертекстКлиент.ПрименитьНастройкиОтображения(Элемент.Документ);
	
	Если ПечататьНемедленно Тогда
		
		Если ПервоеОткрытие = Неопределено Тогда
			
			ПервоеОткрытие = Ложь;
			Элементы.ТекстHTML.Документ.execCommand("Print"); 
			ПодключитьОбработчикОжидания("ЗакрытиеФормы", 1, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормы()
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
