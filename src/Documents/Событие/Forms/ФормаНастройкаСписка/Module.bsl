
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиСпискаПапок = ХранилищеСистемныхНастроек.Загрузить("НастройкиСпискаПапок", 
			"ГруппаОтбораСписокПисем_НастройкиСпискаПапок");
	
	Если НастройкиСпискаПапок <> Неопределено И ТипЗнч(НастройкиСпискаПапок) = Тип("Структура") Тогда
		
		НастройкаВидСписка = НастройкиСпискаПапок.ВидСписка;
		НастройкаВидПапок  = НастройкиСпискаПапок.ВидПапок;
		ОтображатьКолонкуВложения = НастройкиСпискаПапок.ОтображатьКолонкуВложения;
		
		Если ОтображатьКолонкуВложения = Неопределено Тогда
			ОтображатьКолонкуВложения = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НастройкаВидСписка) Тогда
			НастройкаВидСписка = "Расширенный";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НастройкаВидПапок) Тогда
			НастройкаВидПапок = "Использовать";
		КонецЕсли;
		
		ПереключательКомпактный = НастройкаВидСписка;
		ПереключательРасширенный = НастройкаВидСписка;
		
		ПапкиИспользовать = НастройкаВидПапок;
		ПапкиНеИспользовать = НастройкаВидПапок;
		
	Иначе
		ПереключательКомпактный = "";
		ПереключательРасширенный = "Расширенный";
		
		ПапкиИспользовать = "Использовать";
		ПапкиНеИспользовать = "";
		
		ОтображатьКолонкуВложения = Истина;
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	СохранитьНастройкиОтборов();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидРасширенныйПриИзменении(Элемент)
	ПереключательКомпактный = "";
	ПереключательРасширенный = "Расширенный";
	НастройкаВидСписка = ПереключательРасширенный;
КонецПроцедуры

&НаКлиенте
Процедура ВидКомпактныйПриИзменении(Элемент)
	ПереключательКомпактный = "Компактный";
	ПереключательРасширенный = "";
	НастройкаВидСписка = ПереключательКомпактный;
КонецПроцедуры

&НаКлиенте
Процедура ПапкиИспользоватьПриИзменении(Элемент)
	ПапкиИспользовать = "Использовать";
	ПапкиНеИспользовать = "";
	НастройкаВидПапок = ПапкиИспользовать;
КонецПроцедуры

&НаКлиенте
Процедура ПапкиНеИспользоватьПриИзменении(Элемент)
	ПапкиИспользовать = "";
	ПапкиНеИспользовать = "НеИспользовать";
	НастройкаВидПапок = ПапкиНеИспользовать;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	НастройкиСпискаПапок = Новый Структура;
	НастройкиСпискаПапок.Вставить("ВидСписка", НастройкаВидСписка);
	НастройкиСпискаПапок.Вставить("ВидПапок", НастройкаВидПапок);
	НастройкиСпискаПапок.Вставить("ОтображатьКолонкуВложения", ОтображатьКолонкуВложения);
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		
		ХранилищеСистемныхНастроек.Сохранить("НастройкиСпискаПапок", 
			"ГруппаОтбораСписокПисем_НастройкиСпискаПапок",
			НастройкиСпискаПапок);
			
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
