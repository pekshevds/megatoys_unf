#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ЗаполнитьНаименованиеОписание(ДанныеЗаполнения);
		ЗаполнитьПоДаннымКалендаря(ДанныеЗаполнения);
		ЗаполнитьОснование(ДанныеЗаполнения);
	КонецЕсли;
	
	ДозаполнитьПоУмолчанию();
	
КонецПроцедуры

Процедура ДозаполнитьПоУмолчанию()
	
	Если Не ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Автор) Тогда
		Автор = АвторизованныйПользователь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			АвторизованныйПользователь, "ОсновнойОтветственный");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Календарь) Тогда
		Календарь = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнойКалендарь");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(КолонкаКалендаря) Тогда
		КолонкаКалендаря = Справочники.КолонкиКалендарейСотрудников.КолонкаНеобработанное(Календарь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемоеНаименование = ПроверяемыеРеквизиты.Найти("Наименование");
	Если ПроверяемоеНаименование <> Неопределено Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемоеНаименование);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаНачала");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбменСGoogle.ПередЗаписьюИсточникаЗаписиКалендаря(ЭтотОбъект);
	ОбменСВнешнимиКалендарями.ПередЗаписьюИсточникаЗаписиКалендаря(ЭтотОбъект);
	
	Наименование = СтрПолучитьСтроку(Описание, 1);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбменСGoogle.ПриЗаписиИсточникаЗаписиКалендаря(ЭтотОбъект);
	ОбменСВнешнимиКалендарями.ПриЗаписиИсточникаЗаписиКалендаря(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаименованиеОписание(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения.Свойство("Наименование") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ДанныеЗаполнения.Наименование)
		И НЕ СтрНачинаетсяС(Описание, ДанныеЗаполнения.Наименование) Тогда
		Наименование = ДанныеЗаполнения.Наименование;
		Описание = ДанныеЗаполнения.Наименование + Символы.ПС + Описание;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымКалендаря(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения.Свойство("ДанныеЗаписиКалендаря") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполнения.ДанныеЗаписиКалендаря.Свойство("Начало",            ДатаНачала);
	ДанныеЗаполнения.ДанныеЗаписиКалендаря.Свойство("Окончание",         ДатаОкончания);
	ДанныеЗаполнения.ДанныеЗаписиКалендаря.Свойство("Описание",          Описание);
	ДанныеЗаполнения.ДанныеЗаписиКалендаря.Свойство("КолонкаКалендаря",  КолонкаКалендаря);
	ДанныеЗаполнения.ДанныеЗаписиКалендаря.Свойство("Календарь",         Календарь);
	
КонецПроцедуры

Процедура ЗаполнитьОснование(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения.Свойство("Основание") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения.Основание) = Тип("Строка") Тогда
		
		Основание = Неопределено;
		ОснованиеСтроковаяСсылка = ДанныеЗаполнения.Основание;
		ДанныеЗаполнения.Свойство("ОснованиеПредставление", ОснованиеПредставление);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения.Основание) = Тип("Массив") И ДанныеЗаполнения.Основание.Количество() = 1 Тогда
		Основание = ДанныеЗаполнения.Основание[0];
		ОснованиеСтроковаяСсылка = Неопределено;
		ОснованиеПредставление = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
