
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.ПлатежныеРеквизиты) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		Контрагент = Параметры.Контрагент;
	ИначеЕсли ЗначениеЗаполнено(Параметры.БанковскийСчет) Тогда
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.БанковскийСчет, "Владелец");
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДатаДокумента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры.ПлатежныеРеквизиты, "ДатаДокумента", ТекущаяДатаСеанса());
	
	Реквизиты = ДанныеГосударственныхОрганов.ПолучитьПлатежныеРеквизитыКонтрагента(Контрагент);
	Если Параметры.ПлатежныеРеквизиты.Свойство("ПлательщикЕНП") И Параметры.ПлатежныеРеквизиты.ПлательщикЕНП Тогда
		ЕдиныйНалоговыйСчет.ПлатежныеРеквизитыЕдиногоНалоговогоСчета(Реквизиты.ПлатежныеРеквизиты);
	КонецЕсли;
	ПлатежныеРеквизитыДокумента = Параметры.ПлатежныеРеквизиты;
	
	ТолькоНовыеРеквизиты =
		ПлатежиВБюджетКлиентСервер.ПрекращеноДействиеПереходногоПериодаУплатыНаКазначейскиеСчета(ДатаДокумента);
	
	ВДокументеНовыеРеквизиты2021 = БанковскиеПравила.ЭтоБИКТОФК(ПлатежныеРеквизитыДокумента.БИК)
		И БанковскиеПравила.ЭтоКазначейскийСчет(ПлатежныеРеквизитыДокумента.РасчетныйСчет)
		И БанковскиеПравила.ЭтоЕдиныйКазначейскийСчет(ПлатежныеРеквизитыДокумента.КоррСчет);
	
	ПредлагаютсяНовыеРекизиты2021 = БанковскиеПравила.ЭтоБИКТОФК(Реквизиты.ПлатежныеРеквизиты.БИК)
		И БанковскиеПравила.ЭтоКазначейскийСчет(Реквизиты.ПлатежныеРеквизиты.РасчетныйСчет)
		И БанковскиеПравила.ЭтоЕдиныйКазначейскийСчет(Реквизиты.ПлатежныеРеквизиты.КоррСчет);
	
	ЭтоРекомендацияПерейтиНаРеквизиты2021 = Не ТолькоНовыеРеквизиты
		И Не ВДокументеНовыеРеквизиты2021 И ПредлагаютсяНовыеРекизиты2021;
	
	Если ЭтоРекомендацияПерейтиНаРеквизиты2021 Тогда
		ДобавитьПлатежныйРеквизит(НСтр("ru = ''"),
			НСтр("ru = 'В документе (актуальны до 30.04.21)'"),
			НСтр("ru = 'Рекомендуемые значения с 01.01.21'"));
		ТекстПредупреждения =
			НСтр("ru = 'С 2021 года при уплате в бюджет средства перечисляются на казначейские счета Федерального казначейства (Федеральный закон от 27.12.2019 № 479-ФЗ).
			|До 30.04.2021 платить налоговым органам можно также по реквизитам, действовавшим в 2020 году.'");
		Разделитель = Символы.ПС;
	Иначе
		ДобавитьПлатежныйРеквизит(НСтр("ru = ''"), НСтр("ru = 'В документе'"), НСтр("ru = 'Рекомендуемое значение'"));
		ТекстПредупреждения =
			НСтр("ru = 'Возможно, платежные реквизиты заполнены неверно.'");
		Разделитель = " ";
	КонецЕсли;
	
	Если Реквизиты.Вид = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		Элементы.ДекорацияПредупреждение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Элементы.ДекорацияПредупреждение.Заголовок,
			ТекстПредупреждения,
			Разделитель,
			НСтр("ru = 'Уточните актуальные реквизиты платежа в своей налоговой инспекции.'"));
	ИначеЕсли Реквизиты.Вид = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
		Элементы.ДекорацияПредупреждение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Элементы.ДекорацияПредупреждение.Заголовок,
			ТекстПредупреждения,
			Разделитель,
			НСтр("ru = 'Уточните актуальные реквизиты платежа в своем отделении ПФР.'"));
	ИначеЕсли Реквизиты.Вид = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		Элементы.ДекорацияПредупреждение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Элементы.ДекорацияПредупреждение.Заголовок,
			ТекстПредупреждения,
			Разделитель,
			НСтр("ru = 'Уточните актуальные реквизиты платежа в своем отделении ФСС.'"));
	Иначе
		Элементы.ДекорацияПредупреждение.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Элементы.ДекорацияПредупреждение.Заголовок,
			ТекстПредупреждения,
			Разделитель,
			НСтр("ru = 'Уточните актуальные реквизиты платежа в своем отделении гос.органа.'"));
	КонецЕсли;
	
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'Получатель платежа'"),
			ПлатежныеРеквизитыДокумента.ПолучательПлатежа,
			Реквизиты.ПлатежныеРеквизиты.ПолучательПлатежа);
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'ИНН'"),
			ПлатежныеРеквизитыДокумента.ИНН,
			Реквизиты.ИНН);
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'КПП'"),
			ПлатежныеРеквизитыДокумента.КПП,
			Реквизиты.КПП);
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'Расчетный счет'"),
			ПлатежныеРеквизитыДокумента.РасчетныйСчет,
			Реквизиты.ПлатежныеРеквизиты.РасчетныйСчет);
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'БИК'"),
			ПлатежныеРеквизитыДокумента.БИК,
			Реквизиты.ПлатежныеРеквизиты.БИК);
	ДобавитьПлатежныйРеквизит(НСтр("ru = 'Корр. счет'"),
			ПлатежныеРеквизитыДокумента.КоррСчет,
			Реквизиты.ПлатежныеРеквизиты.КоррСчет);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	КрасныйЦвет = Новый Цвет(251, 212, 212);
	ЗеленыйЦвет = Новый Цвет(215, 240, 199);
	
	Если Не ЭтоРекомендацияПерейтиНаРеквизиты2021 Тогда
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПлатежныеРеквизитыЗначениеРеквизитаДокумента.Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПлатежныеРеквизиты.ПлатежныеРеквизитыОтличаются");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", КрасныйЦвет);
	КонецЕсли;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПлатежныеРеквизитыЗначениеРеквизита.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПлатежныеРеквизиты.ПлатежныеРеквизитыОтличаются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЗеленыйЦвет);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПлатежныйРеквизит(ИмяРеквизита, РеквизитДокумента, Реквизит)
	
	ПлатежныйРеквизит = ПлатежныеРеквизиты.Добавить();
	ПлатежныйРеквизит.ИмяРеквизита               = ИмяРеквизита;
	ПлатежныйРеквизит.ЗначениеРеквизитаДокумента = РеквизитДокумента;
	ПлатежныйРеквизит.ЗначениеРеквизита          = Реквизит;
	Если ЗначениеЗаполнено(ИмяРеквизита) Тогда
		ПлатежныйРеквизит.ПлатежныеРеквизитыОтличаются = ПлатежныйРеквизит.ЗначениеРеквизитаДокумента <> ПлатежныйРеквизит.ЗначениеРеквизита;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПлатежныеРеквизиты()
	
	Если НЕ ДанныеГосударственныхОрганов.ПлатежныеРеквизитыАктуальны(Реквизиты) Тогда
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(Реквизиты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодставитьРекомендуемыеЗначения(Команда)
	
	ЗаписатьПлатежныеРеквизиты();
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ПерезаполнитьКонтрагента", Истина);
	ВозвращаемоеЗначение.Вставить("Контрагент", Реквизиты.Ссылка);
	ВозвращаемоеЗначение.Вставить("СчетКонтрагента", Реквизиты.ПлатежныеРеквизиты.БанковскийСчет);
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры
