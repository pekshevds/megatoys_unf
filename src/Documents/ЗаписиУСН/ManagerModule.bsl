#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ЗаписиУСНЗаписиКУДиР.ДоходВсего КАК ДоходВсего,
	|	ЗаписиУСНЗаписиКУДиР.ДоходБаза КАК ДоходБаза,
	|	ЗаписиУСНЗаписиКУДиР.РасходВсего КАК РасходВсего,
	|	ЗаписиУСНЗаписиКУДиР.РасходБаза КАК РасходБаза,
	|	ЗаписиУСНЗаписиКУДиР.НДС КАК НДС,
	|	ЗаписиУСНЗаписиКУДиР.Содержание КАК Содержание,
	|	ЗаписиУСНЗаписиКУДиР.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ЗаписиУСНЗаписиКУДиР.ДатаПервичногоДокумента КАК ДатаПервичногоДокумента,
	|	ЗаписиУСНЗаписиКУДиР.НомерПервичногоДокумента КАК НомерПервичногоДокумента,
	|	ЗаписиУСНЗаписиКУДиР.Ссылка.ВидЗаписей КАК ВидЗаписи,
	|	ЗаписиУСНЗаписиКУДиР.НомерСтроки КАК НомерСтрокиДокумента
	|ИЗ
	|	Документ.ЗаписиУСН.ЗаписиКУДиР КАК ЗаписиУСНЗаписиКУДиР
	|ГДЕ
	|	ЗаписиУСНЗаписиКУДиР.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиУСНГрафикПоОС.ПериодПризнания КАК Период,
	|	&Организация КАК Организация,
	|	ЗаписиУСНГрафикПоОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗаписиУСНГрафикПоОС.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаписиУСН.ГрафикПоОС КАК ЗаписиУСНГрафикПоОС
	|ГДЕ
	|	ЗаписиУСНГрафикПоОС.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);
	
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаКнигаУчетаДоходовИРасходов", Результат[0].Выгрузить());
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаГрафикПризнанияРасходовНаОСиНМА",
		Результат[1].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
