#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.БольничныйЛист.ОчиститьСлучайУходаПриНеобходимости(ЭтотОбъект);
	
	ОбновитьГоловнуюОрганизацию();
	
	Если ЭЛНКарантинПоКоронавирусу
		И Не ОбменЛисткамиНетрудоспособностиФСС.ЭтоКарантинныйЭЛН(
			ПричинаНетрудоспособности,
			НомерЛисткаНетрудоспособности) Тогда
		ЭЛНКарантинПоКоронавирусу = Ложь;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи());
	
	РасчетЗарплатыДляНебольшихОрганизацийСобытия.ДокументыПередЗаписью(ЭтотОбъект, Отказ);
	
	Начислено = Начисления.Итог("Результат");
	Удержано = НДФЛ.Итог("Налог");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	УчетПособийСоциальногоСтрахования.ПриЗаписиДокументаБольничныйЛист(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.СведенияОбЭЛН.ПередУдалениемБольничного(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	// Заполним описание данных для проведения в учете начисленной зарплаты.
	ДанныеДляПроведенияУчетЗарплаты = ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения();
	ДанныеДляПроведенияУчетЗарплаты.Движения 				= Движения;
	ДанныеДляПроведенияУчетЗарплаты.Организация 			= Организация;
	ДанныеДляПроведенияУчетЗарплаты.ПериодРегистрации 		= ПериодРегистрации;
	ДанныеДляПроведенияУчетЗарплаты.ПланируемаяДатаВыплаты 	= ПланируемаяДатаВыплаты;
	ДанныеДляПроведенияУчетЗарплаты.ПорядокВыплаты 			= Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
	ДанныеДляПроведенияУчетЗарплаты.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	
	УчетНачисленнойЗарплаты.ЗарегистрироватьНачисления(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НачисленияПоСотрудникам, Неопределено);
	УчетНачисленнойЗарплаты.ЗарегистрироватьОтработанноеВремя(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам, Истина);
	
	// Подготовка данных для регистрации удержаний, НДФЛ и Корректировок выплаты в учете начисленной зарплаты.
	УчетНачисленнойЗарплаты.СоздатьВТРаспределениеНачисленийТекущегоДокумента(ДанныеДляПроведенияУчетЗарплаты);
	
	// НДФЛ
	СформироватьДоходыНДФЛ(Отказ, ДанныеДляПроведения);
	// Дополняем доходы НДФЛ сведениями о распределении по статьям финансирования и/или статьям расходов.
	ОтражениеЗарплатыВУчетеБазовый.ДополнитьСведенияОДоходахНДФЛСведениямиОРаспределенииПоСтатьям(Движения);
	
	ДатаОперации = ДатаОперацииПоНалогомИВзносам();
	УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.НДФЛ, , Ложь);
	УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеДляМежрасчетногоДокумента(Движения, Отказ, Организация, ФизическоеЛицо, Ссылка);
	
	УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НДФЛПоСотрудникам, Организация, ДатаОперации);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛПоСотрудникам(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НДФЛПоСотрудникам);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛКЗачетуВозврату(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			ДанныеДляПроведения.КорректировкиВыплатыПоСотрудникам, ДанныеДляПроведения.НДФЛПоСотрудникам);
	
	// Страховые взносы
	ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(Организация, ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);
	УчетСтраховыхВзносов.СформироватьСведенияОДоходахСтраховыеВзносы(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, , Истина);
	
	// - регистрация пособий
	УчетСтраховыхВзносов.СформироватьПособия(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения.Пособия, Неопределено);
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляРегистрацииВУчетаСтажаПФР());
	
	Если ПособиеВыплачиваетсяФСС Тогда
		ТекстОшибки = УчетПособийСоциальногоСтрахования.ПроверитьНомерЛН(НомерЛисткаНетрудоспособности);
		Если ТекстОшибки <> "" Тогда
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, ТекстОшибки, "НомерЛисткаНетрудоспособности");
		КонецЕсли;
	КонецЕсли;

	Если Не Отказ Тогда
		
		// формирование проводок
		ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
		ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
		СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы, НачисленныйНДФЛ";
		ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

// В качестве данных заполнения может принимать структуру с полями.
//		Ссылка
//		Действие
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.БольничныйЛист") Тогда
		ЯвляетсяПродолжениемБолезни = Истина;
		ПервичныйБольничныйЛист = ДанныеЗаполнения;
		ЗаполнитьПоПервичномуБольничномуЛисту(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВходящийЗапросФССДляРасчетаПособия") Тогда
		Данные = Документы.ВходящийЗапросФССДляРасчетаПособия.ДанныеДляЗаполненияБольничного(ДанныеЗаполнения);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Данные);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ДатаНачалаСобытия = '00010101';
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		ДатаНачалаСобытия = ДатаНачала;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(ФизическоеЛицо)
		И Не ЗначениеЗаполнено(Сотрудник) Тогда
		Сотрудник = КадровыйУчет.ОсновнойСотрудникФизическогоЛица(ФизическоеЛицо, Организация, ДатаНачалаСобытия);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьДатуВыплаты(ЭтотОбъект, Отказ);
	ПроверитьВыплатуПособийУчастникомПроектаПоПрямымВыплатамФСС(Отказ);
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаНачала, "Объект.ДатаНачала", Отказ,
		НСтр("ru='Начало периода болезни'"));
	
	Если Не ЗначениеЗаполнено(ДатаОкончания) И ЗначениеЗаполнено(ДатаНачала) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена дата окончания освобождения от работы.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ДатаОкончания", , Отказ);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания Тогда
		ТекстСообщения = НСтр("ru = 'Дата окончания освобождения от работы не может быть меньше даты начала.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ДатаОкончания", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНарушенияРежима) Тогда
		
		ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаНарушенияРежима, "Объект.ДатаНарушенияРежима", Отказ,
			НСтр("ru='Дата нарушения режима'"), ДатаНачала, НСтр("ru='даты начала болезни'"));
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НомерЛисткаНетрудоспособности");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПериодыУходаЗаРодственниками.СлучайУхода");
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	РасчетЗарплаты.ЗаполнитьНачисления(ДанныеДляПроведения, Ссылка, , "ДатаНачала");
	РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, Ссылка);
	ЗаполнитьСведенияОПособиях(ДанныеДляПроведения);
	
	// Заполнение отработанного времени
	Для каждого СтрокаРабочегоВремени Из ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам Цикл
		СтрокаРабочегоВремени.ОтработаноДней = СтрокаРабочегоВремени.ОплаченоДней;
		СтрокаРабочегоВремени.ОтработаноЧасов = СтрокаРабочегоВремени.ОплаченоЧасов;
	КонецЦикла;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура ЗаполнитьСведенияОПособиях(ДанныеДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаНачислений.Ссылка КАК Ссылка,
		|	ТаблицаНачислений.Начисление КАК Начисление,
		|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
		|	ТаблицаНачислений.ОплаченоДней КАК ОплаченоДней,
		|	ТаблицаНачислений.Результат КАК Результат,
		|	ТаблицаНачислений.РезультатВТомЧислеЗаСчетФБ КАК РезультатВТомЧислеЗаСчетФБ,
		|	ТаблицаНачислений.Ссылка.ДатаНачалаСобытия КАК ДатаНачалаСобытия
		|ПОМЕСТИТЬ ВТНачисленияДляУчетаПособий
		|ИЗ
		|	Документ.БольничныйЛист.Начисления КАК ТаблицаНачислений
		|ГДЕ
		|	ТаблицаНачислений.Ссылка = &Ссылка
		|	И ТаблицаНачислений.Начисление.КатегорияНачисленияИлиНеоплаченногоВремени В(&КатегорииПособий)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	БольничныйЛистНачисления.Сотрудник КАК Сотрудник,
		|	БольничныйЛистНачисления.ДатаНачалаСобытия КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	ВТНачисленияДляУчетаПособий КАК БольничныйЛистНачисления";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ПособиеПлатитУчастникПилотногоПроекта = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(
		Организация,
		ПериодРегистрации,
		ЭЛНКарантинПоКоронавирусу);
	
	КатегорииПособий = Новый Массив;
	
	Если Не ПособиеПлатитУчастникПилотногоПроекта Тогда
		КатегорииПособий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста);
		КатегорииПособий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодам);
		КатегорииПособий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоПрофзаболевание);
		КатегорииПособий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве);
	КонецЕсли;
	
	КатегорииПособий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя);
	
	Запрос.УстановитьПараметр("КатегорииПособий", КатегорииПособий);
	
	Запрос.Выполнить();
	
	ОписательТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	ОписательТаблиц.ИмяВТКадровыеДанныеСотрудников = "ВТКадровыеДанныеСотрудниковПособий";
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательТаблиц, Истина, "ВидЗанятости");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоУходуЗаВзрослым)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.Карантин)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.Протезирование)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПособиеПриДолечивании)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.БеременностьРоды)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.Профзаболевание)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоУходуЗаРебенком)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.ПустаяСсылка)
		|	КОНЕЦ КАК ВидПособияСоциальногоСтрахования,
		|	ТаблицаНачислений.Ссылка КАК Ссылка,
		|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
		|	СУММА(ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Начисление КАК ПланВидовРасчета.Начисления).КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя)
		|				ТОГДА 0
		|			ИНАЧЕ ТаблицаНачислений.ОплаченоДней
		|		КОНЕЦ) КАК ОплаченныеДни,
		|	СУММА(ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Начисление КАК ПланВидовРасчета.Начисления).КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя)
		|				ТОГДА 0
		|			ИНАЧЕ ТаблицаНачислений.Результат
		|		КОНЕЦ) КАК СуммаВсего,
		|	СУММА(ТаблицаНачислений.РезультатВТомЧислеЗаСчетФБ) КАК СуммаСверхНорм,
		|	СУММА(ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Начисление КАК ПланВидовРасчета.Начисления).КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя)
		|				ТОГДА ТаблицаНачислений.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаФинансируемаяРаботодателем,
		|	ТаблицаНачислений.ДатаНачалаСобытия КАК ДатаНачалаСобытия
		|ПОМЕСТИТЬ ВТСвернутыеНачисленияДляУчетаПособий
		|ИЗ
		|	ВТНачисленияДляУчетаПособий КАК ТаблицаНачислений
		|ГДЕ
		|	НЕ ВЫРАЗИТЬ(ТаблицаНачислений.Начисление КАК ПланВидовРасчета.Начисления).КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.БеременностьРоды)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.Профзаболевание)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание)
		|		КОГДА ВЫРАЗИТЬ(ТаблицаНачислений.Ссылка КАК Документ.БольничныйЛист).ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаНачислений.Ссылка,
		|	ТаблицаНачислений.Сотрудник,
		|	ТаблицаНачислений.ДатаНачалаСобытия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНачислений.ВидПособияСоциальногоСтрахования КАК ВидПособияСоциальногоСтрахования,
		|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
		|	БольничныйЛист.ФинансированиеФедеральнымБюджетом КАК ФинансированиеФедеральнымБюджетом,
		|	БольничныйЛист.ДатаНачалаСобытия КАК ДатаСтраховогоСлучая,
		|	СУММА(ВЫБОР
		|			КОГДА ТаблицаНачислений.ОплаченныеДни = 0
		|				ТОГДА 0
		|			КОГДА БольничныйЛист.ПричинаНетрудоспособности = ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
		|					И ДОБАВИТЬКДАТЕ(БольничныйЛист.ДатаНачалаСобытия, ДЕНЬ, 3) >= БольничныйЛист.ДатаНачалаОплаты
		|					И ДОБАВИТЬКДАТЕ(БольничныйЛист.ДатаНачалаСобытия, ДЕНЬ, 3) <= БольничныйЛист.ДатаОкончанияОплаты
		|				ТОГДА 1
		|			КОГДА БольничныйЛист.ПричинаНетрудоспособности <> ЗНАЧЕНИЕ(Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание)
		|					И БольничныйЛист.ДатаНачалаСобытия = БольничныйЛист.ДатаНачалаОплаты
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтраховыеСлучаи,
		|	СУММА(ТаблицаНачислений.ОплаченныеДни) КАК ОплаченныеДни,
		|	СУММА(ТаблицаНачислений.СуммаВсего) КАК СуммаВсего,
		|	СУММА(ТаблицаНачислений.СуммаСверхНорм) КАК СуммаСверхНорм,
		|	СУММА(ТаблицаНачислений.СуммаФинансируемаяРаботодателем) КАК СуммаФинансируемаяРаботодателем
		|ИЗ
		|	ВТСвернутыеНачисленияДляУчетаПособий КАК ТаблицаНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.БольничныйЛист КАК БольничныйЛист
		|		ПО ТаблицаНачислений.Ссылка = БольничныйЛист.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудниковПособий КАК КадровыеДанныеСотрудников
		|		ПО ТаблицаНачислений.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
		|			И ТаблицаНачислений.ДатаНачалаСобытия = КадровыеДанныеСотрудников.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаНачислений.ВидПособияСоциальногоСтрахования,
		|	БольничныйЛист.ФинансированиеФедеральнымБюджетом,
		|	ТаблицаНачислений.Сотрудник,
		|	БольничныйЛист.ДатаНачалаСобытия,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо,
		|	КадровыеДанныеСотрудников.ВидЗанятости";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	
	ДанныеДляПроведения.Вставить("Пособия", Запрос.Выполнить().Выгрузить());

КонецПроцедуры

Процедура ЗаполнитьПоПервичномуБольничномуЛисту(ПервичныйБольничный) Экспорт
	
	Если ПервичныйБольничный = Неопределено Тогда
		Если Не ЗначениеЗаполнено(НомерПервичногоЛисткаНетрудоспособности) Или Не ЯвляетсяПродолжениемБолезни Тогда
			Возврат;
		КонецЕсли;
		Отбор = Новый Структура;
		Отбор.Вставить("ФизическоеЛицо", ФизическоеЛицо);
		Отбор.Вставить("Проведен",       Истина);
		ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
		ЗначенияРеквизитов = Документы.БольничныйЛист.ПоследнийДокументВЦепочкеИсправлений(
			НомерПервичногоЛисткаНетрудоспособности,
			ГоловнаяОрганизация,
			Отбор);
		Если ЗначенияРеквизитов = Неопределено Тогда
			Отбор.Удалить("Проведен");
			ЗначенияРеквизитов = Документы.БольничныйЛист.ПоследнийДокументВЦепочкеИсправлений(
				НомерПервичногоЛисткаНетрудоспособности,
				ГоловнаяОрганизация,
				Отбор);
			Если ЗначенияРеквизитов = Неопределено Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ПервичныйБольничный = ЗначенияРеквизитов.Ссылка;
	КонецЕсли;
	
	Если ТипЗнч(ПервичныйБольничный) = Тип("ДокументСсылка.БольничныйЛист")
		Или ТипЗнч(ПервичныйБольничный) = Тип("Строка") Тогда
		ДанныеПервичногоБольничногоЛиста = УчетПособийСоциальногоСтрахования.ДанныеБольничногоЛиста(ПервичныйБольничный);
	Иначе
		ДанныеПервичногоБольничногоЛиста = ПервичныйБольничный;
	КонецЕсли;
	
	СреднийЗаработокФСС.Очистить();
	ОтработанноеВремяДляСреднегоФСС.Очистить();
	
	Если ДанныеПервичногоБольничногоЛиста = Неопределено Тогда
		
		ПервичныйБольничныйЛист = Неопределено;
		
	Иначе
		
		ПервичныйБольничныйЛист = ДанныеПервичногоБольничногоЛиста.Ссылка;
		НомерПервичногоЛисткаНетрудоспособности = ДанныеПервичногоБольничногоЛиста.НомерЛисткаНетрудоспособности;
		
		ИменаЗаполняемыхПолей = 
		"Организация, Сотрудник, ПричинаНетрудоспособности, ДатаНачалаОплаты,
		|ДатаНарушенияРежима, ПроцентОплаты, НазначитьПособие, ПрименятьЛьготыПриНачисленииПособия,
		|ФинансированиеФедеральнымБюджетом, ПроцентОплатыБезЛьгот, ОграничениеПособия, ОграничениеПособияБезЛьгот,
		|ДатаНачалаСобытия, СлучайУходаЗаБольнымРебенком,
		|ДатаНачалаПоловиннойОплаты, РайонныйКоэффициентРФНаНачалоСобытия, ПериодРасчетаСреднегоЗаработкаПервыйГод,
		|ПериодРасчетаСреднегоЗаработкаВторойГод, СреднийДневнойЗаработок";
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеПервичногоБольничногоЛиста, ИменаЗаполняемыхПолей);
		
		ИсключаемыеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
		ДатаНачала = УчетПособийСоциальногоСтрахования.ПоследнийДеньБолезни(ПервичныйБольничныйЛист, ИсключаемыеДокументы) + 86400;
		
		Если НазначитьПособие Тогда
			ДатаНачалаОплаты = ДатаНачала;
			Если ДатаОкончанияОплаты < ДатаНачалаОплаты Тогда
				ДатаОкончанияОплаты = Макс(ДатаОкончания, ДатаНачалаОплаты);
			КонецЕсли;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ПервичныйБольничныйЛист);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	БольничныйЛистСреднийЗаработокФСС.ФизическоеЛицо,
			|	БольничныйЛистСреднийЗаработокФСС.Период,
			|	БольничныйЛистСреднийЗаработокФСС.Сумма
			|ИЗ
			|	Документ.БольничныйЛист.СреднийЗаработокФСС КАК БольничныйЛистСреднийЗаработокФСС
			|ГДЕ
			|	БольничныйЛистСреднийЗаработокФСС.Ссылка = &Ссылка";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СреднийЗаработокФСС.Добавить(), Выборка);
		КонецЦикла;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	БольничныйЛистОтработанноеВремяДляСреднегоФСС.ФизическоеЛицо,
			|	БольничныйЛистОтработанноеВремяДляСреднегоФСС.Период,
			|	БольничныйЛистОтработанноеВремяДляСреднегоФСС.ДнейБолезниУходаЗаДетьми
			|ИЗ
			|	Документ.БольничныйЛист.ОтработанноеВремяДляСреднегоФСС КАК БольничныйЛистОтработанноеВремяДляСреднегоФСС
			|ГДЕ
			|	БольничныйЛистОтработанноеВремяДляСреднегоФСС.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ОтработанноеВремяДляСреднегоФСС.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Кэш = Новый Соответствие;
	АвтозаполнениеПериодовОплаты = Истина;
	Документы.БольничныйЛист.ЗаполнитьПараметрыОплаты(ЭтотОбъект, Кэш);
	
КонецПроцедуры

Функция ДанныеДляРегистрацииВУчетаСтажаПФР()
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ДанныеДляРегистрацииВУчете = Документы.БольничныйЛист.ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок);
		
	Возврат ДанныеДляРегистрацииВУчете[Ссылка];
														
КонецФункции	

Процедура ПроверитьВыплатуПособийУчастникомПроектаПоПрямымВыплатамФСС(Отказ)
	
	ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект = ПрямыеВыплатыПособийСоциальногоСтрахования.ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект(Организация, ПериодРегистрации);
	Если ЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект Тогда
		ТекстСообщения = ПрямыеВыплатыПособийСоциальногоСтрахования.ТекстСообщенияЭтоУчастникПилотногоПроектаБезДатыВступленияВПроект(Организация);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.Организация" , , Отказ);
	Иначе
		
		ВыплачиваемыеПособия = Начисления.Выгрузить(, "Начисление, Результат");
		ВыплачиваемыеПособия.Свернуть("Начисление", "Результат");
		
		ВыплачиваемыеПособияДляПроверки = ПрямыеВыплатыПособийСоциальногоСтрахования.ПустаяТаблицаДляПроверкиОплатыПособийУчастникомПилотногоПроектаФСС();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВыплачиваемыеПособия, ВыплачиваемыеПособияДляПроверки);
		
		Ошибка = ПрямыеВыплатыПособийСоциальногоСтрахования.ПроверитьОплатуПособийУчастникомПилотногоПроектаФСС(
			Организация,
			ПериодРегистрации,
			ВыплачиваемыеПособияДляПроверки,
			ЭЛНКарантинПоКоронавирусу);
		Если ЗначениеЗаполнено(Ошибка) Тогда
			ОбщегоНазначения.СообщитьПользователю(Ошибка, , "МесяцНачисленияСтрокой", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДоходыНДФЛ(Отказ = Ложь, ДанныеДляПроведения = Неопределено, Записывать = Ложь) Экспорт
	
	Если ДанныеДляПроведения = Неопределено Тогда
		ДанныеДляПроведения = ДанныеДляПроведения();
	КонецЕсли; 
	
	ДатаОперации = ДатаОперацииПоНалогомИВзносам();
	УчетНДФЛ.СформироватьДоходыНДФЛПоНачислениям(
		Движения, Отказ, Организация, ДатаОперации, ПланируемаяДатаВыплаты, ДанныеДляПроведения.МенеджерВременныхТаблиц, ПериодРегистрации, Записывать, Ложь, , Ссылка);
	
КонецПроцедуры

Функция ДатаОперацииПоНалогомИВзносам()
	Возврат УчетНДФЛ.ДатаОперацииПоДокументу(Дата, ПериодРегистрации);
КонецФункции

Процедура ВключитьАвтозаполнениеПериодовОплаты() Экспорт
	Если Не АвтозаполнениеПериодовОплаты
		И ИсключаемыеПериоды.Количество() = 0
		И НазначитьПособие
		И ДатаНачала = ДатаНачалаОплаты
		И ДатаОкончания = ДатаОкончанияОплаты Тогда
		АвтозаполнениеПериодовОплаты = Истина;
	КонецЕсли;
КонецПроцедуры

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "Организация, ФизическоеЛицо, НомерЛисткаНетрудоспособности, ПометкаУдаления";
	Если ЭтоНовый() Тогда
		Возврат ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
КонецФункции

Процедура ОбновитьГоловнуюОрганизацию() Экспорт
	Если ЗначениеЗаполнено(Организация) Тогда
		ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	Иначе
		ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли