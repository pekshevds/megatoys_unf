
#Область ОписаниеПеременных

#Область ПеременныеФормы

&НаКлиенте
Перем ДанныеВыбораСостояния;

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеОтмененногоЗаказа(
		Список.КомпоновщикНастроек.Настройки.УсловноеОформление);
	
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	
	// СборкаЗаказов
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСборкуЗаказов") Тогда
		Элементы.ОтборСборка.СписокВыбора.Добавить("Без сборки", НСтр("ru = 'Без сборки'"));
		Элементы.ОтборСборка.СписокВыбора.Добавить("Собран частично", НСтр("ru = 'Собран частично'"));
		Элементы.ОтборСборка.СписокВыбора.Добавить("Собран полностью", НСтр("ru = 'Собран полностью'"));
		Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ИспользоватьСборкуЗаказов", Истина);
	Иначе
		Элементы.НомерКартинкиСборки.Видимость = Ложь;
		Элементы.СтатусСборки.Видимость = Ложь;
		Элементы.ГруппаОтборСборка.Видимость = Ложь;
		Элементы.ОтборСборка.Видимость = Ложь;
		Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ИспользоватьСборкуЗаказов", Ложь);
	КонецЕсли;
	// Конец СборкаЗаказов      
	
	//Переработка давальческого сырья
		Элементы.ВидОперации.Видимость = ПолучитьФункциональнуюОпцию("ПереработкаДавальческогоСырья");
	// Конец Переработка давальческого сырья 
	
	СостоянияЗаказов.ЗаполнитьСписокВыбораЗавершенияЗаказа(Элементы.ОтборЗавершениеЗаказа.СписокВыбора);
	ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	
	ВидыОперацийЗаказПокупателя = Новый СписокЗначений;
	ВидыОперацийЗаказПокупателя.Добавить(Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку);
	ВидыОперацийЗаказПокупателя.Добавить(Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВидОперации",
		ВидыОперацийЗаказПокупателя, ВидСравненияКомпоновкиДанных.ВСписке);
	
	Список.Параметры.УстановитьЗначениеПараметра("АктуальнаяДатаСеанса", НачалоДня(ТекущаяДатаСеанса()));
	
	УстановитьОтборТекущиеДела();
	КонтекстноеОткрытие = Параметры.Свойство("ТекущиеДела");
	
	ИспользоватьВидыЗаказовПокупателей = ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей");
	// УНФ.ОтборыСписка
	РаботаСОтборами.ОпределитьПорядокПредопределенныхОтборов(ЭтотОбъект);
	
	Если Не КонтекстноеОткрытие Тогда
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, Список);
	Иначе
		РаботаСОтборамиКлиентСервер.УстановитьОтображениеКомандыСброситьВсеОтборы(ЭтотОбъект);
	КонецЕсли;
	
	Если Элементы.ФильтрыНастройкиИДопИнфо.Видимость Тогда
		Элементы.ПраваяПанель.Ширина = 28;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораОтборОплата();
	ЗаполнитьСписокВыбораОтборОтгрузка();
	ЗаполнитьСписокВыбораОтборСостояниеОригинала();
	// Конец УНФ.ОтборыСписка
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Элементы.ГруппаГлобальныеКомандыЗаказПокупателя.Видимость = Ложь;
		Элементы.ФормаОбщаяКомандаНапомнить.Видимость = Ложь;
		Элементы.ГруппаКомандыЭДО.Видимость = Ложь;
		Элементы.ПраваяПанель.Видимость = Ложь;
		Элементы.ФормаСоздатьПоШаблону.Видимость = Ложь;
		Элементы.ДатаОтгрузки.Видимость = Ложь;
		Элементы.ВидОперации.Видимость = Ложь;
		Элементы.ФормаОбработкаНастройкиПриложенияБольшеВозможностей.Видимость = Ложь;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	ОбновитьКомандыИзмененияСостояний();
	
	ДинамическиеСпискиУНФ.ОтображатьТолькоВремяДляТекущейДаты(Список);
	
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостей.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"УНФ.Документ.ЗаказПокупателя",
		"ФормаСписка",
		Неопределено,
		НСтр("ru='Новости: Заказы покупателей'"),
		Ложь,
		Новый Структура("ПолучатьНовостиНаСервере, ХранитьМассивНовостейТолькоНаСервере", Истина, Истина),
		"ПриОткрытии");
	// Конец ИнтернетПоддержкаПользователей.Новости
	
	// ЭДО
	УправлениеНебольшойФирмойЭлектронныеДокументыСервер.КомандыЭДО_ФормаСписка(ЭтотОбъект, Неопределено);
	// Конец ЭДО
	
	// КомандыПечати
	ПечатьДокументовУНФ.КорректировкаРазмещениеПодчиненнойГруппыКомандПечати(ЭтаФорма, Элементы.ПодменюПечать, Элементы.ПодменюДоговорКонтрагента);
	Элементы.КомандыПечатиДоговорКонтрагента.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	ПечатьДокументовУНФ.КорректировкаРазмещениеПодчиненнойГруппыКомандПечати(ЭтаФорма, Элементы.ПодменюПечать, Элементы.ПодменюКоммерческоеПредложение);
	Элементы.КомандыПечатиКоммерческоеПредложение.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	ПечатьДокументовУНФ.КорректировкаРазмещениеПодчиненнойГруппыКомандПечати(ЭтаФорма, Элементы.ПодменюПечать, Элементы.ПодменюПечатьФаксимиле);
	Элементы.ПодменюПечатьФаксимиле.Вид = ВидГруппыФормы.Подменю;
	
	ШаблоныПечатиОфисныхДокументов.ОпределитьВидимостьКомандШаблоновПечати(Элементы.ФормаОткрытьШаблоныПечатиДоговоровКонтрагентов);
	ШаблоныПечатиОфисныхДокументов.ОпределитьВидимостьКомандШаблоновПечати(Элементы.ФормаОткрытьШаблоныПечатиКоммерческихПредложений);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюКоммерческоеПредложение",
		"Видимость", ОбщегоНазначенияУНФ.ЭтоУНФ());
	// Конец КомандыПечати
	
	// КомандыОрганайзер
	НапоминанияПользователяУНФ.УстановитьОтображениеКомандОрганайзера(Элементы);
	// Конец КомандыОрганайзер
	
	// КомандыОтправкиСообщений
	ЭлектроннаяПочтаУНФ.УстановитьОтображениеКомандОтправкиСообщений(Элементы);
	// Конец КомандыОтправкиСообщений
	
	// УНФ.ПанельКонтактнойИнформации
	КонтактнаяИнформацияПанельУНФ.ПриСозданииНаСервере(ЭтотОбъект, "КонтактнаяИнформация", "СписокКонтекстноеМеню");
	// Конец УНФ.ПанельКонтактнойИнформации
	
	// Яндекс.Доставка
	Элементы.ФормаПередатьВЯндексДоставку.Видимость = ЯндексДоставка.Подключена();
	// Конец Яндекс.Доставка
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументов.ПриСозданииНаСервере_ФормаСписка(ЭтотОбъект, Элементы.Список);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	// Конец ПодключаемоеОборудование
	
	// ЭлектронноеВзаимодействие.ТорговыеПредложения
	ТорговыеПредложения.ПриСозданииПодсказокФормы(ЭтотОбъект, Элементы.ПодсказкиБизнесСеть);
	// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ФормаДокументПриходнаяНакладнаяСоздатьНаОсновании", 
		"Заголовок",
		НСтр("ru = 'Возврат от покупателя'"));
		
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект,
		"СканерШтрихкода");
	// Конец ПодключаемоеОборудование 
	
	// ЭДО
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭДО
	
	// ЭлектронноеВзаимодействие.ТорговыеПредложения
	ТорговыеПредложенияКлиент.ОбновитьПодсказкуФормы(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не КонтекстноеОткрытие И Не ЗавершениеРаботы Тогда
		// УНФ.ОтборыСписка
		СохранитьНастройкиОтборов();
		// Конец УНФ.ОтборыСписка
	КонецЕсли;
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РаботаСФормойКлиент.СписокЗаказовОбработкаОповещенияФрагмент(ЭтотОбъект, ИмяСобытия);
	
	Если ИмяСобытия = "Запись_СостоянияЗаказовПокупателей" Тогда
		УстановитьУсловноеОформлениеИОбновитьКомандыИзмененияСостояний();
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ВидыЗаказовПокупателей" Тогда
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
		ОбновитьКомандыИзмененияСостояний();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ШаблоныПечатиОфисныхДокументов" И Параметр.Свойство("Назначение") Тогда
		ГруппаКомандПечати = Неопределено;
		Если Параметр.Назначение = ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагента")
			ИЛИ Параметр.Назначение = ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагентаЗаказ") Тогда
			ГруппаКомандПечати = Элементы.ПодменюДоговорКонтрагента;
		ИначеЕсли Параметр.Назначение = ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.КоммерческоеПредложение") Тогда
			ГруппаКомандПечати = Элементы.ПодменюКоммерческоеПредложение;
		КонецЕсли;
		Если ГруппаКомандПечати <> Неопределено Тогда
			ШаблоныПечатиОфисныхДокументовКлиент.УстановитьПризнакПоявленияНовойКомандыПечати(ГруппаКомандПечати);
		КонецЕсли;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУНФКлиент.ЕстьНеобработанноеСобытие() Тогда
			ОбработатьШтрихкоды(МенеджерОборудованияУНФКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// ЭДО
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = ЭтотОбъект;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "Список";
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭДО
	
	// УНФ.ПанельКонтактнойИнформации
	Если КонтактнаяИнформацияПанельУНФКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьПанельКонтактнойИнформации();
	КонецЕсли;
	// Конец УНФ.ПанельКонтактнойИнформации
	
	Если ИмяСобытия = "Запись_НастройкиЯндексДоставки" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаПередатьВЯндексДоставку", "Видимость", Истина);
	КонецЕсли;
	
	// УНФ.Интеграция с Яндекс.Кассой
	ОнлайнОплатыУНФКлиент.ОбработкаОповещения_ФормаСписка(Элементы.Список, ИмяСобытия, Параметр, Источник);
	// Конец УНФ.Интеграция с Яндекс.Кассой
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовКлиент.ОбработчикОповещенияФормаСписка(ИмяСобытия, ЭтотОбъект, Элементы.Список);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументовКлиент.СписокВыбор(Поле.Имя, ЭтотОбъект, Элементы.Список, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	КонтактнаяИнформацияПанельУНФКлиент.ПриАктивизацииДинамическогоСписка(ЭтотОбъект, Элемент, ТекущийКонтрагент,
		"Контрагент");
	
	Если ТипЗнч(Элемент.ТекущаяСтрока) <> Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		ВидимостьКомандИзмененияСостояний(Элемент);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	ОценкаПроизводительностиКлиент.ЗамерВремени("СозданиеФормы"
		+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормы"
		+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	УчетОригиналовПервичныхДокументов.ПриПолученииДанныхНаСервере(Строки);
	// Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ОтборВидЗаказаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	
	УстановитьМеткуИОтборСписка("ВидЗаказа", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НайденныеСтроки = ДанныеМеток.НайтиСтроки(Новый Структура("ИмяПоляОтбора", "ВидЗаказа"));
	Если (НайденныеСтроки.Количество() = 1 И ТипЗнч(НайденныеСтроки[0].Метка) = Тип("СправочникСсылка.ВидыЗаказовПокупателей"))
		ИЛИ НЕ ИспользоватьВидыЗаказовПокупателей Тогда
		СтандартнаяОбработка = Ложь;
		Если ИспользоватьВидыЗаказовПокупателей Тогда
			ВыбранныйВидЗаказа = НайденныеСтроки[0].Метка;
		Иначе
			ВыбранныйВидЗаказа = ПредопределенноеЗначение("Справочник.ВидыЗаказовПокупателей.Основной");
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("ВидЗаказа", ВыбранныйВидЗаказа);
		ОткрытьФорму("Справочник.СостоянияЗаказовПокупателей.ФормаВыбора", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СостояниеЗаказа", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния Тогда
		НайденныеСтроки = ДанныеМеток.НайтиСтроки(Новый Структура("ИмяПоляОтбора", "ВидЗаказа"));
		Если НайденныеСтроки.Количество() = 1 И ТипЗнч(НайденныеСтроки[0].Метка) = Тип("СправочникСсылка.ВидыЗаказовПокупателей") Тогда
			ПараметрыПолученияДанных.Вставить("ВидЗаказа", НайденныеСтроки[0].Метка);
		КонецЕсли;
		ДанныеВыбораСостояния = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказовПокупателей"), ПараметрыПолученияДанных);
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Ложь;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеВыбораСостояния;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЗавершениеЗаказаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка(
		"ВариантЗавершения",
		Элемент.Родитель.Имя,
		ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа." + ВыбранноеЗначение),
		Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение).Представление);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОплатаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	Если ВыбранноеЗначение = "Без оплаты"
		Или ВыбранноеЗначение = "Оплачен частично"
		Или ВыбранноеЗначение = "Оплачен полностью" Тогда
		УстановитьМеткуИОтборСписка("СтатусОплаты", Элемент.Родитель.Имя, ВыбранноеЗначение);
	Иначе
		УстановитьМеткуИОтборСписка("НомерКартинкиОплаты", Элемент.Родитель.Имя, ВыбранноеЗначение);
	КонецЕсли;
	ВыбранноеЗначение = Неопределено;

КонецПроцедуры

&НаКлиенте
Процедура ОтборОтгрузкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	Если ВыбранноеЗначение = "Без отгрузки"
		Или ВыбранноеЗначение = "Отгружен частично"
		Или ВыбранноеЗначение = "Отгружен полностью" Тогда
		
		Если ВыбранноеЗначение = "Отгружен полностью" Тогда
			СтатусОтгрузки = 0;
		ИначеЕсли ВыбранноеЗначение = "Отгружен частично" Тогда
			СтатусОтгрузки = 1;
		ИначеЕсли ВыбранноеЗначение = "Без отгрузки" Тогда
			СтатусОтгрузки = 2;
		КонецЕсли;
		УстановитьМеткуИОтборСписка("СтатусОтгрузки", Элемент.Родитель.Имя, СтатусОтгрузки, ВыбранноеЗначение);
	Иначе
		УстановитьМеткуИОтборСписка("НомерКартинкиОтгрузки", Элемент.Родитель.Имя, ВыбранноеЗначение);
	КонецЕсли;
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСборкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = "Собран полностью" Тогда
		СтатусСборки = 2;
	ИначеЕсли ВыбранноеЗначение = "Собран частично" Тогда
		СтатусСборки = 1;
	ИначеЕсли ВыбранноеЗначение = "Без сборки" Тогда
		СтатусСборки = 0;
	Иначе
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СтатусСборки", Элемент.Родитель.Имя, СтатусСборки, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Ответственный", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОригиналаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.СостоянияОригиналовПервичныхДокументов.ПустаяСсылка") Тогда
		УстановитьМеткуИОтборСписка("СостояниеОригинала", Элемент.Родитель.Имя, ВыбранноеЗначение,
			УчетОригиналовПервичныхДокументовУНФКлиентСервер.СостояниеОригиналаНеизвестно());
	Иначе
		УстановитьМеткуИОтборСписка("СостояниеОригинала", Элемент.Родитель.Имя, ВыбранноеЗначение);
	КонецЕсли;

	ВыбранноеЗначение = Неопределено;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(
	"Документ.ЗаказПокупателя",
	Список.КомпоновщикНастроек.Настройки.Отбор.Элементы,
	Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборТекущиеДела()
	
	Если НЕ Параметры.Свойство("ТекущиеДела") Тогда
		Возврат;
	КонецЕсли;
	
	АвтоЗаголовок = Ложь;
	Заголовок = НСтр("ru='Заказы покупателей'");
	
	СотрудникиПользователя = РегистрыСведений.СотрудникиПользователя.ПолучитьСотрудниковПользователя();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Проведен", Истина);
	
	УстановитьМеткуИОтборСписка(
		"ВариантЗавершения",
		Элементы.ОтборЗавершениеЗаказа.Родитель.Имя,
		ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа.ПустаяСсылка"), // В текущих делах все заказы Не завершенные
		Элементы.ОтборЗавершениеЗаказа.СписокВыбора.НайтиПоЗначению("ПустаяСсылка").Представление);
	
	РаботаСОтборами.ПрикрепитьМеткиОтбораИзМассива(ЭтотОбъект, "Ответственный", "ГруппаОтборОтветственный", СотрудникиПользователя);
	РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, Список, "Ответственный");
	
	Если Параметры.Свойство("ПросроченоВыполнение") Тогда
		
		Заголовок = Заголовок + ": " + НСтр("ru='просрочено выполнение'");
		Список.УстановитьОбязательноеИспользование("ПросроченоВыполнение", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПросроченоВыполнение", Истина);
		
	ИначеЕсли Параметры.Свойство("ПросроченаОплата") Тогда
		
		Заголовок = Заголовок + ": " + НСтр("ru='просрочена оплата'");
		Список.УстановитьОбязательноеИспользование("ПросроченаОплата", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПросроченаОплата", Истина);
		
	ИначеЕсли Параметры.Свойство("НаСегодня") Тогда
		
		Заголовок = Заголовок + ": " + НСтр("ru='на сегодня'");
		Список.УстановитьОбязательноеИспользование("НаСегодня", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "НаСегодня", Истина);
		
	ИначеЕсли Параметры.Свойство("НеЗавершенные") Тогда
		
		Заголовок = Заголовок + ": " + НСтр("ru='не завершенные'");
		
	КонецЕсли;
	
	ПредставлениеПериода = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(ОтборПериод);
	РаботаСОтборами.ОбновитьЭлементыМеток(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьШаблоныПечатиДоговоровКонтрагентов(Команда)
	
	ШаблоныПечатиОфисныхДокументовКлиент.ОткрытьШаблоныПечатиОфисныхДокументов(
	ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагентаЗаказ"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьШаблоныПечатиКоммерческихПредложений(Команда)
	
	ШаблоныПечатиОфисныхДокументовКлиент.ОткрытьШаблоныПечатиОфисныхДокументов(
	ПредопределенноеЗначение("Перечисление.НазначенияШаблоновПечатиОфисныхДокументов.КоммерческоеПредложение"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект,
		Новый Структура("ТекШтрихкод", ТекШтрихкод));
	
	#Если МобильныйКлиент Тогда
		ОткрытьФорму("ОбщаяФорма.ФормаПоискаПоШтрихкоду", , , , , , ОбработкаЗавершения);
	#Иначе
		ПоказатьВводЗначения(ОбработкаЗавершения, ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));
	#КонецЕсли
	
КонецПроцедуры

//+mega
&НаКлиенте
Процедура mega_ОбъединитьЗаказы(Команда)
	
	ОбъединитьЗаказыНаКлиенте();	
КонецПроцедуры

&НаКлиенте
Процедура mega_ОтгрузитьЗаказы(Команда)
	
	ОтгрузитьЗаказыНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура mega_РазделитьПо60(Команда)
	
	РазделитьПо60НаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура mega_ЗагрузитьИзФайлаВайлдберриз(Команда)
	
	ЗагрузитьИзФайлаВайлдберризНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура mega_ЗагрузитьИзФайлаОзон(Команда)
	
	ЗагрузитьИзФайлаОзонНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура mega_ЗагрузитьИзФайлаЯндекс(Команда)
	
	ЗагрузитьИзФайлаЯндексНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура mega_ПроверитьВВайлдберриз(Команда)
	
	ПроверитьВВайлдберризНаКлиенте();
КонецПроцедуры
//-mega
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+mega
&НаКлиенте
Процедура ПроверитьВВайлдберризНаКлиенте()
	
	ЗаказыПокупателей = Новый Массив;
	Для Каждого ЗаказПокупателя Из ЭтаФорма.Элементы.Список.ВыделенныеСтроки Цикл 
		
		ЗаказыПокупателей.Добавить(ЗаказПокупателя);
	КонецЦикла;
	
	ПроверитьВВайлдберризНаСервере(ЗаказыПокупателей);
КонецПроцедуры

&НаКлиенте
Процедура ОбъединитьЗаказыНаКлиенте()
	
	ЗаказыПокупателей = Новый Массив;
	Для Каждого ЗаказПокупателя Из ЭтаФорма.Элементы.Список.ВыделенныеСтроки Цикл 
		
		ЗаказыПокупателей.Добавить(ЗаказПокупателя);
	КонецЦикла;
	
	Если ЗаказыПокупателей.Количество() > 1 Тогда 
		
		Если ПроверитьВозможностьОбъединенияЗаказовНаСервере(ЗаказыПокупателей) Тогда 
			
			Если ОбъединенитьЗаказыПокупателейНаСервере(ЗаказыПокупателей) Тогда 
				
				ОбщегоНазначенияКлиент.СообщитьПользователю("Объединение заказов покупателей произведено успешно");
				ЭтаФорма.Элементы.Список.Обновить();
			Иначе				
				
				ОбщегоНазначенияКлиент.СообщитьПользователю("Объединение заказов покупателей не произведено");
			КонецЕсли;
		КонецЕсли;
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Объединить можно 2 и более заказа покупателя");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузитьЗаказыНаКлиенте()
	
	ЗаказыПокупателей = Новый Массив;
	Для Каждого ЗаказПокупателя Из ЭтаФорма.Элементы.Список.ВыделенныеСтроки Цикл 
		
		ЗаказыПокупателей.Добавить(ЗаказПокупателя);
	КонецЦикла;
	
	Если ОтгрузитьЗаказыПокупателейНаСервере(ЗаказыПокупателей) Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Отгрузка заказов покупателей произведена успешно");
		ЭтаФорма.Элементы.Список.Обновить();
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Отгрузка заказов покупателей не произведена");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазделитьПо60НаКлиенте()
	
	ЗаказыПокупателей = Новый Массив;
	Для Каждого ЗаказПокупателя Из ЭтаФорма.Элементы.Список.ВыделенныеСтроки Цикл 
		
		ЗаказыПокупателей.Добавить(ЗаказПокупателя);
	КонецЦикла;
	
	Если РазделитьЗаказыПокупателейПо60НаСервере(ЗаказыПокупателей) Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Разделение заказов покупателей произведено успешно");
		ЭтаФорма.Элементы.Список.Обновить();
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Разделение заказов покупателей не произведено");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаВайлдберризНаКлиенте()
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр                  = НСтр("ru = 'Данные из Вайлдберриз'") + " (*.xlsx)|*.xlsx";
	ДиалогВыбораФайла.Заголовок               = НСтр("ru = 'Выберите файл для загрузки'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "xlsx";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор      = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ДиалогОткрытияФайлаОтчетаВайлдберризЗавершение", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОзонНаКлиенте()
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр                  = НСтр("ru = 'Данные из Озон'") + " (*.csv)|*.csv";
	ДиалогВыбораФайла.Заголовок               = НСтр("ru = 'Выберите файл для загрузки'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "csv";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор      = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ДиалогОткрытияФайлаОтчетаОзонЗавершение", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЯндексНаКлиенте()
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр                  = НСтр("ru = 'Данные из Яндекс'") + " (*.xlsx)|*.xlsx";
	ДиалогВыбораФайла.Заголовок               = НСтр("ru = 'Выберите файл для загрузки'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "xlsx";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор      = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ДиалогОткрытияФайлаОтчетаЯндексЗавершение", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ДиалогОткрытияФайлаОтчетаВайлдберризЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКфайлу = ВыбранныеФайлы[0];
		ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКфайлу);
		
		ЗагрузитьДанныеИзфайлаВайлдберризЗавершение(ПоместитьВоВременноеХранилище(ДвоичныеДанные));
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ДиалогОткрытияФайлаОтчетаОзонЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКфайлу = ВыбранныеФайлы[0];
		ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКфайлу);
		
		ЗагрузитьДанныеИзфайлаОзонЗавершение(ПоместитьВоВременноеХранилище(ДвоичныеДанные));
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ДиалогОткрытияФайлаОтчетаЯндексЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКфайлу = ВыбранныеФайлы[0];
		ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКфайлу);
		
		ЗагрузитьДанныеИзфайлаЯндексЗавершение(ПоместитьВоВременноеХранилище(ДвоичныеДанные));
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Функция ДатаИзСтрокиВайлдберриз(Строка)
	//16:08:15 12.05.2021
	Год = Прав(Строка, 4);
	Месяц = Сред(Строка, 13, 2);
	День = Сред(Строка, 10, 2);
	Секунда = Сред(Строка, 7, 2);
	Минута = Сред(Строка, 4, 2);
	Час = Лев(Строка, 2);
	Возврат Дата(Год+Месяц+День+Час+Минута+Секунда);
КонецФункции

&НаСервере
Функция ДатаИзСтрокиЯндекс(Строка)
	//12.05.2021
	Год = Прав(Строка, 4);
	Месяц = Сред(Строка, 4, 2);
	День = Лев(Строка, 2);
	Секунда = "00";
	Минута = "00";
	Час = "00";
	Возврат Дата(Год+Месяц+День+Час+Минута+Секунда);
КонецФункции

&НаСервере
Функция УбратьКавычки(Значение)
	
	Возврат Сред(Значение, 2, СтрДлина(Значение) - 2);
КонецФункции

&НаСервере
Функция ПреобразоватьДату(Значение)
	//2020-12-16 09:17:02
	
	Возврат Дата(СтрЗаменить(
	СтрЗаменить(
	СтрЗаменить(Значение, "-", ""),
	":", ""), 
	" ", ""));
КонецФункции

&НаСервере
Процедура ЗагрузитьДанныеИзфайлаОзонЗавершение(АдресВоВременномХранилище)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла, КодировкаТекста.UTF8);
	
	ЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
	ЗаказПокупателя.Дата = ТекущаяДата();
	ЗаказПокупателя.Заполнить(Неопределено);
	ЗаказПокупателя.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("УТ0008960");
	ЗаказПокупателя.Контрагент = ЗаказПокупателя.Договор.Владелец;
	ЗаказПокупателя.Организация = ЗаказПокупателя.Договор.Организация;
	ЗаказПокупателя.ДатаОтгрузки = ЗаказПокупателя.Дата;
	
	НомерСтроки = 2;
	Пока НомерСтроки <= ТекстовыйДокумент.КоличествоСтрок() Цикл 
		
		Стр = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки);
		Слова = СтрРазделить(Стр, ";", Истина);
		
		НомерЗадания = УбратьКавычки(Слова[1]);		
		Артикул = УбратьКавычки(Слова[10]);
		АртикулПоставщика = УбратьКавычки(Слова[9]);
			
		Номенклатура = Неопределено;		
		Выборка = Справочники.НоменклатураПоставщиков.НоменклатураПоДаннымНоменклатурыПоставщика(ЗаказПокупателя.Контрагент, , АртикулПоставщика);
		Если Выборка.Следующий() Тогда 
			
			Номенклатура = Выборка.Номенклатура;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Номенклатура)Тогда
			Выборка = Справочники.НоменклатураПоставщиков.НоменклатураПоДаннымНоменклатурыПоставщика(ЗаказПокупателя.Контрагент, , Артикул);
			Если Выборка.Следующий() Тогда 
				
				Номенклатура = Выборка.Номенклатура;
			КонецЕсли;
		КонецЕсли;
				
		Если Не ЗначениеЗаполнено(Номенклатура)Тогда //А вдруг!
			
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", Артикул);
		КонецЕсли;
		
		НоваяСтрока = ЗаказПокупателя.Запасы.Добавить();
		НоваяСтрока.mega_НомерЗаказа = НомерЗадания;
		НоваяСтрока.mega_ДатаЗаказа = ПреобразоватьДату(УбратьКавычки(Слова[2]));
		
		Если ЗначениеЗаполнено(Номенклатура)Тогда 
				
			НоваяСтрока.Номенклатура = Номенклатура;
			
			НоменклатураПоставщика = Справочники.НоменклатураПоставщиков.НоменклатураПоставщика(ЗаказПокупателя.Контрагент, Номенклатура);
			Если НоменклатураПоставщика.Следующий() Тогда 			
				НоваяСтрока.mega_НоменклатураПокупателя = НоменклатураПоставщика.Ссылка;
			КонецЕсли;
			
			НоваяСтрока.ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
			НоваяСтрока.Количество = УбратьКавычки(Слова[13]);
			НоваяСтрока.Цена = УбратьКавычки(Слова[11]);			
			НоваяСтрока.Сумма = УбратьКавычки(Слова[6]);
						
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(НоваяСтрока.Номенклатура.ВидСтавкиНДС);		
			НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * (НоваяСтрока.СтавкаНДС.Ставка / (100 + НоваяСтрока.СтавкаНДС.Ставка));
			НоваяСтрока.Всего = НоваяСтрока.Сумма;
			
		Иначе
			ОбщегоНазначения.СообщитьПользователю("не найдена номенклатура для заказа покупателя №" + НомерЗадания);
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	ЗаказПокупателя.Записать(РежимЗаписиДокумента.Проведение);
	
	УдалитьФайлы(ИмяВременногоФайла);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеИзфайлаВайлдберризЗавершение(АдресВоВременномХранилище)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xlsx");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяВременногоФайла);
	
	
	ЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
	ЗаказПокупателя.Дата = ТекущаяДата();
	ЗаказПокупателя.Заполнить(Неопределено);
	ЗаказПокупателя.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("УТ0006832");
	ЗаказПокупателя.Контрагент = ЗаказПокупателя.Договор.Владелец;
	ЗаказПокупателя.Организация = ЗаказПокупателя.Договор.Организация;
	ЗаказПокупателя.ДатаОтгрузки = ЗаказПокупателя.Дата;
	
	
	НомерКолонкиАртикул = 13;
	НомерКолонкиАртикулПоставщика = 14;
	НомерКолонкиДатаСоздания = 5;
	НомерКолонкиЦена = 11;
		
	НомерСтроки = 2;	
	НомерЗадания = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, 1, НомерСтроки, 1).ТекущаяОбласть.Текст);
	Пока НомерЗадания <> "" Цикл 
		
		Артикул = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиАртикул, НомерСтроки, НомерКолонкиАртикул).ТекущаяОбласть.Текст);
		АртикулПоставщика = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиАртикулПоставщика, НомерСтроки, НомерКолонкиАртикулПоставщика).ТекущаяОбласть.Текст);
		Номенклатура = Неопределено;
		
		Выборка = Справочники.НоменклатураПоставщиков.НоменклатураПоДаннымНоменклатурыПоставщика(ЗаказПокупателя.Контрагент, , АртикулПоставщика);
		Если Выборка.Следующий() Тогда 
			
			Номенклатура = Выборка.Номенклатура;
		КонецЕсли;
				
		Если Не ЗначениеЗаполнено(Номенклатура)Тогда //А вдруг!
			
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", Артикул);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Номенклатура)Тогда //А вдруг!
			
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", АртикулПоставщика);
		КонецЕсли;
		
		НоваяСтрока = ЗаказПокупателя.Запасы.Добавить();
		НоваяСтрока.mega_НомерЗаказа = НомерЗадания;
		НоваяСтрока.mega_ДатаЗаказа = 
			ДатаИзСтрокиВайлдберриз(СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиДатаСоздания, НомерСтроки, НомерКолонкиДатаСоздания).ТекущаяОбласть.Текст));
		
		Если ЗначениеЗаполнено(Номенклатура)Тогда 
				
			НоваяСтрока.Номенклатура = Номенклатура;
			НоменклатураПоставщика = Справочники.НоменклатураПоставщиков.НоменклатураПоставщика(ЗаказПокупателя.Контрагент, Номенклатура);
			Если НоменклатураПоставщика.Следующий() Тогда 			
				НоваяСтрока.mega_НоменклатураПокупателя = НоменклатураПоставщика.Ссылка;
			КонецЕсли;
			
			НоваяСтрока.ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
			НоваяСтрока.Количество = 1;
			НоваяСтрока.Цена = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиЦена, НомерСтроки, НомерКолонкиЦена).ТекущаяОбласть.Текст);			
			НоваяСтрока.Сумма = НоваяСтрока.Цена;
						
			НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(НоваяСтрока.Номенклатура.ВидСтавкиНДС);		
			НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * (НоваяСтрока.СтавкаНДС.Ставка / (100 + НоваяСтрока.СтавкаНДС.Ставка));
			НоваяСтрока.Всего = НоваяСтрока.Сумма;
			
		Иначе
			ОбщегоНазначения.СообщитьПользователю("не найдена номенклатура для заказа покупателя №" + НомерЗадания);
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		НомерЗадания = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, 1, НомерСтроки, 1).ТекущаяОбласть.Текст);
	КонецЦикла;
	
	ЗаказПокупателя.Записать(РежимЗаписиДокумента.Проведение);
	
	УдалитьФайлы(ИмяВременногоФайла);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеИзфайлаЯндексЗавершение(АдресВоВременномХранилище)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xlsx");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяВременногоФайла);
	
	
	ЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
	ЗаказПокупателя.Дата = ТекущаяДата();
	ЗаказПокупателя.Заполнить(Неопределено);
	ЗаказПокупателя.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("УТ0007265");
	ЗаказПокупателя.Контрагент = ЗаказПокупателя.Договор.Владелец;
	ЗаказПокупателя.Организация = ЗаказПокупателя.Договор.Организация;
	ЗаказПокупателя.ДатаОтгрузки = ЗаказПокупателя.Дата;
	
	
	НомерКолонкиАртикул = 4;
	НомерКолонкиАртикулПоставщика = 4;
	НомерКолонкиДатаСоздания = 3;
	НомерКолонкиЦена = 7;
	НомерКолонкиКоличество = 6;
		
	НомерСтроки = 3;	
	НомерЗадания = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, 1, НомерСтроки, 1).ТекущаяОбласть.Текст);
	Пока НомерЗадания <> "" Цикл 
		
		СтатусЗаказа = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, 8, НомерСтроки, 8).ТекущаяОбласть.Текст);
		Если СтатусЗаказа <> "Отменён" Тогда
			
			Артикул = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиАртикул, НомерСтроки, НомерКолонкиАртикул).ТекущаяОбласть.Текст);
			АртикулПоставщика = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиАртикулПоставщика, НомерСтроки, НомерКолонкиАртикулПоставщика).ТекущаяОбласть.Текст);
			Номенклатура = Неопределено;
			
			Выборка = Справочники.НоменклатураПоставщиков.НоменклатураПоДаннымНоменклатурыПоставщика(ЗаказПокупателя.Контрагент, , АртикулПоставщика);
			Если Выборка.Следующий() Тогда 
				
				Номенклатура = Выборка.Номенклатура;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Номенклатура)Тогда //А вдруг!
				
				Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", Артикул);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Номенклатура)Тогда //А вдруг!
				
				Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", АртикулПоставщика);
			КонецЕсли;
			
			НоваяСтрока = ЗаказПокупателя.Запасы.Добавить();
			НоваяСтрока.mega_НомерЗаказа = НомерЗадания;
			НоваяСтрока.mega_ДатаЗаказа = 
				ДатаИзСтрокиЯндекс(СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиДатаСоздания, НомерСтроки, НомерКолонкиДатаСоздания).ТекущаяОбласть.Текст));
			
			Если ЗначениеЗаполнено(Номенклатура)Тогда 
				
				НоваяСтрока.Номенклатура = Номенклатура;
				
				НоменклатураПоставщика = Справочники.НоменклатураПоставщиков.НоменклатураПоставщика(ЗаказПокупателя.Контрагент, Номенклатура);
				Если НоменклатураПоставщика.Следующий() Тогда 			
					НоваяСтрока.mega_НоменклатураПокупателя = НоменклатураПоставщика.Ссылка;
				КонецЕсли;
				
				НоваяСтрока.ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
				НоваяСтрока.Количество = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиКоличество, НомерСтроки, НомерКолонкиКоличество).ТекущаяОбласть.Текст);
				НоваяСтрока.Цена = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, НомерКолонкиЦена, НомерСтроки, НомерКолонкиЦена).ТекущаяОбласть.Текст);			
				НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
				
				НоваяСтрока.СтавкаНДС = Справочники.СтавкиНДС.СтавкаНДС(НоваяСтрока.Номенклатура.ВидСтавкиНДС);		
				НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * (НоваяСтрока.СтавкаНДС.Ставка / (100 + НоваяСтрока.СтавкаНДС.Ставка));
				НоваяСтрока.Всего = НоваяСтрока.Сумма;
				
			Иначе
				ОбщегоНазначения.СообщитьПользователю("не найдена номенклатура для заказа покупателя №" + НомерЗадания);
			КонецЕсли;
		КонецЕсли;
	
		НомерСтроки = НомерСтроки + 1;
		НомерЗадания = СокрЛП(ТабличныйДокумент.ПолучитьОбласть(НомерСтроки, 1, НомерСтроки, 1).ТекущаяОбласть.Текст);
	КонецЦикла;
	
	ЗаказПокупателя.Записать(РежимЗаписиДокумента.Проведение);
	
	УдалитьФайлы(ИмяВременногоФайла);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьВозможностьОбъединенияЗаказовНаСервере(ЗаказыПокупателей)
	
	Организация = ЗаказыПокупателей[0].Организация;
	Контрагент = ЗаказыПокупателей[0].Контрагент;
	Договор = ЗаказыПокупателей[0].Договор;
	
	Для Каждого ТекЗаказ Из ЗаказыПокупателей Цикл 
		
		Если ТекЗаказ.Организация <> Организация Тогда 
			ОбщегоНазначения.СообщитьПользователю("Попытка объединения заказов с разными организациями");
			Возврат Ложь;
		КонецЕсли;
		
		Если ТекЗаказ.Контрагент <> Контрагент Тогда 
			ОбщегоНазначения.СообщитьПользователю("Попытка объединения заказов с разными контрагентами");
			Возврат Ложь;
		КонецЕсли;
		
		Если ТекЗаказ.Договор <> Договор Тогда 
			ОбщегоНазначения.СообщитьПользователю("Попытка объединения заказов с разными договорами");
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция ОбъединенитьЗаказыПокупателейНаСервере(ЗаказыПокупателей)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		ОсновнойЗаказПокупателя = ЗаказыПокупателей[0].ПолучитьОбъект();		
		Для Каждого ЗаказПокупателя Из ЗаказыПокупателей Цикл 
			
			Если ЗаказПокупателя = ОсновнойЗаказПокупателя.Ссылка Тогда 
				
				Продолжить;
			КонецЕсли;
			
			Для Каждого ТекСтрокаЗапасы Из ЗаказПокупателя.Запасы Цикл 
								
				НоваяСтрока = ОсновнойЗаказПокупателя.Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
			КонецЦикла;
			
			ЗаказПокупателяОбъект = ЗаказПокупателя.ПолучитьОбъект(); 
			ЗаказПокупателяОбъект.Запасы.Очистить();
			ЗаказПокупателяОбъект.Записать();
			ЗаказПокупателяОбъект.УстановитьПометкуУдаления(Истина);			
		КонецЦикла;
		
		ОсновнойЗаказПокупателя.Запасы.Свернуть(
		"ТипНоменклатурыЗапас,
		|Номенклатура, 
		|Характеристика,
		|СерииНоменклатуры,
		|ЕдиницаИзмерения,
		|Цена,
		|ПроцентСкидкиНаценки,
		|СтавкаНДС,
		|ДатаОтгрузки,
		|Спецификация,
		|Цена,
		|СтранаПроисхождения,
		|НомерГТД,
		|Партия,		
		|mega_НоменклатураПокупателя,		
		|mega_НомерЗаказа,
		|mega_ДатаЗаказа,
		|mega_Собран", 
		"Количество,Сумма,СуммаНДС,Всего");
		
		
		РежимЗаписи = ?(ОсновнойЗаказПокупателя.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
		ОсновнойЗаказПокупателя.Записать(РежимЗаписи);
	Исключение
		
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;

	ЗафиксироватьТранзакцию();
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция ОтгрузитьЗаказыПокупателейНаСервере(ЗаказыПокупателей)
	
	УстановитьПривилегированныйРежим(Истина);
	
	//КонтролироватьОстаткиПриПроведении = Константы.КонтролироватьОстаткиПриПроведении.Получить();
	
	НачатьТранзакцию();
	Попытка
		
		//Если КонтролироватьОстаткиПриПроведении Тогда 
		//	Константы.КонтролироватьОстаткиПриПроведении.Установить(Ложь);
		//КонецЕсли;
				
		Для Каждого ЗаказПокупателя Из ЗаказыПокупателей Цикл 
			
			РасходнаяНакладная = Документы.РасходнаяНакладная.СоздатьДокумент();
			РасходнаяНакладная.Дата = ТекущаяДатаСеанса();			
			РасходнаяНакладная.Заполнить(ЗаказПокупателя);
			
			РасходнаяНакладная.ПоложениеСклада = Перечисления.ПоложениеРеквизитаНаФорме.ВТабличнойЧасти;
			РасходнаяНакладная.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;		
	Исключение
		
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
	
	//Если КонтролироватьОстаткиПриПроведении Тогда 
	//	Константы.КонтролироватьОстаткиПриПроведении.Установить(Истина);
	//КонецЕсли;
	
	ЗафиксироватьТранзакцию();
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция РазделитьЗаказыПокупателейПо60НаСервере(ЗаказыПокупателей)
	
	УстановитьПривилегированныйРежим(Истина);
			
	НачатьТранзакцию();
	Попытка
						
		Для Каждого ЗаказПокупателя Из ЗаказыПокупателей Цикл 
			
			Если РазделитьЗаказПокупателяПо60НаСервере(ЗаказПокупателя) Тогда 
			КонецЕсли;
		КонецЦикла;		
	Исключение
		
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Ложь;
	КонецПопытки;
			
	ЗафиксироватьТранзакцию();
	Возврат Истина;
КонецФункции

&НаСервереБезКонтекста
Функция РазделитьЗаказПокупателяПо60НаСервере(ЗаказПокупателя)
	ИсходныйЗаказПокупателя = ЗаказПокупателя.ПолучитьОбъект();
	РежимЗаписи = ?(ИсходныйЗаказПокупателя.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
	
	КоличествоСтрок = ИсходныйЗаказПокупателя.Запасы.Количество();
	Если КоличествоСтрок <= 60 Тогда 
		
		Возврат Истина;
	КонецЕсли;
	
	НовыйЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(НовыйЗаказПокупателя, ИсходныйЗаказПокупателя, , "Номер");
	
	Пока КоличествоСтрок > 60 Цикл 
		
		Если НовыйЗаказПокупателя.Запасы.Количество() = 60 Тогда 
			
			НовыйЗаказПокупателя.Записать(РежимЗаписи);
			
			НовыйЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(НовыйЗаказПокупателя, ИсходныйЗаказПокупателя, , "Номер");
		КонецЕсли;
		
		//Добавляем новую строку в новый заказ
		НоваяСтрока = НовыйЗаказПокупателя.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходныйЗаказПокупателя.Запасы[КоличествоСтрок - 1]);
		
		//Удаляем последнюю строку в исходном заказе
		ИсходныйЗаказПокупателя.Запасы.Удалить(ИсходныйЗаказПокупателя.Запасы[КоличествоСтрок - 1]);
		
		КоличествоСтрок = КоличествоСтрок - 1;
	КонецЦикла;
	
	НовыйЗаказПокупателя.Записать(РежимЗаписи);
	ИсходныйЗаказПокупателя.Записать(РежимЗаписи);
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ФункцияПреобразованияЗаписи(Свойство, Значение, ДополнительныеПараметры, Отказ)
	Если ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		Возврат Строка(Значение);
	КонецЕсли;
	Отказ = Истина;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтатусыЗаказовВайлдберриз(НомераЗаказов, Заголовки)
	HTTPСоединение = Новый HTTPСоединение("suppliers-api.wildberries.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
		
	HTTPЗапрос = Новый HTTPЗапрос("/api/v3/orders/status", Заголовки);
	
	Заказы = Новый Структура;
	Заказы.Вставить("orders", НомераЗаказов);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	
	ЗаписатьJSON(ЗаписьJSON, Заказы);
	
	СтрокаТелаЗапроса = ЗаписьJSON.Закрыть();	
	
	HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаТелаЗапроса);	
	Результат = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
	ТелоКакСтрока = Результат.ПолучитьТелоКакСтроку();
	
	Если Результат.КодСостояния >= 200 И Результат.КодСостояния < 300 Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоКакСтрока);
		
		Массив = ПрочитатьJSON(ЧтениеJSON, Истина);								
		ЧтениеJSON.Закрыть();
		
		Возврат Массив["orders"];
	КонецЕсли;
	Возврат Новый Массив;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНомераЗаказовЗаказаВайлберриз(ЗаказПокупателя)
	
	НомераЗаказов = Новый Массив;
	Для Каждого ТекСтрокаЗапасы Из ЗаказПокупателя.Запасы Цикл
		
		Если ЗначениеЗаполнено(ТекСтрокаЗапасы.mega_НомерЗаказа) Тогда
			Попытка      
				НомерЗаказа = Число(ТекСтрокаЗапасы.mega_НомерЗаказа);
			Исключение
				НомерЗаказа = 0;
			КонецПопытки; 
			
			Если НомерЗаказа > 0 Тогда 
				
				НомераЗаказов.Добавить(НомерЗаказа);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат НомераЗаказов;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеСтатусаСборочногоЗадания(Статус)
	
	Структура = Новый Структура;
	Структура.Вставить("new", "Новое сборочное задание");	
	Структура.Вставить("confirm", "На сборке");
	Структура.Вставить("complete", "В доставке");
	Структура.Вставить("cancel", "Отменено продавцом");
	
	Если Структура.Свойство(Статус) Тогда
		Возврат Структура[Статус];
	КонецЕсли;
	
	Возврат "не определено";
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеСтатусаСборочногоЗаданияВСистемеВайлдберриз(Статус)
	
	Структура = Новый Структура;
	Структура.Вставить("waiting", "сборочное задание в работе");	
	Структура.Вставить("sorted", "сборочное задание отсортировано");
	Структура.Вставить("sold", "сборочное задание получено покупателем");
	Структура.Вставить("canceled", "отмена сборочного задания");
	Структура.Вставить("canceled_by_client", "покупатель отменил заказ при получении");
	Структура.Вставить("declined_by_client", "покупатель отменил заказ в первый чаc");
	Структура.Вставить("defect", "отмена сборочного задания по причине брака");
	Структура.Вставить("ready_for_pickup", "сборочное задание прибыло на ПВЗ");
	
	Если Структура.Свойство(Статус) Тогда
		Возврат Структура[Статус];
	КонецЕсли;
	
	Возврат "не определено";
КонецФункции

&НаСервереБезКонтекста
Процедура ПроверитьВВайлдберризНаСервере(ЗаказыПокупателей) 
	ДоговорСВайлдберриз = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("УТ0006832");
	Вайлдберриз = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорСВайлдберриз, "Владелец");
	
	ОбщегоНазначения.СообщитьПользователю("https://openapi.wb.ru/marketplace/api/ru/#tag/Sborochnye-zadaniya/paths/~1api~1v3~1orders~1status/post");
	
	Для Каждого ЗаказПокупателя Из ЗаказыПокупателей Цикл
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказПокупателя, "Контрагент") <> Вайлдберриз Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Заказ покупателя %1 не относится к Вайлдберриз. Проверка не проводится.", ЗаказПокупателя));
			Продолжить;
		КонецЕсли;                                                                                                                                       
		
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Проверка заказ покупателя %1...", ЗаказПокупателя));
		
		НомераЗаказов = ПолучитьНомераЗаказовЗаказаВайлберриз(ЗаказПокупателя);
		СтатусыЗаказов = ПолучитьСтатусыЗаказовВайлдберриз(НомераЗаказов, mega_Маркеплейсы.ПолучитьпараметрыПодключенияКВБЛК1());
		
		Для Каждого ТекущаяЗапись Из СтатусыЗаказов Цикл
			
			НомерЗаказа = Формат(ТекущаяЗапись["id"], "ЧЦ=15; ЧДЦ=0; ЧГ=0");
			Строки = ЗаказПокупателя.Запасы.НайтиСтроки(Новый Структура("mega_НомерЗаказа", НомерЗаказа));
			
			Если Строки.Количество() > 0 Тогда 
				
				Строка0 = Строки[0];
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(" - Номер строки %1, номер заказа %2, статус сборочного задания - %3, статус сборочного задания в системе Wildberries - %4", 
					Строка0.НомерСтроки, 
					НомерЗаказа, 
					ПолучитьОписаниеСтатусаСборочногоЗадания(ТекущаяЗапись["supplierStatus"]), 
					ПолучитьОписаниеСтатусаСборочногоЗаданияВСистемеВайлдберриз(ТекущаяЗапись["wbStatus"])));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
//-mega

&НаСервере
Процедура УстановитьУсловноеОформлениеПоЦветамСостоянийСервер()
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеПоЦветамСостояний(
		Список.КомпоновщикНастроек.Настройки.УсловноеОформление,
		Метаданные.Справочники.СостоянияЗаказовПокупателей.ПолноеИмя());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеИОбновитьКомандыИзмененияСостояний()
	
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	ОбновитьКомандыИзмененияСостояний();
	
КонецПроцедуры

#Область ЗаполнениеСписковОтборов

&НаСервере
Процедура ЗаполнитьСписокВыбораОтборОплата()
	
	РаботаСОтборами.ЗаполнитьСписокВыбораОтборОплата(ЭтаФорма, "ОтборОплата")

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОтборОтгрузка()

	РаботаСОтборами.ЗаполнитьСписокВыбораОтборОтгрузка(ЭтаФорма, "ОтборОтгрузка");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораОтборСостояниеОригинала()

	Элементы.ОтборСостояниеОригинала.СписокВыбора.Добавить(
		Справочники.СостоянияОригиналовПервичныхДокументов.ПустаяСсылка(),
		УчетОригиналовПервичныхДокументовУНФКлиентСервер.СостояниеОригиналаНеизвестно());

	Для Каждого ТекСостояние Из УчетОригиналовПервичныхДокументов.ИспользуемыеСостояния() Цикл
		Элементы.ОтборСостояниеОригинала.СписокВыбора.Добавить(ТекСостояние.Ссылка, ТекСостояние.Наименование);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаСервере
Процедура УстановитьМеткуИОтборСписка(ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение,
	ПредставлениеЗначения = "")
	
	Если ГруппаРодительМетки = "ГруппаОтборОплата" Тогда
		ПредставлениеЗначения = РаботаСОтборами.СформироватьПредставлениеМеткиОплата(ВыбранноеЗначение);
		Если ИмяПоляОтбораСписка = "НомерКартинкиОплаты" Тогда
			ВыбранноеЗначение = РаботаСОтборами.НомерКартинкиПоСтатусуОплаты(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
	Если ГруппаРодительМетки = "ГруппаОтборОтгрузка" Тогда
		Если ИмяПоляОтбораСписка = "НомерКартинкиОтгрузки" Тогда
			ПредставлениеЗначения = РаботаСОтборами.СформироватьПредставлениеМеткиОтгрузка(ВыбранноеЗначение);
			ВыбранноеЗначение = РаботаСОтборами.НомерКартинкиПоСтатусуОплаты(ВыбранноеЗначение);
		Иначе
			ПредставлениеЗначения = РаботаСОтборами.СформироватьПредставлениеМеткиОтгрузка(ПредставлениеЗначения);
		КонецЕсли;
	КонецЕсли;

	Если ПредставлениеЗначения="" Тогда
		ПредставлениеЗначения=Строка(ВыбранноеЗначение);
	КонецЕсли;
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения);
	РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, Список, ИмяПоляОтбораСписка);

КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_МеткаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МеткаИД = Сред(Элемент.Имя, СтрДлина("Метка_") + 1);
	УдалитьМеткуОтбора(МеткаИД);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМеткуОтбора(МеткаИД)
	
	ЧисловойИдентификаторМетки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(МеткаИД);
	СтрокаМеток = ДанныеМеток.НайтиПоИдентификатору(ЧисловойИдентификаторМетки);
	Если СтрокаМеток <> Неопределено И СтрокаМеток.ИмяПоляОтбора = "ВидЗаказа" Тогда
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	КонецЕсли;
	
	РаботаСОтборами.УдалитьМеткуОтбораСервер(ЭтотОбъект, Список, МеткаИД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "Список", "Дата");
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
	
	Если Элементы.ФильтрыНастройкиИДопИнфо.Видимость Тогда
		Элементы.ПраваяПанель.Ширина = 28;
	КонецЕсли;
		
КонецПроцедуры

//
&НаКлиенте
Процедура НастроитьОтборы(Команда)
	
	ИмяРеквизитаСписка = "Список";
	ИмяТЧДанныеМеток = "ДанныеМеток";
	ИмяТЧДанныеОтборов = "ДанныеОтборов";
	ИмяГруппыОтборов = "ГруппаОтборы";
	ИмяПредопределенныеОтборыПоУмолчанию = "ПредопределенныеОтборыПоУмолчанию";
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизитаСписка", ИмяРеквизитаСписка);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеМеток", ИмяТЧДанныеМеток);
	ДополнительныеПараметры.Вставить("ИмяТЧДанныеОтборов", ИмяТЧДанныеОтборов);
	ДополнительныеПараметры.Вставить("ИмяГруппыОтборов", ИмяГруппыОтборов);
	ДополнительныеПараметры.Вставить("ИмяПредопределенныеОтборыПоУмолчанию", ИмяПредопределенныеОтборыПоУмолчанию);
	
	РаботаСОтборамиКлиент.НастроитьОтборыНажатие(ЭтотОбъект, ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры), ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОткрытияФормыСНастройкамиОтборов(ДополнительныеПараметры)
	
	Возврат РаботаСОтборами.ПараметрыДляОткрытияФормыСНастройкамиОтборов(ЭтотОбъект, ДополнительныеПараметры);
	
КонецФункции

&НаКлиенте
Процедура НастройкаОтборовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОтборовЗавершениеНаСервере(Результат.АдресВыбранныеОтборы, Результат.АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаОтборовЗавершениеНаСервере(АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ИмяРеквизитаСписка = "Список";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		ИмяТЧДанныеОтборов = "ДанныеОтборов";
	Иначе
		ИмяРеквизитаСписка = ДополнительныеПараметры.ИмяРеквизитаСписка;
		ИмяТЧДанныеМеток = ДополнительныеПараметры.ИмяТЧДанныеМеток;
		ИмяТЧДанныеОтборов = ДополнительныеПараметры.ИмяТЧДанныеОтборов;
	КонецЕсли;
	
	РаботаСОтборами.НастройкаОтборовЗавершение(ЭтотОбъект, АдресВыбранныеОтборы, АдресУдаленныеОтборы, ДополнительныеПараметры);
	
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОтборПриИзменении(Элемент)
	
	Подключаемый_ОтборПриИзмененииНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборПриИзмененииНаСервере(ЭлементИмя, ЭлементРодительИмя)
	
	РаботаСОтборами.Подключаемый_ОтборПриИзменении(ЭтотОбъект, ЭлементИмя, ЭлементРодительИмя);
	
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОтборОчистка(Элемент)
	
	Подключаемый_ОтборОчисткаНаСервере(Элемент.Имя, Элемент.Родитель.Имя)
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОтборОчисткаНаСервере(ЭлементИмя, ЭлементРодительИмя)
	
	РаботаСОтборами.Подключаемый_ОтборОчистка(ЭтотОбъект, ЭлементИмя);

КонецПроцедуры

&НаКлиенте
Процедура СброситьВсеОтборы(Команда)
	РаботаСОтборамиКлиент.СброситьОтборПоПериоду(ЭтотОбъект, "Список", "Дата");
	СброситьВсеМеткиОтбораНаСервере();
КонецПроцедуры

&НаСервере
Процедура СброситьВсеМеткиОтбораНаСервере()
	РаботаСОтборами.УдалитьМеткиОтбораСервер(ЭтотОбъект, Список);
КонецПроцедуры

#КонецОбласти

#Область ПанельКонтактнойИнформации

// УНФ.ПанельКонтактнойИнформации
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбработатьАктивизациюСтрокиСписка()
	
	ОбновитьПанельКонтактнойИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПанельКонтактнойИнформации()
	
	ДанныеПанелиКИ = ДанныеПанелиКонтактнойИнформации(ТекущийКонтрагент);
	КонтактнаяИнформацияПанельУНФКлиент.ЗаполнитьДанныеПанелиКИ(ЭтотОбъект, ДанныеПанелиКИ);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПанелиКонтактнойИнформации(Контрагент)
	
	Возврат КонтактнаяИнформацияПанельУНФ.ДанныеПанелиКонтактнойИнформации(Контрагент);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ДанныеПанелиКонтактнойИнформацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	КонтактнаяИнформацияПанельУНФКлиент.ДанныеПанелиКонтактнойИнформацииВыбор(ЭтотОбъект, Элемент, ВыбраннаяСтрока,
		Поле, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДанныеПанелиКонтактнойИнформацииПриАктивизацииСтроки(Элемент)
	КонтактнаяИнформацияПанельУНФКлиент.ДанныеПанелиКонтактнойИнформацииПриАктивизацииСтроки(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДанныеПанелиКонтактнойИнформацииВыполнитьКоманду(Команда)
	КонтактнаяИнформацияПанельУНФКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, ТекущийКонтрагент);
КонецПроцедуры
// Конец УНФ.ПанельКонтактнойИнформации

#КонецОбласти

#Область ИзменениеСостоянийЗаказов

&НаСервере
Процедура ОбновитьКомандыИзмененияСостояний()
	
	УдаляемыеЭлементы = Новый Массив;
	
	Если Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы.Количество() <> 0 Тогда
		
		Для ИндексГруппы = 0 По Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы.Количество() - 1 Цикл
			Если Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеЗавершен"
				ИЛИ Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеЗавершенУспешно"
				ИЛИ Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеОтменен" Тогда
				Продолжить;
			КонецЕсли;
			УдаляемыеЭлементы.Добавить(Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы]);
		КонецЦикла;
		
		Если Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы.Количество() <> 0 Тогда
			Для ИндексГруппы = 0 По Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы.Количество() - 1 Цикл
				Если Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеЗавершенФорма"
					ИЛИ Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеЗавершенУспешноФорма"
					ИЛИ Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы].Имя = "УстановитьСостояниеОтмененФорма" Тогда
					Продолжить;
				КонецЕсли;
				УдаляемыеЭлементы.Добавить(Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы[ИндексГруппы]);
			КонецЦикла;
		КонецЕсли;
	
		Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
			Элементы.Удалить(УдаляемыйЭлемент);
		КонецЦикла;
	КонецЕсли;
	
	СостоянияЗаказовПокупателей.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыЗаказовПокупателей.Ссылка КАК Ссылка,
	|	ВидыЗаказовПокупателей.Наименование КАК Наименование,
	|	ВидыЗаказовПокупателей.ПорядокСостояний.(
	|		Состояние КАК Состояние,
	|		НомерСтроки КАК НомерСтроки,
	|		Состояние.Цвет КАК СостояниеЦвет
	|	) КАК ПорядокСостояний
	|ИЗ
	|	Справочник.ВидыЗаказовПокупателей КАК ВидыЗаказовПокупателей
	|ГДЕ
	|	ВидыЗаказовПокупателей.ПорядокСостояний.Состояние.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыЗаказовПокупателей.ПорядокСостояний.НомерСтроки";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ИндексВидаЗаказа = 0;
	
	
	Пока Выборка.Следующий() Цикл
		
		КомандыСостоянийВида = Элементы.Добавить("Вид_" + Строка(ИндексВидаЗаказа), Тип("ГруппаФормы"), Элементы.СписокКонтекстноеМенюУстановитьСостояние);
		КомандыСостоянийВида.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		КомандыСостоянийВидаФорма = Элементы.Добавить("Вид_" + Строка(ИндексВидаЗаказа)+ "_Форма", Тип("ГруппаФормы"), Элементы.ФормаУстановитьСостояние);
		КомандыСостоянийВидаФорма.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		НомерСостояния = 1;
		СписокСостояний = Выборка.ПорядокСостояний.Выгрузить();

		Для каждого СостояниеВида Из СписокСостояний Цикл
			
			Если СостояниеВида.Состояние = Справочники.СостоянияЗаказовПокупателей.Завершен Тогда
				Продолжить;
			КонецЕсли;
			
			СостояниеВТаблице = СостоянияЗаказовПокупателей.НайтиСтроки(Новый Структура("Состояние", Выборка.Ссылка));
			
			Если СостояниеВТаблице.Количество() = 0 Тогда
				НовоеСостояние = СостоянияЗаказовПокупателей.Добавить();
				НовоеСостояние.Состояние = СостояниеВида.Состояние;
				НовоеСостояние.Вид = Выборка.Ссылка;
				НовоеСостояние.ИндексВида = ИндексВидаЗаказа;
			Иначе
				НовоеСостояние = СостояниеВТаблице[0];
			КонецЕсли;
			
			КнопкаУстановитьСостояниеЗаказа = Элементы.Добавить("Состояние_" + Строка(СостоянияЗаказовПокупателей.Индекс(НовоеСостояние)), Тип("КнопкаФормы"),КомандыСостоянийВида);
			КнопкаУстановитьСостояниеЗаказа.Заголовок = Строка(НомерСостояния)+". "+ Строка(СостояниеВида.Состояние);
			
			КнопкаУстановитьСостояниеЗаказаФорма = Элементы.Добавить("Состояние_" + Строка(СостоянияЗаказовПокупателей.Индекс(НовоеСостояние)) + "Форма", Тип("КнопкаФормы"),КомандыСостоянийВидаФорма);
			КнопкаУстановитьСостояниеЗаказаФорма.Заголовок = Строка(НомерСостояния)+". "+ Строка(СостояниеВида.Состояние);
			КнопкаУстановитьСостояниеЗаказаФорма.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
			
			НазваниеКоманды = "Состояние_" + Строка(СостоянияЗаказовПокупателей.Индекс(НовоеСостояние));
			
			Если Команды.Найти(НазваниеКоманды) <> Неопределено Тогда
				КомандаУстановитьСостояниеЗаказа = Команды[НазваниеКоманды];
			Иначе
				КомандаУстановитьСостояниеЗаказа = Команды.Добавить(НазваниеКоманды);
			КонецЕсли;

			КомандаУстановитьСостояниеЗаказа.Действие = "ИзменитьСостояниеЗаказа";
			КомандаУстановитьСостояниеЗаказа.Заголовок = Строка(СостояниеВида.Состояние);
			
			КнопкаУстановитьСостояниеЗаказа.ИмяКоманды = КомандаУстановитьСостояниеЗаказа.Имя;
			КнопкаУстановитьСостояниеЗаказаФорма.ИмяКоманды = КомандаУстановитьСостояниеЗаказа.Имя;
			
			НомерСостояния = НомерСостояния + 1;
		
		КонецЦикла;
		
		ИндексВидаЗаказа = ИндексВидаЗаказа + 1;
		
	КонецЦикла;
	
	Элементы.Переместить(Элементы["УстановитьСостояниеЗавершен"],Элементы["СписокКонтекстноеМенюУстановитьСостояние"]);
	Элементы.Переместить(Элементы["УстановитьСостояниеЗавершенФорма"],Элементы["ФормаУстановитьСостояние"]);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСостояниеЗаказа(Команда)
	
	Если Команда.Имя = "СостояниеЗавершенУспешно" Тогда
		ВидЗаказа = Неопределено;
	Иначе
		ИндексСостояния = Число(Сред(Команда.Имя,11,СтрДлина(Команда.Имя)));
		ВидЗаказа = СостоянияЗаказовПокупателей[ИндексСостояния].Вид;
	КонецЕсли;
	
	ИмяКоманды = Команда.Имя;
	Заказы = Элементы.Список.ВыделенныеСтроки;
	УстановитьСостояниеЗаказа(ИмяКоманды, Заказы, ВидЗаказа);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЗаказа(ИмяКоманды, Заказы, ВидЗаказа = Неопределено, ПричинаОтмены = Неопределено,
	Заметки = Неопределено)

	Если ТипЗнч(Заказы) <> Тип("Массив") Или Заказы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Заказы.Количество() = 1 Тогда

		Если Заметки = "" Тогда
			Заметки = Неопределено;
		КонецЕсли;

		УстановитьСостояниеЗаказаСервер(ВидЗаказа, ИмяКоманды, Заказы, ПричинаОтмены, Заметки);

		Если Заказы.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		ПоказатьОповещениеПользователя(НСтр("ru='Изменение:'"), ПолучитьНавигационнуюСсылку(Заказы[0]), СтрШаблон(НСтр(
			"ru='%1'"), Строка(Заказы[0])), БиблиотекаКартинок.Информация32);

		Элементы.Список.Обновить();
		Оповестить("ИзменениеСостояния_ЗаказПокупателя", Заказы);
		Возврат;

	КонецЕсли;

	Если Заметки = "" Тогда
		Заметки = Неопределено;
	КонецЕсли;

	КоличествоЗаказов = Заказы.Количество();

	Состояние(НСтр("ru='Изменение состояния'"), 49);
	УстановитьСостояниеЗаказаСервер(ВидЗаказа, ИмяКоманды, Заказы, ПричинаОтмены, Заметки);
	Состояние(НСтр("ru='Изменение состояния'"), 100);

	Элементы.Список.Обновить();
	Оповестить("ИзменениеСостояния_ЗаказПокупателя", Заказы);

	ПоказатьОповещениеПользователя(СтрШаблон(НСтр("ru='Изменение (%1)'"), КоличествоИзмененныхЗаказов), , НСтр(
		"ru='Заказы покупателей'"), БиблиотекаКартинок.Информация32);

	ПредупредитьЧтоСостояниеИзмененоЧастично(КоличествоЗаказов);

КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьЧтоСостояниеИзмененоЧастично(КоличествоЗаказов)
	
	Если Не ЗначениеЗаполнено(СтрокаВидЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоВидов = СтрЧислоВхождений(СтрокаВидЗаказа, ",") + 1;
	Если КоличествоВидов = 1 Тогда
		Пояснение = СтрШаблон(НСтр("ru = 'Вид заказа ""%1"" не содержит выбранное состояние.'"), СтрокаВидЗаказа);
	Иначе
		Пояснение = СтрШаблон(НСтр("ru = 'Виды заказов ""%1"" не содержат выбранное состояние.'"), СтрокаВидЗаказа);
	КонецЕсли;
	
	ПредставлениеКоличестваИзмененныхЗаказов = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			 НСтр("ru = ';%1 заказа;;%1 заказов;%1 заказов;%1 заказа'"), КоличествоИзмененныхЗаказов);
			 
	ТекстПредупреждения = СтрШаблон(
		НСтр("ru='Было изменено состояние для %1 из %2.
			 |%3'"), ПредставлениеКоличестваИзмененныхЗаказов, КоличествоЗаказов, Пояснение);
		
	ПоказатьПредупреждение( , ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЗаказаСервер(ВидЗаказа, ИмяКоманды, Заказы, ПричинаОтмены, Заметки)
	
	ВариантЗавершения = Неопределено;
	
	Если ВидЗаказа = Неопределено Тогда
		ВидЗаказа = Справочники.ВидыЗаказовПокупателей.Основной;
	КонецЕсли;

	Если ИмяКоманды = "СостояниеЗавершенУспешно" Тогда
		СсылкаНаСостояние = Справочники.СостоянияЗаказовПокупателей.Завершен;
		ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.Успешно;
	ИначеЕсли ИмяКоманды = "СостояниеОтменен" Тогда
		СсылкаНаСостояние = Справочники.СостоянияЗаказовПокупателей.Завершен;
		ВариантЗавершения = Перечисления.ВариантыЗавершенияЗаказа.Отменен;
	Иначе
		ИндексСостояния = Число(Сред(ИмяКоманды,11,СтрДлина(ИмяКоманды)));
		СсылкаНаСостояние = СостоянияЗаказовПокупателей[ИндексСостояния].Состояние;
	КонецЕсли;
		
	КоличествоИзмененныхЗаказов = 0;
	СтрокаВидЗаказа = "";
	МассивСтрок = Новый Массив;
	ИзмененныеЗаказы = Новый Массив;
	НеизмененныеЗаказы = Новый Массив;
	
	Для Каждого Заказ Из Заказы Цикл
		
		ДанныеЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заказ, "ВидЗаказа,СостояниеЗаказа,ВариантЗавершения");
		
		Если ДанныеЗаказа.ВидЗаказа <> ВидЗаказа Тогда
			
			СостояниеНайдено = НайденоСостояниеДанногоВида(СсылкаНаСостояние, ДанныеЗаказа.ВидЗаказа);
			
			Если НЕ СостояниеНайдено Тогда
				
				Вид = МассивСтрок.Найти(Строка(ДанныеЗаказа.ВидЗаказа));
				
				Если Вид <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				МассивСтрок.Добавить(Строка(ДанныеЗаказа.ВидЗаказа));
				НеизмененныеЗаказы.Добавить(Заказ);
				Продолжить;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДанныеЗаказа.СостояниеЗаказа = СсылкаНаСостояние И СсылкаНаСостояние <> Справочники.СостоянияЗаказовПокупателей.Завершен Тогда
			НеизмененныеЗаказы.Добавить(Заказ);
			Продолжить;
		КонецЕсли;
		
		Если ДанныеЗаказа.СостояниеЗаказа = СсылкаНаСостояние И СсылкаНаСостояние = Справочники.СостоянияЗаказовПокупателей.Завершен
			И (ЗначениеЗаполнено(ДанныеЗаказа.ВариантЗавершения) И ДанныеЗаказа.ВариантЗавершения = ВариантЗавершения) Тогда
			НеизмененныеЗаказы.Добавить(Заказ);
			Продолжить;
		КонецЕсли;

		Попытка
			Документы.ЗаказПокупателя.ИзменитьСостояниеЗаказаПокупателя(Заказ, СсылкаНаСостояние, ВариантЗавершения, ПричинаОтмены, Заметки);
			ИзмененныеЗаказы.Добавить(Заказ);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Заказ);
			НеизмененныеЗаказы.Добавить(Заказ);
			Продолжить;
		КонецПопытки;
		
		РегистрыСведений.ОбъектыИнтеграцииCRM.ПослеЗаписиОбъекта(Заказ);
		
		КоличествоИзмененныхЗаказов = КоличествоИзмененныхЗаказов + 1;
		
	КонецЦикла;
	
	Для Каждого НеизмененныйЗаказ Из НеизмененныеЗаказы Цикл
		ИндексЗаказа = Заказы.Найти(НеизмененныйЗаказ);
		Если ИндексЗаказа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Заказы.Удалить(ИндексЗаказа);
		
	КонецЦикла;
	
	СтрокаВидЗаказа = СтрСоединить(МассивСтрок, ", ");
	
	АссистентУправления.ВыполнитьТекущиеЗадачиСейчасВФоне(ИзмененныеЗаказы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьКомандИзмененияСостояний(Элемент)
	
	Если (Тип(Элемент.ВыделенныеСтроки) <> Тип("Массив") 
		ИЛИ Элемент.ВыделенныеСтроки.Количество() = 0
		ИЛИ Элемент.ТекущиеДанные = Неопределено) И ЭтотОбъект.СостоянияЗаказовПокупателей.Количество() = 0 Тогда
		
		Элементы.СписокКонтекстноеМенюУстановитьСостояние.Видимость = Ложь;
		Элементы.ФормаУстановитьСостояние.Видимость = Ложь;
		Возврат;
		
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Найдено = Новый Массив;
		Найдено.Добавить(ЭтотОбъект.СостоянияЗаказовПокупателей[0]);
	Иначе
		Найдено = ЭтотОбъект.СостоянияЗаказовПокупателей.НайтиСтроки(Новый Структура("Вид", Элемент.ТекущиеДанные.ВидЗаказа));
	КонецЕсли;
		
	Для Каждого Группа Из Элементы.СписокКонтекстноеМенюУстановитьСостояние.ПодчиненныеЭлементы Цикл
		
		Если Группа.Имя = "УстановитьСостояниеЗавершен" Тогда
			Группа.Видимость = Истина;
			Продолжить;
		КонецЕсли;
		
		Если Найдено.Количество() = 0 Тогда
			Группа.Видимость = Ложь;
		Иначе
			Группа.Видимость = Группа.Имя = "Вид_"+ Строка(Найдено[0].ИндексВида);
		КонецЕсли;
		
		Для Каждого Элемент Из Группа.ПодчиненныеЭлементы Цикл
			Элемент.Видимость = Группа.Видимость;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого Группа Из Элементы.ФормаУстановитьСостояние.ПодчиненныеЭлементы Цикл
		
		Если Группа.Имя = "УстановитьСостояниеЗавершенФорма" Тогда
			Группа.Видимость = Истина;
			Продолжить;
		КонецЕсли;
		
		Если Найдено.Количество() = 0 Тогда
			Группа.Видимость = Ложь;
		Иначе
			Группа.Видимость = Группа.Имя = "Вид_"+ Строка(Найдено[0].ИндексВида) + "_Форма";
		КонецЕсли;
		
		Для Каждого Элемент Из Группа.ПодчиненныеЭлементы Цикл
			Элемент.Видимость = Группа.Видимость;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаказы(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УстановитьСостояниеОтменен",ЭтотОбъект);
	ОткрытьФорму("Документ.ЗаказПокупателя.Форма.ФормаОтменыЗаказа", , ЭтотОбъект,,,,Оповещение);
	
КонецПроцедуры

&НаСервере
Функция НайденоСостояниеДанногоВида(СсылкаНаСостояние, ВидЗаказа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыЗаказовПокупателей.Ссылка КАК Ссылка,
	|	ВидыЗаказовПокупателей.ПорядокСостояний.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Состояние КАК Состояние
	|	) КАК ПорядокСостояний
	|ИЗ
	|	Справочник.ВидыЗаказовПокупателей КАК ВидыЗаказовПокупателей
	|ГДЕ
	|	ВидыЗаказовПокупателей.Ссылка = &ВидЗаказа";
	
	Запрос.УстановитьПараметр("ВидЗаказа", ВидЗаказа);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СостоянияВида = Выборка.ПорядокСостояний.Выгрузить();
		
		Отбор = Новый Структура;
		Отбор.Вставить("Состояние", СсылкаНаСостояние);
		НайденныеСтроки = СостоянияВида.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСостояниеОтменен(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКоманды = "СостояниеОтменен";
	Заказы = Элементы.Список.ВыделенныеСтроки;
	УстановитьСостояниеЗаказа(ИмяКоманды, Заказы, , Результат.ПричинаОтмены, Результат.Заметки);
	
КонецПроцедуры

#КонецОбласти

#Область ШтрихкодыИТорговоеОборудование

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, Параметры) Экспорт

	Если Результат = Неопределено Тогда
		ТекШтрихкод = СокрЛП(Параметры.ТекШтрихкод);
	Иначе
		ТекШтрихкод = СокрЛП(Результат);
	КонецЕсли;
		
	Если ПустаяСтрока(ТекШтрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("Штрихкод", ТекШтрихкод);
	Данные.Вставить("Количество", 1);
	
	ОбработатьШтрихкоды(Данные);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкоды(Данные)
	
	МассивСсылок = СсылкаНаЭлементСпискаПоШтрихкоду(Данные.Штрихкод);
	Если ЗначениеЗаполнено(МассивСсылок)  Тогда
		Элементы.Список.ТекущаяСтрока = МассивСсылок[0];
		ПоказатьЗначение(Неопределено, МассивСсылок[0]);
	Иначе
		ШтрихкодированиеПечатныхФормКлиент.ОбъектНеНайден(Данные.Штрихкод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СсылкаНаЭлементСпискаПоШтрихкоду(Штрихкод)
	
	Менеджеры = Новый Массив;
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ЗаказПокупателя.ПустаяСсылка"));
	Возврат ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей.Новости

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, "ПриОткрытии");
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.Новости

// ЭДО

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтаФорма, Элементы.Список);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
КонецПроцедуры
// Конец ЭДО

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПередатьВЯндексДоставку(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заказы", Элементы.Список.ВыделенныеСтроки);
	
	ОткрытьФорму("РегистрСведений.ЯндексДоставка.Форма.ПередатьЗаказы", ПараметрыФормы);
	
КонецПроцедуры

// СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыСостоянияОригинала()
	ОбновитьКомандыСостоянияОригинала();
КонецПроцедуры

&НаСервере
Процедура ОбновитьКомандыСостоянияОригинала()
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
КонецПроцедуры
//Конец СтандартныеПодсистемы.УчетОригиналовПервичныхДокументов

// ЭлектронноеВзаимодействие.ТорговыеПредложения

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ПодсказкиБизнесСетьНажатие(Команда)
	
	ТорговыеПредложенияКлиент.ОткрытьФормуПодсказок(ЭтаФорма);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения

#КонецОбласти

#КонецОбласти
