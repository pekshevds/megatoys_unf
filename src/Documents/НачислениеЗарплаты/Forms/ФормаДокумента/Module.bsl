#Область ОписаниеПеременных

&НаКлиенте
Перем СтрокиКРасчету Экспорт;

&НаКлиенте
Перем ЗакрытьПослеЗаписи;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Параметры.Свойство("МесяцНачисленияНовогоДокумента")
			И ЗначениеЗаполнено(Параметры.МесяцНачисленияНовогоДокумента) Тогда
			
			Объект.МесяцНачисления = НачалоМесяца(Параметры.МесяцНачисленияНовогоДокумента);
			
		КонецЕсли;
		
		// Создается новый документ
		ЗначенияДляЗаполнения = Новый Структура;
		ЗначенияДляЗаполнения.Вставить("ПредыдущийМесяц", "Объект.МесяцНачисления");
		ЗначенияДляЗаполнения.Вставить("Организация", "Объект.Организация");
		ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
		ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		
		Если Параметры.ЗначенияЗаполнения.Свойство("ПерваяПоловинаМесяца") Тогда
			Если РасчетЗарплатыДляНебольшихОрганизаций.ЭтоДатаВыплатыАванса(Объект.Организация, Объект.МесяцНачисления, Объект.Дата) Тогда
				Объект.ПерваяПоловинаМесяца = Истина;
			КонеЦесли;
		КонецЕсли;
		
		УстановитьПараметрыВыбораНачислений();
		УстановитьОтображениеЭлементовФормы();
		УстановитьФункциональныеОпцииФормы();
		ПрочитатьДатуВыплаты();
		
		ОтражениеЗарплатыВБухучете.ОбъектПриСозданииНаСервереПроцентЕНВД(ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);
		
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой");
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");

		УчетНДФЛФормы.ДополнитьФормуПанельюВычетов(ЭтотОбъект);
		УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтотОбъект, Объект.МесяцНачисления);
		
		УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтотОбъект, Объект.МесяцНачисления);
		
		УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
		
		РассчитатьИтогиПоДокументу();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	РассчитыватьДокументыПриРедактировании = Константы.РассчитыватьДокументыПриРедактировании.Получить();
	Если РассчитыватьДокументыПриРедактировании Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПересчитатьДокумент",
			"Видимость",
			Ложь);
			
	КонецЕсли;
	
	РасчетЗарплатыДляНебольшихОрганизаций.ПриСозданииФормыДокументаНачислениеЗарплаты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ЗакрытьПослеЗаписи = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ДополнитьТаблицуНачисления();
	
	УстановитьПараметрыВыбораНачислений();
	УстановитьОтображениеЭлементовФормы();
	УстановитьФункциональныеОпцииФормы();
	ПрочитатьДатуВыплаты();
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой");
	
	ОтражениеЗарплатыВБухучете.ОбъектПриЧтенииНаСервереПроцентЕНВД(ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);
	
	УчетНДФЛФормы.ДополнитьФормуПанельюВычетов(ЭтотОбъект);
	УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтотОбъект, Объект.МесяцНачисления);
	
	УстановитьДоступностьИзмененияНДФЛ();
	УстановитьДоступностьИзмененияВзносов();
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтотОбъект, Объект.МесяцНачисления);
	
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	РассчитатьИтогиПоДокументу();
	
	РасчетЗарплатыДляНебольшихОрганизаций.ПриПолученииДанных(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если СтрокиКРасчету.Количество() > 0 Тогда
		
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюОкончание", ЭтотОбъект, ПараметрыЗаписи);
		
		ТекстВопроса = НСтр("ru='Перед записью документа необходимо рассчитать НДФЛ и взносы.
			|Продолжить?'");
			
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	Иначе
		
		Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ОценкаПроизводительностиКлиент.ЗамерВремени("ПроведениеДокументаНачислениеЗарплаты");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтотОбъект, Объект.МесяцНачисления);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
	
	РасчетЗарплатыДляНебольшихОрганизаций.ПриПолученииДанных(ЭтотОбъект);
	ЗаполнитьДолюНеполногоРабочегоВремени();
	
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	
	Оповестить("Запись_НачислениеЗарплаты", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбработатьИзменениеОрганизацииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ОчиститьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроцентЕНВДСтрокаПриИзменении(Элемент)
	
	ОтражениеЗарплатыВБухучетеКлиент.ПроцентЕНВДСтрокаПриИзменении(ЭтотОбъект, Элемент, ЭтотОбъект.ПроцентЕНВДСтрока);
	ЗаполнитьНДФЛИВзносыПослеИзмененияПроцентаЕНВД();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроцентЕНВДСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьНДФЛИВзносыПослеИзмененияПроцентаЕНВД", ЭтотОбъект);
	ОтражениеЗарплатыВБухучетеКлиент.ПроцентЕНВДСтрокаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНДФЛИВзносыПослеИзмененияПроцентаЕНВД(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не Объект.КорректироватьВзносы Тогда
		ПроцентЕНВДСтрокаПриИзмененииНаСервере();
		ЗаполнитьНДФЛиВзносыНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КорректироватьНДФЛПриИзменении(Элемент)
	
	ПриИзмененииПризнакаКорректироватьНДФЛ();
	Если Не Объект.КорректироватьНДФЛ Тогда
		ЗаполнитьНДФЛиВзносыНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КорректироватьВзносыПриИзменении(Элемент)
	
	ПриИзмененииПризнакаКорректироватьВзносы();
	Если Не Объект.КорректироватьВзносы Тогда
		ЗаполнитьНДФЛиВзносыНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Модифицированность);
	ОбработатьИзменениеМесяцНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт 
	
	Если ЗначениеВыбрано Тогда 
		ОбработатьИзменениеМесяцНачисленияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтотОбъект, "Объект.МесяцНачисления", "МесяцНачисленияСтрокой", Направление, Модифицированность);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияМесяцНачисленияПриИзменении", 0.3, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерваяПоловинаМесяцаПриИзменении(Элемент)
	ПерваяПоловинаМесяцаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиСотрудник" Тогда
		
		ПоказатьЗначение(, Элементы.Сотрудники.ТекущиеДанные.Сотрудник);
		
	ИначеЕсли Поле.Имя = "СотрудникиОтработаноДней"
		Или Поле.Имя = "СотрудникиОтработаноЧасов"
		Или Поле.Имя = "СотрудникиНачислено"
		Или Поле.Имя = "СотрудникиВсегоНачислено" Тогда
		
		ПерейтиКНачислениям(Поле.Имя = "СотрудникиВсегоНачислено");
		
	ИначеЕсли Поле.Имя = "СотрудникиОтпускаБольничные" Тогда
		
		ОткрытьОтпускИлиБольничный();
		
	ИначеЕсли Поле.Имя = "СотрудникиДоговоры" Тогда
		
		ПерейтиКДоговорам();
		
	ИначеЕсли Поле.Имя = "СотрудникиНДФЛ"
		Или Поле.Имя = "СотрудникиВсегоУдержано" Тогда
		
		ПерейтиКНДФЛ();
		
	ИначеЕсли Поле.Имя = "СотрудникиПрочееУдержано" Тогда
		
		ПерейтиКУдержаниям();
		
	ИначеЕсли Поле.Имя = "СотрудникиВзносы" Тогда
		
		ПерейтиКВзносам();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПодобратьСотрудников(Ложь)
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Сотрудники") Тогда
		ДобавляемыеСотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
	Иначе
		ДобавляемыеСотрудники = ВыбранноеЗначение;
	КонецЕсли;
	
	ДобавитьСотрудников(ДобавляемыеСотрудники);
	ЗаполнитьНДФЛиВзносыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	СформироватьСписокОбновляемыхСотрудников("Сотрудники", Элемент.ВыделенныеСтроки);
	СформироватьСписокОбновляемыхФизическихЛиц("Сотрудники", Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	СотрудникиПослеУдаленияНаСервере();
КонецПроцедуры

&НаСервере
Процедура СотрудникиПослеУдаленияНаСервере()
	
	Если ОбновляемыеСотрудники <> Неопределено И ОбновляемыеСотрудники.Количество() > 0 Тогда
		
		Для каждого ОбновляемыйСотрудник Из ОбновляемыеСотрудники Цикл
			
			СтрокиНачислений = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
				Объект.Начисления.Удалить(СтрокаНачисления);
			КонецЦикла;
			
			СтрокиНачислений = Объект.НачисленияПоДоговорам.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
				Объект.НачисленияПоДоговорам.Удалить(СтрокаНачисления);
			КонецЦикла;
		
		КонецЦикла;
		
		ОбновляемыеСотрудники = Неопределено;
		
	КонецЕсли; 
	
	Если ОбновляемыеФизическиеЛица <> Неопределено И ОбновляемыеФизическиеЛица.Количество() > 0 Тогда
		
		Для каждого ОбновляемоеФизическоеЛицо Из ОбновляемыеФизическиеЛица Цикл
		
			СтрокиФизическогоЛица = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
			Если СтрокиФизическогоЛица.Количество() = 0 Тогда
				
				УдаляемыеСтроки = Объект.Удержания.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемоеФизическоеЛицо));
				Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
					Объект.Удержания.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
				УдаляемыеСтроки = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
				Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
					
					ИдентификаторСтрокиНДФЛ = УдаляемаяСтрока.ИдентификаторСтрокиНДФЛ;
					Объект.НДФЛ.Удалить(УдаляемаяСтрока);
					
					УдаляемыеСтрокиВычетов = Объект.ПримененныеВычетыНаДетейИИмущественные.НайтиСтроки(Новый Структура("ИдентификаторСтрокиНДФЛ", ИдентификаторСтрокиНДФЛ));
					Для каждого УдаляемаяСтрокаВычетов Из УдаляемыеСтрокиВычетов Цикл
						Объект.ПримененныеВычетыНаДетейИИмущественные.Удалить(УдаляемаяСтрокаВычетов);
					КонецЦикла;
					
				КонецЦикла;
				
				УдаляемыеСтроки = Объект.Взносы.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
				Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
					Объект.Взносы.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		ОбновляемыеФизическиеЛица = Неопределено;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокОбновляемыхСотрудников(ИмяТабличнойЧасти, Знач ИдентификаторыСтрок)
	
	СотрудникиКОбновлению = Новый Соответствие;
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		СписокИдентификаторовСтрок = ИдентификаторыСтрок;
	Иначе
		СписокИдентификаторовСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;
	
	Для каждого ИдентификаторСтроки Из СписокИдентификаторовСтрок Цикл
		
		СтрокаСотрудника = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаСотрудника <> Неопределено И ЗначениеЗаполнено(СтрокаСотрудника.Сотрудник) Тогда
			СотрудникиКОбновлению.Вставить(СтрокаСотрудника.Сотрудник, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ОбновляемыеСотрудники);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиКОбновлению, "Ключ");
	КонецЕсли;
	
	ОбновляемыеСотрудники = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокОбновляемыхФизическихЛиц(ИмяТабличнойЧасти, Знач ИдентификаторыСтрок, ИдентификаторКолонки = "ФизическоеЛицо")
	
	ФизическиеЛицаКОбновлению = Новый Соответствие;
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		СписокИдентификаторовСтрок = ИдентификаторыСтрок;
	Иначе
		СписокИдентификаторовСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;
	
	Для каждого ИдентификаторСтроки Из СписокИдентификаторовСтрок Цикл
		
		СтрокаФизическогоЛица = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаФизическогоЛица <> Неопределено И ЗначениеЗаполнено(СтрокаФизическогоЛица[ИдентификаторКолонки]) Тогда
			ФизическиеЛицаКОбновлению.Вставить(СтрокаФизическогоЛица[ИдентификаторКолонки], Истина);
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ОбновляемыеФизическиеЛица);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ");
	КонецЕсли;
	
	ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНачислениям(ОткрыватьОтпускИлиБольничный = Ложь)
	
	НайденныеСтроки = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", Элементы.Сотрудники.ТекущиеДанные.Сотрудник));
	Если НайденныеСтроки.Количество() > 0 И (НайденныеСтроки[0].Результат > 0 Или ОткрыватьОтпускИлиБольничный = Ложь) Тогда
		Элементы.Начисления.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийЭлемент = Элементы.Найти("НачисленияСумма");
	ИначеЕсли ОткрыватьОтпускИлиБольничный Тогда
		ОткрытьОтпускИлиБольничный();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКДоговорам()
	
	НайденныеСтроки = Объект.НачисленияПоДоговорам.НайтиСтроки(Новый Структура("Сотрудник", Элементы.Сотрудники.ТекущиеДанные.Сотрудник));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.Удержания.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийЭлемент = Элементы.Найти("НачисленияПоДоговорамРезультат");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНДФЛ()
	
	НайденныеСтроки = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо", Элементы.Сотрудники.ТекущиеДанные.ФизическоеЛицо));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.НДФЛ.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийЭлемент = Элементы.Найти("НДФЛНалог");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКУдержаниям()
	
	НайденныеСтроки = Объект.Удержания.НайтиСтроки(Новый Структура("Сотрудник", Элементы.Сотрудники.ТекущиеДанные.ФизическоеЛицо));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.Удержания.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийЭлемент = Элементы.Найти("УдержанияСумма");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКВзносам()
	
	НайденныеСтроки = Объект.Взносы.НайтиСтроки(Новый Структура("ФизическоеЛицо", Элементы.Сотрудники.ТекущиеДанные.ФизическоеЛицо));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Элементы.Взносы.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийЭлемент = Элементы.Найти("ВзносыФизическоеЛицо");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтпускИлиБольничный()
	
	Если ЗначениеЗаполнено(Элементы.Сотрудники.ТекущиеДанные.ОтпускаБольничные) Тогда
		
		Сотрудник = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
		ЕдинственныйДокумент = ЕдинственныйДокументОтпускИлиБольничный(Сотрудник, Объект.МесяцНачисления);
		Если ЗначениеЗаполнено(ЕдинственныйДокумент) Тогда
			ПоказатьЗначение(, ЕдинственныйДокумент);
		Иначе
			
			ПараметрыОткрытия = Новый Структура;
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Месяц", Объект.МесяцНачисления);
			СтруктураОтбора.Вставить("Сотрудники", Сотрудник);
			
			ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
			
			ОткрытьФорму("ЖурналДокументов.НачислениеЗарплаты.ФормаСписка", ПараметрыОткрытия, ЭтотОбъект);
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕдинственныйДокументОтпускИлиБольничный(Знач Сотрудник, Знач Месяц)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Период", Месяц);
	
	СписокКатегорий = Новый Массив;
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоПрофзаболевание);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодам);
	СписокКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.БолезньБезОплаты);
	
	Запрос.УстановитьПараметр("СписокКатегорий", СписокКатегорий);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НачисленияУдержанияПоСотрудникам.Регистратор
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		|ГДЕ
		|	НачисленияУдержанияПоСотрудникам.Период = &Период
		|	И НачисленияУдержанияПоСотрудникам.НачислениеУдержание.КатегорияНачисленияИлиНеоплаченногоВремени В (&СписокКатегорий)
		|	И НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
		|	И НачисленияУдержанияПоСотрудникам.Сотрудник = &Сотрудник";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		
		Выборка.Следующий();
		Возврат Выборка.Регистратор;
		
	КонецЕсли; 
	
	Возврат Неопределено;
		
КонецФункции

&НаКлиенте
Процедура ПодобратьСотрудников(МножественныйВыбор)
	
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация,
		Объект.Подразделение,
		Объект.МесяцНачисления, 
		КонецМесяца(Объект.МесяцНачисления),
		МножественныйВыбор,
		АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры
	
&НаСервере
Процедура ДобавитьСотрудников(Знач СписокСотрудников)
	
	СотрудникиКДобавлению = Новый Массив;
	Для каждого ДобавляемыйСотрудник Из СписокСотрудников Цикл
		
		Если Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ДобавляемыйСотрудник)).Количество() = 0 Тогда
			СотрудникиКДобавлению.Добавить(ДобавляемыйСотрудник);
		КонецЕсли; 
		
	КонецЦикла;
	
	ДанныеСотрудников = ДанныеСотрудников(СотрудникиКДобавлению);
	Если ДанныеСотрудников <> Неопределено Тогда
		
		Для каждого ДобавляемыйСотрудник Из СотрудникиКДобавлению Цикл
			
			СтрокаСотрудники = Объект.Сотрудники.Добавить();
			СтрокаСотрудники.Сотрудник = ДобавляемыйСотрудник;
			СтрокаДанных = ДанныеСотрудников.Найти(ДобавляемыйСотрудник, "Сотрудник");
			Если СтрокаДанных <> Неопределено Тогда
				СтрокаСотрудники.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
				СтрокаСотрудники.ОтпускаБольничные = СтрокаДанных.Сумма;
			Иначе
				СтрокаСотрудники.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДобавляемыйСотрудник, "ФизическоеЛицо");
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	РезультатНачисления = РасчетЗарплаты.РезультатНачисленияРасчетаЗарплаты(
		Объект.Организация,
		Объект.МесяцНачисления,
		Объект.Ссылка,
		Объект.Подразделение,
		СотрудникиКДобавлению,
		Объект.ПерваяПоловинаМесяца);
		
	Если РезультатНачисления.Количество() > 0 Тогда
		
		РезультатНачисления.Колонки.Добавить("ОтработаноДней");
		РезультатНачисления.Колонки.Добавить("ОтработаноЧасов");
		
		РасчетЗарплатыБазовый.ЗаполнитьОтработанноеВремя(РезультатНачисления, Объект.МесяцНачисления,
			Объект.Организация, Объект.ПерваяПоловинаМесяца);
		
		РасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатНачисления(Объект, РезультатНачисления);
	
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатНачисления, Объект.Начисления);
		
		ОбновляемыеСотрудники = Новый ФиксированныйМассив(СотрудникиКДобавлению);
		
		ОбновитьНДФЛиВзносыСотрудников(СотрудникиКДобавлению);
		
		РасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатУдержаний(Объект, РезультатНачисления);
		РасчетЗарплатыДляНебольшихОрганизаций.СформироватьТабличнуюЧастьСотрудники(Объект);
		РассчитатьИтогиПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеСотрудников(СписокСотрудников)
	
	Возврат 
		РасчетЗарплатыДляНебольшихОрганизаций.СведенияНачисленийПоОтсутствиям(
			СписокСотрудников, 
			Объект.МесяцНачисления, 
			Объект.Организация, 
			Объект.Подразделение, 
			Объект.Ссылка);;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтрокуСотрудника()
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		
		СотрудникиФизическихЛиц = Новый Соответствие;
		Для каждого ОбновляемоеФизическоеЛицо Из ОбновляемыеФизическиеЛица Цикл
			
			СтрокиПоФизическимЛицам = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
			Если СтрокиПоФизическимЛицам.Количество() > 0 Тогда
				ОбновляемыйСотрудник = СтрокиПоФизическимЛицам[0].Сотрудник;
			Иначе
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
				
				КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(
					Запрос.МенеджерВременныхТаблиц,
					Истина,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбновляемоеФизическоеЛицо),
					Объект.Организация,
					'00010101',
					КонецМесяца(Объект.МесяцНачисления));
				
				Запрос.Текст =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ОсновныеСотрудникиФизическихЛиц.Сотрудник
					|ИЗ
					|	ВТОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц";
				
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					
					Выборка = РезультатЗапроса.Выбрать();
					Выборка.Следующий();
					ОбновляемыйСотрудник = Выборка.Сотрудник;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОбновляемыйСотрудник) Тогда
				СотрудникиФизическихЛиц.Вставить(ОбновляемыйСотрудник, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СотрудникиФизическихЛиц.Количество() > 0 Тогда
			
			СотрудникиКОбновлению = Новый Массив;
			Если ОбновляемыеСотрудники <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиКОбновлению, ОбновляемыеСотрудники, Истина);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиКОбновлению, ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиФизическихЛиц, "Ключ"), Истина);
			
			ОбновляемыеСотрудники = Новый ФиксированныйМассив(СотрудникиКОбновлению);
			
		КонецЕсли;
		
		ОбновляемыеФизическиеЛица = Неопределено;
		
	КонецЕсли;
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		
		Для каждого ОбновляемыйСотрудник Из ОбновляемыеСотрудники Цикл
			
			СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Если СтрокиСотрудника.Количество() = 0 Тогда
				
				СтрокаСотрудника = Объект.Сотрудники.Добавить();
				СтрокаСотрудника.Сотрудник = ОбновляемыйСотрудник;
				СтрокаСотрудника.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбновляемыйСотрудник, "ФизическоеЛицо");
				
				ДанныеСотрудника = ДанныеСотрудников(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбновляемыйСотрудник));
				Если ДанныеСотрудника.Количество() > 0 Тогда
					СтрокаСотрудника.ОтпускаБольничные = ДанныеСотрудника[0].Сумма;
				КонецЕсли; 
				
			Иначе
				СтрокаСотрудника = СтрокиСотрудника[0];
			КонецЕсли;
			
			ТаблицаНачислений = Объект.Начисления.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.Сотрудник));
			ТаблицаНачисленийПоДоговорам = Объект.НачисленияПоДоговорам.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.Сотрудник));
			ТаблицаУдержаний = Объект.Удержания.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.ФизическоеЛицо));
			ТаблицаНДФЛ = Объект.НДФЛ.Выгрузить(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.ФизическоеЛицо));
			ТаблицаВзносы = Объект.Взносы.Выгрузить(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.ФизическоеЛицо));
			
			Если ТаблицаНачислений.Количество() = 0
				И ТаблицаНачисленийПоДоговорам.Количество() = 0
				И ТаблицаУдержаний.Количество() = 0
				И ТаблицаНДФЛ.Количество() = 0
				И ТаблицаВзносы.Количество() = 0 Тогда
				
				Объект.Сотрудники.Удалить(СтрокаСотрудника);
				
			Иначе
				
				Для каждого СтрокаТаблицыНачислений Из ТаблицаНачислений Цикл
					
					Если ЗначениеЗаполнено(СтрокаТаблицыНачислений.ОтработаноДней) Тогда
						СтрокаСотрудника.ОтработаноДней = СтрокаТаблицыНачислений.ОтработаноДней;
						СтрокаСотрудника.ОтработаноЧасов = СтрокаТаблицыНачислений.ОтработаноЧасов;
						Прервать;
					КонецЕсли; 
					
				КонецЦикла;
				
				СтрокаСотрудника.Начислено = ТаблицаНачислений.Итог("Результат");
				СтрокаСотрудника.Договоры  = ТаблицаНачисленийПоДоговорам.Итог("Результат");
				СтрокаСотрудника.ВсегоНачислено = СтрокаСотрудника.Начислено + СтрокаСотрудника.ОтпускаБольничные + СтрокаСотрудника.Договоры;
				
				СтрокаСотрудника.ПрочееУдержано = ТаблицаУдержаний.Итог("Результат");
				СтрокаСотрудника.НДФЛ = ТаблицаНДФЛ.Итог("Налог") + ТаблицаНДФЛ.Итог("НалогСПревышения") + СтрокаСотрудника.НДФЛОтпускаБольничные;
				СтрокаСотрудника.ВсегоУдержано = СтрокаСотрудника.ПрочееУдержано + СтрокаСотрудника.НДФЛ;
				
				СуммаВзносов = 0;
				Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(УчетСтраховыхВзносов.РассчитываемыеВзносы(Ложь,,Истина)) Цикл
					СуммаВзносов = СуммаВзносов + ТаблицаВзносы.Итог(ИмяПоля)	
				КонецЦикла;
				СтрокаСотрудника.Взносы = СуммаВзносов
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбновляемыеСотрудники = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ПланируемаяДатаВыплаты = ДатаВыплаты;
	КонецЕсли;
	
	Если Копирование Тогда
		Элемент.ТекущиеДанные.ВходитВБазуРКиСН = Ложь;
		Элемент.ТекущиеДанные.КоэффициентПересчета = 0;
	КонецЕсли;
	
	ОтработаноДней = Элемент.ТекущиеДанные.ОтработаноДней;
	ОтработаноЧасов = Элемент.ТекущиеДанные.ОтработаноЧасов;
	
	СформироватьСписокОбновляемыхСотрудников("Начисления", Элементы.Начисления.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияСотрудникПриИзменении(Элемент)
	
	Если Элементы.Начисления.ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Элементы.Начисления.ТекущиеДанные.Сотрудник) Тогда
			
			Элементы.Начисления.ТекущиеДанные.Подразделение = ПодразделениеСотрудника(Элементы.Начисления.ТекущиеДанные.Сотрудник, Объект.МесяцНачисления);
			Если ЗначениеЗаполнено(Элементы.Начисления.ТекущиеДанные.Подразделение) Тогда
				ТекущийЭлемент = Элементы.НачисленияНачисление;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.СуммаВычета = 0;
	
	ОбработатьИзменениеНачислений(Элементы.Начисления.ТекущаяСтрока);
	ПересчитатьСотрудника(ДанныеСтроки.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПередУдалением(Элемент, Отказ)

	СформироватьСписокОбновляемыхСотрудников("Начисления", Элемент.ВыделенныеСтроки);

КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	НачисленияПриОкончанииРедактированияНаСервере();
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПересчитатьСотрудника(Элемент.ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
	РассчитатьИтогНачислено(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	ОбновитьНДФЛИВзносы();
	РассчитатьИтогНачислено(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияОтработаноДнейПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		
		Если ОтработаноДней <> 0
			И ДанныеСтроки.ОтработаноДней <= ДанныеСтроки.РабочихДней Тогда
			
			ОтработаноДней = ?(ОтработаноДней > ДанныеСтроки.РабочихДней, ДанныеСтроки.РабочихДней, ОтработаноДней);
			
			Если ДанныеСтроки.ОтработаноДней <> 0 И ОтработаноДней <> 0 Тогда
				ДанныеСтроки.Результат = ДанныеСтроки.Результат * ДанныеСтроки.ОтработаноДней / ОтработаноДней;
				ПриИзмененииРезультатаНачислений(ДанныеСтроки);
			КонецЕсли;
			
			Если ДанныеСтроки.ОтработаноДней = ДанныеСтроки.РабочихДней Тогда
				ДанныеСтроки.ОтработаноЧасов = ДанныеСтроки.РабочихЧасов;
			Иначе
				
				ИзмененоДней = ДанныеСтроки.ОтработаноДней - ОтработаноДней;
				ИзмененоЧасов = Окр(ИзмененоДней * 8 * ДанныеСтроки.ДоляНеполногоРабочегоВремени, 2);
				
				Если ДанныеСтроки.ОтработаноЧасов + ИзмененоЧасов > 0 Тогда
					ДанныеСтроки.ОтработаноЧасов = ДанныеСтроки.ОтработаноЧасов + ИзмененоЧасов;
				Иначе
					ДанныеСтроки.ОтработаноЧасов = 0;
				КонецЕсли;
				
			КонецЕсли;
			
			ОтработаноДней  = ДанныеСтроки.ОтработаноДней;
			ОтработаноЧасов = ДанныеСтроки.ОтработаноЧасов;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияОтработаноЧасовПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		
		Если ОтработаноЧасов <> 0
			И ДанныеСтроки.ОтработаноЧасов <= ДанныеСтроки.РабочихЧасов Тогда
			
			ОтработаноЧасов = ?(ОтработаноЧасов > ДанныеСтроки.РабочихЧасов, ДанныеСтроки.РабочихЧасов, ОтработаноЧасов);
			
			Если ДанныеСтроки.ОтработаноЧасов <> 0 И ОтработаноЧасов <> 0 Тогда
				ДанныеСтроки.Результат = ДанныеСтроки.Результат * ДанныеСтроки.ОтработаноЧасов / ОтработаноЧасов;
				ПриИзмененииРезультатаНачислений(ДанныеСтроки);
			КонецЕсли;
			
			ОтработаноЧасов = ДанныеСтроки.ОтработаноЧасов;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияСуммаПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ПриИзмененииРезультатаНачислений(ДанныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисленияПоДоговорам

&НаКлиенте
Процедура НачисленияПоДоговорамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ПланируемаяДатаВыплаты = ДатаВыплаты;
	КонецЕсли;
	
	СформироватьСписокОбновляемыхСотрудников("НачисленияПоДоговорам", Элементы.НачисленияПоДоговорам.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамСотрудникПриИзменении(Элемент)
	
	Если Элементы.НачисленияПоДоговорам.ТекущиеДанные <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Элементы.НачисленияПоДоговорам.ТекущиеДанные.Сотрудник) Тогда
			
			Элементы.НачисленияПоДоговорам.ТекущиеДанные.Подразделение = ПодразделениеСотрудника(Элементы.НачисленияПоДоговорам.ТекущиеДанные.Сотрудник, Объект.МесяцНачисления);
			Если ЗначениеЗаполнено(Элементы.НачисленияПоДоговорам.ТекущиеДанные.Подразделение) Тогда
				ТекущийЭлемент = Элементы.НачисленияПоДоговорамДокументОснование;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПередУдалением(Элемент, Отказ)

	СформироватьСписокОбновляемыхСотрудников("НачисленияПоДоговорам", Элемент.ВыделенныеСтроки);

КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	НачисленияПоДоговорамПриОкончанииРедактированияНаСервере();
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ПересчитатьСотрудника(Элемент.ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
	РассчитатьИтогНачислено(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамПослеУдаления(Элемент)
	
	ОбновитьНДФЛИВзносы();
	РассчитатьИтогНачислено(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамРезультатПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.НачисленияПоДоговорам.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ПриИзмененииРезультатаНачислений(ДанныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоДоговорамДокументОснованиеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НачисленияПоДоговорам.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументОснование) Тогда
		ЗаполнитьСотрудникаПриВыбореДоговораГПХ();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУдержания

&НаКлиенте
Процедура УдержанияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьНДФЛИВзносы();
	
	СформироватьСписокОбновляемыхФизическихЛиц("Удержания", Элемент.ВыделенныеСтроки, "Сотрудник");
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПередУдалением(Элемент, Отказ)

	СформироватьСписокОбновляемыхФизическихЛиц("Удержания", Элемент.ВыделенныеСтроки, "Сотрудник");
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СформироватьСписокОбновляемыхФизическихЛиц("Удержания", Элементы.Удержания.ТекущаяСтрока, "Сотрудник");
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияПослеУдаления(Элемент)
	
	ОбновитьНДФЛИВзносы();
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура УдержанияУдержаниеПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Удержания.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ОбработатьИзменениеУдержания(ДанныеСтроки.Удержание, ДанныеСтроки.КатегорияУдержания, ДанныеСтроки.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбработатьИзменениеУдержания(Удержание, КатегорияУдержания, Контрагент)

	КатегорияУдержания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Удержание, "КатегорияУдержания");
	Если КатегорияУдержания = Перечисления.КатегорииУдержаний.ДСВ Тогда
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;

КонецФункции 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНДФЛ

&НаКлиенте
Процедура НДФЛПриАктивизацииСтроки(Элемент)
	
	УчетНДФЛКлиент.НДФЛПриАктивизацииСтроки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПередУдалением(Элемент, Отказ)
	
	НДФЛВыделенныеСтроки = Новый Массив;
	Для Каждого ИдентификаторСтрок Из Элемент.ВыделенныеСтроки Цикл
		СтрокаНДФЛ = Объект.НДФЛ.НайтиПоИдентификатору(ИдентификаторСтрок);
		Если СтрокаНДФЛ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НДФЛВыделенныеСтроки.Добавить(СтрокаНДФЛ.ИдентификаторСтрокиНДФЛ);
	КонецЦикла;
	УчетНДФЛКлиент.НДФЛПередУдалением(ЭтаФорма, НДФЛВыделенныеСтроки, Отказ);
	СформироватьСписокОбновляемыхФизическихЛиц("НДФЛ", Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПослеУдаления(Элемент)
	
	УчетНДФЛКлиент.УдалитьПримененныеВычетыНаДетейИИмущественные(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();

КонецПроцедуры

&НаКлиенте
Процедура НДФЛПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	УчетНДФЛКлиент.НДФЛПриОкончанииРедактирования(ЭтотОбъект);
	
	НДФЛПриОкончанииРедактированияНаСервере(Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаСервере
Процедура НДФЛПриОкончанииРедактированияНаСервере(Знач ВыделенныеСтроки)
	
	СформироватьСписокОбновляемыхФизическихЛиц("НДФЛ", ВыделенныеСтроки);
	ОбновитьСтрокуСотрудника();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	УчетНДФЛКлиент.НДФЛПриНачалеРедактирования(ЭтотОбъект, Элементы.НДФЛ.ТекущиеДанные, НоваяСтрока, Копирование);
	СформироватьСписокОбновляемыхФизическихЛиц("НДФЛ", Элементы.НДФЛ.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыЛичныеПриИзменении(Элемент)
	
	УчетНДФЛКлиент.НДФЛПриОкончанииРедактирования(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПримененныеВычетыИмущественные

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеПриИзменении()
	
	ВычетыНаДетейИИмущественныеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	УчетНДФЛКлиент.ВычетыПриНачалеРедактирования(Элемент.ТекущиеДанные, НоваяСтрока, Элементы.НДФЛ.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеМесяцПериодаПредоставленияВычетаСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы[ОписаниеПанелиВычетыНаКлиенте().ИмяГруппыФормыПанелиВычеты + "ВычетыНаДетейИИмущественные"].ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеМесяцПериодаПредоставленияВычетаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, Элементы[ОписаниеПанелиВычетыНаКлиенте().ИмяГруппыФормыПанелиВычеты + "ВычетыНаДетейИИмущественные"].ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеМесяцПериодаПредоставленияВычетаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы[ОписаниеПанелиВычетыНаКлиенте().ИмяГруппыФормыПанелиВычеты + "ВычетыНаДетейИИмущественные"].ТекущиеДанные, "МесяцПериодаПредоставленияВычета", "МесяцПериодаПредоставленияВычетаСтрокой", Направление, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеМесяцПериодаПредоставленияВычетаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВычетыНаДетейИИмущественныеМесяцПериодаПредоставленияВычетаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПримененныеВычетыКДоходам

&НаКлиенте
Процедура Подключаемый_ВычетыКДоходамПриИзменении()
	
	ВычетыКДоходамПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВзносы

&НаКлиенте
Процедура ВзносыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.Взносы.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.ДатаПолученияДохода = КонецМесяца(Объект.МесяцНачисления);
			ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ТекущиеДанные, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
		КонецЕсли;
	КонецЕсли;
	
	СформироватьСписокОбновляемыхФизическихЛиц("Взносы", Элементы.Взносы.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьИтогВзносы(ЭтотОбъект);
	
	СформироватьСписокОбновляемыхФизическихЛиц("Взносы", Элемент.ВыделенныеСтроки);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПередУдалением(Элемент, Отказ)
	
	СформироватьСписокОбновляемыхФизическихЛиц("Взносы", Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыПослеУдаления(Элемент)
	
	РассчитатьИтогВзносы(ЭтотОбъект);
	ОбновитьСтрокуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойПриИзменении(ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеТекущейСтроки = Элементы.Взносы.ТекущиеДанные;
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойНачалоВыбора(ЭтотОбъект, ДанныеТекущейСтроки, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносыДатаПолученияДоходаСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	УчетСтраховыхВзносовКлиент.ВзносыДатаПолученияДоходаСтрокойРегулирование(
		Элементы.Взносы.ТекущиеДанные, 
		"ДатаПолученияДохода", 
		"ДатаПолученияДоходаСтрокой",
		Направление, 
		Модифицированность);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, Объект.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Не ЗарплатаКадрыКлиент.ОрганизацияЗаполнена(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаНачислениеЗарплаты");
	
	ОчиститьСообщения();
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрНалоговогоУчетаПоНДФЛ(Команда)
	
	НДФЛВыделенныеСтроки = Элементы.НДФЛ.ВыделенныеСтроки;
	Если НДФЛВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РегистрНалоговогоУчетаПоНДФЛ = НДФЛПодробнееНаСервере(НДФЛВыделенныеСтроки);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.РегистрНалоговогоУчетаПоНДФЛ", 
		"РегистрНалоговогоУчетаПоНДФЛ", 
		РегистрНалоговогоУчетаПоНДФЛ, 
		ЭтотОбъект);
			
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛисток(Команда)
	
	НачисленияВыделенныеСтроки = Элементы.Начисления.ВыделенныеСтроки;
	Если НачисленияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(НачисленияВыделенныеСтроки, "Начисления");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокДоговорникиГПХ(Команда)
	
	ВыделенныеСтроки = Элементы.НачисленияПоДоговорам.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(ВыделенныеСтроки, "НачисленияПоДоговорам");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокУдержания(Команда)
	
	УдержанияВыделенныеСтроки = Элементы.Удержания.ВыделенныеСтроки;
	Если УдержанияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(УдержанияВыделенныеСтроки, "Удержания");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокумент(Команда)
	
	ПересчитатьДокументНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСотрудника(Сотрудник)
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		
		Если РассчитыватьДокументыПриРедактировании Тогда
			ОбновитьУдержанияНДФЛиВзносыСотрудника(Сотрудник);
		Иначе
			
			МассивСотрудников = СтрокиКРасчету.Получить("Сотрудники");
			Если МассивСотрудников = Неопределено Тогда
				МассивСотрудников = Новый Массив;
			КонецЕсли;
			
			МассивСотрудников.Добавить(Сотрудник);
			СтрокиКРасчету.Вставить("Сотрудники", МассивСотрудников);
			
			РасчетЗарплатыКлиент.УстановитьОтображениеКнопкиПересчитать(ЭтотОбъект, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПодобратьСотрудников(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаУчетаПоСтраховымВзносам(Команда)
	
	ВзносыВыделенныеСтроки = Элементы.Взносы.ВыделенныеСтроки;
	Если ВзносыВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КарточкаУчета = КарточкаУчетаПоСтраховымВзносамНаСервере(ВзносыВыделенныеСтроки);
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.КарточкаУчетаПоСтраховымВзносам",
		"КарточкаУчетаПоСтраховымВзносамПодробнее",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент", КарточкаУчета));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ПриИзмененииРезультатаНачислений(ДанныеСтроки)
	
	Если ЗначениеЗаполнено(ДанныеСтроки.КодВычета) Тогда 
		ДанныеСтроки.СуммаВычета = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	РасчетЗарплаты.ЗаполнитьДокументНачисленияЗарплаты(Объект, Объект.ПерваяПоловинаМесяца);
	ПослеЗаполненияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаполненияНаСервере(ОбновитьВторичныеДанныеТабличнойЧастиНФЛ = Истина)
	
	Если ОбновитьВторичныеДанныеТабличнойЧастиНФЛ Тогда
		УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтотОбъект, Объект.МесяцНачисления);
	КонецЕсли; 
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");	
	
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНДФЛиВзносыНаСервере(ПерезаполнитьТолькоВзносы = Ложь)
	
	Если РассчитыватьДокументыПриРедактировании Тогда
		ЗаполнитьНДФЛиВзносы(ПерезаполнитьТолькоВзносы);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУдержанияНДФЛиВзносыСотрудника(Сотрудник)
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		МассивСотрудников = Новый Массив(ОбновляемыеСотрудники);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), Истина);
	Иначе
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	КонецЕсли;
	
	ОбновитьУдержанияНДФЛиВзносыСотрудников(МассивСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУдержанияНДФЛиВзносыСотрудников(МассивСотрудников)
	
	ОбновитьНДФЛиВзносыСотрудников(МассивСотрудников);
	
	РезультатНачисления = Неопределено;
	Отбор = Новый Структура("Сотрудник");
	Для каждого Сотрудник Из МассивСотрудников Цикл
		
		Отбор.Сотрудник = Сотрудник;
		ТаблицаНачислений = Объект.Начисления.Выгрузить(Отбор);
		Если РезультатНачисления = Неопределено Тогда
			РезультатНачисления = ТаблицаНачислений;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНачислений, РезультатНачисления);
		КонецЕсли;
		ТаблицаНачислений = Объект.НачисленияПоДоговорам.Выгрузить(Отбор);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНачислений, РезультатНачисления);
		
	КонецЦикла;
	
	РасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатУдержаний(Объект, РезультатНачисления);
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьНДФЛиВзносыСотрудников(Сотрудники)
	
	Если Объект.КорректироватьНДФЛ И Объект.КорректироватьВзносы Тогда
		// Не пересчитываем, т.к. и НДФЛ, и взносы в режиме корректировки.
		Возврат;
	КонецЕсли;
	
	// Составляем временные таблицы для расчета НДФЛ и взносов.
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РасчетЗарплатыБазовый.СоздатьВТДляРасчетаНДФЛиВзносов(МенеджерВременныхТаблиц, Объект, Сотрудники);
	
	// Составляем массив физических лиц.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица";
	
	ФизическиеЛицаМассив = Новый Массив;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ФизическиеЛицаМассив.Добавить(Выборка.ФизическоеЛицо);
	КонецЦикла;
	
	ОтборСтрок = Новый Структура("ФизическоеЛицо", ФизическиеЛицаМассив);
	
	// Рассчитываем НДФЛ и взносы
	ДатаОперации = Мин(Объект.Дата, КонецДня(Объект.МесяцНачисления));
	
	НачатьТранзакцию();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Объект.КорректироватьНДФЛ Тогда
		РезультатРасчетаНДФЛ = РасчетЗарплатыБазовый.РезультатРасчетаНДФЛ(МенеджерВременныхТаблиц, Объект, ДатаОперации, Истина);
		СдвигИдентификатора = УчетНДФЛФормы.МаксимальныйИдентификаторСтрокиНДФЛ(Объект.НДФЛ) + 1;
		// Перенумеруем строки новых коллекций.
		УчетНДФЛФормы.НазначитьИдентификаторыНовымСтрокамТаблицамНДФЛИПримененныеВычетыНаДетейИИмущественные(
			СдвигИдентификатора, РезультатРасчетаНДФЛ.НДФЛ, РезультатРасчетаНДФЛ.ПримененныеВычетыНаДетейИИмущественные);
		// Выполняем замену прежних строк на новые.
		РасчетЗарплаты.ЗаменитьСтрокиНаНовыеДанные(Объект.НДФЛ, РезультатРасчетаНДФЛ.НДФЛ, "ФизическоеЛицо", , ОтборСтрок);
		// В таблице вычетов, т.к. она не отображается, достаточно просто добавить новые строки в любое место, старые будут
		// удалены перед записью.
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатРасчетаНДФЛ.ПримененныеВычетыНаДетейИИмущественные, Объект.ПримененныеВычетыНаДетейИИмущественные);
	КонецЕсли;
	
	Если Не Объект.КорректироватьВзносы Тогда
		РезультатРасчетаВзносов = РасчетЗарплатыБазовый.РезультатРасчетаВзносов(МенеджерВременныхТаблиц, Объект.Ссылка, Объект.Организация, Объект.МесяцНачисления);	
		// Выполняем замену прежних строк на новые.
		РасчетЗарплаты.ЗаменитьСтрокиНаНовыеДанные(Объект.Взносы, РезультатРасчетаВзносов, "ФизическоеЛицо", , ОтборСтрок);
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОтменитьТранзакцию();
	
	Если Не Объект.КорректироватьВзносы Тогда
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");		
	КонецЕсли;
	
	ОбновитьСтрокуСотрудника();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНДФЛиВзносыНаКлиенте(ПерезаполнитьТолькоВзносы = Ложь)
	
	Если Не РассчитыватьДокументыПриРедактировании Тогда
		
		СтрокиКРасчету.Вставить("Взносы", Истина);
		Если Не ПерезаполнитьТолькоВзносы Тогда
			СтрокиКРасчету.Вставить("НДФЛ", Истина);
		КонецЕсли; 
		
		РасчетЗарплатыКлиент.УстановитьОтображениеКнопкиПересчитать(ЭтотОбъект, Истина);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНДФЛиВзносы(ПерезаполнитьТолькоВзносы = Ложь)
	
	ПерезаполнитьНДФЛ = Не Объект.КорректироватьНДФЛ И Не ПерезаполнитьТолькоВзносы;
	ПерезаполнитьВзносы = Не Объект.КорректироватьВзносы;
	
	Если ПерезаполнитьНДФЛ Или ПерезаполнитьВзносы Тогда
		
		РасчетЗарплатыБазовый.ЗаполнитьНДФЛиВзносы(Объект, ПерезаполнитьНДФЛ, ПерезаполнитьВзносы);
		
		Если ПерезаполнитьНДФЛ Тогда
			УчетНДФЛФормы.ЗаполнитьВторичныеДанныеТабличныхЧастей(ЭтотОбъект, Объект.МесяцНачисления);
		КонецЕсли;
		
		Если ПерезаполнитьВзносы Тогда
			ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.Взносы, "ДатаПолученияДохода", "ДатаПолученияДоходаСтрокой");		
		КонецЕсли;	
		
	КонецЕсли;
	
	ОбновитьСтрокуСотрудника();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНачислений(ИдентификаторСтроки)
	
	СтрокаНачисления = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если Не ЗначениеЗаполнено(СтрокаНачисления.Начисление) Тогда
		СтрокаНачисления.ВычетПримененныйКДоходам = Ложь;
	Иначе
		ДанныеНачисления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаНачисления.Начисление, "КодДоходаНДФЛ,КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
		
		КатегорияНачисленияИлиНеоплаченногоВремени = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
		Если КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда
			Или КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее
			И ДанныеНачисления.ВходитВБазуРКИСН Тогда
			СтрокаНачисления.ВходитВБазуРКиСН = Истина;
		КонецЕсли; 
		
		КодДоходаНДФЛ = ДанныеНачисления.КодДоходаНДФЛ;
		Если ЗначениеЗаполнено(КодДоходаНДФЛ) Тогда
			СтрокаНачисления.ВычетПримененныйКДоходам = ЭтотОбъект.СоответствиеКодовВычетовКодамДоходов.Получить(КодДоходаНДФЛ) <> Неопределено
		КонецЕсли; 
		
		Если СтрокаНачисления.ВычетПримененныйКДоходам Тогда
			КодВычета = УчетНДФЛ.КодВычетаПоКодуДоходаНДФЛ(КодДоходаНДФЛ);
			Если Не ЗначениеЗаполнено(СтрокаНачисления.КодВычета) Тогда
				СтрокаНачисления.КодВычета = УчетНДФЛ.КодВычетаПоКодуДоходаНДФЛ(КодДоходаНДФЛ);
			КонецЕсли; 
		Иначе
			СтрокаНачисления.КодВычета = Справочники.ВидыВычетовНДФЛ.ПустаяСсылка();
			СтрокаНачисления.СуммаВычета = 0;
		КонецЕсли;
			
		Если ЗначениеЗаполнено(СтрокаНачисления.КодВычета) И СтрокаНачисления.СуммаВычета = 0 Тогда
			СтрокаНачисления.СуммаВычета = УчетНДФЛ.ВычетКДоходуСотрудника(
				Объект.Ссылка,
				Объект.Организация,
				Объект.МесяцНачисления,
				СтрокаНачисления.Сотрудник,
				КодДоходаНДФЛ,
				СтрокаНачисления.КодВычета,
				СтрокаНачисления.Результат,
				1);
		КонецЕсли; 
	КонецЕсли; 
	
	ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияМесяцНачисленияПриИзменении()

	ОбработатьИзменениеМесяцНачисленияНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОрганизацииНаСервере()

	УстановитьПараметрыВыбораНачислений();
	УстановитьОтображениеЭлементовФормы();
	УстановитьФункциональныеОпцииФормы();
	ПрочитатьДатуВыплаты();
	
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеОрганизацииПроцентЕНВД(ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);

	ОчиститьТабличныеЧасти();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеМесяцНачисленияНаСервере()

	УстановитьПараметрыВыбораНачислений();
	УстановитьОтображениеЭлементовФормы();
	УстановитьФункциональныеОпцииФормы();
	ПрочитатьДатуВыплаты();
	
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);
	
	УчетСтраховыхВзносов.УстановитьВидимостьКолонокТаблицыСтраховыхВзносов(ЭтотОбъект, Объект.МесяцНачисления);
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОчиститьТабличныеЧасти();
		Возврат;
	КонецЕсли;
	
	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ЗаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("Организация", Объект.Организация);
	ПараметрыФО.Вставить("Период", НачалоДня(Объект.МесяцНачисления));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);

КонецПроцедуры

&НаСервере
Процедура ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки)
	
	СтрокаИнициаторПересчета = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если Не СтрокаИнициаторПересчета.ВходитВБазуРКиСН Тогда
		Возврат;
	КонецЕсли;
	
	ОкладТариф = 0;
	СтрокиНачисленийОкладТариф = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник,ВходитВБазуРКиСН", СтрокаИнициаторПересчета.Сотрудник, Истина));
	Для Каждого СтрокОкладТариф Из СтрокиНачисленийОкладТариф Цикл
		ОкладТариф = ОкладТариф + СтрокОкладТариф.Результат;
	КонецЦикла;
	
	СтрокиНачислений = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник,ВходитВБазуРКиСН", СтрокаИнициаторПересчета.Сотрудник, Ложь));
	Для Каждого НайденнаяСтрока Из СтрокиНачислений Цикл
		Если НайденнаяСтрока.КоэффициентПересчета <> 0 Тогда
			НайденнаяСтрока.Результат = ОкладТариф * НайденнаяСтрока.КоэффициентПересчета;
		КонецЕсли; 
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуНачисления()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Получаем количество дней по календарю.
	КалендарьОрганизации = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.КалендарьОрганизации(Объект.Организация);
	
	Если КалендарьОрганизации <> Неопределено Тогда
		
		// Если производственный календарь определить не удалось - оставляем число дней нулевым.
		РабочихДнейЧасов = РасчетЗарплатыБазовый.РабочихДнейЧасовВПериоде(КалендарьОрганизации, НачалоМесяца(Объект.МесяцНачисления), КонецМесяца(Объект.МесяцНачисления));
		
		КоличествоДней = РабочихДнейЧасов.Дней;
		КоличествоЧасов = РабочихДнейЧасов.Часов;
		
	КонецЕсли;
	
	// Формируем массив сотрудников.
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник", Истина);
	
	// Получаем кадровые данные
	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц, Истина, Сотрудники, "ГоловнаяОрганизация,Организация,Подразделение,ДоляНеполногоРабочегоВремени,ПроцентСевернойНадбавки", Объект.МесяцНачисления);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТКадровыеДанныеСотрудников.Сотрудник,
	|	ВЫБОР
	|		КОГДА ПодразделенияОрганизаций.РайонныйКоэффициент > 1
	|			ТОГДА ПодразделенияОрганизаций.РайонныйКоэффициент
	|		КОГДА Организации.РайонныйКоэффициент > 1
	|			ТОГДА Организации.РайонныйКоэффициент
	|		КОГДА ГоловныеОрганизации.РайонныйКоэффициент > 1
	|			ТОГДА ГоловныеОрганизации.РайонныйКоэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК РайонныйКоэффициент,
	|	ВТКадровыеДанныеСотрудников.ПроцентСевернойНадбавки,
	|	ВТКадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ПО ВТКадровыеДанныеСотрудников.Подразделение = ПодразделенияОрганизаций.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ГоловныеОрганизации
	|		ПО ВТКадровыеДанныеСотрудников.ГоловнаяОрганизация = ГоловныеОрганизации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТКадровыеДанныеСотрудников.Организация = Организации.Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	Начисления = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Начисление", Истина);
	ДанныеНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Начисления, "КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
	
	Для Каждого СтрокаНачисления Из Объект.Начисления Цикл
		
		Если ЗначениеЗаполнено(СтрокаНачисления.Начисление) Тогда
			
			ДанныеНачисления = ДанныеНачислений[СтрокаНачисления.Начисление];
			
			КатегорияНачисления = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда
				Или КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее
						И ДанныеНачисления.ВходитВБазуРКИСН Тогда
				
				СтрокаНачисления.ВходитВБазуРКиСН = Истина;
				СтрокаНачисления.РабочихДней = КоличествоДней;
				СтрокаНачисления.РабочихЧасов = КоличествоЧасов;
				
			КонецЕсли;
			
			ДанныеСотрудника = ДанныеСотрудников.Найти(СтрокаНачисления.Сотрудник, "Сотрудник");
			Если ДанныеСотрудника = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент Тогда
				СтрокаНачисления.КоэффициентПересчета = ДанныеСотрудника.РайонныйКоэффициент - 1;
			КонецЕсли;
			
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка Тогда
				СтрокаНачисления.КоэффициентПересчета = ДанныеСотрудника.ПроцентСевернойНадбавки / 100;
			КонецЕсли;
			
			СтрокаНачисления.ДоляНеполногоРабочегоВремени = ДанныеСотрудника.ДоляНеполногоРабочегоВремени;
			СтрокаНачисления.РабочихЧасов = Окр(СтрокаНачисления.РабочихЧасов * СтрокаНачисления.ДоляНеполногоРабочегоВремени, 2);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДолюНеполногоРабочегоВремени()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Формируем массив сотрудников.
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник", Истина);
	
	// Получаем кадровые данные
	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц, Истина, Сотрудники, "КоличествоСтавок,ДоляНеполногоРабочегоВремени", Объект.МесяцНачисления);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТКадровыеДанныеСотрудников.Сотрудник,
	|	ВТКадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаНачисления Из Объект.Начисления Цикл
		ДанныеСотрудника = ДанныеСотрудников.Найти(СтрокаНачисления.Сотрудник, "Сотрудник");
		Если ДанныеСотрудника = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНачисления.ДоляНеполногоРабочегоВремени = ДанныеСотрудника.ДоляНеполногоРабочегоВремени;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеПанелиВычеты()
	
	ОписаниеПанелиВычеты = УчетНДФЛКлиентСервер.ОписаниеПанелиВычеты();
	ОписаниеТабличнойЧастиНДФЛ = УчетНДФЛКлиентСервер.ОписаниеТабличнойЧастиНДФЛ();
	ОписаниеТабличнойЧастиНДФЛ.ИмяПоляПериод = "";
	ОписаниеПанелиВычеты.ТабличнаяЧастьНДФЛ = ОписаниеТабличнойЧастиНДФЛ;
	
	НастраиваемыеПанели = Новый Соответствие;
	НастраиваемыеПанели.Вставить("ВычетыЛичные", 				Истина);
	НастраиваемыеПанели.Вставить("ВычетыНаДетейИИмущественные", "Объект.ПримененныеВычетыНаДетейИИмущественные");
	НастраиваемыеПанели.Вставить("ВычетыКДоходам", 				"Объект.Начисления");
	
	ОписаниеПанелиВычеты.НастраиваемыеПанели = НастраиваемыеПанели;
	
	Возврат ОписаниеПанелиВычеты;
	
КонецФункции

&НаКлиенте
Функция ОписаниеПанелиВычетыНаКлиенте() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции

&НаСервере
Функция ОписаниеПанелиВычетыНаСервере() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции
 
&НаСервере
Функция НДФЛПодробнееНаСервере(ИдентификаторыСтрок)
	
	ФизическиеЛица = Новый Массив;
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ФизическиеЛица.Добавить(Объект.НДФЛ.НайтиПоИдентификатору(ИдентификаторСтроки).ФизическоеЛицо);
	КонецЦикла;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	НДФЛПодробнее = Новый Массив;
	НДФЛПодробнее.Добавить(ДокументОбъект.Ссылка);
	НДФЛПодробнее.Добавить(УчетНДФЛФормы.РегистрНалоговогоУчетаПоНДФЛ(ДокументОбъект, Модифицированность, ФизическиеЛица, Объект.МесяцНачисления));
	
	Возврат НДФЛПодробнее;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьИзмененияНДФЛ()
	
	Элементы.ГруппаНДФЛВертикальная.ТолькоПросмотр = Не Объект.КорректироватьНДФЛ;
	Элементы.ГруппаКарточка.ТолькоПросмотр = Не Объект.КорректироватьНДФЛ;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьИзмененияВзносов()
	
	Элементы.Взносы.ТолькоПросмотр = Не Объект.КорректироватьВзносы;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПризнакаКорректироватьНДФЛ()
	
	УстановитьДоступностьИзмененияНДФЛ();
	
	Если Не Объект.КорректироватьНДФЛ Тогда
		// Восстанавливаем по состоянию учета.
		ЗаполнитьНДФЛиВзносыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПризнакаКорректироватьВзносы()
	
	УстановитьДоступностьИзмененияВзносов();
	
	Если Не Объект.КорректироватьВзносы Тогда
		ЗаполнитьНДФЛиВзносыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов()
	
	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	Элементы.Организация.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	Элементы.ПодразделениеОрганизации.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	Элементы.МесяцНачисленияСтрокой.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	
КонецПроцедуры

&НаСервере
Функция ЕстьЗаполненныеТабличныеЧасти()
	
	ДанныеВТЧЕсть = Ложь;
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
			ДанныеВТЧЕсть = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеВТЧЕсть;
	
КонецФункции

&НаСервере
Функция СписокТабличныхЧастейДокумента()
	
	СписокТабличныхЧастей = Новый Массив;
	
	СписокТабличныхЧастей.Добавить("Сотрудники");
	СписокТабличныхЧастей.Добавить("Начисления");
	СписокТабличныхЧастей.Добавить("Удержания");
	СписокТабличныхЧастей.Добавить("НДФЛ");
	СписокТабличныхЧастей.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	СписокТабличныхЧастей.Добавить("Взносы");
	
	Возврат СписокТабличныхЧастей;
	
КонецФункции

&НаСервере
Процедура ОчиститьТабличныеЧасти()
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Объект[ИмяТабличнойЧасти].Очистить();
	КонецЦикла;
	
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогНачислено(Форма)
	
	ОписанияСоставляющихНачисления = Новый Массив;
	ОписанияСоставляющихНачисления.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='зарплата'")));
	
	ОписанияСоставляющихНачисленияПоДоговорам = Новый Массив;
	ОписанияСоставляющихНачисленияПоДоговорам.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='по договорам ГПХ'")));
		
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Начисления", ОписанияСоставляющихНачисления));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НачисленияПоДоговорам", ОписанияСоставляющихНачисленияПоДоговорам));
		
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНачислено", ОписанияСоставляющих);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогУдержано(Форма)
	
	ОписанияСоставляющихУдержания = Новый Массив;
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='удержания'")));
		
	ОписанияСоставляющихНДФЛ = Новый Массив;
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Налог", НСтр("ru='НДФЛ'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышения", НСтр("ru='НДФЛ с превышения'")));
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Удержания", ОписанияСоставляющихУдержания));
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НДФЛ", ОписанияСоставляющихНДФЛ));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогУдержано", ОписанияСоставляющих);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогВзносы(Форма)
	
	ПоляВзносов = УчетСтраховыхВзносовКлиентСервер.ОписаниеПолейВзносовВСоответствии(Форма.Объект.МесяцНачисления);
	
	ОписанияСоставляющихВзносы = Новый Массив;
	Для каждого ОписаниеПоля Из ПоляВзносов Цикл
		ОписанияСоставляющихВзносы.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита(ОписаниеПоля.Ключ, ОписаниеПоля.Значение));
	КонецЦикла;
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Взносы", ОписанияСоставляющихВзносы));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогВзносы", ОписанияСоставляющих);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоДокументу()
	
	РассчитатьИтогНачислено(ЭтотОбъект);
	РассчитатьИтогУдержано(ЭтотОбъект);
	РассчитатьИтогВзносы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВычетыНаДетейИИмущественныеПриИзмененииНаСервере()
	
	УчетНДФЛФормы.ОбновитьПредставлениеВычетовНаДетейИИмущественныхСтрокиНДФЛ(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВычетыКДоходамПриИзмененииНаСервере()
	
	УчетНДФЛФормы.ОбновитьПредставлениеВычетовКДоходамСтрокиНДФЛ(ЭтотОбъект);
	
КонецПроцедуры

#Область ПроцедурыИФункцииРедактированияПроцентаЕнвдВФормеДокумента

&НаКлиенте
Процедура ЗарегистрироватьПроцентЕНВД() Экспорт

	ЗарегистрироватьПроцентЕНВДНаСервере(Объект.Организация, Объект.МесяцНачисления, ЭтотОбъект.ПроцентЕНВД, ЭтотОбъект.ПроцентЕНВДСтрока);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗарегистрироватьПроцентЕНВДНаСервере(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока)
	
	ОтражениеЗарплатыВБухучете.ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока);
	
КонецПроцедуры

&НаСервере
Функция ТекстПредупрежденияПриРедактированииПроцентаЕНВД() Экспорт

	ОрганизацияНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "Наименование");
	МесяцНачисленияСтрокой  = ЗарплатаКадрыКлиентСервер.ПолучитьПредставлениеМесяца(Объект.МесяцНачисления);
	
	ТекстПредупреждения = 
		НСтр("ru = 'При изменении введенного ранее процента деятельности ЕНВД будут перерассчитаны взносы текущего документа,
		|также необходимо перезаполнить все документы начисления зарплаты за'")
			+ " " + МесяцНачисленияСтрокой + " " + НСтр("ru='г'") + " "
			+ НСтр("ru='по'")+ " """ + ОрганизацияНаименование + """";
					
	Возврат ТекстПредупреждения;

КонецФункции

&НаСервере
Процедура ПроцентЕНВДСтрокаПриИзмененииНаСервере()
	
	ЗаполнитьНДФЛиВзносыНаСервере(Истина);
	ПослеЗаполненияНаСервере();

КонецПроцедуры

#КонецОбласти

&НаСервере
Функция КарточкаУчетаПоСтраховымВзносамНаСервере(ИдентификаторыСтрок)
	
	ФизическиеЛица = Новый Массив;
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ФизическиеЛица.Добавить(Объект.Взносы.НайтиПоИдентификатору(ИдентификаторСтроки).ФизическоеЛицо);
	КонецЦикла;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Возврат УчетСтраховыхВзносов.КарточкаУчетаПоСтраховымВзносамПодробнее(ДокументОбъект, Модифицированность, ФизическиеЛица, Объект.МесяцНачисления);
	
КонецФункции

&НаКлиенте
Процедура ВывестиРасчетныйЛисток(ИдентификаторыВыделенныхСтрок, ИмяТабличнойЧасти)
	
	РасчетныйЛисток = РасчетныйЛистокНаСервере(ИдентификаторыВыделенныхСтрок, ИмяТабличнойЧасти);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.АнализНачисленийИУдержаний", 
		"РасчетныйЛистокПодробнее", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка), ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент", РасчетныйЛисток));
	
КонецПроцедуры

&НаСервере
Функция РасчетныйЛистокНаСервере(ИдентификаторыВыделенныхСтрок, ИмяТабличнойЧасти)
	
	СписокСотрудников = Новый Массив;
	Для каждого ИдентификаторСтроки Из ИдентификаторыВыделенныхСтрок Цикл
		
		Если ИмяТабличнойЧасти = "Начисления" Тогда
			СписокСотрудников.Добавить(Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки).Сотрудник);
		ИначеЕсли ИмяТабличнойЧасти = "НачисленияПоДоговорам" Тогда
			СписокСотрудников.Добавить(Объект.НачисленияПоДоговорам.НайтиПоИдентификатору(ИдентификаторСтроки).Сотрудник);	
		Иначе
			СписокСотрудников.Добавить(Объект.Удержания.НайтиПоИдентификатору(ИдентификаторСтроки).Сотрудник);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИмяТабличнойЧасти = "Начисления" Тогда
		МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСотрудников, "ФизическоеЛицо"), "Значение", Истина);
	ИначеЕсли ИмяТабличнойЧасти = "НачисленияПоДоговорам" Тогда
		МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокСотрудников, "ФизическоеЛицо"), "Значение", Истина);	
	Иначе
		МассивФизическихЛиц = СписокСотрудников;
	КонецЕсли;
	
	Если Объект.Проведен И Не Модифицированность Тогда
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, МассивФизическихЛиц, Объект.МесяцНачисления);
	Иначе
		
		ДанныеДокумента = ДанныеДокументаФизическихЛиц(МассивФизическихЛиц);
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, МассивФизическихЛиц, Объект.МесяцНачисления, Объект.Ссылка, ДанныеДокумента);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НачисленияПриОкончанииРедактированияНаСервере()
	
	ИдентификаторСтроки = Элементы.Начисления.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаНачислений = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаНачислений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНачислений.Сотрудник) Тогда
		СтрокаНачислений.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНачислений.Сотрудник, "ФизическоеЛицо");
		ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаНачислений.ФизическоеЛицо));
	Иначе
		СтрокаНачислений.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли; 
	
	ОбработатьИзменениеНачислений(ИдентификаторСтроки);
	
КонецПроцедуры

&НаСервере
Процедура НачисленияПоДоговорамПриОкончанииРедактированияНаСервере()
	
	ИдентификаторСтроки = Элементы.НачисленияПоДоговорам.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаНачислений = Объект.НачисленияПоДоговорам.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаНачислений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНачислений.Сотрудник) Тогда
		СтрокаНачислений.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНачислений.Сотрудник, "ФизическоеЛицо");
		ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаНачислений.ФизическоеЛицо));
	Иначе
		СтрокаНачислений.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСотрудникаПриВыбореДоговораГПХ()
	
	ИдентификаторСтроки = Элементы.НачисленияПоДоговорам.ТекущаяСтрока;
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		ТекущиеДанные = Объект.НачисленияПоДоговорам.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.Сотрудник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанные.ДокументОснование, "Сотрудник");
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокументНаКлиенте()
	
	Если СтрокиКРасчету.Количество() > 0 Тогда
		
		ПерезаполнитьНДФЛ = СтрокиКРасчету.Получить("НДФЛ");
		ПерезаполнитьВзносы = СтрокиКРасчету.Получить("Взносы");
		МассивСотрудников = СтрокиКРасчету.Получить("Сотрудники");
		
		Если ПерезаполнитьНДФЛ = Истина
			ИЛИ ПерезаполнитьВзносы = Истина
			ИЛИ МассивСотрудников = Неопределено Тогда
			
			ЗаполнитьНДФЛиВзносы(ПерезаполнитьНДФЛ <> Истина);
			
			
		Иначе
			ОбновитьУдержанияНДФЛиВзносыСотрудников(МассивСотрудников);
		КонецЕсли;
		
		СтрокиКРасчету.Очистить();
		РасчетЗарплатыКлиент.УстановитьОтображениеКнопкиПересчитать(ЭтотОбъект, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюОкончание(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПересчитатьДокументНаКлиенте();
		Записать(Параметры);
		
	Иначе
		ЗакрытьПослеЗаписи = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНДФЛИВзносы()
	
	Если РассчитыватьДокументыПриРедактировании Тогда
		ЗаполнитьНДФЛиВзносы();
	КонецЕсли;
	ЗаполнитьНДФЛиВзносыНаКлиенте();
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеСотрудника(Сотрудник, ДатаСведений)
	
	КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "Подразделение", ДатаСведений);
	Если КадровыеДанныеСотрудника.Количество() > 0 Тогда
		Возврат КадровыеДанныеСотрудника[0].Подразделение;
	КонецЕсли;
	
	Возврат Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	
КонецФункции

&НаСервере
Функция ДанныеДокументаФизическихЛиц(ФизическиеЛица)
	
	ТабличныеЧасти = ЗарплатаКадрыОтчеты.ТабличныеЧастиДляФормированияНабораДанныхДокументов();
	
	ОписаниеТаблицыНачислений = Новый Структура;
	ОписаниеТаблицыНачислений.Вставить("ИмяТаблицы", "Начисления");
	ОписаниеТаблицыНачислений.Вставить("ПутьКДанным", "Объект.Начисления");
	ОписаниеТаблицыНачислений.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаНачала", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаОкончания", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаВидРасчета", "Начисление");
	
	ОписаниеТаблицыНачислений.Вставить("ЗаполнятьОплаченноеВремяОтработанным", Истина);
	
	ТабличныеЧасти.ТаблицыНачислений.Добавить(ОписаниеТаблицыНачислений);
	
	ОписаниеТаблицыНачислений = Новый Структура;
	ОписаниеТаблицыНачислений.Вставить("ИмяТаблицы", "НачисленияПоДоговорам");
	ОписаниеТаблицыНачислений.Вставить("ПутьКДанным", "Объект.НачисленияПоДоговорам");
	ОписаниеТаблицыНачислений.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаНачала", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаОкончания", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыНачислений.Добавить(ОписаниеТаблицыНачислений);
	
	ОписаниеТаблицыНДФЛ = Новый Структура;
	ОписаниеТаблицыНДФЛ.Вставить("ИмяТаблицы", "НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ПутьКДанным", "Объект.НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяПоляРезультат", "Налог");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыНДФЛ);
	
	ОписаниеТаблицыУдержаний = Новый Структура;
	ОписаниеТаблицыУдержаний.Вставить("ИмяТаблицы", "Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ПутьКДанным", "Объект.Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаВидРасчета", "Удержание");
	
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаФизическоеЛицо", "Сотрудник");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыУдержаний);
	
	Возврат ЗарплатаКадрыОтчеты.ДанныеДокументаФизическихЛиц(ЭтотОбъект, ФизическиеЛица, ТабличныеЧасти);
	
КонецФункции

&НаСервере
Процедура ПерваяПоловинаМесяцаПриИзмененииНаСервере()
	
	ПрочитатьДатуВыплаты();
	УстановитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДатуВыплаты()
	ДатыВыплаты = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.ДатыВыплатыОрганизации(Объект.Организация, Объект.МесяцНачисления);
	Если Объект.ПерваяПоловинаМесяца Тогда
		ДатаВыплаты = ДатыВыплаты.Аванс;
	Иначе
		ДатаВыплаты = ДатыВыплаты.Зарплата;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДоговорыСтраница",
		"Видимость",
		Не Объект.ПерваяПоловинаМесяца);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВзносыСтраница",
		"Видимость",
		Не Объект.ПерваяПоловинаМесяца);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СотрудникиВзносы",
		"Видимость",
		Не Объект.ПерваяПоловинаМесяца);

	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачисленияПланируемаяДатаВыплаты",
		"ТолькоПросмотр",
		Объект.ПерваяПоловинаМесяца);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораНачислений()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ФормаДокументаУстановитьПараметрыВыбораНачислений(
			ЭтотОбъект, Объект.Организация, Объект.МесяцНачисления);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

СтрокиКРасчету = Новый Соответствие;
ЗакрытьПослеЗаписи = Ложь;

#КонецОбласти