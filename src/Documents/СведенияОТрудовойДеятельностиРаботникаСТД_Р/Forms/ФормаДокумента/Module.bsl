#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец КадровыйЭДО
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ЗапрашиваемыеЗначения = Новый Структура;
		ЗапрашиваемыеЗначения.Вставить("Организация", "Объект.Организация");
		ЗапрашиваемыеЗначения.Вставить("Ответственный", "Объект.Ответственный");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначения);
		
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			Объект.Организация = ЗарплатаКадры.ГоловнаяОрганизация(Объект.Организация);
		КонецЕсли;
		
		ОрганизацияПриИзмененииНаСервере();
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;
	
	ЭлектронныеТрудовыеКнижки.УстановитьУсловноеОформлениеТаблицыМероприятий(ЭтотОбъект, Объект.Организация, Ложь);
	ЭлектронныеТрудовыеКнижки.УстановитьУсловноеОформлениеПредставленияДолжностиТаблицыМероприятий(ЭтотОбъект);
	
	УстановитьУсловноеОформлениеТаблицыМероприятийНеПереданных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьФайлаСТДР"
		И Источник = УникальныйИдентификатор Тогда
		
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			ПрисоединенныеФайлы.ЗагрузитьЗначения(Параметр);
		КонецЕсли;
		БлокировкаИзмененияОбъектовКлиентСервер.ОбновитьГруппуБлокировкиИзмененияОбъекта(ЭтотОбъект, Истина);
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// КадровыйЭДО
	КадровыйЭДОКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец КадровыйЭДО
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец КадровыйЭДО
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ГоловнаяОрганизация = Объект.Организация;
	Попытка
		
		Объект.Организация = ПоследняяОрагнизация();
		
		// ЗарплатаКадрыПодсистемы.ПодписиДокументов
		ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
		// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
		
		Объект.Организация = ГоловнаяОрганизация;
		
	Исключение
		Объект.Организация = ГоловнаяОрганизация;
	КонецПопытки;
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	// КадровыйЭДО
	КадровыйЭДО.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец КадровыйЭДО
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СведенияОТрудовойДеятельностиРаботникаСТД_Р", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик подсистемы "ПодписиДокументов".
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент) 
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент) 
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец Обработчик подсистемы "ПодписиДокументов".

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УведомленииОСпособеИзмененияДекорацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Документ.РегистрацияТрудовойДеятельности.ФормаСписка");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыМероприятия

&НаКлиенте
Процедура МероприятияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура МероприятияПередУдалением(Элемент, Отказ)
	
	Если Элементы.Мероприятия.ВыделенныеСтроки.Количество() > 0 Тогда
		
		ОрганизацииУдаляемыхСтрок = Новый Массив;
		УдаляемыеСтроки = Новый Массив;
		Для Каждого ИдентификаторСтроки Из Элементы.Мероприятия.ВыделенныеСтроки Цикл
			
			ДанныеСтроки = Объект.Мероприятия.НайтиПоИдентификатору(ИдентификаторСтроки);
			Если Не ДанныеСтроки.Передано Тогда
				УдаляемыеСтроки.Добавить(ДанныеСтроки);
			Иначе
				Если ОрганизацииУдаляемыхСтрок.Найти(ДанныеСтроки.Организация) = Неопределено Тогда
					ОрганизацииУдаляемыхСтрок.Добавить(ДанныеСтроки.Организация);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если УдаляемыеСтроки.Количество() < Элементы.Мероприятия.ВыделенныеСтроки.Количество() Тогда
			Если ОрганизацииДокумента().Количество() > 1
				И ОрганизацииУдаляемыхСтрок.Количество() = 1
				И Элементы.Мероприятия.ВыделенныеСтроки.Количество() = 1 Тогда
				
				ТекстВопроса = СтрШаблон(
					НСтр("ru = 'Удалить из документа все мероприятия по орагнизации %1?'"),
					ОрганизацииУдаляемыхСтрок[0]);
				Оповещение = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОбУдаленииМероприятиОрганизации",
					ЭтотОбъект, Новый Структура("Организация", ОрганизацииУдаляемыхСтрок[0]));
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			Иначе
				ПоказатьПредупреждение(, НСтр("ru = 'Допускается удалять только не принятые в ПФР мероприятия'"));
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			Объект.Мероприятия.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Перезаполнить(Команда)
	
	ЗаполнитьМероприятияСотрудника(Истина);
	
КонецПроцедуры

// БлокировкаИзмененияОбъектов

&НаКлиенте
Процедура Подключаемый_РазблокироватьФормуОбъекта(Команда)
	
	Если УдалитьПрисоединенныеФайлы(ПрисоединенныеФайлы) Тогда
		БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Конец БлокировкаИзмененияОбъектов

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервереБезКонтекста
Функция УдалитьПрисоединенныеФайлы(ПрисоединенныеФайлы)
	
	Для Каждого ПрисоединенныйФайл Из ПрисоединенныеФайлы Цикл
		
		Попытка
			ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.Значение.ПолучитьОбъект();
			ПрисоединенныйФайлОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не удалось пометить на удаление файл СТД-Р'") + " (" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())+ ")");
			Возврат Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
	ПрисоединенныеФайлы.Очистить();
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьПодписантов();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	Объект.Фамилия = "";
	Объект.Имя = "";
	Объект.Отчество = "";
	Объект.ДатаРождения = '00010101';
	Объект.СтраховойНомерПФР = "";
	
	ЗаполнитьМероприятияСотрудника(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМероприятияСотрудника(ЗаполнитьТолькоМероприятия)
	
	Объект.Мероприятия.Очистить();
	
	Документы.СведенияОТрудовойДеятельностиРаботникаСТД_Р.ЗаполнитьДокумент(Объект, ЗаполнитьТолькоМероприятия);
	ЭлектронныеТрудовыеКнижкиКлиентСервер.УстановитьОтображениеТаблицыМероприятия(ЭтотОбъект, Объект.Организация, Объект.Мероприятия);
	
	ЗаполнитьПодписантов();
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	РазрядКатегорияВидимость = ЭлектронныеТрудовыеКнижки.РазрядКатегорияВидимость();
	КодПоРееструДолжностейВидимость = ЭлектронныеТрудовыеКнижки.КодПоРееструДолжностейВидимость();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Мероприятия", Объект.Мероприятия.Выгрузить(, "НомерСтроки,ИдМероприятия,Отменено,Организация"));
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Ссылка КАК Ссылка,
		|	Мероприятия.Организация КАК Организация,
		|	Мероприятия.ИдМероприятия КАК ИдМероприятия,
		|	Мероприятия.Отменено КАК Отменено,
		|	Мероприятия.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТМероприятияДокумента
		|ИЗ
		|	&Мероприятия КАК Мероприятия";
	
	Запрос.Выполнить();
	
	ЗапросДанныхМероприятий = Документы.СведенияОТрудовойДеятельностиРаботникаСТД_Р.ЗапросВТДанныеМероприятий("");
	ЗапросДанныхМероприятий.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	ДанныеМероприятий = ЗапросДанныхМероприятий.Выполнить().Выгрузить();
	Для Каждого СтрокаМероприятия Из Объект.Мероприятия Цикл
		
		СтруктураПоиска = Новый Структура("ИдМероприятия,Отменено,Организация");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаМероприятия);
		
		СтрокиДанных = ДанныеМероприятий.НайтиСтроки(СтруктураПоиска);
		Если СтрокиДанных.Количество() > 0 И ЗначениеЗаполнено(СтрокиДанных[0].ВидМероприятия) Тогда
			ЗаполнитьЗначенияСвойств(СтрокаМероприятия, СтрокиДанных[0], , "ИдМероприятия,Отменено,Организация");
		Иначе
			
			Если СтрокаМероприятия.Отменено Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не найдены сведения об отмененном мероприятии с идентификатором - %1'"), СтрокаМероприятия.ИдМероприятия);
			Иначе
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не найдены сведения о мероприятии с идентификатором - %1'"), СтрокаМероприятия.ИдМероприятия);
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Мероприятия[" + Формат(СтрокаМероприятия.НомерСтроки - 1, "ЧН=; ЧГ=") + "].ВидМероприятия", "Объект");
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЭлектронныеТрудовыеКнижкиКлиентСервер.УстановитьОтображениеТаблицыМероприятия(ЭтотОбъект, Объект.Организация, Объект.Мероприятия);
	
	Если Объект.Ссылка.Пустая() Или Не Объект.Проведен Тогда
		ПрисоединенныеФайлы.Очистить();
	Иначе
		ПрисоединенныеФайлы.ЗагрузитьЗначения(ОбщегоНазначения.ВыгрузитьКолонку(
			Документы.СведенияОТрудовойДеятельностиРаботникаСТД_Р.ПрисоединенныеФайлыСТДР(Объект.Ссылка), "Значение"));
	КонецЕсли;
	
	ТолькоПросмотр = ЗначениеЗаполнено(ПрисоединенныеФайлы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеТаблицыМероприятийНеПереданных()
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Объект.Мероприятия.Передано");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение	= Ложь;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Мероприятия");
	ОформляемоеПоле.Использование = Истина;
	
КонецПроцедуры

// КадровыйЭДО

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	КадровыйЭДОКлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры

// Конец КадровыйЭДО

&НаСервере
Процедура ЗаполнитьПодписантов()
	
	ГоловнаяОрганизация = Объект.Организация;
	Попытка
		
		Объект.Организация = ПоследняяОрагнизация();
		
		// ЗарплатаКадрыПодсистемы.ПодписиДокументов
		ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
		// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
		
		Объект.Организация = ГоловнаяОрганизация;
		
	Исключение
		Объект.Организация = ГоловнаяОрганизация;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОбУдаленииМероприятиОрганизации(Ответ, ДополнительныенПараметры) Экспорт
	
	Если Ответ = Неопределено
		Или Ответ <> КодВозвратаДиалога.Да Тогда
		
		Возврат;
	КонецЕсли;
	
	УдалитьМероприятияОрганизации(ДополнительныенПараметры.Организация);
	
КонецПроцедуры

&НаСервере
Функция ПоследняяОрагнизация()
	
	Организация = Неопределено;
	Если Объект.Мероприятия.Количество() > 0 Тогда
		Организация = Объект.Мероприятия[Объект.Мероприятия.Количество() - 1].Организация;
	Иначе
		Организация = Объект.Организация;
	КонецЕсли;
	
	Возврат Организация;
	
КонецФункции

&НаСервере
Процедура УдалитьМероприятияОрганизации(Организация)
	
	Организация = ПоследняяОрагнизация();
	СтрокиУдаляемыхМероприятий = Новый Массив;
	Для Каждого СтрокаМероприятия Из Объект.Мероприятия Цикл
		Если СтрокаМероприятия.Организация = Организация Тогда
			СтрокиУдаляемыхМероприятий.Добавить(СтрокаМероприятия);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаУдаляемыхМероприятий Из СтрокиУдаляемыхМероприятий Цикл
		Объект.Мероприятия.Удалить(СтрокаУдаляемыхМероприятий);
	КонецЦикла;
	Если Организация <> ПоследняяОрагнизация() Тогда
		ЗаполнитьПодписантов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ОрганизацииДокумента()
	Организации = Новый Массив();
	Для Каждого СтрокаМероприятия Из Объект.Мероприятия Цикл
		Если Организации.Найти(СтрокаМероприятия.Организация) = Неопределено Тогда
			Организации.Добавить(СтрокаМероприятия.Организация)
		КонецЕсли;
	КонецЦикла;
	Возврат Организации;
КонецФункции

#КонецОбласти
